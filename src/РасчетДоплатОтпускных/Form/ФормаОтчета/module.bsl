
// УстановитьОтбор
//
Процедура УстановитьОтбор(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра)
	ЭлементОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПараметра);
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ЗначениеПараметра;	
		Попытка
			лЭтоГруппа = ЗначениеПараметра.ЭтоГруппа;
			Если лЭтоГруппа Тогда
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
			Иначе
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		Исключение
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецПопытки;
	Иначе
		ЭлементОтбора.Использование = Ложь;
	КонецЕсли;
КонецПроцедуры

// УстановитьПараметр
//
Процедура УстановитьПараметр(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра)
	ПараметрДанныхТекущий = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	ПараметрДанныхТекущий.Значение = ЗначениеПараметра;
	ПараметрДанныхТекущий.Использование = ЗначениеЗаполнено(ЗначениеПараметра);
КонецПроцедуры

// УстановитьПараметрыОтборы
//
Процедура УстановитьПараметрыОтборы()
	УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоМесяца(ДатаОтчета) );
	УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецМесяца(ДатаОтчета) );
	УстановитьПараметр(КомпоновщикНастроек, "ДоплатаДоСреднегоКомандировки", ДоплатаДоСреднегоКомандировки );
	УстановитьПараметр(КомпоновщикНастроек, "ОплатаПоСреднемуЗаработку", ОплатаПоСреднемуЗаработку );
КонецПроцедуры

// ПриОткрытии
//
Процедура ПриОткрытии()
	Если Не ЗначениеЗаполнено(ДатаОтчета) Тогда ДатаОтчета = КонецМесяца(ТекущаяДата()); КонецЕсли;
	Если Не ЗначениеЗаполнено(ДоплатаДоСреднегоКомандировки) Тогда ДоплатаДоСреднегоКомандировки = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.НайтиПоНаименованию("23. Доплата до средньоденного (відрядження)"); КонецЕсли;
	Если Не ЗначениеЗаполнено(ОплатаПоСреднемуЗаработку) Тогда ОплатаПоСреднемуЗаработку = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ОплатаПоСреднему; КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОтчета) Тогда 
		Сообщить("Не указана дата отчета!");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДоплатаДоСреднегоКомандировки) Тогда 
		Сообщить("Не указан вид расчета '23. Доплата до средньоденного (відрядження)'!");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ОплатаПоСреднемуЗаработку) Тогда 
		Сообщить("Не указан вид расчета '24. Оплата по средньому зарабітку (відрядження)'!");
	КонецЕсли;
	
	УстановитьПараметрыОтборы();
КонецПроцедуры

// ДатаОтчетаПриИзменении
//
Процедура ДатаОтчетаПриИзменении(Элемент)
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура ЗаполнитьДокумент(Кнопка)
	Если Не ЗначениеЗаполнено(Документ) Тогда Сообщить("Не выбран документ!"); Возврат; КонецЕсли;
	Если Не НачалоМесяца(ДатаОтчета) = НачалоМесяца(Документ.Дата) Тогда Сообщить("Дата документа находится не в отчетном месяце!"); Возврат; КонецЕсли;
	Если Документ.ОсновныеНачисления.Количество()>0 Тогда
		// Проверим, что у нас документ по нужному виду начислений
		лтзРез = Документ.ОсновныеНачисления.Выгрузить(,"ВидРасчета");
		лтзРез.Свернуть("ВидРасчета","");
		Для каждого лСтрока Из лтзРез Цикл
			Если Не СокрЛП(лСтрока.ВидРасчета.Наименование) = "20. Доплата до відпускних" Тогда
				Сообщить("В документе - " + Документ + " вид начисления отличается от '20. Доплата до відпускних'!");
				Возврат;
			КонецЕсли;
		КонецЦикла;
		Если Не Вопрос("Перезаполнить документ - '" + Документ + "' ?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	лДокОбъект = Документ.ПолучитьОбъект();
	лДокОбъект.ОсновныеНачисления.Очистить();
	Если лДокОбъект.Проведен Тогда
		лДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЕсли;
	
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ДатаОтчета));
	лЗапрос.УстановитьПараметр("КонецПериода", КонецМесяца(ДатаОтчета));
	лЗапрос.УстановитьПараметр("ДоплатаДоСреднегоКомандировки", ДоплатаДоСреднегоКомандировки);
	лЗапрос.УстановитьПараметр("ОплатаПоСреднемуЗаработку", ОплатаПоСреднемуЗаработку);
	лЗапрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Рег.Сотрудник КАК Сотрудник,
	|	СУММА(Рег.Результат) КАК СуммаОтпускных
	|ПОМЕСТИТЬ Отпускные
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Рег
	|ГДЕ
	|	Рег.ПериодДействия МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Рег.Результат <> 0
	|	И (Рег.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОплатаПоСреднемуОтп)
	|			ИЛИ Рег.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ДополнительныйЕжегодныйОтпуск)
	|			ИЛИ Рег.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ДополнительныйОтпускНаДетей)
	|			ИЛИ Рег.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.УчебныйОтпуск)
	|			ИЛИ Рег.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ЧернобыльскийОтпуск)
	|			ИЛИ Рег.ВидРасчета.Код = ""00025"")
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Рег.Сотрудник КАК Сотрудник,
	|	СУММА(Рег.Результат) КАК СуммаНачислено
	|ПОМЕСТИТЬ Начислено
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Рег
	|ГДЕ
	|	Рег.ПериодДействия МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Рег.Результат <> 0
	|	И (Рег.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОкладПоДням)
	|			ИЛИ Рег.ВидРасчета = &ДоплатаДоСреднегоКомандировки
	|			ИЛИ Рег.ВидРасчета = &ОплатаПоСреднемуЗаработку)
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Рег.Сотрудник,
	|	СУММА(ВЫБОР
	|			КОГДА Курсы.Кратность ЕСТЬ NULL
	|					ИЛИ Курсы.Кратность = 0
	|				ТОГДА Рег.Показатель1
	|			ИНАЧЕ ВЫРАЗИТЬ(Рег.Показатель1 * Курсы.Курс / Курсы.Кратность КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) КАК Оклад
	|ПОМЕСТИТЬ Оклады
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(
	|			&КонецПериода,
	|			Активность
	|				И ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОкладПоДням)) КАК Рег
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом.СрезПоследних(&КонецПериода, ) КАК Курсы
	|		ПО Рег.Валюта1 = Курсы.Валюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Отпускные.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(Оклады.Оклад, 0) КАК Оклад,
	|	ЕСТЬNULL(Начислено.СуммаНачислено, 0) КАК СуммаНачислено,
	|	Отпускные.СуммаОтпускных КАК СуммаОтпускных,
	|	ЕСТЬNULL(Оклады.Оклад, 0) - ЕСТЬNULL(Начислено.СуммаНачислено, 0) - Отпускные.СуммаОтпускных КАК Разница
	|ИЗ
	|	Отпускные КАК Отпускные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Начислено КАК Начислено
	|		ПО Отпускные.Сотрудник = Начислено.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Оклады КАК Оклады
	|		ПО Отпускные.Сотрудник = Оклады.Сотрудник
	|ГДЕ
	|	ЕСТЬNULL(Оклады.Оклад, 0) > (ЕСТЬNULL(Начислено.СуммаНачислено, 0) + Отпускные.СуммаОтпускных)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	
	Если лтзРез.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	лВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.НайтиПоНаименованию( "20. Доплата до відпускних" );
	Для каждого лСтрока Из лтзРез Цикл
		лНоваяСтрока = лДокОбъект.ОсновныеНачисления.Добавить();
		лНоваяСтрока.Авторасчет = Истина;
		лНоваяСтрока.Сотрудник = лСтрока.Сотрудник;
		лНоваяСтрока.ГрафикРаботы = лСтрока.Сотрудник;
		лНоваяСтрока.Физлицо = лСтрока.Сотрудник.Физлицо;
		лНоваяСтрока.ВидРасчета = лВидРасчета;
		лНоваяСтрока.ДатаНачала = НачалоМесяца(ДатаОтчета);
		лНоваяСтрока.ДатаОкончания = КонецМесяца(ДатаОтчета);
		лНоваяСтрока.БазовыйПериодНачало = НачалоМесяца(ДатаОтчета);
		лНоваяСтрока.БазовыйПериодКонец = КонецМесяца(ДатаОтчета);
		лНоваяСтрока.Результат = лСтрока.Разница;
		лНоваяСтрока.Показатель1 = лСтрока.Разница;
		лНоваяСтрока.ПодразделениеОрганизации = лСтрока.Сотрудник.ТекущееПодразделениеОрганизации;
	КонецЦикла;
	лДокОбъект.Записать(РежимЗаписиДокумента.Запись);
КонецПроцедуры
