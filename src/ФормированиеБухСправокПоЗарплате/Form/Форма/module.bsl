// ЗаполнитьТЧ_БухСправки
//
Процедура ЗаполнитьТЧ_БухСправки()
	лмасВидыРасчета = Новый Массив;
	лмасВидыРасчета.Добавить( ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.РезервыОтпусков );
	лмасВидыРасчета.Добавить( ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка );
	лмасВидыРасчета.Добавить( ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ТорговыйБонусЕжемесячный );
	лмасВидыРасчета.Добавить( ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ТорговыйБонусГодовой );
	лмасВидыРасчета.Добавить( ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.АдминистративныйБонусГодовой );
	
	Для ном = 0 По 4 Цикл
		лСтрока = БухСправки[ ном ];
		лВидРасчета = лмасВидыРасчета[ ном ];
		Если Не лСтрока.ВидРасчета = лВидРасчета Тогда лСтрока.ВидРасчета = лВидРасчета; КонецЕсли;
	КонецЦикла;
	Для каждого лСтрока Из БухСправки Цикл
		лСтрока.БухСправка = НайтиБухСправку( Дата, лСтрока.ВидРасчета );
	КонецЦикла;
КонецПроцедуры // ЗаполнитьТЧ_БухСправки

// СформироватьПроводкиБонусы
//
Функция СформироватьПроводкиБонусы( пДата, пВидРасчета )
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Дата", пДата);
	лЗапрос.УстановитьПараметр("ВидРасчета", пВидРасчета);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Рег.Курс / Рег.Кратность КАК СпотКурс
	|ПОМЕСТИТЬ СпотКурсДоллара
	|ИЗ
	|	РегистрСведений.КурсыВалютДляЗакрытия.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.Доллар)) КАК Рег
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.Период КАК ПериодНач,
	|	МИНИМУМ(ЕСТЬNULL(КурсыВалют1.Период, КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ))) КАК ПериодКон,
	|	КурсыВалют.Курс / КурсыВалют.Кратность КАК Курс
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют1
	|		ПО КурсыВалют.Валюта = КурсыВалют1.Валюта
	|			И КурсыВалют.Период < КурсыВалют1.Период
	|ГДЕ
	|	КурсыВалют.Период >= ДАТАВРЕМЯ(2014, 1, 1)
	|	И КурсыВалют.Период <= КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	КурсыВалют.Валюта,
	|	КурсыВалют.Период,
	|	КурсыВалют.Курс / КурсыВалют.Кратность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.Сотрудник КАК Сотрудник,
	|	Рег.ПодразделениеОрганизации КАК Подразделение,
	|	Рег.ПодразделениеОрганизации.Родитель КАК ПодразделениеРодитель
	|ПОМЕСТИТЬ Подразделения
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ) КАК Рег
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Спр.Ссылка,
	|	Спр.ТекущееПодразделениеОрганизации,
	|	Спр.ТекущееПодразделениеОрганизации.Родитель
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК Спр
	|ГДЕ
	|	ПОДСТРОКА(Спр.Наименование, 1, 13) = ""Годовой бонус""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокТЧ.Ссылка.ВидРасчета КАК ВидРасчета,
	|	ВЫБОР
	|		КОГДА ДокТЧ.Ссылка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.МедицинскоеСтрахование)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДокТЧ.Ссылка.ВидРасчета.СпособОтраженияВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.Зарплата)
	|				КОГДА ДокТЧ.Ссылка.ВидРасчета.СпособОтраженияВБухучете.СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АдминистративныеРасходы)
	|					ТОГДА ДокТЧ.Ссылка.ВидРасчета.СпособОтраженияВБухучете.СубконтоДт2
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.Зарплата)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяЗатрат,
	|	ВЫБОР
	|		КОГДА НЕ ДокТЧ.Ссылка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка)
	|				ИЛИ ДокТЧ.Ссылка.Выплачено = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСпотКурс,
	|	ВЫБОР
	|		КОГДА ДокТЧ.Ссылка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка)
	|				И НЕ ДокТЧ.Ссылка.Выплачено = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДокТЧ.Ссылка.Выплачено, ДЕНЬ), СЕКУНДА, -1)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), СЕКУНДА, -1)
	|	КОНЕЦ КАК ДатаКурса,
	|	ДокТЧ.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(Подразделения.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ДокТЧ.Валюта КАК Валюта,
	|	ДокТЧ.Сумма КАК Сумма
	|ПОМЕСТИТЬ Начисления
	|ИЗ
	|	Документ.ПредварительныеНачисления.ОсновныеНачисления КАК ДокТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Подразделения КАК Подразделения
	|		ПО ДокТЧ.Сотрудник = Подразделения.Сотрудник
	|ГДЕ
	|	НЕ ДокТЧ.Сумма = 0
	|	И ДокТЧ.Ссылка.Проведен
	|	И ДокТЧ.Ссылка.ВидРасчета = &ВидРасчета
	|	И (НАЧАЛОПЕРИОДА(ДокТЧ.ДатаНачала, ДЕНЬ) >= НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
	|				И КОНЕЦПЕРИОДА(ДокТЧ.ДатаОкончания, ДЕНЬ) <= КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|				И (ДокТЧ.Ссылка.Выплачено = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ КОНЕЦПЕРИОДА(ДокТЧ.Ссылка.Выплачено, ДЕНЬ) > КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ))
	|			ИЛИ ДокТЧ.Ссылка.ЕжемесячноеРезервирование
	|				И ДокТЧ.Ссылка.Дата <= КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|				И (ДокТЧ.Ссылка.Выплачено = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ КОНЕЦПЕРИОДА(ДокТЧ.Ссылка.Выплачено, ДЕНЬ) > КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.СтатьяЗатрат КАК СтатьяЗатрат,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА Начисления.ЭтоСпотКурс
	|				ИЛИ КурсыВалют.Курс ЕСТЬ NULL
	|			ТОГДА СпотКурсДоллара.СпотКурс
	|		ИНАЧЕ КурсыВалют.Курс
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА Начисления.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.Гривна)
	|			ТОГДА Начисления.Сумма
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Начисления.ЭтоСпотКурс
	|						ИЛИ КурсыВалют.Курс ЕСТЬ NULL
	|					ТОГДА ВЫРАЗИТЬ(Начисления.Сумма * СпотКурсДоллара.СпотКурс КАК ЧИСЛО(12, 2))
	|				ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма * КурсыВалют.Курс КАК ЧИСЛО(12, 2))
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаГрн,
	|	ВЫБОР
	|		КОГДА Начисления.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.Гривна)
	|			ТОГДА ВЫБОР
	|					КОГДА Начисления.ЭтоСпотКурс
	|							ИЛИ КурсыВалют.Курс ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(Начисления.Сумма / СпотКурсДоллара.СпотКурс КАК ЧИСЛО(12, 2))
	|					ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма / КурсыВалют.Курс КАК ЧИСЛО(12, 2))
	|				КОНЕЦ
	|		ИНАЧЕ Начисления.Сумма
	|	КОНЕЦ КАК СуммаДол
	|ПОМЕСТИТЬ НачисленияСКурсами
	|ИЗ
	|	Начисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО Начисления.ДатаКурса >= КурсыВалют.ПериодНач
	|			И Начисления.ДатаКурса < КурсыВалют.ПериодКон,
	|	СпотКурсДоллара КАК СпотКурсДоллара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияСКурсами.ВидРасчета КАК ВидРасчета,
	|	НачисленияСКурсами.Подразделение КАК Подразделение,
	|	НачисленияСКурсами.СтатьяЗатрат КАК СтатьяЗатрат,
	|	НачисленияСКурсами.Сотрудник КАК Сотрудник,
	|	НачисленияСКурсами.Валюта КАК Валюта,
	|	НачисленияСКурсами.Курс КАК Курс,
	|	СУММА(НачисленияСКурсами.СуммаГрн) КАК СуммаГрн,
	|	СУММА(НачисленияСКурсами.СуммаДол) КАК СуммаДол
	|ПОМЕСТИТЬ НачисленияСвернуто
	|ИЗ
	|	НачисленияСКурсами КАК НачисленияСКурсами
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияСКурсами.ВидРасчета,
	|	НачисленияСКурсами.Подразделение,
	|	НачисленияСКурсами.СтатьяЗатрат,
	|	НачисленияСКурсами.Сотрудник,
	|	НачисленияСКурсами.Валюта,
	|	НачисленияСКурсами.Курс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АдминистративныеРасходы) КАК СчетДт,
	|	Врем4.Подразделение КАК СубконтоДт1,
	|	Врем4.СтатьяЗатрат КАК СубконтоДт2,
	|	Врем4.Сотрудник.Физлицо КАК СубконтоДт3,
	|	Врем4.Валюта КАК ВалютаДт,
	|	Врем4.СуммаДол КАК ВалютнаяСуммаДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность) КАК НалоговоеНазначениеДт,
	|	ВЫБОР
	|		КОГДА Врем4.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСОтечественнымиПоставщиками)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбеспечениеМатериальногоПоощерения)
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА Врем4.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|		ИНАЧЕ Врем4.СтатьяЗатрат
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА Врем4.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		ИНАЧЕ Врем4.Подразделение
	|	КОНЕЦ КАК СубконтоКт2,
	|	ВЫБОР
	|		КОГДА Врем4.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Врем4.Сотрудник.Физлицо
	|	КОНЕЦ КАК СубконтоКт3,
	|	Врем4.Валюта КАК ВалютаКт,
	|	Врем4.СуммаДол КАК ВалютнаяСуммаКт,
	|	Врем4.Курс КАК КурсДоллара,
	|	Врем4.СуммаГрн КАК Сумма,
	|	0 КАК СуммаНУ
	|ИЗ
	|	НачисленияСвернуто КАК Врем4";
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	
	Возврат лтзРез;
КонецФункции // СформироватьПроводкиБонусы

// СформироватьПроводкиРезервыОтпусков
//
Функция СформироватьПроводкиРезервыОтпусков( пДата )
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Дата", пДата);
	лЗапрос.УстановитьПараметр("ВидОтпуска", Справочники.ВидыОтпусков.ПустаяСсылка());
	лЗапрос.УстановитьПараметр("Серна", Справочники.ПодразделенияОрганизаций.НайтиПоНаименованию("Центральний офіс, м.Київ") );
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Рег.ВидОтпуска
	|ПОМЕСТИТЬ ВидыОтпусков
	|ИЗ
	|	РегистрСведений.ггуНормыОтпусков.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&Дата, ДЕНЬ),
	|			ВидОтпуска.ВедетсяУчетОстатков
	|				И НЕ ВидОтпуска.ОтпускНеПереноситсяНаСледующийГод
	|				И НЕ ВидОтпуска.ОтпускНеКомпенсируется
	|				И (ВидОтпуска = &ВидОтпуска
	|					ИЛИ &ВидОтпуска = ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.ПустаяСсылка))) КАК Рег
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег.ВидОтпуска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.Сотрудник,
	|	Рег.ВидОтпуска,
	|	Рег.КвоДней КАК КоличествоДнейОтпускаВГод
	|ПОМЕСТИТЬ НормыОтпусковПоСотрудникам
	|ИЗ
	|	РегистрСведений.ггуНормыОтпусков.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&Дата, ДЕНЬ),
	|			НЕ Сотрудник = ЗНАЧЕНИЕ(Справочник.СотрудникиОрганизаций.ПустаяСсылка)
	|				И ВидОтпуска В
	|					(ВЫБРАТЬ
	|						ВидыОтпусков.ВидОтпуска
	|					ИЗ
	|						ВидыОтпусков КАК ВидыОтпусков)) КАК Рег
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.ВидОтпуска,
	|	Рег.КвоДней КАК КоличествоДнейОтпускаВГод
	|ПОМЕСТИТЬ НормыОтпусковПоВсем
	|ИЗ
	|	РегистрСведений.ггуНормыОтпусков.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&Дата, ДЕНЬ),
	|			Сотрудник = ЗНАЧЕНИЕ(Справочник.СотрудникиОрганизаций.ПустаяСсылка)
	|				И ВидОтпуска В
	|					(ВЫБРАТЬ
	|						ВидыОтпусков.ВидОтпуска
	|					ИЗ
	|						ВидыОтпусков КАК ВидыОтпусков)) КАК Рег
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Работники.Организация КАК Организация,
	|	Работники.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ Уволенные
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ, 1), МЕСЯЦ), ) КАК Работники
	|ГДЕ
	|	(Работники.ЗанимаемыхСтавок = 0
	|			ИЛИ Работники.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СписокПределов.Предел) КАК Предел
	|ПОМЕСТИТЬ ПределЕСВ
	|ИЗ
	|	(ВЫБРАТЬ
	|		Рег.Предел КАК Предел
	|	ИЗ
	|		РегистрСведений.СведенияОВзносахВФонды.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВРаботники)) КАК Рег
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < ДАТАВРЕМЯ(2015, 9, 1)
	|				ТОГДА 20706
	|			КОГДА НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < ДАТАВРЕМЯ(2015, 12, 1)
	|				ТОГДА 23426
	|			КОГДА НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < ДАТАВРЕМЯ(2016, 5, 1)
	|				ТОГДА 34450
	|			КОГДА НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < ДАТАВРЕМЯ(2016, 12, 1)
	|				ТОГДА 36250
	|			ИНАЧЕ 40000
	|		КОНЕЦ) КАК СписокПределов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Рег.Сотрудник,
	|	Рег.Период,
	|	Рег.ПериодЗавершения
	|ПОМЕСТИТЬ Беременные
	|ИЗ
	|	РегистрСведений.СостояниеРаботниковОрганизаций.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|			Активность
	|				И (Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускПоУходуЗаРебенком)
	|					ИЛИ Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускПоБеременностиИРодам))
	|				И СостояниеЗавершения = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает)) КАК Рег
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Оклады.Сотрудник,
	|	ВЫБОР
	|		КОГДА Уволенные.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ВЫРАЗИТЬ(Оклады.Показатель1 * Курсы.Курс / Курсы.Кратность КАК ЧИСЛО(12, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Оклад,
	|	ВЫБОР
	|		КОГДА Уволенные.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ВЫРАЗИТЬ(Оклады.Показатель1 * Курсы.Курс / Курсы.Кратность / ВЫБОР
	|						КОГДА КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ) < ДАТАВРЕМЯ(2015, 12, 1)
	|							ТОГДА 29.58
	|						ИНАЧЕ 29.5
	|					КОНЕЦ КАК ЧИСЛО(12, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОкладЗаДень
	|ПОМЕСТИТЬ Оклады1
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|			Активность
	|				И Актуальность
	|				И ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОкладПоДням)) КАК Оклады
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляЗакрытия.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ) КАК Курсы
	|		ПО Оклады.Валюта1 = Курсы.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Уволенные КАК Уволенные
	|		ПО Оклады.Организация = Уволенные.Организация
	|			И Оклады.Сотрудник = Уволенные.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(Рег.Сотрудник, Оклады1.Сотрудник) КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА Рег.ПроцентЕСВ ЕСТЬ NULL 
	|			ТОГДА 22
	|		ИНАЧЕ Рег.ПроцентЕСВ
	|	КОНЕЦ КАК ПроцентЕСВ,
	|	ВЫБОР
	|		КОГДА Рег.СуммаГрн ЕСТЬ NULL 
	|				ИЛИ Рег.СуммаГрн = 0
	|			ТОГДА Оклады1.ОкладЗаДень
	|		ИНАЧЕ Рег.СуммаГрн
	|	КОНЕЦ КАК ОкладЗаДень,
	|	ВЫБОР
	|		КОГДА Рег.СуммаГрн ЕСТЬ NULL 
	|				ИЛИ Рег.СуммаГрн = 0
	|			ТОГДА Оклады1.Оклад
	|		ИНАЧЕ Рег.СуммаГрн * ВЫБОР
	|				КОГДА КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ) < ДАТАВРЕМЯ(2015, 12, 1)
	|					ТОГДА 29.58
	|				ИНАЧЕ 29.5
	|			КОНЕЦ
	|	КОНЕЦ КАК Оклад
	|ПОМЕСТИТЬ Оклады
	|ИЗ
	|	РегистрСведений.ггуСредняяЗарплата.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ) КАК Рег
	|		ПОЛНОЕ СОЕДИНЕНИЕ Оклады1 КАК Оклады1
	|		ПО Рег.Сотрудник = Оклады1.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Рег.Сотрудник,
	|	Рег.ПодразделениеОрганизации КАК Подразделение,
	|	Рег.ПодразделениеОрганизации.Родитель КАК ПодразделениеРодитель
	|ПОМЕСТИТЬ Подразделения
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ) КАК Рег
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК Признак,
	|	Рег.Организация,
	|	Рег.Сотрудник,
	|	Рег.Период КАК ДатаПриема,
	|	Рег.Период КАК Период
	|ПОМЕСТИТЬ Периоды
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК Рег
	|ГДЕ
	|	Рег.Активность
	|	И Рег.Период >= НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
	|	И Рег.Период <= КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|	И (Рег.Сотрудник.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ Рег.Сотрудник.ДатаУвольнения > КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ))
	|	И Рег.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.ПриемНаРаботу)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	Рег.Организация,
	|	Рег.Сотрудник,
	|	Рег.Период,
	|	ВЫБОР
	|		КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ, ДЕНЬ(Рег.Период) - 2) > КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|			ТОГДА НАЧАЛОПЕРИОДА(НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ)
	|		КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ, ДЕНЬ(Рег.Период) - 2) < НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
	|			ТОГДА НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ, ДЕНЬ(Рег.Период) - 2)
	|	КОНЕЦ
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК Рег
	|ГДЕ
	|	Рег.Активность
	|	И Рег.Период < НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
	|	И Рег.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.ПриемНаРаботу)
	|	И (Рег.Сотрудник.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ Рег.Сотрудник.ДатаУвольнения > КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Рег.Организация,
	|	Рег.Сотрудник,
	|	Рег.ВидОтпуска,
	|	Рег.КвоДнейНачальныйОстаток,
	|	Рег.КвоДнейПриход,
	|	Рег.КвоДнейРасход,
	|	Рег.КвоДнейКонечныйОстаток
	|ПОМЕСТИТЬ ОстатокДнейПоРегистру
	|ИЗ
	|	РегистрНакопления.СернаДниОтпуска.ОстаткиИОбороты(
	|			НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ),
	|			КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|			,
	|			,
	|			ВидОтпуска В
	|				(ВЫБРАТЬ
	|					ВидыОтпусков.ВидОтпуска
	|				ИЗ
	|					ВидыОтпусков КАК ВидыОтпусков)) КАК Рег
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Работники.Организация КАК Организация,
	|	Работники.Сотрудник КАК Сотрудник,
	|	ВидыОтпусков.ВидОтпуска
	|ПОМЕСТИТЬ ВидыОтпусковПоСотрудникам
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ) КАК Работники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Уволенные КАК Уволенные
	|		ПО Работники.Организация = Уволенные.Организация
	|			И Работники.Сотрудник = Уволенные.Сотрудник,
	|	ВидыОтпусков КАК ВидыОтпусков
	|ГДЕ
	|	Работники.ЗанимаемыхСтавок > 0
	|	И Уволенные.Сотрудник ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыОтпусковПоСотрудникам.Организация,
	|	ВидыОтпусковПоСотрудникам.Сотрудник,
	|	ВидыОтпусковПоСотрудникам.ВидОтпуска,
	|	ЕСТЬNULL(Остаток.КвоДнейНачальныйОстаток, 0) КАК КвоДнейНачальныйОстаток,
	|	ЕСТЬNULL(Остаток.КвоДнейПриход, 0) КАК КвоДнейПриход,
	|	ЕСТЬNULL(Остаток.КвоДнейРасход, 0) КАК КвоДнейРасход,
	|	ЕСТЬNULL(Остаток.КвоДнейНачальныйОстаток, 0) + ЕСТЬNULL(Остаток.КвоДнейПриход, 0) - ЕСТЬNULL(Остаток.КвоДнейРасход, 0) КАК КвоДнейОстаток,
	|	ЕСТЬNULL(НормыОтпусковПоСотрудникам.КоличествоДнейОтпускаВГод, ЕСТЬNULL(НормыОтпусковПоВсем.КоличествоДнейОтпускаВГод, 0)) КАК КоличествоДнейОтпускаВГод
	|ПОМЕСТИТЬ ОстатокДней
	|ИЗ
	|	ВидыОтпусковПоСотрудникам КАК ВидыОтпусковПоСотрудникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстатокДнейПоРегистру КАК Остаток
	|		ПО ВидыОтпусковПоСотрудникам.Организация = Остаток.Организация
	|			И ВидыОтпусковПоСотрудникам.Сотрудник = Остаток.Сотрудник
	|			И ВидыОтпусковПоСотрудникам.ВидОтпуска = Остаток.ВидОтпуска
	|		ЛЕВОЕ СОЕДИНЕНИЕ НормыОтпусковПоСотрудникам КАК НормыОтпусковПоСотрудникам
	|		ПО ВидыОтпусковПоСотрудникам.Сотрудник = НормыОтпусковПоСотрудникам.Сотрудник
	|			И ВидыОтпусковПоСотрудникам.ВидОтпуска = НормыОтпусковПоСотрудникам.ВидОтпуска
	|		ЛЕВОЕ СОЕДИНЕНИЕ НормыОтпусковПоВсем КАК НормыОтпусковПоВсем
	|		ПО ВидыОтпусковПоСотрудникам.ВидОтпуска = НормыОтпусковПоВсем.ВидОтпуска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Остаток.Организация,
	|	Остаток.Сотрудник,
	|	Остаток.ВидОтпуска,
	|	Периоды.ДатаПриема КАК ДатаПриема,
	|	Периоды.Период КАК Период,
	|	ЕСТЬNULL(Подразделения.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(Подразделения.ПодразделениеРодитель, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеРодитель,
	|	Остаток.КвоДнейНачальныйОстаток КАК КвоДнейНачальныйОстаток,
	|	Остаток.КвоДнейПриход КАК КвоДнейНачислено,
	|	Остаток.КвоДнейРасход КАК КвоДнейИспользовано,
	|	ВЫБОР
	|		КОГДА Беременные.Период ЕСТЬ NULL 
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) > Беременные.ПериодЗавершения
	|				ИЛИ КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ) < Беременные.Период
	|			ТОГДА ВЫРАЗИТЬ(Остаток.КоличествоДнейОтпускаВГод * ВЫБОР
	|						КОГДА Остаток.ВидОтпуска.УчетПоКалендарнымГодам
	|							ТОГДА РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ), НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ), ДЕНЬ)
	|						ИНАЧЕ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ЕСТЬNULL(Периоды.Период, ДАТАВРЕМЯ(1, 1, 1)), ДЕНЬ), НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ), ДЕНЬ)
	|					КОНЕЦ / (РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ), НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ), ДЕНЬ) + 1) / 12 КАК ЧИСЛО(12, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КвоДнейДоначислено,
	|	Остаток.КвоДнейОстаток КАК КвоДнейОстаток,
	|	ВЫБОР
	|		КОГДА Оклады.ПроцентЕСВ ЕСТЬ NULL 
	|				ИЛИ Оклады.ПроцентЕСВ = 0
	|			ТОГДА ВЫБОР
	|					КОГДА КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ) < ДАТАВРЕМЯ(2015, 12, 1)
	|						ТОГДА 0.3726
	|					ИНАЧЕ 0.22
	|				КОНЕЦ
	|		ИНАЧЕ Оклады.ПроцентЕСВ / 100
	|	КОНЕЦ КАК ПроцентЕСВ,
	|	ЕСТЬNULL(Оклады.Оклад, 0) КАК Оклад,
	|	ЕСТЬNULL(Оклады.ОкладЗаДень, 0) КАК ОкладЗаДень
	|ПОМЕСТИТЬ Врем
	|ИЗ
	|	ОстатокДней КАК Остаток
	|		ЛЕВОЕ СОЕДИНЕНИЕ Периоды КАК Периоды
	|		ПО Остаток.Организация = Периоды.Организация
	|			И Остаток.Сотрудник = Периоды.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Подразделения КАК Подразделения
	|		ПО Остаток.Сотрудник = Подразделения.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Беременные КАК Беременные
	|		ПО Остаток.Сотрудник = Беременные.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Оклады КАК Оклады
	|		ПО Остаток.Сотрудник = Оклады.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Врем.Сотрудник,
	|	Врем.ДатаПриема,
	|	Врем.Период КАК Период,
	|	Врем.Сотрудник.КодГленкор КАК КодГленкор,
	|	Врем.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Врем.ПодразделениеРодитель = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|				ИЛИ Врем.ПодразделениеРодитель = &Серна
	|			ТОГДА Врем.Подразделение
	|		ИНАЧЕ Врем.ПодразделениеРодитель
	|	КОНЕЦ КАК Подразделение2,
	|	Врем.КвоДнейНачальныйОстаток КАК КвоДнейНачальныйОстаток,
	|	Врем.КвоДнейНачислено КАК КвоДнейНачислено,
	|	Врем.КвоДнейИспользовано КАК КвоДнейИспользовано,
	|	Врем.КвоДнейДоначислено КАК КвоДнейДоначислено,
	|	Врем.КвоДнейОстаток + Врем.КвоДнейДоначислено КАК КвоДнейОстаток,
	|	Врем.Оклад КАК Оклад,
	|	Врем.ОкладЗаДень,
	|	Врем.ПроцентЕСВ,
	|	ВЫРАЗИТЬ(Врем.ОкладЗаДень * (Врем.КвоДнейОстаток + Врем.КвоДнейДоначислено) КАК ЧИСЛО(12, 2)) КАК СуммаГрн,
	|	ВЫРАЗИТЬ(Врем.ОкладЗаДень * (Врем.КвоДнейОстаток + Врем.КвоДнейДоначислено) * Врем.ПроцентЕСВ КАК ЧИСЛО(12, 2)) КАК СуммаЕСВГрн
	|ПОМЕСТИТЬ Врем2
	|ИЗ
	|	Врем КАК Врем
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Врем.Сотрудник,
	|	Врем.ДатаПриема,
	|	Врем.Период КАК Период,
	|	Врем.Подразделение КАК Подразделение,
	|	Врем.Подразделение2 КАК Подразделение2,
	|	Врем.Оклад,
	|	Врем.ОкладЗаДень,
	|	Врем.ПроцентЕСВ,
	|	СУММА(Врем.КвоДнейНачальныйОстаток) КАК КвоДнейНачальныйОстаток,
	|	СУММА(Врем.КвоДнейНачислено) КАК КвоДнейНачислено,
	|	СУММА(Врем.КвоДнейИспользовано) КАК КвоДнейИспользовано,
	|	СУММА(Врем.КвоДнейДоначислено) КАК КвоДнейДоначислено,
	|	СУММА(Врем.КвоДнейОстаток) КАК КвоДнейОстаток,
	|	СУММА(Врем.СуммаГрн) КАК СуммаГрн,
	|	СУММА(Врем.СуммаЕСВГрн) КАК СуммаЕСВГрн
	|ПОМЕСТИТЬ Врем3
	|ИЗ
	|	Врем2 КАК Врем
	|
	|СГРУППИРОВАТЬ ПО
	|	Врем.Сотрудник,
	|	Врем.ДатаПриема,
	|	Врем.Период,
	|	Врем.Подразделение,
	|	Врем.Подразделение2,
	|	Врем.Оклад,
	|	Врем.ОкладЗаДень,
	|	Врем.ПроцентЕСВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.РезервыОтпусков) КАК ВидРасчета,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.РезервыОтпусков) КАК СтатьяЗатрат,
	|	Врем.Сотрудник,
	|	Врем.ДатаПриема,
	|	Врем.Период КАК Период,
	|	Врем.Сотрудник.КодГленкор КАК КодГленкор,
	|	Врем.Подразделение КАК Подразделение,
	|	Врем.Подразделение2 КАК Подразделение2,
	|	Врем.КвоДнейНачальныйОстаток КАК КвоДнейНачальныйОстаток,
	|	Врем.КвоДнейНачислено КАК КвоДнейНачислено,
	|	Врем.КвоДнейИспользовано КАК КвоДнейИспользовано,
	|	Врем.КвоДнейДоначислено КАК КвоДнейДоначислено,
	|	Врем.КвоДнейОстаток КАК КвоДнейОстаток,
	|	Врем.Оклад КАК Оклад,
	|	ВЫРАЗИТЬ(Врем.Оклад / Курсы.КурсДоллара КАК ЧИСЛО(12, 2)) КАК ОкладДол,
	|	ВЫРАЗИТЬ(Врем.ОкладЗаДень КАК ЧИСЛО(12, 2)) КАК СуммаГрнЗаДень,
	|	ВЫРАЗИТЬ(Врем.ОкладЗаДень / Курсы.КурсДоллара КАК ЧИСЛО(12, 2)) КАК СуммаДолЗаДень,
	|	Врем.СуммаГрн КАК СуммаГрн,
	|	ВЫРАЗИТЬ(Врем.СуммаГрн / Курсы.КурсДоллара КАК ЧИСЛО(12, 2)) КАК СуммаДол,
	|	ВЫБОР
	|		КОГДА Врем.СуммаГрн > ЕСТЬNULL(ПределЕСВ.Предел, 34450)
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПределЕСВ.Предел, 34450) * Врем.ПроцентЕСВ КАК ЧИСЛО(12, 2))
	|		ИНАЧЕ Врем.СуммаЕСВГрн
	|	КОНЕЦ КАК СуммаЕСВГрн,
	|	ВЫБОР
	|		КОГДА Врем.СуммаГрн > ЕСТЬNULL(ПределЕСВ.Предел, 34450)
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ПределЕСВ.Предел, 34450) * Врем.ПроцентЕСВ / Курсы.КурсДоллара КАК ЧИСЛО(12, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(Врем.СуммаЕСВГрн / Курсы.КурсДоллара КАК ЧИСЛО(12, 2))
	|	КОНЕЦ КАК СуммаЕСВДол,
	|	Курсы.КурсДоллара
	|ПОМЕСТИТЬ Врем4
	|ИЗ
	|	Врем3 КАК Врем,
	|	ПределЕСВ КАК ПределЕСВ,
	|	(ВЫБРАТЬ
	|		Курсы.Курс / Курсы.Кратность КАК КурсДоллара
	|	ИЗ
	|		РегистрСведений.КурсыВалютДляЗакрытия.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.Доллар)) КАК Курсы) КАК Курсы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем3.СтатьяЗатрат,
	|	Врем3.Сотрудник,
	|	Врем3.Подразделение,
	|	Врем3.КурсДоллара,
	|	СУММА(Врем3.СуммаГрн) + СУММА(Врем3.СуммаЕСВГрн) КАК СуммаГрн,
	|	СУММА(Врем3.СуммаДол) + СУММА(Врем3.СуммаЕСВДол) КАК СуммаДол
	|ПОМЕСТИТЬ Врем5
	|ИЗ
	|	Врем4 КАК Врем3
	|
	|СГРУППИРОВАТЬ ПО
	|	Врем3.СтатьяЗатрат,
	|	Врем3.Сотрудник,
	|	Врем3.Подразделение,
	|	Врем3.КурсДоллара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АдминистративныеРасходы) КАК СчетДт,
	|	Врем4.Подразделение КАК СубконтоДт1,
	|	Врем4.СтатьяЗатрат КАК СубконтоДт2,
	|	Врем4.Сотрудник.Физлицо КАК СубконтоДт3,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.Гривна) КАК ВалютаДт,
	|	Врем4.СуммаДол КАК ВалютнаяСуммаДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность) КАК НалоговоеНазначениеДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбеспечениеВыплатОтпусков) КАК СчетКт,
	|	Врем4.СтатьяЗатрат КАК СубконтоКт1,
	|	Врем4.Подразделение КАК СубконтоКт2,
	|	Врем4.Сотрудник.Физлицо КАК СубконтоКт3,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.Гривна) КАК ВалютаКт,
	|	Врем4.СуммаДол КАК ВалютнаяСуммаКт,
	|	Врем4.КурсДоллара,
	|	Врем4.СуммаГрн КАК Сумма,
	|	0 КАК СуммаНУ
	|ИЗ
	|	Врем5 КАК Врем4
	|";
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	
	Возврат лтзРез;
КонецФункции // СформироватьПроводкиРезервыОтпусков

// НайтиБухСправку
//
Функция НайтиБухСправку( пДата, пВидРасчета )
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Дата", пДата);
	лЗапрос.УстановитьПараметр("ВидРасчета", пВидРасчета);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка
	|ИЗ
	|	Документ.БухгалтерскаяСправка КАК Док
	|ГДЕ
	|	Док.ВидРасчета = &ВидРасчета
	|	И Док.Дата >= НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
	|	И Док.Дата <= КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|
	|УПОРЯДОЧИТЬ ПО Ссылка АВТОУПОРЯДОЧИВАНИЕ
	|";
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	Если лтзРез.Количество()=0 Тогда
		Возврат Документы.БухгалтерскаяСправка.ПустаяСсылка();
	Иначе
		Возврат лтзРез[0].Ссылка;
	КонецЕсли;
КонецФункции // НайтиБухСправку

Процедура СформироватьБухСправки(Кнопка)
	Если Не ЗначениеЗаполнено(Дата) Тогда Сообщить("Не заполнено поле 'Дата'"); Возврат; КонецЕсли;
	
	// Пройдем в цикле по строкам из ТЧ
	Для каждого лСтрокаТЧ Из БухСправки Цикл
		Если Не лСтрокаТЧ.Формировать Тогда Продолжить; КонецЕсли;
		
		лНужныйВидРасчета = лСтрокаТЧ.ВидРасчета;
		лКомментарий = "Автозаполнение : Начисление  - " + лНужныйВидРасчета.Наименование;
		
		Если Не ЗначениеЗаполнено(лСтрокаТЧ.БухСправка) Тогда
			// Надо создать бух.справку
			лБухСправка = Документы.БухгалтерскаяСправка.СоздатьДокумент();
			лБухСправка.Дата = КонецДня(Дата) - 600;
			лБухСправка.Организация = глЗначениеПеременной("ОсновнаяОрганизация");
			лБухСправка.Ответственный = ОбщегоНазначения.ПолучитьЗначениеПеременной("глТекущийПользователь");
			лБухСправка.Редактировавший = ОбщегоНазначения.ПолучитьЗначениеПеременной("глТекущийПользователь");
			лБухСправка.Комментарий = лКомментарий;
			лБухСправка.ВидРасчета = лНужныйВидРасчета;
			лБухСправка.СторнироватьАвтоматически = ?(лНужныйВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.МедицинскаяСтраховка, Ложь, Истина);
			лБухСправка.ДатаСторнирования = НачалоДня(КонецМесяца(КонецМесяца(Дата) + 2)) + 2;
			лБухСправка.Записать();
			лСтрокаТЧ.БухСправка = лБухСправка.Ссылка;
		ИначеЕсли лСтрокаТЧ.БухСправка.НеПереформировывать Тогда
			Сообщить("Бух.справку нельзя переформировывать - " + лСтрокаТЧ.БухСправка + " !"); 
			Продолжить;
		КонецЕсли;
		
		Если лНужныйВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.РезервыОтпусков Тогда
			лтзПроводки = СформироватьПроводкиРезервыОтпусков( лСтрокаТЧ.БухСправка.Дата );
		Иначе
			лтзПроводки = СформироватьПроводкиБонусы( лСтрокаТЧ.БухСправка.Дата, лНужныйВидРасчета );
		КонецЕсли;
		лГривна = ОбщегоНазначения.ПолучитьЗначениеПеременной("ВалютаРегламентированногоУчета");
		Для каждого лСтрока Из лтзПроводки Цикл
			Если Не лСтрока.ВалютаДт = лГривна Тогда лСтрока.ВалютаДт = лГривна; КонецЕсли;
			Если Не лСтрока.ВалютаКт = лГривна Тогда лСтрока.ВалютаКт = лГривна; КонецЕсли;
		КонецЦикла;
		Если лтзПроводки.Количество() > 0 Тогда
			лБухСправка = лСтрокаТЧ.БухСправка.Ссылка.ПолучитьОбъект();
			лБухСправка.Проводки.Очистить();
			лБухСправка.Проводки.Загрузить( лтзПроводки );
			лБухСправка.Курс = лтзПроводки[0].КурсДоллара;
			лБухСправка.Записать( ?(лБухСправка.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись) );
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОткрытии()
	Если Не ЗначениеЗаполнено(Дата) Тогда Дата = КонецМесяца( ТекущаяДата() ) - 600; КонецЕсли;
	
	Для ном = (БухСправки.Количество() + 1) По 5 Цикл БухСправки.Добавить(); КонецЦикла;
	
	ЗаполнитьТЧ_БухСправки();
КонецПроцедуры

Процедура ДатаПриИзменении(Элемент)
	ЗаполнитьТЧ_БухСправки();
КонецПроцедуры
