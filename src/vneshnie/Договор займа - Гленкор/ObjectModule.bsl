
// Печать
//
Функция Печать() Экспорт
	
	// Для отладки
	лТест = Серна.ЕстьРеквизитДокумента("Организация", СсылкаНаОбъект.Метаданные());
	
	Объект = СсылкаНаОбъект;
	Организация = СсылкаНаОбъект.Организация;
	Дата = СсылкаНаОбъект.Дата;
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("Договор");
	
	// печать производится на языке, указанном в настройках пользователя
	КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм(УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "РежимФормированияПечатныхФорм"));
	Макет.КодЯзыкаМакета = КодЯзыкаПечать;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Гривна",Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("Ссылка",СсылкаНаОбъект.Ссылка);
	Запрос.УстановитьПараметр("ФизЛицо",СсылкаНаОбъект.ФизЛицо);
	Запрос.УстановитьПараметр("Руководитель", Перечисления.ОтветственныеЛицаОрганизаций.ФинДиректор);
	Запрос.УстановитьПараметр("Женский",Перечисления.ПолФизическихЛиц.Женский);
	Запрос.УстановитьПараметр("Дата",СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("Адрес",Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("АдресЮридический",Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорЗаймаСРаботником.Дата,
	|	ДоговорЗаймаСРаботником.Номер,
	|	ДоговорЗаймаСРаботником.Организация.НаименованиеПолное КАК НазваниеОрганизации,
	|	ДоговорЗаймаСРаботником.ФизЛицо,
	|	ДоговорЗаймаСРаботником.ФизЛицо.КодПоДРФО КАК КодПоДРФО,
	|	ДоговорЗаймаСРаботником.ПроцентЗаПользованиеЗаймом,
	|	ВЫБОР
	|		КОГДА ДоговорЗаймаСРаботником.ВалютаДокумента = &Гривна
	|			ТОГДА ""грн.""
	|		ИНАЧЕ ДоговорЗаймаСРаботником.ВалютаДокумента.Наименование
	|	КОНЕЦ КАК НаименованиеВалюты,
	|	ДоговорЗаймаСРаботником.СуммаЗайма,
	|	ДоговорЗаймаСРаботником.Комментарий,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ДоговорЗаймаСРаботником.ФизЛицо.Наименование) КАК Работник,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид.Наименование КАК ДокументВид,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан,
	|	КонтактнаяИнформация.Представление КАК АдресОрганизации,
	|	ВЫБОР
	|		КОГДА ДоговорЗаймаСРаботником.ФизЛицо.Пол = &Женский
	|			ТОГДА ""ая""
	|		ИНАЧЕ ""ый""
	|	КОНЕЦ КАК Окончание,
	|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьРуководителя,
	|	ЕСТЬNULL(ФИОФизЛиц.Фамилия + ВЫБОР
	|			КОГДА ПОДСТРОКА(ФИОФизЛиц.Имя, 1, 1) <> """"
	|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛиц.Имя, 1, 1) + "".""
	|			ИНАЧЕ """"
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ПОДСТРОКА(ФИОФизЛиц.Отчество, 1, 1) <> """"
	|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛиц.Отчество, 1, 1) + "".""
	|			ИНАЧЕ """"
	|		КОНЕЦ, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя
	|ИЗ
	|	Документ.ДоговорЗаймаСРаботником КАК ДоговорЗаймаСРаботником
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&Дата, ФизЛицо = &ФизЛицо) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО ДоговорЗаймаСРаботником.Организация = КонтактнаяИнформация.Объект
	|			И (КонтактнаяИнформация.Тип = &Адрес)
	|			И (КонтактнаяИнформация.Вид = &АдресЮридический)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&Дата, ОтветственноеЛицо = &Руководитель) КАК ОтветственныеЛицаОрганизацийСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ) КАК ФИОФизЛиц
	|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛиц.ФизЛицо
	|		ПО ДоговорЗаймаСРаботником.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
	|ГДЕ
	|	ДоговорЗаймаСРаботником.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Макет.Параметры.Заполнить(Выборка);
	Макет.Параметры.Дата = Формат(Дата,"Л="+КодЯзыкаПечать+";ДЛФ=DD");
	Макет.Параметры.ОкончаниеПогашения = Формат(ДобавитьМесяц(СсылкаНаОбъект.НачалоПогашения, СсылкаНаОбъект.СрокПогашения),"Л="+КодЯзыкаПечать+";ДЛФ=DD");
	Макет.Параметры.ДатаДоговора =  Формат(Дата,"Л="+КодЯзыкаПечать+";ДЛФ=DD");
	// КодЕДРПОУ
	лОтбор = Новый Структура("Организация", СсылкаНаОбъект.Организация);
	лРег = РегистрыСведений.КодыОрганизации.СрезПоследних(СсылкаНаОбъект.Дата, лОтбор);
	Макет.Параметры.КодЕДРПОУОрганизации =  лРег[0].КодПоЕДРПОУ;
	// НомерКарточкиФизЛица
	лОтбор = Новый Структура("Сотрудник, Организация, Банк", СсылкаНаОбъект.Сотрудник, СсылкаНаОбъект.Организация, Справочники.Контрагенты.НайтиПоНаименованию("УкрСибБанк"));
	лРег = РегистрыСведений.ПараметрыВыплатыЗПРаботников.СрезПоследних(СсылкаНаОбъект.Дата, лОтбор);
	Макет.Параметры.НомерКарточкиФизЛица = СокрЛП(лРег[0].НомерКарточки);
	
	Если  СсылкаНаОбъект.ПорядокПогашенияЗайма = Перечисления.ПорядокПогашенияЗаймаПроцентов.Ежемесячно Тогда
		Макет.Параметры.ТекстПорядка = НСтр("ru='ежемесячно';uk='щомісячно'",КодЯзыкаПечать);
	Иначе
		Макет.Параметры.ТекстПорядка = НСтр("ru='в конце срока';uk='у кінці строку'",КодЯзыкаПечать);
	КонецЕсли;	
	ТабДокумент.Вывести(Макет);
	
	Возврат ТабДокумент;
	
КонецФункции

// СведенияОВнешнейОбработке
//
Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	МассивНазначений.Добавить("Документ.ДоговорЗаймаСРаботником");
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	//возможны варианты - ЗаполнениеОбъекта, ДополнительныйОтчет, СозданиеСвязанныхОбъектов,
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "Договор займа с работником (Гленкор)"); //имя под kt обработка зарегистрирована будет в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", ЛОЖЬ);
	ПараметрыРегистрации.Вставить("Информация", "Договор займа с работником (Гленкор)");
	//команды
	ТаблицаКоманд = Новый ТаблицаЗначений;
	ТаблицаКоманд.Колонки.Добавить("Представление"); //как будет выглядеть описание печ.формы для пользователя
	ТаблицаКоманд.Колонки.Добавить("Идентификатор"); //имя нашего макета
	ТаблицаКоманд.Колонки.Добавить("Использование"); //ВызовСерверногоМетода
	ТаблицаКоманд.Колонки.Добавить("ПоказыватьОповещение"); //Истина
	ТаблицаКоманд.Колонки.Добавить("Модификатор"); //ПечатьМХL
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = "Договор займа с работником (Гленкор)";
	НоваяКоманда.Идентификатор = "ДоговорЗаймаГленкор"; //Внешняя печатная форма
	НоваяКоманда.Использование = "ВызовКлиентскогоМетода"; //здесь можно прописать использование как серверного так и клиентского, отличие в том, что серверный метод будет обращаться к экспортной процедуре из модуля объекта, клиентский - к экспортной процедуре из модуля формы объекта
	НоваяКоманда.ПоказыватьОповещение = Истина;
	НоваяКоманда.Модификатор = "ПечатьMXL";
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	Возврат ПараметрыРегистрации;
КонецФункции
