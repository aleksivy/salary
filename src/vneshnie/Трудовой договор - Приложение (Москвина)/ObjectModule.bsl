
// Печать
//
Функция Печать() Экспорт
	
	// Для отладки
	лТест = Серна.ЕстьРеквизитДокумента("Организация", СсылкаНаОбъект.Метаданные());
	
	Объект = СсылкаНаОбъект;
	Организация = СсылкаНаОбъект.Организация;
	Дата = СсылкаНаОбъект.Дата;
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТрудовойДоговор_Гленкор";
	
	// запоминаем области макета
	Макет = ПолучитьМакет("Приложение");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
	
	Для каждого лСтрока Из Объект.ОсновныеНачисления Цикл
		Если Не лСтрока.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ОкладПоДням Тогда Продолжить; КонецЕсли;
		Если Не лСтрока.Действие = Перечисления.ВидыДействияСНачислением.Начать и Не лСтрока.Действие = Перечисления.ВидыДействияСНачислением.Изменить Тогда Продолжить; КонецЕсли;
		
		// Каждое приложение на отдельной странице
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Определим приказ о приеме на работу данного сотрудниками
		лЗапрос = Новый Запрос("ВЫБРАТЬ МАКСИМУМ(ДокТЧ.Ссылка) КАК Ссылка ИЗ Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ДокТЧ ГДЕ ДокТЧ.Ссылка.Проведен и ДокТЧ.Сотрудник = &Сотрудник");
		лЗапрос.УстановитьПараметр("Сотрудник", лСтрока.Сотрудник);
		лВыборка = лЗапрос.Выполнить().Выбрать();
		Если Не лВыборка.Следующий() Тогда
			Сообщить("Не найден приказ о приеме на работу для сотрудника - " + лСтрока.Сотрудник);
			Продолжить;
		Иначе
			Если лВыборка.Ссылка = Null Тогда
				Сообщить("Не найден приказ о приеме на работу для сотрудника - " + лСтрока.Сотрудник);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		лПриказ = лВыборка.Ссылка;
		
		// Нужные параметры : 
		// Шапка : ТабельныйНомер, ДатаДоговора, ДатаДоговораАнгл, ФИО, ФИОАнгл, Должность, ДолжностьАнгл, ДатаПриема, ДатаПриемаАнгл, 
		лстрПараметрыШапка = Новый Структура;
		лТабельныйНомер = СокрЛП(лСтрока.Сотрудник.Код);
		Пока (Лев(лТабельныйНомер, 1) = "0") Цикл
			лТабельныйНомер = Прав(лТабельныйНомер, СтрДлина(лТабельныйНомер)-1);
		КонецЦикла;
		лстрПараметрыШапка.Вставить("ТабельныйНомер", лТабельныйНомер);
		лФИО = СокрЛП(лСтрока.ФизЛицо.Наименование);
		лФИОАнгл = СокрЛП(СокрЛП(лСтрока.Сотрудник.NameFamily) + " " + СокрЛП(лСтрока.Сотрудник.Name) + " " + СокрЛП(лСтрока.Сотрудник.NameSecond));
		лстрПараметрыШапка.Вставить("ФИО", лФИО);
		лстрПараметрыШапка.Вставить("ФИОАнгл", лФИОАнгл);
		лстрПараметрыШапка.Вставить("ДатаДоговора", Формат(лПриказ.Дата, "ДЛФ=ДД"));
		лстрПараметрыШапка.Вставить("ДатаДоговораАнгл", Формат(лПриказ.Дата, "Л=en; ДЛФ=ДД"));
		лстрПараметрыШапка.Вставить("ДатаПриложения", Формат(лСтрока.ДатаДействия, "ДЛФ=ДД"));
		лстрПараметрыШапка.Вставить("ДатаПриложенияАнгл", Формат(лСтрока.ДатаДействия, "Л=en; ДЛФ=ДД"));
		
		лОклад = лСтрока.Показатель1;
		лОкладГривны = Цел(лОклад);
		лОкладКопейки = лОклад - Цел(лОклад);
		лстрПараметрыШапка.Вставить("СуммаЧислом", "" + Формат(лОклад, "ЧДЦ=2"));
		лстрПараметрыШапка.Вставить("СуммаПрописью", ОбщегоНазначения.СформироватьСуммуПрописью(лОклад, Справочники.Валюты.Гривна, "uk") );
		лстрПараметрыШапка.Вставить("ЧислоКопеек", ?(лОкладКопейки = 0, "00", Прав("00"+лОкладКопейки,2)));
		Если лОкладКопейки = 0 Тогда
			лстрПараметрыШапка.Вставить("СуммаПрописьюАнгл", ЧислоПрописью( лОклад, "Л=en_US",",,,,0"));
		Иначе
			лстрПараметрыШапка.Вставить("СуммаПрописьюАнгл", ЧислоПрописью( лОклад, "Л=en_US",",,,,2"));
		КонецЕсли;
		
		
		
		ОбластьШапка.Параметры.Заполнить( лстрПараметрыШапка );
		Для каждого лПараметр Из лстрПараметрыШапка Цикл
			Если Не ЗначениеЗаполнено(лПараметр.Значение) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указан параметр - '" + лПараметр.Ключ + "' для сотрудника - " + лСтрока.Сотрудник);
			КонецЕсли;
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьШапка);
		
		// Нужные параметры : 
		// Подписи : ФИО, ФИОАнгл, ПаспортСерия, ПаспортНомер, ПаспортВыдан, ИНН, Адрес, АдресАнгл
		лстрПараметрыПодписи = Новый Структура;
		лстрПараметрыПодписи.Вставить("ФИО", лФИО);
		лстрПараметрыПодписи.Вставить("ФИОАнгл", лФИОАнгл);
		лДанныеПаспорта = РегистрыСведений.ПаспортныеДанныеФизЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизЛицо", лСтрока.ФизЛицо));
		лстрПараметрыПодписи.Вставить("ПаспортСерия", СокрЛП(лДанныеПаспорта.ДокументСерия));
		лстрПараметрыПодписи.Вставить("ПаспортНомер", СокрЛП(лДанныеПаспорта.ДокументНомер));
		лстрПараметрыПодписи.Вставить("ПаспортВыдан", СокрЛП(лДанныеПаспорта.ДокументКемВыдан) + " " + Формат(лДанныеПаспорта.ДокументДатаВыдачи, "ДЛФ=ДД"));
		лстрПараметрыПодписи.Вставить("ИНН", СокрЛП(лСтрока.ФизЛицо.КодПоДРФО));
		лЗапрос = Новый Запрос("ВЫБРАТЬ Рег.Представление ИЗ РегистрСведений.КонтактнаяИнформация КАК Рег ГДЕ Рег.Объект = &ФизЛицо И Рег.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес) И Рег.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица)");
		лЗапрос.УстановитьПараметр("ФизЛицо", лСтрока.ФизЛицо);
		лВыборка = лЗапрос.Выполнить().Выбрать();
		Если лВыборка.Следующий() Тогда
			лстрПараметрыПодписи.Вставить("Адрес", СокрЛП(лВыборка.Представление));
		Иначе
			лстрПараметрыПодписи.Вставить("Адрес", "");
		КонецЕсли;
		лЗапрос = Новый Запрос("ВЫБРАТЬ Рег.Представление ИЗ РегистрСведений.КонтактнаяИнформация КАК Рег ГДЕ Рег.Объект = &ФизЛицо И Рег.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес) И Рег.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ИнострАдресФизЛица)");
		лЗапрос.УстановитьПараметр("ФизЛицо", лСтрока.ФизЛицо);
		лВыборка = лЗапрос.Выполнить().Выбрать();
		Если лВыборка.Следующий() Тогда
			лстрПараметрыПодписи.Вставить("АдресАнгл", СокрЛП(лВыборка.Представление));
		Иначе
			лстрПараметрыПодписи.Вставить("АдресАнгл", "");
		КонецЕсли;
		
		ОбластьПодписи.Параметры.Заполнить( лстрПараметрыПодписи );
		Для каждого лПараметр Из лстрПараметрыПодписи Цикл
			Если Не ЗначениеЗаполнено(лПараметр.Значение) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указан параметр - '" + лПараметр.Ключ + "' для физического лица - " + лСтрока.ФизЛицо);
			КонецЕсли;
		КонецЦикла;
		ТабДокумент.Вывести(ОбластьПодписи);
		Прервать;
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

// СведенияОВнешнейОбработке
//
Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	МассивНазначений.Добавить("Документ.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций");
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	//возможны варианты - ЗаполнениеОбъекта, ДополнительныйОтчет, СозданиеСвязанныхОбъектов,
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "Приложение 2 к Трудовому договору (Гленкор)"); //имя под kt обработка зарегистрирована будет в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", ЛОЖЬ);
	ПараметрыРегистрации.Вставить("Информация", "Приложение 2 к Трудовому договору (Гленкор)");
	//команды
	ТаблицаКоманд = Новый ТаблицаЗначений;
	ТаблицаКоманд.Колонки.Добавить("Представление"); //как будет выглядеть описание печ.формы для пользователя
	ТаблицаКоманд.Колонки.Добавить("Идентификатор"); //имя нашего макета
	ТаблицаКоманд.Колонки.Добавить("Использование"); //ВызовСерверногоМетода
	ТаблицаКоманд.Колонки.Добавить("ПоказыватьОповещение"); //Истина
	ТаблицаКоманд.Колонки.Добавить("Модификатор"); //ПечатьМХL
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = "Приложение 2 к Трудовому договору (Гленкор)";
	НоваяКоманда.Идентификатор = "ТрудовойДоговорПриложение2Гленкор"; //Внешняя печатная форма
	НоваяКоманда.Использование = "ВызовКлиентскогоМетода"; //здесь можно прописать использование как серверного так и клиентского, отличие в том, что серверный метод будет обращаться к экспортной процедуре из модуля объекта, клиентский - к экспортной процедуре из модуля формы объекта
	НоваяКоманда.ПоказыватьОповещение = Истина;
	НоваяКоманда.Модификатор = "ПечатьMXL";
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	Возврат ПараметрыРегистрации;
КонецФункции
