Перем мСведенияОВидахРасчетаНачислений;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

// Процедура прописывает заголовок формы 
//
// Параметры:
//  Элемент - элемент формы, который отображает организацию
//  
Процедура ОрганизацияПриИзменении(Элемент)

	Заголовок = НСтр("ru='Основные начисления работников организации ';uk='Основні нарахування працівників організації '") + Элемент.Значение.Наименование;
    Отбор.Организация.Значение = Элемент.Значение;
    Отбор.Организация.Использование = Не Элемент.Значение.Пустая();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Если форма открывается из списка документов, процедура заполняет организацию 
// прописывает заголовок формы и выставляет отбор регистру
//
Процедура ПриОткрытии()
	
	Если ЗначениеЗаполнено(ПараметрОтборПоРегистратору) Тогда
		
		МетаданныеДокумента = ПараметрОтборПоРегистратору.Метаданные();
		
		Если МетаданныеДокумента.Реквизиты.Найти("СторнируемыйДокумент") <> Неопределено Тогда
			
			МетаданныеДокумента 		= ПараметрОтборПоРегистратору.СторнируемыйДокумент.Метаданные();
			ПараметрОтборПоРегистратору = ПараметрОтборПоРегистратору.СторнируемыйДокумент;
			
		КонецЕсли;
		
		Если МетаданныеДокумента.Реквизиты.Найти("Организация") <> Неопределено Тогда
			ГоловнаяОрганизация = ПараметрОтборПоРегистратору.Организация;
			
			Отбор.Организация.Значение = ГоловнаяОрганизация;
			Заголовок				   = НСтр("ru='Основные начисления работников организации ';uk='Основні нарахування працівників організації '") + Отбор.Организация.Значение.Наименование;
			ЭлементыФормы.Организация.ТолькоПросмотр = Истина
			
		КонецЕсли;
		
		Отбор.Организация.Использование = Истина;
		
	Иначе
		
		Отбор.Организация.Значение = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),"ОсновнаяОрганизация");
		Заголовок				   = НСтр("ru='Основные начисления работников организации ';uk='Основні нарахування працівників організації '") + Отбор.Организация.Значение.Наименование;
	    Отбор.Организация.Использование = Не Отбор.Организация.Значение.Пустая();

	КонецЕсли;
	ЭлементыФормы.РегистрРасчетаСписок.РежимВыбора = РежимВыбора;
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ПараметрОтборПоРегистратору) Тогда
		
		МетаданныеДокумента = ПараметрОтборПоРегистратору.Метаданные();
		
		Если МетаданныеДокумента.Реквизиты.Найти("Организация") <> Неопределено Тогда
			ГоловнаяОрганизация = ПараметрОтборПоРегистратору.Организация;
			
			Отбор.Организация.Значение = ГоловнаяОрганизация;
			Заголовок = НСтр("ru='Основные начисления работников организации ';uk='Основні нарахування працівників організації '") + Отбор.Организация.Значение.Наименование;
			ЭлементыФормы.Организация.ТолькоПросмотр = Истина;
			Возврат
			
		КонецЕсли;
		
		Отбор.Организация.Использование = Истина;

	Иначе
		
		Отбор.Организация.Значение = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),"ОсновнаяОрганизация");
	    Отбор.Организация.Использование = Не Отбор.Организация.Значение.Пустая();
		
	КонецЕсли;
	
	Заголовок = НСтр("ru='Основные начисления работников организации';uk='Основні нарахування працівників організації'");
	ЭлементыФормы.Организация.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

Процедура РегистрРасчетаСписокВыборЗначения(Элемент, СтандартнаяОбработка, Значение)
	// получим данные записи регистра
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	*
	                      |ИЗ
	                      |	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
	                      |
	                      |ГДЕ
	                      |	ОсновныеНачисленияРаботниковОрганизаций.Регистратор = &парамРегистратор И
	                      |	ОсновныеНачисленияРаботниковОрганизаций.НомерСтроки = &парамНомерСтроки");
	Запрос.УстановитьПараметр("парамРегистратор", Значение.Регистратор);
	Запрос.УстановитьПараметр("парамНомерСтроки", Значение.НомерСтроки);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ОповеститьОВыборе(Выборка);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события вывода строки списка регистра
Процедура РегистрРасчетаСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	// не показываем некоторые ячейки
	РаботаСДиалогами.ПриВыводеСтрокиНачисленийИУдержаний(ОформлениеСтроки.ДанныеСтроки, ОформлениеСтроки, мСведенияОВидахРасчетаНачислений);
	
КонецПроцедуры

мСведенияОВидахРасчетаНачислений = Новый Соответствие;


