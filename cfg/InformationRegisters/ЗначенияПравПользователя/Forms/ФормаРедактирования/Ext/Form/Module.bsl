
// Функция возвращает список Перечислений типа НаборПравПользователей, без Перечислений предназначенных для удаления.
// Выбирает значения Перечисления НаборПравПользователей, у которых Имя не содержит слово "УДАЛИТЬ" в начале.
// 
// Возвращаемое значение:
//  СписокДоступныхПрав - СписокЗначений
//
Функция ПолучитьНаборДоступныхПрав()
	
	СписокДоступныхПрав = Новый СписокЗначений;
	
	// Выполнить обход значений Перечисления.НаборПравПользователей. Выбрать нужные значения.
	Для Каждого ПравоПользователя Из Перечисления.НаборПравПользователей Цикл
		
		Если Врег(Лев(ПравоПользователя, 7)) <> "УДАЛИТЬ" Тогда
			СписокДоступныхПрав.Добавить(ПравоПользователя);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокДоступныхПрав;
	
КонецФункции // ПолучитьНаборДоступныхПрав()

Процедура ЗаполнитьДерево()
	ДеревоПрав.Строки.Очистить();
	Запрос = Новый Запрос("	
	|Выбрать Родитель, Ссылка, ЭтоГруппа, ЗначениеПрав.Значение Из ПланВидовХарактеристик.ПраваПользователей КАК Права
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|РегистрСведений.ЗначенияПравПользователя КАК ЗначениеПрав
	|ПО ЗначениеПрав.Право=Права.Ссылка
	|И  ЗначениеПрав.НаборПрав=&НаборПрав
	|УПОРЯДОЧИТЬ По Права.ЭтоГруппа ИЕРАРХИЯ, Права.Наименование
	|");
	Запрос.УстановитьПараметр("НаборПрав",НаборПрав);
	Запрос.Выполнить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если  Не Выборка.Родитель.Пустая() Тогда
			СтрокаГруппы = ДеревоПрав.Строки.Найти(Выборка.Родитель, "Право", Истина);
			Если СтрокаГруппы=Неопределено Тогда
				СтрокаГруппы = ДеревоПрав.Строки.Добавить();
				СтрокаГруппы.Право = Выборка.Родитель;
				СтрокаГруппы.ЭтоГруппа = Выборка.ЭтоГруппа;
			КонецЕсли;
		Иначе
			СтрокаГруппы = ДеревоПрав;
		КонецЕсли;		
		СтрокаПрава = СтрокаГруппы.Строки.Добавить();
		СтрокаПрава.Право = Выборка.Ссылка;
		СтрокаПрава.Значение = Выборка.Ссылка.ТипЗначения.ПривестиЗначение(Выборка.Значение);		
		СтрокаПрава.ЭтоГруппа = Выборка.ЭтоГруппа;
	КонецЦикла;
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЭлементыФормы.НаборПрав.СписокВыбора = ПолучитьНаборДоступныхПрав();
	
	Если НЕ ЗначениеЗаполнено(НаборПрав) Тогда
		ЭлементыФормы.НаборПрав.Значение = ЭлементыФормы.НаборПрав.СписокВыбора[0].Значение;
	КонецЕсли;
			
КонецПроцедуры

Процедура ДеревоПравПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Право.ОтображатьКартинку = Истина;
	
	Если ДанныеСтроки.ЭтоГруппа Тогда
		ОформлениеСтроки.Ячейки.Значение.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Право.ИндексКартинки=0;
	Иначе
		ОформлениеСтроки.Ячейки.Право.ИндексКартинки=1;
	КонецЕсли;
	Если ДанныеСтроки.Право.ТипЗначения.СодержитТип(Тип("Булево"))
		 И ДанныеСтроки.Право.ТипЗначения.Типы().Количество()=1 Тогда
		ОформлениеСтроки.Ячейки.Значение.ОтображатьТекст = Ложь;
		ОформлениеСтроки.Ячейки.Значение.ОтображатьФлажок = Истина;
		ОформлениеСтроки.Ячейки.Значение.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.Значение.Флажок = ДанныеСтроки.Право.ТипЗначения.ПривестиЗначение(ДанныеСтроки.Значение);
	КонецЕсли;
КонецПроцедуры

Процедура ДеревоПравПриИзмененииФлажка(Элемент, Колонка)
	СтрокаДерева = ДеревоПрав.Строки.Найти(Элемент.ТекущиеДанные.Право, "Право", Истина);
	СтрокаДерева.Значение = Не СтрокаДерева.Право.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

Процедура ОбновитьНастройки()
	Набор = РегистрыСведений.ЗначенияПравПользователя.СоздатьНаборЗаписей();
	Набор.Отбор.НаборПрав.Использование = Истина;
	Набор.Отбор.НаборПрав.Значение = НаборПрав;
	ЗаполнитьНаборЗаписей(ДеревоПрав.Строки, Набор);
	Набор.Записать();
	ЭтаФорма.Модифицированность = Ложь;
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей(СтрокиДерева, НаборЗаписей)
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если Не СтрокаДерева.ЭтоГруппа Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.НаборПрав = НаборПрав;
			Запись.Право = СтрокаДерева.Право;
			Запись.Значение = СтрокаДерева.Право.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
		Иначе
			ЗаполнитьНаборЗаписей(СтрокаДерева.Строки, НаборЗаписей);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЗаполнитьДерево();
	Если ЗначениеЗаполнено(Право) Тогда
		СтрокаДерева = ДеревоПрав.Строки.Найти(Право, "Право", Истина);
		Если СтрокаДерева <> Неопределено Тогда
			ЭлементыФормы.Дерево.ТекущаяСтрока = СтрокаДерева;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДеревоЗначениеПриИзменении(Элемент)
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ЭтаФорма.Модифицированность Тогда
		Ответ = Вопрос(НСтр("ru='Данные были изменены. Сохранить изменения?';uk='Дані були змінені. Зберегти зміни?'"), РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОбновитьНастройки();
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура НаборПравПриИзменении(Элемент)
	ЗаполнитьДерево();
КонецПроцедуры

Процедура КоманднаяПанельПрименить(Кнопка)
	ОбновитьНастройки();
КонецПроцедуры

Процедура КоманднаяПанельОК(Кнопка)
	
	ОбновитьНастройки();
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

Процедура НаборПравНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Если ЭтаФорма.Модифицированность Тогда
		Ответ = Вопрос(НСтр("ru='Данные были изменены. Сохранить изменения?';uk='Дані були змінені. Зберегти зміни?'"), РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОбновитьНастройки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


ДеревоПрав.Колонки.Добавить("ЭтоГруппа");
Право = Неопределено;
