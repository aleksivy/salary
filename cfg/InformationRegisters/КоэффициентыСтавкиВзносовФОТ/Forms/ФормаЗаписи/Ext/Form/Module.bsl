Процедура РассчитатьНажатие(Элемент)
	
	Если МесяцНачисления >=  ПроведениеРасчетов.ДатаИзмененияЕСВ1863() Тогда
		Результат = ПроведениеРасчетов.РассчитатьКоэффициентСтавки1863(Организация, МесяцНачисления, БазаСОграничением);
	Иначе
		Результат = ПроведениеРасчетов.РассчитатьКоэффициентСтавки(Организация, МесяцНачисления);
	КонецЕсли;	
	
	База2014 = Результат.База2014;
	База = Результат.База;
	КоэффициентСтавки = Результат.КоэффициентСтавки;
	Если МесяцНачисления >=  ПроведениеРасчетов.ДатаИзмененияЕСВ1863() Тогда
		КоличествоЗЛ = Результат.КоличествоЗЛ;
		КоличествоЗЛ2014 = Результат.КоличествоЗЛ2014
	КонецЕсли;	

КонецПроцедуры

Процедура ПриОткрытии()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда	
		Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(МесяцНачисления) Тогда	
    	МесяцНачисления = НачалоМесяца(ТекущаяДата());
    КонецЕсли;
	СтрокаПериод = РаботаСДиалогами.ПолучитьПредставлениеПериодаРегистрации(МесяцНачисления);

	БазаСОграничением = ВосстановитьЗначение("ПонижающийКоэффициентБазаСОграничением")
	
КонецПроцедуры

Процедура СтрокаПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	РаботаСДиалогами.РегулированиеПредставленияПериодаРегистрации(Направление, СтандартнаяОбработка, МесяцНачисления, СтрокаПериод);
	
КонецПроцедуры

Процедура СтрокаПериодНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	РаботаСДиалогами.НачалоВыбораИзСпискаПредставленияПериодаРегистрации(Элемент, СтандартнаяОбработка, МесяцНачисления, ЭтаФорма);

КонецПроцедуры

Процедура СтрокаПериодОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

Процедура ПроверитьНажатие(Элемент)
	
	ТаблицаУсловия.Очистить();
	
	Если МесяцНачисления >=  ПроведениеРасчетов.ДатаИзмененияЕСВ1863() Тогда
		
		Результат = ПроведениеРасчетов.ПроверитьУсловияКоэффициентСтавки1863(Организация, МесяцНачисления, КоэффициентСтавки, КоличествоЗЛ, КоличествоЗЛ2014);
		
		НоваяСтрока = ТаблицаУсловия.Добавить();
		НоваяСтрока.Показатель = Окр(?(База2014 =0,1, МАКС(База/База2014-1,0)*100),2);
		НоваяСтрока.Отметка = (НоваяСтрока.Показатель >= 20);
		НоваяСтрока.Условие = НСтр("ru='Рост средней базы ЕСВ';uk='Ріст середньої бази ЄСВ'");
		НоваяСтрока.Комментарий = НСтр("ru='Не менее 20%';uk='Не менш 20%'");
		
		НоваяСтрока = ТаблицаУсловия.Добавить();
		НоваяСтрока.Показатель = Результат.СреднийПлатеж;
		НоваяСтрока.Отметка = (Результат.СреднийПлатеж >= Результат.СреднийПлатеж2014);
		НоваяСтрока.Условие = НСтр("ru='Средний страховой платеж на одного человека';uk='Середній страховий платіж на одну людину'");
		НоваяСтрока.Комментарий = НСтр("ru='Не менее среднего платежа за 2014 г. (в 2014 г.';uk='Не менш середнього платежу за 2014 р. (в 2014 р.'")+Формат(Результат.СреднийПлатеж2014,"ЧЦ=15; ЧДЦ=2")+" грн).";
		
		НоваяСтрока = ТаблицаУсловия.Добавить();
		НоваяСтрока.Показатель = КоличествоЗЛ;
		НоваяСтрока.Отметка = (КоличествоЗЛ <= 2*КоличествоЗЛ2014);
		НоваяСтрока.Условие = НСтр("ru='Количество застрахованных лиц';uk='Кількість застрахованих осіб'");
		НоваяСтрока.Комментарий = НСтр("ru='Превышает показатель 2014 г. не более 2 раз';uk='Перевищує показник 2014 р. не більше 2 разів'");
		
	Иначе	
		Результат = ПроведениеРасчетов.ПроверитьУсловияКоэффициентСтавки(Организация, МесяцНачисления, КоэффициентСтавки);
		
		НоваяСтрока = ТаблицаУсловия.Добавить();
		НоваяСтрока.Показатель = Результат.СреднийПлатеж;
		НоваяСтрока.Отметка = (Результат.СреднийПлатеж >= 700);
		НоваяСтрока.Условие = НСтр("ru='Средний страховой платеж на одного человека';uk='Середній страховий платіж на одну людину'");
		НоваяСтрока.Комментарий = НСтр("ru='Не менее 700 грн.';uk='Не менше 700 грн.'");
		
		НоваяСтрока = ТаблицаУсловия.Добавить();
		НоваяСтрока.Показатель = Результат.СредняяЗП;
		НоваяСтрока.Отметка = (Результат.СредняяЗП >= 3*Результат.МинимальнаяЗП);
		НоваяСтрока.Условие = НСтр("ru='Средняя заработная плата';uk='Середня заробітна плата'");
		НоваяСтрока.Комментарий = НСтр("ru='Не менее размера 3 минимальных заработных плат (';uk='Не менше розміру 3 мінімальних заробітних плат ('")+Формат(3*Результат.МинимальнаяЗП,"ЧЦ=15; ЧДЦ=2")+" грн).";
		
		
		НоваяСтрока = ТаблицаУсловия.Добавить();
		НоваяСтрока.Показатель = ?(Результат.СредняяЗП2014 =0,1, МАКС(Результат.СредняяЗП/Результат.СредняяЗП2014-1,0))*100;
		НоваяСтрока.Отметка = (НоваяСтрока.Показатель >= 30);
		НоваяСтрока.Условие = НСтр("ru='Рост средней заработной платы';uk='Зростання середньої заробітної плати'");
		НоваяСтрока.Комментарий = НСтр("ru='Не менее 30% ( средняя зарплата 2014 г. ';uk='Не менше 30% ( середня зарплата 2014 р. '")+Формат(Результат.СредняяЗП2014,"ЧЦ=15; ЧДЦ=2")+" грн).";
	КонецЕсли;	
	
КонецПроцедуры

Процедура ФлажокБазаСограничениемПриИзменении(Элемент)
	СохранитьЗначение("ПонижающийКоэффициентБазаСОграничением", БазаСОграничением);
КонецПроцедуры
