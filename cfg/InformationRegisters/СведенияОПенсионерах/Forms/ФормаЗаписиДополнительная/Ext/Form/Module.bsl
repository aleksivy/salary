Процедура УстановитьДоступность()
	
	ЭлементыФормы.СвидетельствоСерия.Доступность = Пенсионер;
	ЭлементыФормы.СвидетельствоНомер.Доступность = Пенсионер;
	ЭлементыФормы.СвидетельствоДатаВыдачи.Доступность = Пенсионер;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФизЛицоПриОткрытии             = ФизЛицо;
	ПериодПриОткрытии              = Период;
	ДатаВыдачиПриОткрытии 		   = СвидетельствоДатаВыдачи;
	ПенсионерПриОткрытии  	   	   = Пенсионер;
	НомерПриОткрытии			   = СвидетельствоНомер;
	СерияПриОткрытии    		   = СвидетельствоСерия;
	КомментарийПриОткрытии 		   = Комментарий;
		
	Заголовок = Заголовок + " " + ФизЛицо.Наименование;
	
	Прочитать();
	
	// У записи обязательно должны быть заполнены реквизиты ФизЛицо и Период
	Если ФизЛицо.Пустая() Тогда
	    ФизЛицо = ФизЛицоПриОткрытии;
		Период  = ПериодПриОткрытии;
		// выставим по умолчанию
		Пенсионер = Истина;
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Если поменяли дату, то тогда новая  запись
	Если ПериодПриОткрытии <> Период Тогда
		
		// Если что-то поменяли тогда запишем 
		Если ДатаВыдачиПриОткрытии <> СвидетельствоДатаВыдачи ИЛИ
			ПенсионерПриОткрытии <> Пенсионер ИЛИ
			НомерПриОткрытии <> СвидетельствоНомер ИЛИ
			СерияПриОткрытии <> СвидетельствоСерия ИЛИ
			КомментарийПриОткрытии <> Комментарий Тогда
		
			МенеджерЗаписи = РегистрыСведений.СведенияОПенсионерах.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период   = Период;
			МенеджерЗаписи.ФизЛицо  = ФизЛицо;
			
			МенеджерЗаписи.СвидетельствоДатаВыдачи	= СвидетельствоДатаВыдачи;
			МенеджерЗаписи.Пенсионер		 		= Пенсионер;
			МенеджерЗаписи.СвидетельствоНомер       = СвидетельствоНомер;
			МенеджерЗаписи.СвидетельствоСерия       = СвидетельствоСерия;
			МенеджерЗаписи.Комментарий 		 		= Комментарий;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","Пенсионер"), ФизЛицо);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.СведенияОПенсионерах.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры

Процедура ПенсионерПриИзменении(Элемент)
	
	Если НЕ Пенсионер Тогда
		СвидетельствоСерия = "";
		СвидетельствоНомер = "";
		СвидетельствоДатаВыдачи = "";
	КонецЕсли;
	
	УстановитьДоступность();
		
КонецПроцедуры






