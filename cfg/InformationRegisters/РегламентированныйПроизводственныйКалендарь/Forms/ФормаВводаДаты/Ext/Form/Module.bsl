// Виды дней
Перем ВидДняРабочий;
Перем ВидДняПраздник;
Перем ВидДняПредпраздничный;

Перем ИсходнаяДата;

Функция ПроверитьПравильностьВыбораДаты()
	
	ВыбраннаяДата = ИсходнаяДата;
	ВыбраннаяДатаПереноса = КалендарнаяДата;
	
	Если ВыбраннаяДата = ВыбраннаяДатаПереноса Тогда
		Предупреждение(НСтр("ru='Выбраны одинаковые даты!';uk='Вибрані однакові дати!'"));
		Возврат Ложь;
	КонецЕсли;
	
	ГодВыбраннойДаты = Год(ВыбраннаяДата);
	
	Если (ГодВыбраннойДаты <> Год(ВыбраннаяДатаПереноса)) Тогда
		Предупреждение(НСтр("ru='Выбираемая дата должна быть из ';uk='Дата, що обирається, повинна бути з '")+Строка(ГодВыбраннойДаты)+НСтр("ru=' года';uk=' року'"));
		Возврат Ложь;
	КонецЕсли;
	
			
	ДеньНеделиВыбраннойДаты = ДеньНедели(ВыбраннаяДата);
	ДеньНеделиВыбраннойДатыПереноса = ДеньНедели(ВыбраннаяДатаПереноса);
	ВыбраннаяДатаЭтоВыходной = (ДеньНеделиВыбраннойДаты = 6) Или (ДеньНеделиВыбраннойДаты = 7);
	ВыбраннаяДатаПереносаЭтоВыходной = (ДеньНеделиВыбраннойДатыПереноса = 6) ИЛИ (ДеньНеделиВыбраннойДатыПереноса = 7); 
	
	НачальнаяЧастьСтрокиПредупреждения = формат(ВыбраннаяДата, "ДФ = ""д ММММ, дддд""")+НСтр("ru=' на ';uk=' на '")+
			формат(ВыбраннаяДатаПереноса, "ДФ = ""д ММММ, дддд""")+".";
			
	Если (ВыбраннаяДатаЭтоВыходной И ВыбраннаяДатаПереносаЭтоВыходной) 
		ИЛИ (НЕ ВыбраннаяДатаЭтоВыходной И НЕ ВыбраннаяДатаПереносаЭтоВыходной) Тогда
		
		Предупреждение(НСтр("ru='Вы выполняете перенос ';uk='Ви виконуєте перенесення'")+НачальнаяЧастьСтрокиПредупреждения + НСтр("ru=' При переносе ';uk=' При перенесенні '")+
			?(ВыбраннаяДатаЭтоВыходной, НСтр("ru='праздничного дня вы можете выбрать только рабочий день.';uk='святкового дня ви можете вибрати тільки робочий день.'"),
			НСтр("ru='рабочего дня вы можете выбрать только праздничный день.';uk='робочого дня ви можете вибрати тільки святковий день.'")));
		Возврат Ложь;
	КонецЕсли;

	Для Каждого СтрокаТаблицы Из ТаблицаПеренесенныхДней Цикл
		
		ВыбраннаяДатаПереносаРабочийДень = СтрокаТаблицы.РабочийДень = ВыбраннаяДатаПереноса;
		ВыбраннаяДатаРабочийДень = СтрокаТаблицы.РабочийДень = ВыбраннаяДата;
		ВыбраннаяДатаПереносаВыходнойДень = СтрокаТаблицы.ВыходнойДень = ВыбраннаяДатаПереноса;
		ВыбраннаяДатаВыходнойДень = СтрокаТаблицы.ВыходнойДень = ВыбраннаяДата;
		
		Если (ВыбраннаяДатаПереносаРабочийДень И НЕ ВыбраннаяДатаВыходнойДень)
			ИЛИ (ВыбраннаяДатаПереносаВыходнойДень И НЕ ВыбраннаяДатаРабочийДень)
			ИЛИ (ВыбраннаяДатаВыходнойДень И НЕ ВыбраннаяДатаПереносаРабочийДень)
			ИЛИ (ВыбраннаяДатаРабочийДень И НЕ ВыбраннаяДатаПереносаВыходнойДень) Тогда
			
			Предупреждение(НСтр("ru='Указанный день уже участвовал в переносе:';uk='Зазначений день вже брав участь у перенесенні:'")+Символы.ПС+НачальнаяЧастьСтрокиПредупреждения);
			Возврат Ложь;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции


Процедура ОсновныеДействияФормыОК(Кнопка)
	Если ПроверитьПравильностьВыбораДаты() Тогда
		Закрыть(КалендарнаяДата);
	КонецЕсли;
КонецПроцедуры


Процедура УстановитьЗаголовокФормы(ТекстПояснения) Экспорт
	ЭлементыФормы.Пояснение.Заголовок = ТекстПояснения;
КонецПроцедуры


Процедура КалендарнаяДатаВыбор(Элемент, ВыбраннаяДата)
	Если ПроверитьПравильностьВыбораДаты() Тогда
		Закрыть(КалендарнаяДата);
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	ИсходнаяДата = КалендарнаяДата;
	
	ВидДняРабочий = Перечисления.ВидыДнейПроизводственногоКалендаря.Рабочий;
	ВидДняПраздник = Перечисления.ВидыДнейПроизводственногоКалендаря.Праздник;
	ВидДняПредпраздничный = Перечисления.ВидыДнейПроизводственногоКалендаря.Предпраздничный;

КонецПроцедуры

