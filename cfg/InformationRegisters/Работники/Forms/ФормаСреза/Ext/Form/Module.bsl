////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// настройка порядка
	ЭлементыФормы.Работники.НастройкаПорядка.Физлицо.Доступность = Истина;
	
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	СтруктураКолонок = Новый Структура();

	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок.Вставить("ФизЛицо");

	// Установить ограничение - изменять видимость колонок для табличной части 
	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.Работники.Колонки, СтруктураКолонок);
	
	РегистрСведенийСписок.Колонки.Добавить("ПричинаИзмененияСостояния", Ложь);
	
	ЭлементыФормы.ДействияФормы.Кнопки.История.Пометка					= Истина;
	ЭлементыФормы.ДействияФормы.Кнопки.Подменю.Кнопки.История.Пометка	= Истина;
	
	РаботаСДиалогами.УстановитьВидимостьЭУПоУчетнойПолитикеПоПерсоналу(ЭлементыФормы,глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналу"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНДНОЙ ПАНЕЛИ ФОРМЫ

Процедура ДействияФормыПриемНаРаботу(Кнопка)
	Документы.ПриемНаРаботу.ПолучитьФормуНовогоДокумента().Открыть();
КонецПроцедуры

// Процедура формирует документ "Приказ об увольнении".
Процедура ДействияФормыПриказОбУвольнении(Кнопка)
	
	Если ЭлементыФормы.Работники.ТекущаяСтрока = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	НовыйДокумент = Документы.Увольнение.СоздатьДокумент();
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(НовыйДокумент);
	НовыйДокумент.УстановитьНовыйНомер();
	НоваяСтрока = НовыйДокумент.Работники.Добавить();
	НоваяСтрока.ФизЛицо			  = ЭлементыФормы.Работники.ТекущаяСтрока.ФизЛицо;
	НоваяСтрока.ДатаУвольнения	  =	РабочаяДата;
	НоваяСтрока.ПричинаУвольнения =	Справочники.ОснованияУвольнения.ПоСобственномуЖеланию;
	НовыйДокумент.ПолучитьФорму().Открыть();
	
КонецПроцедуры

// Процедуда формирует документ "Кадровое перемещение".
Процедура ДействияФормыКадровоеПеремещение(Кнопка)
	
	Если ЭлементыФормы.Работники.ТекущаяСтрока = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	НовыйДокумент = Документы.КадровоеПеремещение.СоздатьДокумент();
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(НовыйДокумент);
	НовыйДокумент.УстановитьНовыйНомер();
	НоваяСтрока = НовыйДокумент.Работники.Добавить();
	НоваяСтрока.ФизЛицо			  = ЭлементыФормы.Работники.ТекущаяСтрока.ФизЛицо;
	НоваяСтрока.ЗанимаемыхСтавок  = 1;
	НоваяСтрока.ДатаНачала		  = РабочаяДата;
	НовыйДокумент.ПолучитьФорму().Открыть();
	
КонецПроцедуры

// Процедура открывает форму списка регистра. 
Процедура ДействияФормыИстория(Кнопка)
	
	ПоказыватьВсеЗаписиРегистра = Не Кнопка.Пометка;
	
	ЭлементыФормы.Работники.ВыбиратьСрез = ?(ПоказыватьВсеЗаписиРегистра,ИспользованиеСреза.НеИспользовать,ИспользованиеСреза.Последние);
	ЭлементыФормы.ДействияФормы.Кнопки.История.Пометка					= ПоказыватьВсеЗаписиРегистра;
	ЭлементыФормы.ДействияФормы.Кнопки.Подменю.Кнопки.История.Пометка	= ПоказыватьВсеЗаписиРегистра;
	ЭлементыФормы.Работники.Колонки.Картинка.ОтображатьСтандартнуюКартинку = ПоказыватьВсеЗаписиРегистра;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ Работники

// Процедура - обработчик события "ПередНачаломДобавления" табличного поля.
Процедура РаботникиОрганизацииПередНачаломДобавления(Элемент, Отказ, Копирование)
	Документы.ПриемНаРаботу.ПолучитьФормуНовогоДокумента().Открыть();
КонецПроцедуры

// Процедура обеспечивает управление картинкой строки. 
//
Процедура РаботникиОрганизацииПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если Элемент.Колонки.Картинка.Видимость И Не ЭлементыФормы.ДействияФормы.Кнопки.История.Пометка Тогда 
		ОформлениеСтроки.Ячейки.Картинка.ОтображатьКартинку = Истина;
		ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = Число(ДанныеСтроки.ПричинаИзмененияСостояния = Перечисления.ПричиныИзмененияСостояния.Увольнение);
	КонецЕсли;
КонецПроцедуры
