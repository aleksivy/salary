Процедура УстановитьДоступность()
	
	ЭлементыФормы.Группа.Доступность = Инвалидность;
	ЭлементыФормы.УдостоверениеСерия.Доступность = Инвалидность;
	ЭлементыФормы.УдостоверениеНомер.Доступность = Инвалидность;
	ЭлементыФормы.УдостоверениеДатаВыдачи.Доступность = Инвалидность;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФизЛицоПриОткрытии             = ФизЛицо;
	ПериодПриОткрытии              = Период;
	ГруппаПриОткрытии  			   = Группа;
	ДатаВыдачиПриОткрытии 		   = УдостоверениеДатаВыдачи;
	ИнвалидностьПриОткрытии  	   = Инвалидность;
	НомерПриОткрытии			   = УдостоверениеНомер;
	СерияПриОткрытии    		   = УдостоверениеСерия;
	КомментарийПриОткрытии 		   = Комментарий;
		
	Заголовок = Заголовок + " " + ФизЛицо.Наименование;
	
	Прочитать();
	
	// У записи обязательно должны быть заполнены реквизиты ФизЛицо и Период
	Если ФизЛицо.Пустая() Тогда
	    ФизЛицо = ФизЛицоПриОткрытии;
		Период  = ПериодПриОткрытии;
		// выставим по умолчанию
		Группа = Перечисления.ГруппыИнвалидности.ТретьяГруппа;
		Инвалидность = Истина;
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Если поменяли дату, то тогда новая  запись
	Если ПериодПриОткрытии <> Период Тогда
		
		// Если что-то поменяли тогда запишем 
		Если ГруппаПриОткрытии <> Группа ИЛИ
			ДатаВыдачиПриОткрытии <> УдостоверениеДатаВыдачи ИЛИ
			ИнвалидностьПриОткрытии <> Инвалидность ИЛИ
			НомерПриОткрытии <> УдостоверениеНомер ИЛИ
			СерияПриОткрытии <> УдостоверениеСерия ИЛИ
			КомментарийПриОткрытии <> Комментарий Тогда
		
			МенеджерЗаписи = РегистрыСведений.СведенияОбИнвалидностиФизлиц.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период   = Период;
			МенеджерЗаписи.ФизЛицо  = ФизЛицо;
			
			МенеджерЗаписи.Группа			   		= Группа;
			МенеджерЗаписи.УдостоверениеДатаВыдачи	= УдостоверениеДатаВыдачи;
			МенеджерЗаписи.Инвалидность		 		= Инвалидность;
			МенеджерЗаписи.УдостоверениеНомер       = УдостоверениеНомер;
			МенеджерЗаписи.УдостоверениеСерия       = УдостоверениеСерия;
			МенеджерЗаписи.Комментарий 		 		= Комментарий;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","Инвалидность"), ФизЛицо);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.СведенияОбИнвалидностиФизлиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры

Процедура ИнвалидностьПриИзменении(Элемент)
	
	Если НЕ Инвалидность Тогда
		Группа = Перечисления.ГруппыИнвалидности.ПустаяСсылка();
		УдостоверениеСерия = "";
		УдостоверениеНомер = "";
		УдостоверениеДатаВыдачи = "";
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры





