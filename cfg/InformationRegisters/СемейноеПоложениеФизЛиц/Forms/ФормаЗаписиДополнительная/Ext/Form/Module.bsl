////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФизЛицоПриОткрытии           = ФизЛицо;
	ПериодПриОткрытии   		 = Период;
	СемейноеПоложениеПриОткрытии = СемейноеПоложение;
	        
	Заголовок = Заголовок + " " + ФизЛицо.Наименование;
	
	Прочитать();
	
	// У записи обязательно должны быть заполнены реквизиты ФизЛицо и Период
	Если ФизЛицо.Пустая() Тогда
	    ФизЛицо = ФизЛицоПриОткрытии;
		Период  = ПериодПриОткрытии;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Если поменяли дату, то тогда новая  запись
	Если ПериодПриОткрытии <> Период Тогда
		
		// Если что-то поменяли тогда запишем 
		Если СемейноеПоложениеПриОткрытии <> СемейноеПоложение Тогда
		
			МенеджерЗаписи = РегистрыСведений.СемейноеПоложениеФизЛиц.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период   = ПериодПриОткрытии;
			МенеджерЗаписи.ФизЛицо  = ФизЛицо;
			МенеджерЗаписи.СемейноеПоложение = СемейноеПоложениеПриОткрытии;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли; 
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","СемейноеПоложение"), ФизЛицо);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.СемейноеПоложениеФизЛиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры
