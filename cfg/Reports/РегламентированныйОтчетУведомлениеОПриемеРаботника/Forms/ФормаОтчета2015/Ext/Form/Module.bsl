////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// поле табличного документа
Перем тбСхемыXML;

Перем мОтчетОПринятыхНаРаботу, мСлужебныеДанные;

Перем ЕДРПОУ, КВЕД, Инд;

// хранит форматную строку представления
// денежных показателей отчета
Перем мСтрокаФормата;

// хранит коллекцию цветов раскраски автоматически
// заполняемых ячеек табличного документа
Перем мСтруктураЦветовРаскраски;

// хранит значение делителя при представлении
// чисел в назначенной размерности
Перем мДелитель;

// хранит версию формы отчета
Перем мВерсияФормы;

//вспомогательные переменные
Перем ТаблицаРезультатов;

// хранят имена многострочных разделов
Перем мИдГруппы1;

// хранят структуру многострочных разделов
Перем мСтруктураМногострочныхРазделов;
Перем мСтруктураИсхКолвоСтрокРазделов;

Перем мПечатныеФормы Экспорт;

// Хранит название макета состава показателей
Перем мСоставПоказателей Экспорт;

Перем ЕстьДуплекс;

Перем мМенеджерЗвит1С Экспорт;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция собирает значения редактирукемых ячеек табличного документа
// в коллекцию значений типа "структура".
//
// Параметры:
//  ТабличноеПоле - поле табличного документа текущей формы.
//
// Возвращаемое значение:
//  Структура.
//
Функция СобратьДанныеТекущегоТаблПоля(ТабличноеПоле)
	
	СтруктураПоказателейВнутрСвязи = Новый Структура;
	
	СтруктураДанныхПоля = Новый Структура;
	
	Для Инд = 0 По ТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличноеПоле.Области[Инд];
		Если Тип ("РисунокТабличногоДокумента") <> ТипЗнч (ТекущаяОбласть) Тогда
			Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если (ТекущаяОбласть.Имя <> "Подвал" И ТекущаяОбласть.Имя <> "Расчет" И ТекущаяОбласть.Имя <> "Шапка") Тогда
			Если НЕ(ТекущаяОбласть.СодержитЗначение) Тогда
				Продолжить;
			КонецЕсли;
			
			//Если Лев(ТекущаяОбласть.Имя, 2) = "R0" Тогда
			//	Продолжить;
			//КонецЕсли;
			
			//Если (ТекущаяОбласть.Защита)Тогда
			//	Если Не СтруктураПоказателейВнутрСвязи.Свойство(ТекущаяОбласть.Имя) Тогда
			//		Продолжить;
			//	КонецЕсли;
			//КонецЕсли;
			
			ИмяПоказателя      = ТекущаяОбласть.Имя;
			ЗначениеПоказателя = ТекущаяОбласть.Значение;
			СтруктураДанныхПоля.Вставить(ИмяПоказателя, ЗначениеПоказателя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураДанныхПоля;
	
КонецФункции // СобратьДанныеТекущегоТаблПоля()

// Функция определяет варианты заполнения ячеек табличного документа
// по имени страницы основной панели формы.
//
// Параметры:
//   ИмяСтраницыПанели - строка, имя страницы основной панели формы.
//   ПолучитьЭталонную - булево, если истина, получаем эталонную
//                               (исходную) таблицу вариантов заполнения.
//
// Возвращаемое значение:
//   таблица значений, содержащая информацию о вариантах автозаполнения ячеек.
//
Функция ПолучитьТаблицуВариантовЗаполнения(ИмяСтраницыПанели, ПолучитьЭталонную = Ложь)
	Перем МассивВариантовЗаполненияСтраниц;
	Перем ТаблицаВариантыЗаполнения;
	Перем ТаблицаСтраницРаздела;
	
	Если мСтруктураВариантыЗаполнения.Свойство(ИмяСтраницыПанели, МассивВариантовЗаполненияСтраниц) Тогда
		
		Если НЕ ПолучитьЭталонную Тогда
			// получим варианты заполнения ячеек, заданные для активной страницы
			НомерТекущейСтраницы = 1;
			
			ТаблицаВариантыЗаполнения = МассивВариантовЗаполненияСтраниц[НомерТекущейСтраницы - 1];
		Иначе
			// Получим предустановленные (эталонные) варианты заполнения ячеек поля табличного
			// документа на заданной странице
			
			ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);
			
			ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);
			
			ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);
			
			ОписаниеТиповЧисло15   = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);
			
			ТаблВариантыЗаполнения = Новый ТаблицаЗначений;
			ТаблВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока15  );
			ТаблВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
			ТаблВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
			ТаблВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
			ТаблВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );
			
			// варианты заполнения определяем по макету СоставПоказателей
			МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет("СоставПоказателей2015");
			
			ТекОбласть = МакетСоставаПоказателей.Области.Найти(ИмяСтраницыПанели);
			Если ТекОбласть <> Неопределено Тогда
				
				Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
					// Перебираем строки макета.
					// Код показателя (по составу показателей) определяется по первой колонке макета
					КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
					ИмяПоказателя = КодПоказателя;
					
					Если КодПоказателя = "===" Тогда         // признак конечной строки
						Прервать;
					КонецЕсли;
					
					Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
						Продолжить;
					КонецЕсли;
					
					// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
					КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
					// вариант заполнения ячейки определяется по колонке 6 макета
					стрВариантЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 6).Текст);
					
					чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));
					Если чВариантЗаполнения <> 0 Тогда
						// Если в колонке 6 задан варианет заполнения показателя, 
						// то его значение может определяться по данным ИБ.
						// Дополняем таблицу значений вариантов заполнения
						НоваяСтрока = ТаблВариантыЗаполнения.Добавить();
						НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
						НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			Если ТаблВариантыЗаполнения.Количество() > 0 Тогда
				ТаблицаВариантыЗаполнения = ТаблВариантыЗаполнения.Скопировать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаВариантыЗаполнения;
	
КонецФункции // ПолучитьТаблицуВариантовЗаполнения()

// Обновляет пользовательские настройки по вариантам заполнения
// показателей отчета в случае изменения версии формы отчета.
//
// Возвращаемое значение:
//   Булево - признак модифицированность пользовательских настроек.
//
Функция ОбновитьВариантыЗаполнения()
	Перем МассивВариантовЗаполненияСтраниц;
	
	ЕстьИзменения = Ложь;
	
	// Подлежат обновлению следующие структуры:
	// 1.Пользовательские настройки вариантов заполнения таблиц
	
	// Сначала создадим эталонные модели вариантов автозаполнения таблиц.
	// Их определяем по макету СоставПоказателей
	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет("СоставПоказателей2015");
	
	ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);
	
	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);
	
	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);
	
	ОписаниеТиповЧисло15   = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);
	
	ЕстьИзменения = Ложь;
	
	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл
		// организован цикл по именованным областям макета,
		// содержащим состав показателей и варианты автозаполнения
		// таблиц на каждой странице основной панели формы.
		
		ТекОбласть  = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы = ТекОбласть.Имя;
		
		// Таблица значений содержит информацию о вариантах заполнения показателя отчета.
		// В колонках таблицы значений отображается следуящая информация:
		//   - имя ячейки (код показателя);
		//   - вариант заполнения ячейки;
		//   - вычисленное по данным ИБ значение ячейки;
		//   - величина автокорректировки значения ячейки;
		//   - примечание к ячейке.
		//
		ТаблицаВариантыЗаполнения = Новый ТаблицаЗначений;
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока15  );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );
		
		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета
			
			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;
			
			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;
			
			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;
			
			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 6).Текст);
			
			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));
			Если чВариантЗаполнения <> 0 Тогда
				// Если в колонке 6 задан варианет заполнения показателя, 
				// то его значение может определяться по данным ИБ.
				// Дополняем таблицу значений вариантов заполнения
				НоваяСтрока = ТаблицаВариантыЗаполнения.Добавить();
				НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
				НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не мСтруктураВариантыЗаполнения.Свойство(ИмяСтраницы, МассивВариантовЗаполненияСтраниц) Тогда
			Если ТаблицаВариантыЗаполнения.Количество() > 0 Тогда
				// Рассмотрим вариант, когда в предыдущей версии формы по данной странице
				// не было предусмотрено автозаполнение по данным ИБ.
				// Добавим такую возможность в пользовательскую настройку
				
				// Для обеспечения назначения разных вариантов заполнения одного и того же показателя
				// на разных страницах, создаем массив, хранящий информацию о вариантах заполнения
				// таблиц по каждой странице многостраничного раздела.
				МассивВариантовЗаполненияСтраниц = Новый Массив;
				// Первым элементом массива является "эталонная" таблица значений вариантов заполнения
				МассивВариантовЗаполненияСтраниц.Добавить(ТаблицаВариантыЗаполнения);
				
				// сохраняем варианты заполнения таблиц на данной странице
				мСтруктураВариантыЗаполнения.Вставить(ИмяСтраницы, МассивВариантовЗаполненияСтраниц);
				
				ЕстьИзменения = Истина;
			КонецЕсли;
		Иначе
			Если ТаблицаВариантыЗаполнения.Количество() = 0 Тогда
				// Рассмотрим вариант, когда в предыдущей версии формы по данной странице
				// было предусмотрено автозаполнение таблицы, а в текущей версии - отключено.
				// Соответственно, из структуры вариантов автозаполнения удаляем элемент,
				// хранящий параметры автозаполнения ячеек текущей станицы.
				мСтруктураВариантыЗаполнения.Удалить(ИмяСтраницы);
				
				ЕстьИзменения = Истина;
			Иначе
				// Рассмотрим вариант, когда и в предыдущей, и в текущей версии формы было предусмотрено
				// автозаполнение текущей страницы. В данном случае обновляем таблицу значений, содержащей
				// пользовательские настройки параметров автозаполнения текущей страницы.
				
				Для каждого ЭлементМассива Из МассивВариантовЗаполненияСтраниц Цикл
					Для каждого СтрокаТаблицы Из ТаблицаВариантыЗаполнения Цикл
						КодПоказателя     = СтрокаТаблицы.КодПоказателя;
						ВариантЗаполнения = СтрокаТаблицы.ВариантЗаполнения;
						
						Если ЭлементМассива.Найти(КодПоказателя, "КодПоказателя") = Неопределено Тогда
							// Рассмотри вариант, когда в новой версии формы список реквизитов,
							// заполняемых автоматически по данным ИБ, дополнен новым реквизитом.
							НоваяСтрока = ЭлементМассива.Добавить();
							НоваяСтрока.КодПоказателя     = КодПоказателя;
							НоваяСтрока.ВариантЗаполнения = ВариантЗаполнения;
							
							ЕстьИзменения = Истина;
						КонецЕсли;
					КонецЦикла;
					
					Для каждого СтрокаТаблицы Из ЭлементМассива Цикл
						КодПоказателя = СтрокаТаблицы.КодПоказателя;
						
						Если ТаблицаВариантыЗаполнения.Найти(КодПоказателя, "КодПоказателя") = Неопределено Тогда
							// Рассмотрим вариант, когда в предыдущей версии формы для данной страницы
							// было предусмотрено автозаполнение ячейки по коду КодПоказателя, а в текущей - отключено.
							// В этом случае удаляем ячейку из таблицы пользовательских настроек вариантов автозаполнения.
							ЭлементМассива.Удалить(СтрокаТаблицы);
							
							ЕстьИзменения = Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЕстьИзменения;
	
КонецФункции // ОбновитьВариантыЗаполнения()

// Процедура управляет показом в форме периода построения отчета,
Процедура ПоказатьПериод()
	
	мОтчетОПринятыхНаРаботу.Области.HFILL.Значение = ДатаПодписи;
	
	// Период в заголовке отчета
	ЭтаФорма.Заголовок = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление();
	
КонецПроцедуры // ПоказатьПериод()

// Процедура управляет выводом сведений о выбранной организации
// в соответствующие ячейки табличного документа.
// пока не прописаны коды - вернуться
Процедура ЗаполнитьСведенияОбОрганизации()
	
	ОргНазв = Организация.НаименованиеПолное;
	мОтчетОПринятыхНаРаботу.Области.HNAME.Значение = ОргНазв;	
	
	СписокПоказателей = Новый СписокЗначений;
	СписокПоказателей.Добавить("","ФИОРук");
	СписокПоказателей.Добавить("","ИННРук");
	СписокПоказателей.Добавить("","ФИОБух");
	СписокПоказателей.Добавить("","ИННБух");
	СписокПоказателей.Добавить("","КодПоЕДРПОУ");
	СписокПоказателей.Добавить("","КодКВЕД");
	СписокПоказателей.Добавить("","КодОПФГ");
	СписокПоказателей.Добавить("","НалоговаяИнспекция");

	Сведения = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, мДатаКонцаПериодаОтчета, СписокПоказателей);
	
	ФИОДиректора          = Сведения.ФИОРук;
	ФИОГлавБуха           = Сведения.ФИОБух;
	ИННРук       		  = Сведения.ИННРук;
	ИННБух                = Сведения.ИННБух;
	КодЕДРПОУ             = Сведения.КодПоЕДРПОУ;
	НалоговаяИнспекция    = Сведения.НалоговаяИнспекция;
	
	мОтчетОПринятыхНаРаботу.Области.HTIN.Значение = КодЕДРПОУ;
	мСлужебныеДанные.Области.TIN.Значение         = КодЕДРПОУ;
	
	мОтчетОПринятыхНаРаботу.Области.HKBOS.Значение  = ИННРук;
	мОтчетОПринятыхНаРаботу.Области.HKBUH.Значение  = ИННБух;
	
	Данные = Новый Структура("СтруктурнаяЕдиница",Организация, "ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	ОтвЛица = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(КонецДня(ДатаПодписи), Данные);
	Результат = ОтвЛица.Найти(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Если Результат <> Неопределено Тогда
		ДанныеФИО = Новый Структура("ФизЛицо",Результат.ФизическоеЛицо);
		ФИОСрез = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(ДатаПодписи, ДанныеФИО);
		мОтчетОПринятыхНаРаботу.Области.HBOS.Значение = Лев(СокрЛ(ФИОСрез.Имя), 1) + ". " + Лев(СокрЛ(ФИОСрез.Отчество), 1) + ". " + СокрЛП(ФИОСрез.Фамилия);
	КонецЕсли;

	Данные = Новый Структура("СтруктурнаяЕдиница",Организация, "ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	ОтвЛица = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(КонецДня(ДатаПодписи), Данные);
	Результат = ОтвЛица.Найти(Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	Если Результат <> Неопределено Тогда
		ДанныеФИО = Новый Структура("ФизЛицо",Результат.ФизическоеЛицо);
		ФИОСрез = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(ДатаПодписи, ДанныеФИО);
		мОтчетОПринятыхНаРаботу.Области.HBUH.Значение = Лев(СокрЛ(ФИОСрез.Имя), 1) + ". " + Лев(СокрЛ(ФИОСрез.Отчество), 1) + ". " + СокрЛП(ФИОСрез.Фамилия);
	КонецЕсли;
	
	мОтчетОПринятыхНаРаботу.ОБласти.H01.Значение = "Х";
	мОтчетОПринятыхНаРаботу.ОБласти.H02.Значение = "";
	
    Если (НалоговаяИнспекция <> Неопределено) Тогда 
		мСлужебныеДанные.Области.C_RAJ.Значение   = НалоговаяИнспекция.КодАдмРайона;
		мСлужебныеДанные.Области.C_REG.Значение   = НалоговаяИнспекция.Родитель.Код;
		мСлужебныеДанные.Области.C_STI.Значение   = НалоговаяИнспекция.Код;
		мСлужебныеДанные.Области.C_STI_ORIG.Значение   = НалоговаяИнспекция.Код;
	КонецЕсли;
	
	мСлужебныеДанные.Области.C_DOC_TYPE.Значение = 0; //номер исправленного доекумента. Для первого подающегося документа  = 0
	мСлужебныеДанные.Области.C_DOC_CNT.Значение  = 1;  //номер документа в периоде. 
	мСлужебныеДанные.Области.C_DOC_CNT.Значение = ПолучитьСледующийНомерДокументаВПериоде();
	
	мСлужебныеДанные.Области.PERIOD_MONTH.Значение = Месяц(мДатаНачалаПериодаОтчета);
	мСлужебныеДанные.Области.PERIOD_YEAR.Значение = Год(мДатаНачалаПериодаОтчета);
	мСлужебныеДанные.Области.PERIOD_TYPE.Значение = 1;
	
	мСлужебныеДанные.Области.C_DOC_STAN.Значение = 1;
	
КонецПроцедуры // ЗаполнитьСведенияОбОрганизации()

Функция ПолучитьСледующийНомерДокументаВПериоде()

	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(*) КАК Всего
	               |ИЗ
	               |	Документ.РегламентированныйОтчет КАК РегламентированныйОтчет
	               |ГДЕ
	               |	РегламентированныйОтчет.ИсточникОтчета ПОДОБНО &ИсточникОтчета
	               |	И РегламентированныйОтчет.Организация = &Организация
	               |	И (НЕ РегламентированныйОтчет.ПометкаУдаления)
	               |	И РегламентированныйОтчет.Дата МЕЖДУ &НачГода И &КонГода";
				   
	Запрос.УстановитьПараметр("Организация", Организация);			   
	Запрос.УстановитьПараметр("ИсточникОтчета", Метаданные().Имя);
	Запрос.УстановитьПараметр("НачГода", НачалоДня(мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("КонГода", КонецДня(мДатаКонцаПериодаОтчета));
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТекНомерОтчет = 0;
	Если Выборка.Следующий() Тогда
		ТекНомерОтчет = ?(ЗначениеЗаполнено(Выборка.Всего), Выборка.Всего, 0);
	КонецЕсли;
	
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(*) КАК Всего
	               |ИЗ
	               |	Документ.ВыгрузкаРегламентированныхОтчетов КАК ВыгрузкаРегламентированныхОтчетов
	               |ГДЕ
	               |	ВыгрузкаРегламентированныхОтчетов.ВыгружаемыйОтчет ПОДОБНО &ИсточникОтчета
	               |	И ВыгрузкаРегламентированныхОтчетов.Организация = &Организация
	               |	И (НЕ ВыгрузкаРегламентированныхОтчетов.ПометкаУдаления)
	               |	И ВыгрузкаРегламентированныхОтчетов.Дата МЕЖДУ &НачГода И &КонГода";
				   
	Запрос.УстановитьПараметр("Организация", Организация);			   
	Запрос.УстановитьПараметр("ИсточникОтчета", Метаданные().Имя);
	Запрос.УстановитьПараметр("НачГода", НачалоДня(мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("КонГода", КонецДня(мДатаКонцаПериодаОтчета));
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТекНомерВыгрузка = 0;
	Если Выборка.Следующий() Тогда
		ТекНомерВыгрузка = ?(ЗначениеЗаполнено(Выборка.Всего), Выборка.Всего, 0);
	КонецЕсли;
	
	
	Если ТекНомерОтчет + ТекНомерВыгрузка = 0 Тогда
		Возврат 1;
	Иначе
		Возврат ТекНомерОтчет + ТекНомерВыгрузка + 1;
	КонецЕсли;

КонецФункции


// Процедура управляет цветом фона автозаполняемых ячеек при:
//  а) автозаполнении;
//  б) ручном выборе другого варианта заполнения;
//  в) добавлении/удалении и листании дополнительных страниц.
//
// Цвет фона устанавливается в зависимости от варианта заполнения ячейки.
//
// Параметры:
//  ИмяЯчейки                - строка, имя ячейки, для которой устанавливаем цвет фона;
//  ВариантЗаполнения        - число,  вариант заполнения ячейки;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели, на которой для всех автозаполняемых
//                             ячеек табличного документа следует установить цвет фона.
//
Процедура УстановитьЦветФонаЯчейки(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "")
	Перем ЦветФона;
	
	Если Не ИмяЯчейки = Неопределено Тогда
		// устанавливаем цвет фона конкретной ячейки
		ИмяСтраницыПанели = "Уведомление";
		Если ИмяСтраницыПанели = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ИмяПоляТаблДокумента = "ПолеТабличногоДокумента" + ИмяСтраницыПанели;
		
		Ключ = "_" + ВариантЗаполнения;
		мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
		
		ЭлементыФормы[ИмяПоляТаблДокумента].Области[ИмяЯчейки].ЦветФона = ЦветФона;
		
	Иначе
		// устанавливаем цвет фона всех автозаполняемых ячеек активной страницы
		
		ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
		Если ТаблицаВариантыЗаполнения = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого Строка Из ТаблицаВариантыЗаполнения Цикл
			ИмяПоказателя     = Строка.КодПоказателя;
			ВариантЗаполнения = Строка.ВариантЗаполнения;
			Коммент       = Строка.Комментарий;
			
			ТекОбластьЯчеек = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя];
			Если (Не ПустаяСтрока(Коммент)) ИЛИ (Не ПустаяСтрока(ТекОбластьЯчеек.Примечание.Текст)) Тогда
				ТекОбластьЯчеек.Примечание.Текст = Коммент;
			КонецЕсли;
			
			Ключ = "_" + ВариантЗаполнения;
			мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
			
			ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя].ЦветФона = ЦветФона;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьЦветФонаЯчейки()

// Обновляет пользовательские настройки при изменении версии формы.
// унифицирована, однако использование пока ... вопрос
Процедура ОбновитьПользовательскиеНастройки(СписокСохранения)
	Перем ПоказателиОтчета;
	
	Сообщить(НСтр("ru='Измененилась версия формы отчета."
	"Выполняется обновление пользовательских настроек.';uk='Змінилася версія форми звіту."
	"Виконується відновлення користувальницьких настройок.'"), СтатусСообщения.Информация);
	
	// Обновляем варианты заполнения показателей отчета
	ЕстьИзмененияВариантыЗаполнения = ОбновитьВариантыЗаполнения();
	
	ВерсияФормы = СписокСохранения.ВерсияФормы;
	Если ЕстьИзмененияВариантыЗаполнения ИЛИ ВерсияФормы = "01/01/2005" Тогда
		
		Если  ВерсияФормы = "01/01/2005" Тогда
			ЧастиДатыПодписи = РегламентированнаяОтчетность.ПолучитьЧастиДатыДляПодписи(ДатаПодписи,"uk_UA");
			Если СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета) Тогда
				Для Каждого ЭлементСтруктуры Из ПоказателиОтчета Цикл
					ИмяГруппы       = ЭлементСтруктуры.Ключ;
					СписокПоказателей = ЭлементСтруктуры.Значение;
					
					Если ИмяГруппы = "ПолеТабличногоДокументаУведомление" Тогда
						Для каждого Показатель Из СписокПоказателей Цикл
							Если Показатель.Ключ = "ГодДаты" Тогда
									СписокПоказателей.Вставить(Показатель.Ключ, Формат(ДатаПодписи, "ДФ=yy"));
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				СписокСохранения.ПоказателиОтчета =  ПоказателиОтчета;
			КонецЕсли;
		КонецЕсли;
		// Если были изменения в пользовательских настройках,
		// то устанавливаем флаг модифицированности формы.
		Модифицированность = Истина;
		
		Сообщить(НСтр("ru='Обновление пользовательских настроек успешно завершено!';uk='Оновлення користувальницьких настройок успішно завершене!'"));
		
	Иначе
		
		Сообщить(НСтр("ru='Изменения в структуре пользовательских настроек не обнаружены!';uk='Зміни в структурі користувальницьких настройок не виявлені!'"));
		
	КонецЕсли;
	
КонецПроцедуры // ОбновитьПользовательскиеНастройки()

//// Процедура устанавливает новый вариант заполнения активной ячейки,
//// выполняет реорганизацию структуры вариантов заполнения ячеек
//// и управляет видимостью элементов формы, отображающих параметры
//// автозаполнения активной ячейки.
////
//// Параметры:
////    НовыйВариант - число, соттветствует варианту заполнения ячейки.
////                   Может принимать значение:
////                       1 - не заполнять автоматически;
////                       2 - заполнять автоматически с корректировкой;
////                       3 - заполнять автоматически без корректировки.
////
//Процедура ИзменитьВариантЗаполненияЯчейки(НовыйВариант)
//	
//	//ТекущаяСтраницаПанели     = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
//	ИмяТекущейСтраницыПанели  = "Уведомление";
//	
//	ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
//	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
//		Возврат;
//	КонецЕсли;
//	
//	ТекущаяОбласть  = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
//	ИмяПоказателя   = ТекущаяОбласть.Имя;
//	
//	НайденнаяСтрока = ТаблицаВариантыЗаполнения.Найти(ИмяПоказателя, "КодПоказателя");
//	Если НайденнаяСтрока <> Неопределено Тогда
//		НайденнаяСтрока.ВариантЗаполнения = НовыйВариант;
//	КонецЕсли;
//	
//	Если НовыйВариант = 1 Тогда
//		
//		НайденнаяСтрока.Дельта = 0;
//		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = 0;
//		
//	ИначеЕсли НовыйВариант = 2 Тогда
//		
//		Если (ТекущаяОбласть.Значение = 0) И (НайденнаяСтрока.Дельта = 0) Тогда
//			ТекущаяОбласть.Значение = НайденнаяСтрока.ЗначениеАвто;
//			Расчет();
//		КонецЕсли;
//		
//		Дельта = ТекущаяОбласть.Значение - НайденнаяСтрока.ЗначениеАвто;
//		
//		НайденнаяСтрока.Дельта = Дельта;
//		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = Дельта;
//		
//	ИначеЕсли НовыйВариант = 3 Тогда
//		
//		Если (ТекущаяОбласть.Значение = 0) И (НайденнаяСтрока.Дельта = 0) Тогда
//			ТекущаяОбласть.Значение = НайденнаяСтрока.ЗначениеАвто;
//		КонецЕсли;
//		
//		НайденнаяСтрока.Дельта = 0;
//		НайденнаяСтрока.Комментарий = "";
//		
//		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = 0;
//		ЭлементыФормы["ПолеВводаКомментарий"           + ИмяТекущейСтраницыПанели].Значение = "";
//		
//		// Очищаем комментарий редактируемой ячейки
//		ТекущаяОбласть.Примечание.Текст = "";
//		
//	КонецЕсли;
//	
//	УстановитьЦветФонаЯчейки(ИмяПоказателя, НовыйВариант);
//	ДоступностьЭлементовВариантаЗаполнения(НовыйВариант);
//	
//КонецПроцедуры // ИзменитьВариантЗаполненияЯчейки()

//// Процедура устанавливает новое значение автокорректировки активной ячейки,
//// участвующей в автозаполнении, выполняет реорганизацию структуры вариантов
//// заполнения ячеек и пересчитывает значение активной ячейки с учетом назначенной
//// автокорректировки.
////
//// Параметры:
////   Дельта - число, новое значение автокорректировки активной ячейки.
////
//Процедура ИзменитьЗначениеАвтокорректировки(Дельта)
//	
//	//ТекущаяСтраницаПанели     = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
//	ИмяТекущейСтраницыПанели  = "Уведомление";
//	
//	ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
//	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
//		Возврат;
//	КонецЕсли;
//	
//	ТекущаяОбласть    = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
//	ИмяПоказателя     = ТекущаяОбласть.Имя;
//	
//	НайденнаяСтрока   = ТаблицаВариантыЗаполнения.Найти(ИмяПоказателя, "КодПоказателя");
//	Если НайденнаяСтрока <> Неопределено Тогда
//		НайденнаяСтрока.Дельта = Дельта;
//		ТекущаяОбласть.Значение = НайденнаяСтрока.ЗначениеАвто + Дельта;
//		
//		Расчет();
//	КонецЕсли;
//	
//КонецПроцедуры // ИзменитьЗначениеАвтокорректировки()

//// Процедура устанавливает комментарий по активной ячейки, участвующей в автозаполнении,
//// выполняет реорганизацию структуры вариантов заполнения ячеек и устанавливает примечание
//// по активной ячейке табличного документа.
////
//// Параметры:
////   Комментарий - строка, содержащая примечание по активной ячейке.
////
//Процедура ИзменитьКомментарийПоЯчейке(Коммент)
//	
//	//ТекущаяСтраницаПанели     = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
//	ИмяТекущейСтраницыПанели  = "Уведомление";
//	
//	ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
//	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
//		Возврат;
//	КонецЕсли;
//	
//	ТекущаяОбласть  = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
//	ИмяПоказателя   = ТекущаяОбласть.Имя;
//	
//	НайденнаяСтрока = ТаблицаВариантыЗаполнения.Найти(ИмяПоказателя, "КодПоказателя");
//	Если НайденнаяСтрока <> Неопределено Тогда
//		НайденнаяСтрока.Комментарий = Коммент;
//	КонецЕсли;
//	
//	ТекущаяОбласть.Примечание.Текст = Коммент;
//	
//КонецПроцедуры // ИзменитьКомментарийПоЯчейке()

// Процедура очищает содержимое редактируемыхи и вычисляемых ячеек
// поля табличного документа, переданного параметром ВыбТабличноеПоле.
//
// Параметры:
//  ВыбТабличноеПоле - поле табличного документа.
//
Процедура ОчиститьТабличноеПоле(ВыбТабличноеПоле)
	
	Для Инд = 0 По ВыбТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ВыбТабличноеПоле.Области[Инд];
		
		Если Тип ("РисунокТабличногоДокумента") = ТипЗнч (ТекущаяОбласть) Тогда
			ТекущаяОбласть.Значение = ""; 
		Иначе	
			
			Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ТекущаяОбласть.СодержитЗначение Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекущаяОбласть.Защита Тогда
				Продолжить;
			КонецЕсли;
			
			Если Найти(Врег(ТекущаяОбласть.Имя), ВРег("Штампик")) > 0  Тогда
				Продолжить;
			КонецЕсли;
			
			ТекущаяОбласть.Очистить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ОчиститьТабличноеПоле()

// Процедура очищает содержимое редактируемых и вычисляемых
// ячеек табличного документа.
//
Процедура Очистить()
	
	// Очищаем содержимое ячеек поля табличного документа
	ТекТабличноеПоле = мОтчетОПринятыхНаРаботу;
	ОчиститьТабличноеПоле(ТекТабличноеПоле);
	
	// Выполняем расчет вычисляемых показателей отчета
	// чтобы очистить соответствующие ячейки
	Расчет();
	
	// Удалим добавленные строки многострочной области
	УдалитьДополнительнуюСтроку(Истина);
	
	// Устанавливаем флаг модифицированности формы
	Модифицированность = Истина;
 	ЗаполнитьСведенияОбОрганизации();
	
	ПоказатьПериод();
	
КонецПроцедуры // Очистить()

Процедура РазбитьПечатнуюФорму(ПечФорма, ИмяФормы = "")
	
	УчитыватьАвтомасштаб = Ложь;

	ПустойТаблДок = Новый ТабличныйДокумент;
	ПустойТаблДок.АвтоМасштаб = УчитыватьАвтомасштаб;
	ПустойТаблДок.ОриентацияСтраницы = ПечФорма.ОриентацияСтраницы;
	ПустойТаблДок.ЧерноБелаяПечать = Истина;
	ПустойТаблДок.ЧерноБелыйПросмотр = Истина;
	ПослВерх = 1;
	Если ЕстьДуплекс Тогда
		НовТаблДок = Новый ТабличныйДокумент();
	КонецЕсли;
	
	Сч = 0;
	Пока Сч < ПечФорма.ВысотаТаблицы Цикл
		 Сч = Сч + 1;
		 
		Если НЕ ПустойТаблДок.ПроверитьВывод(ПечФорма.ПолучитьОбласть(ПослВерх, 0, Сч, 0)) Тогда
			
			// проверим, не должна ли предпоследняя строка печататься вместе со текущей ("не влезшей") строкой.
			// если так, то "откатимся" вверх
			Пока ПослВерх < Сч-1 И ПечФорма.ПолучитьОбласть(Сч-1, , Сч-1, ).Область().ВместеСоСледующим Цикл
			  Сч = Сч - 1;
			КонецЦикла; 
			
			Если НЕ ЕстьДуплекс Тогда
				НовТаблДок = Новый ТабличныйДокумент();
			КонецЕсли;
			НовТаблДок.Вывести(ПечФорма.ПолучитьОбласть(ПослВерх, 0, Сч - 1, 0));
			НовТаблДок.АвтоМасштаб = УчитыватьАвтомасштаб;
			НовТаблДок.ОриентацияСтраницы = ПечФорма.ОриентацияСтраницы;
			НовТаблДок.ЧерноБелаяПечать = Истина;
			НовТаблДок.ЧерноБелыйПросмотр = Истина;
			НовТаблДок.ОтображатьСетку = Ложь;
			НовТаблДок.ОтображатьЗаголовки = Ложь;
			Если НЕ ЕстьДуплекс Тогда
				мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
			КонецЕсли;
			ПослВерх = Сч;
		КонецЕсли;
		
		Если ПечФорма.Область(Сч, 0, Сч, 0).КонецСтраницы Тогда
			Если НЕ ЕстьДуплекс Тогда
				НовТаблДок = Новый ТабличныйДокумент();
			КонецЕсли;
			НовТаблДок.Вывести(ПечФорма.ПолучитьОбласть(ПослВерх, 0, Сч, 0));
			НовТаблДок.АвтоМасштаб = УчитыватьАвтомасштаб;
			НовТаблДок.ОриентацияСтраницы = ПечФорма.ОриентацияСтраницы;
			НовТаблДок.ЧерноБелаяПечать = Истина;
			НовТаблДок.ЧерноБелыйПросмотр = Истина;
			НовТаблДок.ОтображатьСетку = Ложь;
			НовТаблДок.ОтображатьЗаголовки = Ложь;
			Если НЕ ЕстьДуплекс Тогда
				мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
			КонецЕсли;
			ПослВерх = Сч + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПослВерх <= ПечФорма.ВысотаТаблицы Тогда
		Если НЕ ЕстьДуплекс Тогда
			НовТаблДок = Новый ТабличныйДокумент();
		КонецЕсли;
		НовТаблДок.Вывести(ПечФорма.ПолучитьОбласть(ПослВерх, 0, ПечФорма.ВысотаТаблицы, 0));
		НовТаблДок.АвтоМасштаб = УчитыватьАвтомасштаб;
		НовТаблДок.ОриентацияСтраницы = ПечФорма.ОриентацияСтраницы;
		НовТаблДок.ЧерноБелаяПечать = Истина;
		НовТаблДок.ЧерноБелыйПросмотр = Истина;
		НовТаблДок.ОтображатьСетку = Ложь;
		НовТаблДок.ОтображатьЗаголовки = Ложь;
		Если НЕ ЕстьДуплекс Тогда
			мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
		КонецЕсли;
	КонецЕсли;
	Если ЕстьДуплекс Тогда
		мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
	КонецЕсли;
	
КонецПроцедуры

Функция ТаблицаМногострочнойЧастиПуста(СтруктураГруппы, ИдГруппы)
	
	Если СтруктураГруппы.Количество() > мСтруктураИсхКолвоСтрокРазделов[ИдГруппы] Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Инд = 1 По мСтруктураИсхКолвоСтрокРазделов[ИдГруппы] Цикл
		СтрокаГруппы = СтруктураГруппы[Инд - 1];
		Для каждого Колонка Из СтруктураГруппы.Колонки Цикл
			
			Если ЗначениеЗаполнено(СтрокаГруппы[Колонка.Имя]) Тогда
				Возврат Ложь;
			КонецЕсли;
			
		КонецЦикла;		
	
	КонецЦикла;

	Возврат Истина;
	
КонецФункции // ТаблицаМногострочнойЧастиПуста


Процедура ЗначениеВКлетку(Знач Значение, ВыводитьНули, Размер, ИмяПараметра, ТекущаяОбластьМакета, ЗаполнятьПараметр = Ложь)
	
	Пустышка = ?(ВыводитьНули, "0", " ");
	
	ЗначСтр = "";
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		ЗначСтр = Формат(Значение, "ЧГ=");
	Иначе	
		ЗначСтр = Строка(Значение);
	КонецЕсли;
	
	ДлинаЗнач = СтрДлина(ЗначСтр);
	Пока ДлинаЗнач < Размер Цикл
		Если (Размер = 27) или (Размер = 25) или (Размер = 18) или (Размер = 16) или (Размер = 15) или (Размер = 17)  Тогда
			ЗначСтр = ЗначСтр + Пустышка;
		Иначе  
			// дозаполним значение пустышками (слева)
			ЗначСтр = Пустышка + ЗначСтр;
		КонецЕсли;
		ДлинаЗнач = ДлинаЗнач + 1;
	КонецЦикла;
	
	// заполним массив
	Для Ном = 1 По Размер Цикл
		Если ЗаполнятьПараметр Тогда
			ТекущаяОбластьМакета.Параметры[ИмяПараметра + Строка(Ном)] = Сред(ЗначСтр,Ном,1) 
		Иначе	
		    ТекущаяОбластьМакета.Области[ИмяПараметра + Строка(Ном)].Значение = Сред(ЗначСтр,Ном,1) 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура управляет выводом отчета на печать.
//
// Параметры:
//  ВидПечати - строка, задающая способ печати:
//              "ПоказатьБланк" - непосредственный вывод на печать;
//              "ПечататьСразу" - с предварительным просмотром.
//
Процедура Печать(ВидПечати)
	
	мПечатныеФормы.Очистить();
	
	ОбновитьСтруктуруМногострочныхРазделов();
	
	СтруктураГруппы = Неопределено;
	
	мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, СтруктураГруппы);

	Если ТаблицаМногострочнойЧастиПуста(СтруктураГруппы, мИдГруппы1) Тогда		
		Возврат;
	КонецЕсли;
	
	НомерЛиста =0;
	
	ТабДокумент  = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет("ПечатнаяФорма2015");
	
	Шапка  = Макет.ПолучитьОбласть("Шапка");
	ЗначениеВКлетку(мОтчетОПринятыхНаРаботу.Области.HTIN.Значение + "          ", Ложь, 10, "ЕДРПОУ", Шапка, Истина);
	
	Шапка.Параметры.НазваниеОрганизации = мОтчетОПринятыхНаРаботу.Области.HNAME.Значение;
	Шапка.Параметры.Начальная 		= мОтчетОПринятыхНаРаботу.Области.H01.Значение;
	Шапка.Параметры.Отменяющая 		= мОтчетОПринятыхНаРаботу.Области.H02.Значение;
	                                     
	Строка = Макет.ПолучитьОбласть("Строка");
	
	Подвал = Макет.ПолучитьОбласть("Подвал");		
	ЗначениеВКлетку(День(ДатаПодписи),  Истина,    2, "деньПодписи",   Подвал, Истина);
	ЗначениеВКлетку(Месяц(ДатаПодписи), Истина,    2, "месПодписи",    Подвал, Истина);
	ЗначениеВКлетку(Год(ДатаПодписи),   Истина,    4, "годПодписи",    Подвал, Истина);
	ЗначениеВКлетку(мОтчетОПринятыхНаРаботу.Области.HKBOS.Значение + "          ", Ложь, 10, "РуководительИНН", Подвал, Истина);
	ЗначениеВКлетку(мОтчетОПринятыхНаРаботу.Области.HKBUH.Значение + "          ", Ложь, 10, "БухгалтерИНН", Подвал, Истина);
	Подвал.Параметры.РуководительФИО = мОтчетОПринятыхНаРаботу.Области.HBOS.Значение;
	Подвал.Параметры.БухгалтерФИО 	 = мОтчетОПринятыхНаРаботу.Области.HBUH.Значение;
	
	НоваяСтраница = Истина;
	КоличествоСтрок = 1;
	Для каждого СтрокаГруппы Из СтруктураГруппы Цикл
		
		Если НоваяСтраница Тогда
			НомерЛиста = НомерЛиста + 1;
			Шапка.Параметры.НомерЛиста = НомерЛиста;
			
			ТабДокумент.Вывести(Шапка);
			
			КоличествоСтрок = 1;
			НоваяСтраница = Ложь;
		КонецЕсли;
		
		
		Строка.Параметры.НомерСтроки  = КоличествоСтрок;
		
		Строка.Параметры.Категория  = СтрокаГруппы.RG4;
		
		ЗначениеВКлетку(СтрокаГруппы.RG5S + "          ", Ложь,    10, "ДРФО",    Строка, Истина);
		Строка.Параметры.Фамилия  = СтрокаГруппы.RG61S;
		Строка.Параметры.Имя 	  = СтрокаГруппы.RG62S;
		Строка.Параметры.Отчество = СтрокаГруппы.RG63S;
		
		Строка.Параметры.НомерПриказа = СтрокаГруппы.RG7S;
				
		Если ЗначениеЗаполнено(СтрокаГруппы.RG8D) Тогда
		    ЗначениеВКлетку(Формат(СтрокаГруппы.RG8D, "ДФ=ddMMyyyy"), Ложь, 8, "ДатаПриема", Строка, Истина);
		Иначе	
			ЗначениеВКлетку("", Ложь,    8, "ДатаПриема", Строка, Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаГруппы.RG9D) Тогда
		    ЗначениеВКлетку(Формат(СтрокаГруппы.RG9D, "ДФ=ddMMyyyy"), Ложь, 8, "ДатаНачала", Строка, Истина);
		Иначе	
			ЗначениеВКлетку("", Ложь,    8, "ДатаНачала", Строка, Истина);
		КонецЕсли;
		
		ТабДокумент.Вывести(Строка);
		
		Если КоличествоСтрок = 6 Тогда
			ЗначениеВКлетку(КоличествоСтрок, Истина, 2, "КоличествоСтрок", Подвал, Истина);
			ТабДокумент.Вывести(Подвал);
			НоваяСтраница = Истина;
		Иначе
			КоличествоСтрок = КоличествоСтрок + 1;
		КонецЕсли;
	
	КонецЦикла;
	
	Если НЕ НоваяСтраница Тогда
		
		// последняя не полная страница
		
		// дополним пустыми строками
		Строка = Макет.ПолучитьОбласть("Строка");
		Для НомПараметра = 0 По Строка.Параметры.Количество() - 1 Цикл
			Строка.Параметры[НомПараметра] = "";
		КонецЦикла;
		
		Для НомерСтркиПустышки = КоличествоСтрок По 6 Цикл
			Строка.Параметры.НомерСтроки = НомерСтркиПустышки;
			ТабДокумент.Вывести(Строка);
		КонецЦикла;
		
		ЗначениеВКлетку(КоличествоСтрок - 1, Истина, 2, "КоличествоСтрок", Подвал, Истина);
		ТабДокумент.Вывести(Подвал);
	КонецЕсли;
	
	РазбитьПечатнуюФорму(ТабДокумент, НСтр("ru = 'Уведомление'; uk = 'Повідомлення'"));
	
	РегламентированнаяОтчетность.ПроставитьНомераЛистов(ЭтаФорма, ВидПечати, Ложь);

КонецПроцедуры // Печать()


// Формирует структуру данных отчета и помещает в реквизит типа ХранилищеЗначения
// документа РегламентированныйОтчет.
// 
// При первом сохранении данных отчета создает новый документ этого вида.
// Структура данных отчета состоит из элементов:
//   - значения редактируемых реквизитов отчета;
//   - данные по многострочным разделам отчета;
//   - данные по  многостраничным разделам отчета;
//   - данные по вариантам заполнения реквизитов,
//     участвующих в автозаполнении.
//
Функция Сохранить() Экспорт
	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчета(ЭтаФорма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если (мСохраненныйДок = Неопределено) ИЛИ (мСкопированаФорма <> Неопределено) Тогда
		// Отчет новый или был скопирован.
		
		Если мСохраненныйДок = Неопределено Тогда
			// создаем новый документ вида РегламентированныйОтчет
			мСохраненныйДок  = Документы.РегламентированныйОтчет.СоздатьДокумент();
		КонецЕсли;
		
		мСохраненныйДок.Дата = мДатаКонцаПериодаОтчета;
		мСохраненныйДок.УстановитьВремя();
		мСохраненныйДок.УстановитьНовыйНомер();
		
	КонецЕсли;
	
	// установим текущие значения реквизитов документа
	мСохраненныйДок.ИсточникОтчета             = ЭтотОбъект.Метаданные().Имя;
	мСохраненныйДок.НаименованиеОтчета         = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление();
	мСохраненныйДок.ДатаНачала                 = мДатаНачалаПериодаОтчета;
	мСохраненныйДок.ДатаОкончания              = мДатаКонцаПериодаОтчета;
	мСохраненныйДок.ВыбраннаяФорма             = мВыбраннаяФорма;
	мСохраненныйДок.Организация                = Организация;
	мСохраненныйДок.ДатаПодписи                = ДатаПодписи;
	мСохраненныйДок.ЕдиницаИзмерения           = ЕдиницаИзмерения;
	мСохраненныйДок.ТочностьЕдиницыИзмерения   = ТочностьЕдиницыИзмерения;
	мСохраненныйДок.ВидОтчетности              = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	мСохраненныйДок.Комментарий                = Комментарий;
	
	// обновляем структуру многострочных разделов
	ОбновитьСтруктуруМногострочныхРазделов();
	
	// формируем данные редактируемых ячеек таблицы
	ПоказателиОтчета = Новый Структура();
	
	ИмяТекТабличногоПоля    = "ПолеТабличногоДокументаУведомление";
	ТекТабличноеПоле        = мОтчетОПринятыхНаРаботу;
	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
	ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);
	
	
	ИмяТекТабличногоПоля    = "ПолеТабличногоДокументаСлужебныеДанные";
	ТекТабличноеПоле        = мСлужебныеДанные;
	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
	ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);
	
	// формируем список сохранения
	СписокСохранения = Новый Структура();
	
	// вставляем данные редактируемых ячеек таблицы	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	
	// вставляем данные вариантов автозаполнения ячеек
	СписокСохранения.Вставить("ДанныеВариантовАвтоЗаполнения", мСтруктураВариантыЗаполнения);
	
	// вставляем данные многострочных разделов
	СписокСохранения.Вставить("ДанныеМногострочныхРазделов", мСтруктураМногострочныхРазделов);
	
	// вствляем версию формы
	СписокСохранения.Вставить("ВерсияФормы", мВерсияФормы);
	СписокСохранения.Вставить("ДатаНач", ДатаНач);
	СписокСохранения.Вставить("ДатаКон", ДатаКон);
	
	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;
	
	// Сохраняем выбранные листы для печати в хранилище
	ХранилищеДанных = Новый ХранилищеЗначения(мДеревоСтраницОтчета);
	мСохраненныйДок.ДеревоНастройкиСтраниц = ХранилищеДанных;
	
	// записываем документ, хранящий данные отчета
	мСохраненныйДок.Записать();
	Модифицированность = Ложь;
	
	Возврат Истина;	
	
КонецФункции // Сохранить()

// Восстанавливает сохраненные данные отчета.
//
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногострочныхРазделов;
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем ВерсияФормы;
	
	// восстанавливаем реквизиты отчета
	Организация              = мСохраненныйДок.Организация;
	
	ЭтаФорма.ЭлементыФормы.НадписьНазваниеОрганизации.Заголовок = Организация.Наименование;
	
	ДатаПодписи              = мСохраненныйДок.ДатаПодписи;
	ЕдиницаИзмерения         = мСохраненныйДок.ЕдиницаИзмерения;
	ТочностьЕдиницыИзмерения = мСохраненныйДок.ТочностьЕдиницыИзмерения;
	Комментарий              = мСохраненныйДок.Комментарий;
	
	// восстанавливаем сохраненные данные отчета
	СписокСохранения = мСохраненныйДок.ДанныеОтчета.Получить();
	
	Если НЕ СписокСохранения.Свойство("ДатаНач", ДатаНач) Тогда
		ДатаНач = ДатаПодписи;
	КонецЕсли;
	
	Если НЕ СписокСохранения.Свойство("ДатаКон", ДатаКон) Тогда
		ДатаКон = ДатаПодписи;
	КонецЕсли;
	
	// восстанавливаем сохраненные данные многострочных разделов
	Если СписокСохранения.Свойство( "ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов) Тогда
		мСтруктураМногострочныхРазделов = ДанныеМногострочныхРазделов;
		
		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы       = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭлементСтруктуры.Значение;
			
			ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
		КонецЦикла;
	КонецЕсли;
	
	
	// восстанавливаем сохраненные данные вариантов автозаполнения ячеек
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) Тогда
		мСтруктураВариантыЗаполнения = ДанныеВариановАвтоЗаполнения;
	КонецЕсли;
	
	// восстанавливаем версию формы
	Если СписокСохранения.Свойство("ВерсияФормы", ВерсияФормы) Тогда
		Если ВерсияФормы <> мВерсияФормы Тогда
			// Версия формы изменилась.
			// Обновляем пользовательские настройки.
			ОбновитьПользовательскиеНастройки(СписокСохранения);
		КонецЕсли;
	КонецЕсли;
	
	// восстановим сохраненные данные редактируемых ячеек
	СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета );
	
	Для Каждого ПоказателиСтраницы Из ПоказателиОтчета Цикл
		ИмяТекТабличноеПоле = ПоказателиСтраницы.Ключ;
		ТекТабличноеПоле    = ЭлементыФормы[ИмяТекТабличноеПоле];
		ПоказателиТаблПоле  = ПоказателиСтраницы.Значение;
		
		Для Каждого Показатель Из ПоказателиТаблПоле Цикл
			ИмяПоказателя       = Показатель.Ключ;
			ЗначениеПоказателя  = Показатель.Значение;
			
			// установим значение в таблице
			Попытка
				ТекТабличноеПоле.Области[ИмяПоказателя].Значение = ЗначениеПоказателя;
			Исключение
			КонецПопытки;
		КонецЦикла;
	КонецЦикла;
	
	Расчет();
	
КонецПроцедуры // ВосстановитьСохраненныеДанные()

// Выполняет расчет вычисляемых показателей отчета
// (ячеек, выделенных зеленым цветом).
//
Процедура Расчет()	
	Возврат;	
КонецПроцедуры // Расчет()

Функция ПолучитьОбластьМногострочногоРаздела(ИдГруппы, Колонка)
	Перем СтруктураГруппы;
	
	мСтруктураМногострочныхРазделов.Свойство(ИдГруппы, СтруктураГруппы);
	КолСтрок = СтруктураГруппы.Количество();
	Префикс = ПолучитьПрефикс(КолСтрок);	
	
	КодКолонки = Колонка - 1;
	ИмяНужнойЯчейки = Сред(СтруктураГруппы.Колонки[КодКолонки].Имя,1,1) + Префикс + КолСтрок + Сред(СтруктураГруппы.Колонки[КодКолонки].Имя,2,СтрДлина(СтруктураГруппы.Колонки[КодКолонки].Имя)-1);	
	Возврат мОтчетОПринятыхНаРаботу.Области[ИмяНужнойЯчейки];
	
КонецФункции	

// Процедура выполняет заполнение отчета автоматически по данным ИБ.
//
Процедура ЗаполнитьАвто() Экспорт     
	
	КвоСтрок = 0;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФИОФизЛиц.Фамилия КАК Фамилия,
	               |	ФИОФизЛиц.Имя КАК Имя,
	               |	ФИОФизЛиц.Отчество КАК Отчество,
	               |	РаботникиОрганизаций.Сотрудник.Физлицо.КодПоДРФО КАК ИНН,
	               |	РаботникиОрганизаций.Регистратор.Ссылка КАК Приказ,
	               |	РаботникиОрганизаций.Регистратор.Дата КАК ДатаПриказа,
	               |	ВЫБОР
	               |		КОГДА РаботникиОрганизаций.Сотрудник.ВидЗанятости = &ВидЗанятостиОсновное
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК Категория,
	               |	РаботникиОрганизаций.Период КАК ДатаНачала
	               |ИЗ
	               |	РегистрСведений.РаботникиОрганизаций.СрезПервых(, Организация = &Организация) КАК РаботникиОрганизаций
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаОкончания, ) КАК ФИОФизЛиц
	               |		ПО (ФИОФизЛиц.ФизЛицо = РаботникиОрганизаций.Сотрудник.Физлицо)
	               |ГДЕ
				   |	РаботникиОрганизаций.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
				   |";
	
	
	Запрос.УстановитьПараметр("ДатаНачала",    НачалоДня(ДатаНач));
	//Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаПодписи));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаКон));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.УстановитьПараметр("ВидЗанятостиОсновное", Перечисления.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Результат = РезультатЗапроса.Выбрать();
		
		Пока Результат.Следующий() Цикл
			Если КвоСтрок > 0 Тогда
				ВставитьДополнительнуюСтроку(мИдГруппы1); 		
			КонецЕсли;
			
			КвоСтрок = КвоСтрок + 1;
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 2);
			ОбластьЯчейки.Значение = Результат.Категория;      		   
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 3);	   
			ОбластьЯчейки.Значение = Результат.ИНН;      		   
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 4);	   
			ОбластьЯчейки.Значение = Результат.Фамилия;      		   
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 5);
			ОбластьЯчейки.Значение = Результат.Имя;
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 6);
			ОбластьЯчейки.Значение = Результат.отчество;
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 7);
			ОбластьЯчейки.Значение = ОбщегоНазначения.ПолучитьНомерНаПечать(Результат.Приказ);
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 8);
			ОбластьЯчейки.Значение = Результат.ДатаПриказа;
			
			ОбластьЯчейки = ПолучитьОбластьМногострочногоРаздела(мИдГруппы1, 9);
			ОбластьЯчейки.Значение = Результат.ДатаНачала;
			
		КонецЦикла;   
		
	КонецЕсли;
	
	ОбновитьСтруктуруМногострочныхРазделов();	
	
	Расчет();
	
	Модифицированность = Истина;
	
КонецПроцедуры // ЗаполнитьАвто()

Процедура ЗаполнитьНачальныеНастройки(СоставПоказателей) Экспорт
	
	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет(СоставПоказателей);
	
	ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);
	
	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);
	
	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);
	
	ОписаниеТиповЧисло15   = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);
	
	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл
		
		ТекОбласть    = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы   = ТекОбласть.Имя;
		
		// Таблица значений содержит информацию о вариантах заполнения показателя отчета.
		// В колонках таблицы значений отображается следуящая информация:
		//   - имя ячейки (код показателя);
		//   - вариант заполнения ячейки;
		//   - вычисленное по данным ИБ значение ячейки;
		//   - величина автокорректировки значения ячейки;
		//   - примечание к ячейке.
		//
		ТаблицаВариантыЗаполнения = Новый ТаблицаЗначений;
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока15  );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );
		
		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета
			
			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;
			
			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;
			
			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;
			
			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// признак многострочности определяется по третьей колонке макета
			ПризнМногострочность = СокрЛП(МакетСоставаПоказателей.Область(Ном, 3).Текст);
			// по четвертой колонке определяется тип данных реквизита
			ТипДанныхРеквизита   = СокрЛП(МакетСоставаПоказателей.Область(Ном, 4).Текст);
			// по колонке 5 определяется исходное количество строк многострочного раздела
			стрКолСтрокИсходный  = СокрЛП(МакетСоставаПоказателей.Область(Ном, 5).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 6).Текст);
			
			чКолСтрокИсходный    = ? (ПустаяСтрока(стрКолСтрокИсходный),  0, Число(стрКолСтрокИсходный));
			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));
			
			// формируем таблицу значений, содержащей состав показателей отчета
			НоваяСтрока = мТаблицаСоставПоказателей.Добавить();
			НоваяСтрока.ИмяПоляТаблДокумента   = ИмяСтраницы;
			НоваяСтрока.КодПоказателяПоСоставу = КодПоказателя;
			НоваяСтрока.КодПоказателяПоФорме   = КодПоказателяПоФорме;
			НоваяСтрока.ПризнМногострочности   = ПризнМногострочность;
			НоваяСтрока.ТипДанныхПоказателя    = ТипДанныхРеквизита;
			
			Если чВариантЗаполнения <> 0 Тогда
				// Если в колонке 6 задан варианет заполнения показателя, 
				// то его значение может определяться по данным ИБ.
				// Дополняем таблицу значений вариантов заполнения
				НоваяСтрока = ТаблицаВариантыЗаполнения.Добавить();
				НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
				НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТаблицаВариантыЗаполнения.Количество() > 0 Тогда
			// имеются автоматически заполняемые ячейки
			
			// Для обеспечения возможности назначения разных вариантов заполнения одного и того же показателя
			// на разных страницах, создаем массив, хранящий информацию о вариантах заполнения ячеек по каждой
			// странице многостраничного раздела.
			МассивВариантовЗаполненияСтраниц = Новый Массив;
			МассивВариантовЗаполненияСтраниц.Добавить(ТаблицаВариантыЗаполнения);
			
			// Создаем структуру, содержащей варианты заполнения показателей отчета.
			//
			// В качестве ключа используется имя страницы основной панели формы, содержащей
			//   табличный документ с автоматически заполняемыми показателями.
			// В качестве значения принимаем Массив из таблицы значений - вариантов заполнения
			//   показателей по каждой странице многостраничного раздела.
			//
			мСтруктураВариантыЗаполнения.Вставить(ИмяСтраницы, МассивВариантовЗаполненияСтраниц);
		Конецесли;
		
	КонецЦикла;
	
	ЭлементыФормы.ПолеТабличногоДокументаУведомление.ОбластьПечати = ЭлементыФормы.ПолеТабличногоДокументаУведомление.Области.Найти("ПредупреждениеОПечати");
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

//// Процедура управляет доступностью полей указания параметров заполнения ячеек,
//// которые могут быть заполнены при автозаполнении отчета.
////
//// Параметры:
////  Вариант         - число, вариант заполнения ячейки;
////  Текущая область - область ячеек табличного документа, ссылка на ячейку.
////
//Процедура ДоступностьЭлементовВариантаЗаполнения(Вариант = Неопределено, ТекущаяОбласть = Неопределено)
//	
//	//ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
//	ИмяТекущейСтраницыПанели = "Уведомление";
//	
//	Если Не мСтруктураВариантыЗаполнения.Свойство(ИмяТекущейСтраницыПанели) Тогда
//		Возврат;
//	КонецЕсли;
//	
//	ДоступностьПолеВыбораВариантЗаполнения    = Ложь;
//	ДоступностьПолеВводаЗначениеКорректировки = Ложь;
//	ДоступностьПолеВводаКомментарий           = Ложь;
//	
//	Если Вариант <> Неопределено Тогда
//		ДоступностьПолеВыбораВариантЗаполнения            = Истина;
//		Если Вариант <> 3 Тогда
//			ДоступностьПолеВводаКомментарий               = Истина;
//			Если Вариант = 2 Тогда
//				ДоступностьПолеВводаЗначениеКорректировки = Истина;
//			КонецЕсли;
//		КонецЕсли;
//		
//	Иначе
//		ОписаниеЯчейки = "";
//		Если ТекущаяОбласть.ЦветФона = Новый Цвет(192, 220, 192) Тогда
//			ОписаниеЯчейки    = НСтр("ru='Значение вычисляется автоматически';uk='Значення обчислюється автоматично'");
//		ИначеЕсли ТекущаяОбласть.ЦветФона = Новый Цвет(255, 255, 192) Тогда
//			ОписаниеЯчейки    = НСтр("ru='Значение вводится вручную';uk='Значення вводиться вручну'");
//		КонецЕсли;
//		
//		ЭлементыФормы["ПолеВыбораВариантЗаполнения"    + ИмяТекущейСтраницыПанели].Значение = "";
//		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = 0;
//		ЭлементыФормы["ПолеВводаКомментарий"           + ИмяТекущейСтраницыПанели].Значение = ОписаниеЯчейки;
//		
//	КонецЕсли;
//	
//	ЭлементыФормы["ПолеВыбораВариантЗаполнения"    + ИмяТекущейСтраницыПанели].Доступность = ДоступностьПолеВыбораВариантЗаполнения;
//	ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Доступность = ДоступностьПолеВводаЗначениеКорректировки;
//	ЭлементыФормы["ПолеВводаКомментарий"           + ИмяТекущейСтраницыПанели].Доступность = ДоступностьПолеВводаКомментарий;
//	
//КонецПроцедуры // ДоступностьЭлементовВариантаЗаполнения()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
		
	// выполняем начальные настройки
	ЗаполнитьНачальныеНастройки("СоставПоказателей2015");
	
	// формируем список элементов подменю, связанное с кнопкой <Печать>
	ЭлементМеню = ЭлементыФормы.КоманднаяПанельДействий.Кнопки.ПодменюПечати;
	Если ЭлементМеню.Кнопки.Количество() = 0 Тогда
		ЭлементМеню.Кнопки.Добавить("ПечататьСразу", ТипКнопкиКоманднойПанели.Действие,
		НСтр("ru='Печатать сразу';uk='Друкувати відразу'"), Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
		
		ЭлементМеню.Кнопки.Добавить("ПоказатьБланк", ТипКнопкиКоманднойПанели.Действие,
	                            НСтр("ru='Предварительный просмотр';uk='Попередній перегляд'"), Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
								
		ЭлементМеню.Кнопки.Добавить("Разделитель1", ТипКнопкиКоманднойПанели.Разделитель);

		ЭлементМеню.Кнопки.Добавить("ЕстьДуплекс", ТипКнопкиКоманднойПанели.Действие,
		                            НСтр("ru='Принтер поддерживает автом. дуплекс';uk='Принтер підтримує автом. дуплекс'"), Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
									
		ЭлементМеню.Кнопки.ЕстьДуплекс.Пометка = ЕстьДуплекс;
	КонецЕсли;							
	
	ЛинияСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 2);
	ЛинияПусто = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	ЭтаФорма.ЭлементыФормы.НадписьНазваниеОрганизации.Заголовок = Организация.Наименование;
	
	мМассивТиповЛиний = Новый Массив();
	мМассивТиповЛиний.Вставить(0, ЛинияПусто);
	мМассивТиповЛиний.Вставить(1, ЛинияСнизу);
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	Перем ИсходноеКоличествоСтрокГруппы;
	
	ЭтаФорма.АвтоЗаголовок = истина;
	
	мОтчетОПринятыхНаРаботу = ЭлементыФормы.ПолеТабличногоДокументаУведомление;			
	мСлужебныеДанные = ЭлементыФормы.ПолеТабличногоДокументаСлужебныеДанные;			
	
	ТекТабличноеПоле = мОтчетОПринятыхНаРаботу;
	
	Если мСохраненныйДок = Неопределено Тогда
		
		// создаем исходную структуру многострочных разделов
		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭлементСтруктуры.Значение;
			ТекТабличноеПоле = мОтчетОПринятыхНаРаботу;
			мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
			Для НСтр = 1 По  ИсходноеКоличествоСтрокГруппы Цикл
				СтрокаГруппы = СтруктураГруппы.Добавить();
				Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
					ИмяКолонки    = Колонка.Имя;
					Префикс = ПолучитьПрефикс(Нстр);
					КодПоказателя    = Сред (ИмяКолонки,1,1) + Префикс + НСтр + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
					ОбластьЯчейки = ТекТабличноеПоле.Области[КодПоказателя];
					Если ОбластьЯчейки.СодержитЗначение Тогда
						ЗначениеЯчейки = ОбластьЯчейки.Значение;
					Иначе
						ЗначениеЯчейки = ОбластьЯчейки.Текст;
					КонецЕсли;
					СтрокаГруппы[ИмяКолонки] = ЗначениеЯчейки;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		Если мСкопированаФорма <> Неопределено Тогда
			// документ скопирован
			ВосстановитьСохраненныеДанные();
		Иначе
			
			ЭлементыФормы.НадписьНазваниеОрганизации.Заголовок =Организация.Наименование;
			
			ЗаполнитьСведенияОбОрганизации();
			
			ДатаНач = ДатаПодписи;	
			ДатаКон = ДатаПодписи;

		КонецЕсли;
		
	Иначе
		// При открытии или при копировании сохраненного
		// отчета восстанавливаем сохраненные данные.
		ВосстановитьСохраненныеДанные();
		
	КонецЕсли;
	
	ПоказатьПериод();
	мОтчетОПринятыхНаРаботу.ТолькоПросмотр = Истина;			
	мСлужебныеДанные.ТолькоПросмотр = Истина;
	
	УстановитьЦветФонаЯчейки(,,"Уведомление");		
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПередЗакрытием" формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность = Истина Тогда
		
		Ответ = Вопрос(НСтр("ru='Данные были изменены. Сохранить изменения ?';uk='Дані були змінені. Зберегти зміни ?'"), РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Если НЕ Сохранить() Тогда
				Отказ = Истина;
			КонецЕсли;
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
    СохранитьЗначение("РегламентированныеОтчеты_ЕстьДуплекс", ЕстьДуплекс);
	
КонецПроцедуры // ПередЗакрытием()



////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Заполнить" 
// командной панели формы.
//
Процедура КоманднаяПанельФормыЗаполнить(Кнопка)
	
	Очистить();
	
	ЗаполнитьСведенияОбОрганизации();
	
	// Удалим добавленные строки многострочной области
	УдалитьДополнительнуюСтроку(Истина);
	
	ЗаполнитьАвто();
	
КонецПроцедуры // КоманднаяПанельФормыЗаполнить

// Процедура вызывается при нажатии кнопки "Очистить".
//
Процедура КоманднаяПанельФормыОчистить(Кнопка)
	
	Очистить();
	
КонецПроцедуры // КоманднаяПанельФормыОчистить()

// Процедура вызывается при выборе пункта подменю "Печать" командной панели
// действий формы. 
// Отрабатывает выбор печатной формы.
//
Процедура КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы(Кнопка)
	
	Если Кнопка <> Неопределено Тогда
		Если Найти(Кнопка.Имя, "Дуплекс") >0 Тогда
		     ЕстьДуплекс    = НЕ Кнопка.Пометка;
			 Кнопка.Пометка = НЕ Кнопка.Пометка;
		Иначе
			 Печать(Кнопка.Имя);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры // КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы()

// Процедура вызывается нажатием кнопки "ОК" командной панели действий формы.
// Выполняет сохранение данных отчета и закрывает текущую форму.
//
Процедура КоманднаяПанельДействийДействиеСохранитьИЗакрыть(Кнопка)
	
	Если Сохранить() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // КоманднаяПанельДействийДействиеСохранитьИЗакрыть()

// Процедура вызывается нажатием кнопки "Записать" командной панели действий формы.
// Выполняет сохранение данных отчета.
//
Процедура КоманднаяПанельДействийДействиеЗаписать(Кнопка)
	
	Сохранить();
	
КонецПроцедуры // КоманднаяПанельДействийДействиеЗаписать()

// Процедура - обработчик события "ПриИзменении" поля ввода даты
// представления отчета.
//
Процедура ПолеВводаДатаСдачиПриИзменении(Элемент)
	
	Если ДатаНач > ДатаКон Тогда
		ДатаКон	= ДатаНач; 
	КонецЕсли;
	
	мДатаНачалаПериодаОтчета = НачалоМесяца(ДатаПодписи);
	мДатаКонцаПериодаОтчета = КонецМесяца(ДатаПодписи);
	
	мСлужебныеДанные.Области.PERIOD_MONTH.Значение = Месяц(мДатаНачалаПериодаОтчета);
	мСлужебныеДанные.Области.PERIOD_YEAR.Значение  = Год(мДатаНачалаПериодаОтчета);
	
	ПоказатьПериод();
	
	//	мОтчетФССУтратаТрудосп.Области.ДатаПодписиОтчета.Значение = ДатаПодписи;
	Модифицированность = Истина;
	
КонецПроцедуры // ПолеВводаДатаСдачиПриИзменении()

// Процедура вызывается при активизации поля табличного документа.
//   Определяет текущую область табличного документа и вызывает процедуру 
// управления доступностью кнопок "Добавить" и "Удалить" на странице формы.
//
Процедура ПолеТабличногоДокументаУведомлениеПриАктивизацииОбласти(Элемент)
	
	ТекущаяОбласть = Элемент.ТекущаяОбласть;
	ДоступностьКнопокДобавитьУдалить(ТекущаяОбласть);
	
КонецПроцедуры // ПолеТабличногоДокументаОтчетПринятыхРаботникахПриАктивизацииОбласти()

Процедура ДоступностьКнопокДобавитьУдалить(ТекущаяОбласть = Неопределено)

	ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
	ИмяТекущейСтраницыПанели = ТекущаяСтраницаПанели.Имя;

	Если Не ТекущаяОбласть = Неопределено Тогда
		КнопкаДобавитьУдалить = ЭлементыФормы.Найти("КнопкаУдалитьДопСтроки" + ИмяТекущейСтраницыПанели);

		Если КнопкаДобавитьУдалить <> Неопределено Тогда
			Если РазделТекущейОбласти( ТекущаяОбласть ) = Неопределено Тогда
				КнопкаДобавитьУдалить.Доступность = Ложь;
			Иначе
				КнопкаДобавитьУдалить.Доступность = Истина;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ДоступностьКнопокДобавитьУдалить()

Функция РазделТекущейОбласти(ТекущаяОбласть)

	Если ТекущаяОбласть = Неопределено Тогда
		Возврат Неопределено; // отсутствует активная область табличного документа
	КонецЕсли;
	
	ИмяТекущейОбласти   = ТекущаяОбласть.Имя;
	Если    Лев(ИмяТекущейОбласти,1) = "R"
		 И Сред(ИмяТекущейОбласти,8,1) = "G" Тогда
		ИмяАктивногоРаздела = Сред(ИмяТекущейОбласти,1,1) + Сред(ИмяТекущейОбласти,8) ;
	КонецЕсли;

	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = ЭлементСтруктуры.Значение;

		Если СтруктураГруппы.Колонки.Найти(ИмяАктивногоРаздела) <> Неопределено Тогда
			Возврат ИмяГруппы;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;

КонецФункции // РазделТекущейОбласти()

//// Процедура - обработчик события "ПриИзменении" значения в поле ввода
//// величины автокорректировки.
////
//Процедура ПолеВводаЗначениеКорректировкиУведомлениеПриИзменении(Элемент)
//	
//	Дельта = Элемент.Значение;
//	ИзменитьЗначениеАвтокорректировки(Дельта);
//	
//	Модифицированность = Истина;
//	
//КонецПроцедуры // ПолеВводаЗначениеКорректировкиПриИзменении

Функция ПолучитьПрефикс(ТекущееЧисло)
	
	ТекЧислоСтр = Формат(ТекущееЧисло, "ЧГ=0");
	Префикс = ЛЕВ("000000", 6 - СтрДлина(ТекЧислоСтр));
	
	Возврат Префикс;
	
КонецФункции

// Обновляет структуру многострочных разделов.
// Заполняет таблицу значений многострочного раздела
// значениями соответствующих ячеек.
// похоже унифицирована и актуальна только при наличии таких разделов
Процедура ОбновитьСтруктуруМногострочныхРазделов()
	// Заполняет таблицу значений многострочного раздела
	// указанными в соответствующих ячейках значениями
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = ЭлементСтруктуры.Значение;
		ТекТабличноеПоле = мОтчетОПринятыхНаРаботу;		
		
		Для НСтр = 1 По СтруктураГруппы.Количество() Цикл
			ТекСтрокаТаблицы = СтруктураГруппы[НСтр - 1];
			Префикс = ПолучитьПрефикс(НСтр);
			НСтрНов = НСтр + 1;
			ПрефиксНов = ПолучитьПрефикс(НСтрНов);
			Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
				ИмяКолонки    = Колонка.Имя;
				ИмяЯчейки    = Сред (ИмяКолонки,1,1) + Префикс + НСтр + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
				ИмяНовОбластиЯчейки = Сред (ИмяКолонки,1,5) + ПрефиксНов + НСтрНов + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
				ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяЯчейки];
				Если ОбластьЯчейки.СодержитЗначение Тогда
					ЗначениеЯчейки = ОбластьЯчейки.Значение;
				Иначе
					ЗначениеЯчейки = ОбластьЯчейки.Текст;
				КонецЕсли;
				ТекСтрокаТаблицы[ИмяКолонки] = ЗначениеЯчейки;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // ОбновитьСтруктуруМногострочныхРазделов()

Процедура ВставитьДополнительнуюСтроку(ИмяГруппы)
	Перем СтруктураГруппы;	
	
	ТекТабличноеПоле = мОтчетОПринятыхНаРаботу;
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	// определяем последнюю строку многострочной группы
	КолСтрок = СтруктураГруппы.Количество();
	Префикс = ПолучитьПрефикс(КолСтрок);
	ИмяПервойЯчейкиПоследнейСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Префикс + КолСтрок + Сред(СтруктураГруппы.Колонки[0].Имя,2,3);
	// область первой ячейки последней строки многострочного раздела
	ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];
	
	// номера строк исходной области
	НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
	НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
	
	СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;
	
	// номера строк вставляемой области
	НомерВерхЯчейкиНовойОбласти = НомерВерхЯчейкиОбласти + СдвигСтрок;
	НомерНижнЯчейкиНовойОбласти = НомерНижнЯчейкиОбласти + СдвигСтрок;
	
	// определяем вставляемую область
	ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
	
	// добавляем в таблицу новую строку
	ТекТабличноеПоле.ВставитьОбласть( ВставляемаяОбласть,,ТипСмещенияТабличногоДокумента.ПоВертикали );
	КолСтрокНов = КолСтрок + 1;
	// переопределяем имена вставленной области
	Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
		ИмяКолонки          = Колонка.Имя;
		ИмяОбластиЯчейки    = Сред (ИмяКолонки,1,1) + Префикс + КолСтрок + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
		ПрефиксНов = ПолучитьПрефикс(КолСтрокНов);
		ИмяНовОбластиЯчейки = Сред (ИмяКолонки,1,1) + ПрефиксНов + (КолСтрокНов) + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];
		ОбластьЯчейки.Имя = ИмяНовОбластиЯчейки;
		
		// определяем границы ячейки исходной области
		НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
		НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
		НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
		НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;
		
		// Назначаем имя ячейки исходной области,
		// (т.к. в результате вставки новой области оно было очищено)
		ОбластьЯчейкиИсходнойОбласти = ТекТабличноеПоле.Область(НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
		ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЯчейки;
		
		// очищаем значения ячеек вставленной области
		Если (ОбластьЯчейки.СодержитЗначение)  Тогда
			ОбластьЯчейки.Очистить();
		Иначе
			// порядековый номер последней строки перед вставкой
			ИсходныйНомер        = Число(ОбластьЯчейки.Текст);
			НовыйПорядковыйНомер = ИсходныйНомер + 1;
			ОбластьЯчейки.Текст  = Строка(НовыйПорядковыйНомер);
		КонецЕсли;
	КонецЦикла;
	Если (ИмяГруппы = мИдГруппы1) Тогда 
		ОбластьТекстовыйНомер = ТекТабличноеПоле.Область(ОбластьЯчейки.Верх,2,ОбластьЯчейки.Верх,2 );
		//	ОбластьТекстовыйНомер.Текст = Строка(КолСтрок + 1);
	КонецЕсли;
	// Назначим текущей областью таблицы первую редактируемую ячейку добавленной строки.
	// Определяем текущую область таблицы
	
	ИмяТекущейОбласти = Сред (СтруктураГруппы.Колонки[0].Имя,1,1) + ПрефиксНов + (КолСтрокНов) + Сред (СтруктураГруппы.Колонки[0].Имя,2,3);
	ТекТабличноеПоле.ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	
	// установим фокус на поле табличн. документа
	ТекущийЭлемент = ТекТабличноеПоле;
	
	// добавляем в структуру многострочной группы новую строку
	СтруктураГруппы.Добавить();	
	Модифицированность = Истина;
КонецПроцедуры // ВставитьДополнительнуюСтроку()

// Удаляет строку многострочного раздела формы.
//
Процедура УдалитьДополнительнуюСтроку(Очистка = Ложь)
	Перем ИсходноеКоличествоСтрокГруппы;
	Перем СтруктураГруппы;
	
	// Сначала выполним проверку на активную страницу панели формы,
	// которая содержит многострочный раздел
	ИмяГруппы = мИдГруппы1;
	
	// исходное количество строк группы
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	ТекТабличноеПоле = мОтчетОПринятыхНаРаботу;
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	// Выполним также проверку вхождения текущей (выделенной) ячейки в многострочный раздел
	ТекущаяОбласть = ТекТабличноеПоле.ТекущаяОбласть;
	ИмяЯчейки      = ТекущаяОбласть.Имя;
	
	//Если ИмяГруппы = Неопределено Тогда
	//	Сообщить(НСтр("ru='Не выбрана удаляемая строка многострочного раздела.';uk='Не обрано строку багаторядкового розділу, що видаляється'"), СтатусСообщения.Внимание);
	//	Возврат;
	//КонецЕсли;

	// предварительно обновим структуру многострочных разделов
	ОбновитьСтруктуруМногострочныхРазделов();
	
	// номер строки многострочного раздела определим
	// по имени выделенной ячейки
	НомУдаляемойСтроки = СтруктураГруппы.Количество();
	
	Если Очистка Тогда
		Пока НомУдаляемойСтроки > 1 Цикл
			НомерСтрокиТаблицы = НомУдаляемойСтроки - 1; // нумерация строк в таблице значений начинается с нуля    
			
			Если СтруктураГруппы.Количество() > ИсходноеКоличествоСтрокГруппы Тогда
				// удаляется добавленная строка
				Префикс = ПолучитьПрефикс(НомУдаляемойСтроки);
				ИмяПервойЯчейкиУдаляемойСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Префикс + НомУдаляемойСтроки + Сред(СтруктураГруппы.Колонки[0].Имя,2,СтрДлина(СтруктураГруппы.Колонки[0].Имя)-1);
				
				// область первой ячейки удаляемой строки многострочного раздела
				ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
				
				// номера строк удаляемой области
				НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
				НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
				
				// Определим область удаляемой строки
				УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
				
				// непосредственно удаляем область строки табличного документа
				ТекТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
				
				КолвоСдвигаемыхСтрок = СтруктураГруппы.Количество() - НомУдаляемойСтроки;
				
				// обновим имена ячеек сдвинутых строк
				Если КолвоСдвигаемыхСтрок > 0 Тогда
					Для НСтр = НомУдаляемойСтроки + 1 По СтруктураГруппы.Количество() Цикл
						ТекСтрока = СтруктураГруппы[НСтр-1];
						НСтрНов = НСтр - 1;
						Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
							ПрефиксНов = ПолучитьПрефикс(НСтрНов);
							ИмяСдвинутойЯчейки    = Сред (Колонка.Имя,1,1) + Префикс + НСтр + Сред (Колонка.Имя,2,СтрДлина(Колонка.Имя)-1);
							НовоеИмяСдвинутойЯчейки = Сред (Колонка.Имя,1,1) + ПрефиксНов + (НСтрНов) + Сред (Колонка.Имя,2,СтрДлина(Колонка.Имя)-1);
							ОбластьСдвинутойЯчейки     = ТекТабличноеПоле.Области[ИмяСдвинутойЯчейки];
							ОбластьСдвинутойЯчейки.Имя = НовоеИмяСдвинутойЯчейки;
							
							Если НЕ(ОбластьСдвинутойЯчейки.СодержитЗначение) Тогда
								// обновляем порядковый номер строки
								СтарыйНомер = Число(ТекСтрока[Колонка.Имя]);
								НовыйПорядковыйНомер = Строка(Макс(СтарыйНомер - 1, 0));
								ОбластьСдвинутойЯчейки.Текст = НовыйПорядковыйНомер;
								// обновляем значение элемента таблицы значений
								ТекСтрока[Колонка.Имя] = НовыйПорядковыйНомер;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
				
				// обновляем структуру многострочного раздела
				СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
			Иначе
				// строка не удаляется в форме табличного документа,
				// а должна осуществляться сдвиг строк
				
				// обновляем структуру многострочного раздела
				// методом удаления выбранной строки и добавления пустой строки
				СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
				ДобавляемаяСтрока = СтруктураГруппы.Добавить();
				
				// обновляем порядковые номера строк, выводимых в табличный документ
				Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
					Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл
						
						НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
						НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
						Префикс = ПолучитьПрефикс(НомерСтрокиВФормеТаблицы);
						ИмяЯчейки    = Сред (КолонкаТаблицы.Имя,1,1) + Префикс + НомерСтрокиВФормеТаблицы + Сред(КолонкаТаблицы.Имя,2,СтрДлина(КолонкаТаблицы.Имя)-1);
						ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
						
						Если НЕ(ОбластьЯчейкиТаблицы.СодержитЗначение) Тогда
							ПервыйНомерВГруппе = 1;
							// обновляем порядковый номер строки
							НовыйПорядковыйНомер = Строка(ПервыйНомерВГруппе + НомерСтрокиВТаблице);
							СтрокаТаблицы[КолонкаТаблицы.Имя] = НовыйПорядковыйНомер;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
				
				// Обновляем экранную форму табличного документа
				ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
			КонецЕсли;
			
			// назначим текущей областью таблицы первую редактируемую ячейку
			// удаленной строки, если ее порядковый номер меньше кол-ва строк
			// многострочного раздела, или последней строки раздела в противном случае
			
			// определяем текущую область таблицы
			НомТекущейСтроки  = Мин( НомУдаляемойСтроки, СтруктураГруппы.Количество() );
			Префикс = ПолучитьПрефикс(НомТекущейСтроки);
			ИмяТекущейОбласти    = Сред (СтруктураГруппы.Колонки[0].Имя,1,1) + Префикс + НомТекущейСтроки + Сред (СтруктураГруппы.Колонки[0].Имя,2,СтрДлина(СтруктураГруппы.Колонки[0].Имя)-1);
			
			ТекТабличноеПоле.ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
			
			// установим фокус на поле табличн. документа
			ТекущийЭлемент = ТекТабличноеПоле;
			
			НомУдаляемойСтроки = СтруктураГруппы.Количество();
			
			Модифицированность = Истина;
			
		КонецЦикла;		
	Иначе
		
		Попытка
				НомУдаляемойСтроки = Число(Сред(ИмяЯчейки,2,6));
				КоличествоСтрокПослеУдаления = СтруктураГруппы.Количество() -1;
				Ответ = Вопрос(НСтр("ru='Удалить выбранную строку?"
				"(Внимание! данная операция необратима)';uk='Видалити обраний рядок?"
				"(Увага! дана операція необоротна)'"), РежимДиалогаВопрос.ДаНетОтмена, 60, КодВозвратаДиалога.Да);

		  		Если Ответ <> КодВозвратаДиалога.Да Тогда
					Возврат;
				КонецЕсли;

		Исключение
			Ответ = Вопрос(НСтр("ru='Удалить последнюю строку?"
								"(Внимание! данная операция необратима)';uk='Видалити останній рядок?"
								"(Увага! дана операція необоротна)'"), РежимДиалогаВопрос.ДаНетОтмена, 60, КодВозвратаДиалога.Да);

				Если Ответ <> КодВозвратаДиалога.Да Тогда
					Возврат;
				КонецЕсли;
			НомУдаляемойСтроки = СтруктураГруппы.Количество();
		КонецПопытки;
			
		НомерСтрокиТаблицы = НомУдаляемойСтроки - 1; // нумерация строк в таблице значений начинается с нуля    
		
		Если СтруктураГруппы.Количество() > ИсходноеКоличествоСтрокГруппы Тогда
			// удаляется добавленная строка
			Префикс = ПолучитьПрефикс(НомУдаляемойСтроки);
			ИмяПервойЯчейкиУдаляемойСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Префикс + НомУдаляемойСтроки + Сред(СтруктураГруппы.Колонки[0].Имя,2,СтрДлина(СтруктураГруппы.Колонки[0].Имя)-1);
			
			// область первой ячейки удаляемой строки многострочного раздела
			ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
			
			// номера строк удаляемой области
			НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
			НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
			
			// Определим область удаляемой строки
			УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
			
			// непосредственно удаляем область строки табличного документа
			ТекТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
			
			КолвоСдвигаемыхСтрок = СтруктураГруппы.Количество() - НомУдаляемойСтроки;
			
			// обновим имена ячеек сдвинутых строк
			Если КолвоСдвигаемыхСтрок > 0 Тогда
				Для НСтр = НомУдаляемойСтроки + 1 По СтруктураГруппы.Количество() Цикл
					ТекСтрока = СтруктураГруппы[НСтр-1];
					НСтрНов = НСтр - 1;
					Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
						Префикс = ПолучитьПрефикс(НСтр);
						ПрефиксНов = ПолучитьПрефикс(НСтрНов);
						ИмяСдвинутойЯчейки    = Сред (Колонка.Имя,1,1) + Префикс + НСтр + Сред (Колонка.Имя,2,СтрДлина(Колонка.Имя)-1);
						НовоеИмяСдвинутойЯчейки = Сред (Колонка.Имя,1,1) + ПрефиксНов + (НСтрНов) + Сред (Колонка.Имя,2,СтрДлина(Колонка.Имя)-1);
						ОбластьСдвинутойЯчейки     = ТекТабличноеПоле.Области[ИмяСдвинутойЯчейки];
						ОбластьСдвинутойЯчейки.Имя = НовоеИмяСдвинутойЯчейки;
						
						Если НЕ(ОбластьСдвинутойЯчейки.СодержитЗначение) Тогда
							// обновляем порядковый номер строки
							СтарыйНомер = Число(ТекСтрока[Колонка.Имя]);
							НовыйПорядковыйНомер = Строка(Макс(СтарыйНомер - 1, 0));
							ОбластьСдвинутойЯчейки.Текст = НовыйПорядковыйНомер;
							// обновляем значение элемента таблицы значений
							ТекСтрока[Колонка.Имя] = НовыйПорядковыйНомер;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
			// обновляем структуру многострочного раздела
			СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
		Иначе
			// строка не удаляется в форме табличного документа,
			// а должна осуществляться сдвиг строк
			
			// обновляем структуру многострочного раздела
			// методом удаления выбранной строки и добавления пустой строки
			СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
			ДобавляемаяСтрока = СтруктураГруппы.Добавить();
			
			// обновляем порядковые номера строк, выводимых в табличный документ
			Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
				Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл
					
					НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
					НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
					Префикс = ПолучитьПрефикс(НомерСтрокиВФормеТаблицы);
					ИмяЯчейки    = Сред (КолонкаТаблицы.Имя,1,1) + Префикс + НомерСтрокиВФормеТаблицы + Сред(КолонкаТаблицы.Имя,2,СтрДлина(КолонкаТаблицы.Имя)-1);
					ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
					
					Если НЕ(ОбластьЯчейкиТаблицы.СодержитЗначение) Тогда
						ПервыйНомерВГруппе = 1;
						// обновляем порядковый номер строки
						НовыйПорядковыйНомер = Строка(ПервыйНомерВГруппе + НомерСтрокиВТаблице);
						СтрокаТаблицы[КолонкаТаблицы.Имя] = НовыйПорядковыйНомер;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			// Обновляем экранную форму табличного документа
			ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
		КонецЕсли;
		
		// назначим текущей областью таблицы первую редактируемую ячейку
		// удаленной строки, если ее порядковый номер меньше кол-ва строк
		// многострочного раздела, или последней строки раздела в противном случае
		
		// определяем текущую область таблицы
		НомТекущейСтроки  = Мин( НомУдаляемойСтроки, СтруктураГруппы.Количество() );
		Префикс = ПолучитьПрефикс(НомТекущейСтроки);
		ИмяТекущейОбласти    = Сред (СтруктураГруппы.Колонки[0].Имя,1,1) + Префикс + НомТекущейСтроки + Сред (СтруктураГруппы.Колонки[0].Имя,2,СтрДлина(СтруктураГруппы.Колонки[0].Имя)-1);
		
		ТекТабличноеПоле.ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
		
		// установим фокус на поле табличн. документа
		ТекущийЭлемент = ТекТабличноеПоле;
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры // УдалитьДополнительнуюСтроку()	

// Процедура выводит строки многострочного раздела в табличный документ.
// При отсутствии в табличном документе необходимого количества строк
// добавляет отсутствующие строки.
//
// Параметры:
//  ИмяГруппы       - строка, имя многострочного раздела;
//  СтруктураГруппы - таблица значений, содержащая значения показателей многострочного раздела.
//
Процедура ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы)
	Перем ИсходноеКоличествоСтрокГруппы;
	
	// определим исходное колво строк многострочного раздела,
	// соответствующее бумажной форме отчета
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	// реальное кол-во строк (колво заполненных строк)
	ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();
	ТекТабличноеПоле = мОтчетОПринятыхНаРаботу;
	
	// При восстановлении сохраненных данных отчета сравниваем исходное кол-во
	// отображаемых в форме строк многострочного раздела с реальным количеством
	// созданных строк в сохраненном отчете.
	Если ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы Тогда
		
		// При превышении реального кол-ва строк над исходным кол-вом (по шаблону),
		// автоматически добавляем необходимое колво строк.
		Для НомНовойСтроки = ИсходноеКоличествоСтрокГруппы + 1 По ТекущееКоличествоСтрокГруппы Цикл
			НомИсходнойСтроки = НомНовойСтроки - 1;
			Префикс = ПолучитьПрефикс(НомИсходнойСтроки);
			ПрефиксНов = ПолучитьПрефикс(НомНовойСтроки);
			
			// определяем имя первой ячейки последней из исходных строк
			ИмяПервойЯчейкиПоследнейСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Префикс + НомИсходнойСтроки + Сред(СтруктураГруппы.Колонки[0].Имя,2,СтрДлина(СтруктураГруппы.Колонки[0].Имя)-1);
			// область первой ячейки последней из исходных строк
			ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];
			// номера строк исходной области
			НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
			НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
			СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;
			// номера строк вставляемой области
			НомерВерхЯчейкиНовойОбласти = НомерВерхЯчейкиОбласти + СдвигСтрок;
			НомерНижнЯчейкиНовойОбласти = НомерНижнЯчейкиОбласти + СдвигСтрок;
			// определяем вставляемую область
			ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
			// добавляем в таблицу новую строку
			ТекТабличноеПоле.ВставитьОбласть( ВставляемаяОбласть,,ТипСмещенияТабличногоДокумента.ПоВертикали );
			// переопределяем имена вставленной области
			Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
				ИмяКолонки          = Колонка.Имя;
				ИмяОбластиЯчейки    = Сред (ИмяКолонки,1,1) + Префикс + НомИсходнойСтроки + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
				ИмяНовОбластиЯчейки = Сред (ИмяКолонки,1,1) + ПрефиксНов + НомНовойСтроки + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
				ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];
				ОбластьЯчейки.Имя = ИмяНовОбластиЯчейки;
				// определяем границы ячейки исходной области
				НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
				НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
				НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
				НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;
				// Назначаем имя ячейки исходной области,
				// (т.к. в результате вставки новой области оно было очищено)
				ОбластьЯчейкиИсходнойОбласти = ТекТабличноеПоле.Область(НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
				ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЯчейки;
				// очищаем значения ячеек вставленной области
				Если ОбластьЯчейки.СодержитЗначение Тогда
					ОбластьЯчейки.Очистить();
				Иначе
					// порядековый номер последней строки перед вставкой
					ИсходныйНомер        = Число(ОбластьЯчейки.Текст);
					НовыйПорядковыйНомер = ИсходныйНомер + 1;
					ОбластьЯчейки.Текст  = Строка(НовыйПорядковыйНомер);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	//  выводим значения строк
	Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
		Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл
			НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
			НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
			Префикс = ПолучитьПрефикс(НомерСтрокиВФормеТаблицы);
			ИмяКолонки = КолонкаТаблицы.Имя;
			ИмяЯчейки    = Сред (ИмяКолонки,1,1) + Префикс + НомерСтрокиВФормеТаблицы + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
			ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
			ЗначениеЯчейки           = СтрокаТаблицы[КолонкаТаблицы.Имя];
			
			Если ОбластьЯчейкиТаблицы.СодержитЗначение Тогда
				// задаем значение ячейки
				ОбластьЯчейкиТаблицы.Значение = ЗначениеЯчейки;
			Иначе
				// устанавливаем порядковый номер строки
				ОбластьЯчейкиТаблицы.Текст    = ЗначениеЯчейки;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // ВывестиРазделВТабличныйДокумент()

Процедура КнопкаУдалитьДопСтрокиНажатие(Элемент)
	
	УдалитьДополнительнуюСтроку();
	
КонецПроцедуры

//Процедура ПолеВыбораВариантЗаполненияУведомлениеПриИзменении(Элемент)
//	ИзменитьВариантЗаполненияЯчейки(Элемент.Значение);
//КонецПроцедуры

//Процедура ПолеВводаКомментарийУведомлениеПриИзменении(Элемент)
//	Коммент = Элемент.Значение;
//	ИзменитьКомментарийПоЯчейке(Коммент);
//КонецПроцедуры

Функция СформироватьДанныеДляЗвит1С() Экспорт 
	
	мСохраненныйДок   = Неопределено;	
	мСкопированаФорма = Неопределено;
	
	
	ПередОткрытием(Ложь, Истина);
	ПриОткрытии();
	
	Очистить();
	ЗаполнитьСведенияОбОрганизации();
	
	Если мМенеджерЗвит1С.ФлагОтладкиПакетноеТестирование = Ложь Тогда
		мМенеджерЗвит1С.Компонента.Bring1CToFront();
		Предупреждение(НСтр("ru = 'Укажите период приказов по приему на работу!'; uk = 'Вкажіть період наказів про прийом на роботу!'"));
	КонецЕсли;
	
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.РедактироватьКакИнтервал = Ложь;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки  = ВариантНастройкиПериода.Период;
	НастройкаПериода.ДатаНачала    = РабочаяДата;
	НастройкаПериода.ДатаОкончания = РабочаяДата;
	
	Если мМенеджерЗвит1С.ФлагОтладкиПакетноеТестирование = Ложь Тогда
		НастройкаПериода.Редактировать();
	КонецЕсли;
	
	ДатаНач	    = НастройкаПериода.ДатаНачала;
	Если Не ЗначениеЗаполнено(ДатаНач) Тогда
		ДатаНач = РабочаяДата;
	КонецЕсли;
	ДатаПодписи = НастройкаПериода.ДатаОкончания;
	Если Не ЗначениеЗаполнено(ДатаПодписи) Тогда
		ДатаПодписи = РабочаяДата;
	КонецЕсли;
	ДатаКон = ДатаПодписи;
	ДатаНачПриИзменении(Неопределено);
	
	мМенеджерЗвит1С.Компонента.BringZvitToFront();
	
	ЗаполнитьАвто();
	
	Если мМенеджерЗвит1С.ФлагОтладки Тогда
		ЭтаФорма.ОткрытьМодально();
	КонецЕсли;
	
	// формируем список сохранения
	СписокСохранения = Новый Структура();
	// расшифровка
	Попытка
		ВЫПОЛНИТЬ("СписокСохранения.Вставить(""РасшифровкаОтчета"", СпП);");
	Исключение КонецПопытки;
	
	ПоказателиОтчета = Новый Структура();
	Для каждого СтраницаПанели из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл
		ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + СтраницаПанели.Имя;
		ТекТабличноеПоле        = ЭлементыФормы[ИмяТекТабличногоПоля];
		ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
		ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);
	КонецЦикла;
	
	ТЧ = мСтруктураМногострочныхРазделов[мИдГруппы1];
	Колонки = ТЧ.Колонки;
	Инд = 0;
	Для каждого СтрокаТЧ Из ТЧ Цикл
		Инд = Инд + 1;
		НомерСтроки = ФОрмат(Инд, "ЧЦ=6; ЧДЦ=0; ЧН=; ЧВН=; ЧГ=0");
		Для каждого Колонка Из Колонки Цикл
			ПоказателиОтчета.ПолеТабличногоДокументаУведомление.Вставить(Колонка.Имя + "_" + НомерСтроки, СтрокаТЧ[Колонка.Имя]);
		КонецЦикла;	
	КонецЦикла;
	
	СлужебныеДанные = новый Структура;
	СлужебныеДанные.Вставить("R011G1", Инд);
	
	ПоказателиОтчета.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	// показатели
	СписокСохранения.Вставить("ДанныеОтчета", ПоказателиОтчета);
	
	Возврат СписокСохранения;
	
КонецФункции

Процедура КоманднаяПанельДействийДействиеXML(Кнопка) Экспорт
	
	Перем спДанныеОтчета, спПараметры;
	Перем НужнаяСтруктура;
	
  	Если Модифицированность И НЕ Сохранить() Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьТаблицуСоСхемамиВыгрузки();
	
	//Создадим структуру со списком параметров для передачи их в обработку выгрузки
 	спПараметры = Новый Структура();
	спПараметры.Вставить("Команда","ЭкспортXML"); //Операция
	
	ОпределитьСписокВыгружаемыхСхем();
	спПараметры.Вставить("Схемы",тбСхемыXML );	//Схемы (правила) выгрузки. Определеяются ГНАУ
	
	// обновляем структуру многострочных разделов
	ОбновитьСтруктуруМногострочныхРазделов();

	// формируем данные редактируемых ячеек таблицы
	ДанныеОтчета = Новый Структура();
	
	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ЭлементыФормы["ПолеТабличногоДокументаСлужебныеДанные"]);
	ДанныеОтчета.Вставить("ПолеТабличногоДокументаСлужебныеДанные", ПоказателиТекущегоЛиста);
	
	// добавим поле для проверки заполнения 
	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ЭлементыФормы["ПолеТабличногоДокументаУведомление"]);
	ДанныеОтчета.Вставить("ПолеТабличногоДокументаУведомление", ПоказателиТекущегоЛиста);
	
	C_DOC_CNT = мСлужебныеДанные.Области.C_DOC_CNT.Значение;
	
	// переименуем колонки, добавим префкис "Т1"
	ВремСтруктура = Неопределено;
	мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, ВремСтруктура);
	
	СтруктураГруппы1 = ВремСтруктура.Скопировать();
	Для каждого Колонка Из СтруктураГруппы1.Колонки Цикл
		Колонка.Имя = "T1" + Колонка.Имя;	
	КонецЦикла;
	
	ДанныеМногострочныхРазделов = Новый Структура;	
	
	Для Сч = 1 По тбСхемыXML.Количество() Цикл
		
		Если тбСхемыXML.Количество() = 1 Тогда
			
			КоличествоСтрокНаСтранице = СтруктураГруппы1.Количество();
			КопияСтруктуры 			  = СтруктураГруппы1;
			
		Иначе
			
			КоличествоСтрокНаСтранице = 0;
			КопияСтруктуры = СтруктураГруппы1.СкопироватьКолонки();
			
			Для Сч1 = Сч*6 - 5 По Сч*6 Цикл
				
				Если Сч1 > СтруктураГруппы1.Количество() Тогда
					Прервать;
				КонецЕсли;
				
				КоличествоСтрокНаСтранице = КоличествоСтрокНаСтранице + 1;
				
				СтрокаГруппы1 = СтруктураГруппы1[Сч1 - 1];
			    СтрокаКопии   = КопияСтруктуры.Добавить();
				
				ЗаполнитьЗначенияСвойств(СтрокаКопии,СтрокаГруппы1);
				
			КонецЦикла;
			
		КонецЕсли;		
		
		ДанныеМногострочныхРазделов.Вставить("_" + ФОрмат(Сч, "ЧГ=0") + "T1R", КопияСтруктуры);
		
		ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ЭлементыФормы["ПолеТабличногоДокументаУведомление"]);
		ПоказателиТекущегоЛиста.Вставить("HPAGES", Сч);
		ПоказателиТекущегоЛиста.Вставить("R011G1", КоличествоСтрокНаСтранице);
		ПоказателиТекущегоЛиста.Вставить("C_DOC_CNT", C_DOC_CNT + Сч - 1);
		
		ДанныеОтчета.Вставить("ПолеТабличногоДокументаУведомление_" + ФОрмат(Сч, "ЧГ=0"), ПоказателиТекущегоЛиста);
		
	КонецЦикла;
	// вставляем данные  ячеек таблицы	
	спПараметры.Вставить("ДанныеОтчета", ДанныеОтчета);
	// вставляем данные многострочных разделов
	спПараметры.Вставить("ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов);
	
	КаталогДанныхФирмы = Организация.НаименованиеПолное;
	спПараметры.Вставить("КаталогДанныхФирмы",КаталогДанныхФирмы); //каталог для сохранения выгрузки
	ОбработкаЭкспортXML = РегламентированнаяОтчетность.РеглОтчеты("РегламентированныйОтчетМастерВыгрузкиРегламентированнойОтчетности",Истина);
	
	Ошибка = ПроверитьКорректностьЗаполненияОтчета(ДанныеОтчета, ОбработкаЭкспортXML);
	Если Ошибка = Истина Тогда
		Возврат;
	КонецЕсли;

	ИдФормы = "Форма";   
	ФормаОбр = ОбработкаЭкспортXML.ПолучитьФорму(ИдФормы,ЭтаФорма);
	ФормаОбр.спПараметры = спПараметры; 
	ФормаОбр.ИмяНужногоОтчета = "РегламентированныйОтчетУведомлениеОПриемеРаботника";
	
	ФормаОбр.тбСхемы = спПараметры.Схемы;
	ФормаОбр.Открыть();
	
	ФормаОбр.Объект = ЭтотОбъект;
	ФормаОбр.Док    = мСохраненныйДок;
	
	ФормаОбр.ДлинаНомераВМногострочке = 6;
	
	ФормаОбр.ВыгружатьПакетом = Ложь;
	ФормаОбр.ЭлементыФормы.тбСхемы.Колонки.Декларация.Видимость = Ложь;
	ФормаОбр.ЭлементыФормы.тбСхемы.Колонки.Приложение.Видимость = Ложь;
	
КонецПроцедуры

Процедура ОпределитьСписокВыгружаемыхСхем();
	Для Каждого стр Из  тбСхемыXML Цикл
	    стр.фВыгрузка = 1;
	КонецЦикла;
КонецПроцедуры	

Процедура СоздатьТаблицуСоСхемамиВыгрузки() Экспорт
	
	//для возможности выгрузки в XML файл	
	тбСхемыXML = Новый ТаблицаЗначений;
	ТипКолИмяСхемы = СоздатьОписанияТипов("Строка",8);
	тбСхемыXML.Колонки.Добавить("ИмяСхемы",ТипКолИмяСхемы,НСтр("ru='Имя файла-схемы';uk=""Ім'я файлу-схеми"""));
	ТипКолПредставлениеСхемы = СоздатьОписанияТипов("Строка",80);
	тбСхемыXML.Колонки.Добавить("ПредставлениеСхемы",ТипКолПредставлениеСхемы,НСтр("ru='Наименование регл.отчета';uk='Найменування регл.звіту'"));
	ТипКолНазваниеСтраницыПанели = СоздатьОписанияТипов("Строка",60);
	тбСхемыXML.Колонки.Добавить("НазваниеСтраницыПанели",ТипКолНазваниеСтраницыПанели,НСтр("ru='Наименование страницы панели';uk='Найменування сторінки панелі'"));

	ТипКолфВыгрузка = СоздатьОписанияТипов("Число",1,0);
	тбСхемыXML.Колонки.Добавить("фВыгрузка",ТипКолфВыгрузка,НСтр("ru='Выгружать?';uk='Вивантажувати?'"));
	ТипКолНазваниеСтраницыПанели = СоздатьОписанияТипов("Строка",60);
	ТипПрефикса = СоздатьОписанияТипов("Строка",2);
	тбСхемыXML.Колонки.Добавить("Префикс",ТипПрефикса,НСтр("ru='Префикс';uk='Префікс'"));

	КоличествоСтраниц = 1;
	//////////// Определимся, как будем печатать (HPAGES) КоличествоСтраниц: или одна страничка или пачка страниц по 6 строк
	//////////СтруктураГруппы1 = Неопределено;
	//////////мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, СтруктураГруппы1);
	//////////КоличествоСтрокУведомления = СтруктураГруппы1.Количество();
	//////////
	//////////КоличествоСтраниц = Цел(КоличествоСтрокУведомления/6) + ?(КоличествоСтрокУведомления%6 = 0, 0, 1);
	//////////Если КоличествоСтраниц > 1 Тогда
	//////////
	//////////	ТекстВопроса  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Выгрузить отчет как одну страницу или разбить на %1 страниц по 6 строк?'; uk = 'Вивантажити звіт як одну сторінку або розбити звіт на %1 сторінок по 6 рядків?'"), КоличествоСтраниц);
	//////////	
	//////////	КнопкиВопроса = Новый СписокЗначений();
	//////////	КнопкиВопроса.Добавить(1, НСтр("ru = 'Одна страница'; uk = 'Одна сторінка'"));
	//////////	КнопкиВопроса.Добавить(КоличествоСтраниц, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 страниц(а)'; uk = '%1 сторінка(ок) по 6 рядків'"), КоличествоСтраниц));
	//////////	ЗаголовокВопроса = НСтр("ru = 'Вариант выгрузки'; uk = 'Варіант вивантаження'");
	//////////	
	//////////	КоличествоСтраниц = Вопрос(ТекстВопроса, КнопкиВопроса,,1, ЗаголовокВопроса);
	//////////	
	//////////КонецЕсли;
	
	Для Сч = 1 По КоличествоСтраниц Цикл
		
		НовСтрока = тбСхемыXML.Добавить();
		
		Если Организация.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			НовСтрока.ИмяСхемы = "J3001001"; 
		Иначе	
			НовСтрока.ИмяСхемы = "F3001001"; 
		КонецЕсли;
			
		НовСтрока.ПредставлениеСхемы 	 = НСтр("ru='Уведомление о приеме работника на работу';uk='Повідомлення про прийняття працівника на роботу'");
		НовСтрока.ПредставлениеСхемы 	 = НовСтрока.ПредставлениеСхемы  + НСтр("ru = ' стр. '; uk = ' стор. '") + Формат(Сч, "ЧГ=0");
		
		НовСтрока.НазваниеСтраницыПанели = "Уведомление_" + Формат(Сч, "ЧГ=0");
		НовСтрока.Префикс 				 = "_" + ФОрмат(Сч, "ЧГ=0");
		
	КонецЦикла;
	
КонецПроцедуры

//создает массив описания типов для таблицы значений со схемами XML
Функция СоздатьОписанияТипов(НазваниеТипа, Длина,Точность="")
  мас = Новый Массив;
  мас.Добавить(Тип(НазваниеТипа));
  Если Точность = "" Тогда
	 КвалифСтроки = Новый КвалификаторыСтроки(Длина,ДопустимаяДлина.Переменная); 
  Иначе
	 КвалифЧисла = Новый КвалификаторыЧисла(Длина,Точность,ДопустимыйЗнак.Неотрицательный); 
  КонецЕсли;	   
  Возврат Новый ОписаниеТипов(мас,КвалифСтроки,КвалифЧисла);
КонецФункции	

Функция ПолучитьНастройкиПроверкиЗаполенения(Макет = "СоставПоказателей2015") Экспорт

	СтруктураПроверкиЗаполенения = Новый Структура();
	
	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет(Макет);
	
	МассивТипов = Новый Массив; 
	МассивТипов.Добавить(Тип("Строка"));
	ОписаниеТиповСтрока100 = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыСтроки(100, ДопустимаяДлина.Переменная));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Число"));
	ОписаниеТиповЧисло15   = Новый ОписаниеТипов(МассивТипов, Новый КвалификаторыЧисла(15,2));

	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл

		ТекОбласть    = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы   = ТекОбласть.Имя;
		
		ТаблицаПроверкаЗаполнения = Новый ТаблицаЗначений;
		ТаблицаПроверкаЗаполнения.Колонки.Добавить( "ИмяПоказателя", 	   ОписаниеТиповСтрока100 );
		ТаблицаПроверкаЗаполнения.Колонки.Добавить( "ПроверкаЗаполненияРеквизитов",  ОписаниеТиповЧисло15 );
		ТаблицаПроверкаЗаполнения.Колонки.Добавить( "ИмяТабличногоПоля",   ОписаниеТиповСтрока100 );

		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета

			// Код показателя (по составу показателей) определяется по первой колонке макета
			ИмяПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);

			Если ИмяПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(ИмяПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// вариант заполнения ячейки определяется по колонке 6 макета
			стрПроверкаЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 7).Текст);
			ПроверкаЗаполненияРеквизитов    = ? (ПустаяСтрока(стрПроверкаЗаполнения), 0, Число(стрПроверкаЗаполнения));
			ИмяТабличногоПоля	  = СокрЛП(МакетСоставаПоказателей.Область(Ном, 8).Текст);

			Если ПроверкаЗаполненияРеквизитов <> 0  Тогда
				// формируем таблицу значений, содержащей состав показателей отчета
				НоваяСтрока = ТаблицаПроверкаЗаполнения.Добавить();
				НоваяСтрока.ИмяПоказателя   	 = ИмяПоказателя;
				НоваяСтрока.ПроверкаЗаполненияРеквизитов   = ПроверкаЗаполненияРеквизитов;
				НоваяСтрока.ИмяТабличногоПоля    = ?(ЗначениеЗаполнено(ИмяТабличногоПоля), ИмяТабличногоПоля, ИмяСтраницы);
			КонецЕсли;

		КонецЦикла;

		Если ТаблицаПроверкаЗаполнения.Количество() > 0 Тогда
			СтруктураПроверкиЗаполенения.Вставить(ИмяСтраницы, ТаблицаПроверкаЗаполнения);
		Конецесли;

	КонецЦикла;
	
	Возврат СтруктураПроверкиЗаполенения;
	
КонецФункции

Процедура ПроверитьЗаполнениеТЧ(ТаблицаНезаполненныхПоказателей, ДанныеОтчета, ОбработкаЭкспортXML)
	
	НомерСтроки = 0;
	
	СтруктураМногострочнойЧасти = ДанныеОтчета.ПолеТабличногоДокументаУведомление;
	
	Пока Истина Цикл
		
		НомерСтроки = НомерСтроки + 1;
		ИмяТестПоказателя = "R" + Формат(НомерСтроки , "ЧЦ=6; ЧВН=; ЧГ=0") + "G4";
		ЗначениеПоказателя = "";
		
		Если СтруктураМногострочнойЧасти.Свойство(ИмяТестПоказателя,ЗначениеПоказателя) Тогда
			Если НЕ ЗначениеЗаполнено(ЗначениеПоказателя) Тогда
				 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				 НезаполненныйПоказатель.ИмяПоказателя 		= ИмяТестПоказателя;
				 НезаполненныйПоказатель.ИмяТабличногоПоля  = "ПолеТабличногоДокументаУведомление";
			КонецЕсли;

			ИмяПоказателя = "R" + Формат(НомерСтроки , "ЧЦ=6; ЧВН=; ЧГ=0") + "G5S";
			Если НЕ ЗначениеЗаполнено(СтруктураМногострочнойЧасти[ИмяПоказателя]) Тогда
				 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				 НезаполненныйПоказатель.ИмяПоказателя 		= ИмяПоказателя;
				 НезаполненныйПоказатель.ИмяТабличногоПоля  = "ПолеТабличногоДокументаУведомление";
		 	КонецЕсли;
			 
			ИмяПоказателя = "R" + Формат(НомерСтроки , "ЧЦ=6; ЧВН=; ЧГ=0") + "G61S";
			Если НЕ ЗначениеЗаполнено(СтруктураМногострочнойЧасти[ИмяПоказателя]) Тогда
				 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				 НезаполненныйПоказатель.ИмяПоказателя 		= ИмяПоказателя;
				 НезаполненныйПоказатель.ИмяТабличногоПоля  = "ПолеТабличногоДокументаУведомление";
		 	КонецЕсли;
			 
			ИмяПоказателя = "R" + Формат(НомерСтроки , "ЧЦ=6; ЧВН=; ЧГ=0") + "G62S";
			Если НЕ ЗначениеЗаполнено(СтруктураМногострочнойЧасти[ИмяПоказателя]) Тогда
				 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				 НезаполненныйПоказатель.ИмяПоказателя 		= ИмяПоказателя;
				 НезаполненныйПоказатель.ИмяТабличногоПоля  = "ПолеТабличногоДокументаУведомление";
		 	КонецЕсли;
			 
			ИмяПоказателя = "R" + Формат(НомерСтроки , "ЧЦ=6; ЧВН=; ЧГ=0") + "G7S";
			Если НЕ ЗначениеЗаполнено(СтруктураМногострочнойЧасти[ИмяПоказателя]) Тогда
				 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				 НезаполненныйПоказатель.ИмяПоказателя 		= ИмяПоказателя;
				 НезаполненныйПоказатель.ИмяТабличногоПоля  = "ПолеТабличногоДокументаУведомление";
		 	КонецЕсли;
			
			ИмяПоказателя = "R" + Формат(НомерСтроки , "ЧЦ=6; ЧВН=; ЧГ=0") + "G8D";
			Если НЕ ЗначениеЗаполнено(СтруктураМногострочнойЧасти[ИмяПоказателя]) Тогда
				 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				 НезаполненныйПоказатель.ИмяПоказателя 		= ИмяПоказателя;
				 НезаполненныйПоказатель.ИмяТабличногоПоля  = "ПолеТабличногоДокументаУведомление";
		 	КонецЕсли;
			
			ИмяПоказателя = "R" + Формат(НомерСтроки , "ЧЦ=6; ЧВН=; ЧГ=0") + "G9D";
			Если НЕ ЗначениеЗаполнено(СтруктураМногострочнойЧасти[ИмяПоказателя]) Тогда
				 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				 НезаполненныйПоказатель.ИмяПоказателя 		= ИмяПоказателя;
				 НезаполненныйПоказатель.ИмяТабличногоПоля  = "ПолеТабличногоДокументаУведомление";
		 	КонецЕсли;
			
		Иначе
			// закончились строки
			Прервать
		КонецЕсли;
	
	КонецЦикла; 
	
КонецПроцедуры

Функция ПроверитьКорректностьЗаполненияОтчета(ДанныеОтчета, ОбработкаЭкспортXML)
	
	ТаблицаНезаполненныхПоказателей = Новый ДеревоЗначений;
	ТаблицаНезаполненныхПоказателей.Колонки.Добавить( "ИмяПоказателя", 		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100, ДопустимаяДлина.Переменная)));
	ТаблицаНезаполненныхПоказателей.Колонки.Добавить( "ИмяТабличногоПоля",  Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100, ДопустимаяДлина.Переменная)));
	
	// ПроверитьЗаполнениеОсобыхПоказателей(ТаблицаНезаполненныхПоказателей,ДанныеОтчета);
	
	ПроверитьЗаполнениеТЧ(ТаблицаНезаполненныхПоказателей, ДанныеОтчета, ОбработкаЭкспортXML);
	
	СтруктураПроверки = ПолучитьНастройкиПроверкиЗаполенения();
	Для каждого СтраницаОтчета Из СтруктураПроверки Цикл
		
		ГруппыОбязательныхПолей = Новый Соответствие;
		
		// Проверим заполенение полей не в группах
		Для каждого Поле  Из СтраницаОтчета.Значение Цикл
			
			// поле - строка таблицы значений
			Если Поле.ПроверкаЗаполненияРеквизитов <> 0 Тогда
				
				Если Поле.ПроверкаЗаполненияРеквизитов > 1 Тогда
					// это группа обязательных полей, добавим в соответствие, проверим позже
					ГруппаОбязательныхПолей = ГруппыОбязательныхПолей[Поле.ПроверкаЗаполненияРеквизитов];
					Если ГруппаОбязательныхПолей = Неопределено Тогда
						ГруппаОбязательныхПолей = Новый Массив();
					КонецЕсли;
					ГруппаОбязательныхПолей.Добавить(Поле);
					ГруппыОбязательныхПолей[Поле.ПроверкаЗаполненияРеквизитов] = ГруппаОбязательныхПолей;
					Продолжить;	
				КонецЕсли;
				
				// табличное поле может быть "СлужебныеДанные" или другим
				ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + Поле.ИмяТабличногоПоля;
				
				ЗначениеПоказателя = ДанныеОтчета[ИмяТекТабличногоПоля][Поле.ИмяПоказателя];												
				Если НЕ ЗначениеЗаполнено(ЗначениеПоказателя) Тогда
				     // показатель не заполнен
					 НезаполненныйПоказатель = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				     НезаполненныйПоказатель.ИмяПоказателя 		= Поле.ИмяПоказателя;
					 НезаполненныйПоказатель.ИмяТабличногоПоля  = ИмяТекТабличногоПоля;
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЦикла;
		
		// Проверим заполенение полей в группах
		Для каждого ГруппаОбязательныхПолей Из ГруппыОбязательныхПолей Цикл
			
			ЕстьЗаполненный = Ложь;
			Для каждого Поле Из ГруппаОбязательныхПолей.Значение Цикл
				
				// табличное поле может быть "СлужебныеДанные" или другим
				ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + Поле.ИмяТабличногоПоля;
				
				ЗначениеПоказателя = ДанныеОтчета[ИмяТекТабличногоПоля][Поле.ИмяПоказателя];												
				Если ЗначениеЗаполнено(ЗначениеПоказателя) Тогда
					ЕстьЗаполненный	= Истина;
					Прервать;
				КонецЕсли;
			
			КонецЦикла;
			
			
			Если Не ЕстьЗаполненный Тогда
				
				НезаполненныйПоказательГруппа = ТаблицаНезаполненныхПоказателей.Строки.Добавить();
				НезаполненныйПоказательГруппа.ИмяПоказателя = НСтр("ru = 'Ни один из показателей:'; uk = 'Жоден з показників:'");
		
				Для каждого Поле Из ГруппаОбязательныхПолей.Значение Цикл
				
					// табличное поле может быть "СлужебныеДанные" или другим
				    ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + Поле.ИмяТабличногоПоля;
					НезаполненныйПоказатель = НезаполненныйПоказательГруппа.Строки.Добавить();
					НезаполненныйПоказатель.ИмяПоказателя 		= Поле.ИмяПоказателя;
					НезаполненныйПоказатель.ИмяТабличногоПоля   = ИмяТекТабличногоПоля;
				
			    КонецЦикла;
				 
			 КонецЕсли;	
			
		КонецЦикла; 
		
	КонецЦикла;
	
	ФормаОшибок = ОбработкаЭкспортXML.ПолучитьФорму("ФормаОтображенияНезаполенныхПоказателей", ЭтаФорма);

	Если ТаблицаНезаполненныхПоказателей.Строки.Количество()> 0 Тогда
		
		ФормаОшибок.ДеревоСтрок = ТаблицаНезаполненныхПоказателей;
		Для каждого Строка Из ФормаОшибок.ДеревоСтрок.Строки Цикл
			ФормаОшибок.ЭлементыФормы.ДеревоСтрок.Развернуть(Строка,Истина);	
		КонецЦикла;
		
		ФормаОшибок.Открыть();
		
		ТекстВопроса = НСтр("ru = 'Существуют незаполненные показатели, которые являются обязательными для выгрузки отчета в формате XML! Выгрузить отчет?'; uk = ""Існують незаповнені показники, що є обов'язковими для вивантаження звіту в форматі XML! Выгрузить отчет?""");
		
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, Нстр("ru = 'Ошибка!'; uk = 'Помилка!'")) <> КодВозвратаДиалога.Да Тогда
			// ошибка
			Возврат Истина;	
		КонецЕсли;
	Иначе
		
		Если ФормаОшибок.Открыта() Тогда
		
			ФормаОшибок.Закрыть();	
		
		КонецЕсли;

	КонецЕсли;

	// нет ошибки
	Возврат Ложь;
	
КонецФункции // ()

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	Перем ИмяПоказателя;
	Перем ИмяТабличногоПоля;
	
	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		
		// если это оповещение из формы списка незаполенных показателей
		Если  ЗначениеВыбора.Свойство("ИмяПоказателя", 		ИмяПоказателя)
			И ЗначениеВыбора.Свойство("ИмяТабличногоПоля", 	ИмяТабличногоПоля) Тогда
		
			Попытка
				
				// переключаемся на текущий отчет
				Активизировать();
				
				Если Найти(ИмяПоказателя, "T1R") > 0 Тогда
					ИмяПоказателя = СтрЗаменить(ИмяПоказателя, "T1R", "R");	
				КонецЕсли;
				
				Если Найти(ИмяТабличногоПоля, "_") > 0 Тогда
					ИмяТабличногоПоля = Лев(ИмяТабличногоПоля,Найти(ИмяТабличногоПоля, "_")  - 1);	
				КонецЕсли;	
				
				ТекущийЭлемент = ЭлементыФормы[ИмяТабличногоПоля];
				
				// выделяем ячейку так, чтобы она оказалось видимой при любом положении полосы прокрутки.
				ТекущийЭлемент.ВыделенныеОбласти.Очистить();
				ТекущийЭлемент.ВыделенныеОбласти.Вставить(ТекущийЭлемент.Области.Найти(ИмяПоказателя),0);
				ТекущийЭлемент.ВыделенныеОбласти.Удалить(ТекущийЭлемент.Область("R1C1"));
				
			Исключение
			КонецПопытки;	
			
		КонецЕсли;	
	
	КонецЕсли;	
	
КонецПроцедуры



Процедура ДатаНачОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура ДатаНачПриИзменении(Элемент)
	
	Если ДатаКон < ДатаНач Тогда
		ДатаКон = ДатаНач;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДатаКонПриИзменении(Элемент)
	Если ДатаКон < ДатаНач Тогда
		ДатаКон = ДатаНач;
	КонецЕсли;
КонецПроцедуры

Процедура ДатаКонОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


Процедура ПолеТабличногоДокументаРасчетНалогаНаРекламуВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ПриИзмененииПолейВКлеточках(Элемент, Область);
		
КонецПроцедуры

Процедура ПриИзмененииПолейВКлеточках(Элемент, Область)
	
	ИмяОбласти = Область.Имя;
	
	Если  ИмяОбласти = "H01"  Тогда
		
		Элемент.Области.H01.Значение	  = "Х";
		Элемент.Области.H02.Значение  = "";
		
		мСлужебныеДанные.Области.C_DOC_STAN.Значение = 1;
		
	ИначеЕсли ИмяОбласти = "H02" Тогда
		
		Элемент.Области.H01.Значение	 = "";
		Элемент.Области.H02.Значение = "Х";
		
		мСлужебныеДанные.Области.C_DOC_STAN.Значение = 1;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаРасчетНалогаНаРекламуПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	Возврат;	
	
КонецПроцедуры

Процедура КнопкаДобавитьДопСтрокиНажатие(Элемент)
	ВставитьДополнительнуюСтроку(мИдГруппы1);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ОписаниеТиповЧисло = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);

ОписаниеТиповЧисло1 = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1,0);

ОписаниеТиповСтрока = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(254);

МассивТипов = Новый Массив;
МассивТипов.Добавить(Тип("Дата"));
ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));

ЦветЗаполняетсяТолькоВручную    = Новый Цвет(255, 255, 225);
ЦветЗаполняетсяАвтоКорректир    = Новый Цвет(255, 240, 200);
ЦветЗаполняетсяАвтоБезКорректир = Новый Цвет(230, 240, 220);

// структура цветов раскраски автозаполняемых ячеек
мСтруктураЦветовРаскраски = Новый Структура;
мСтруктураЦветовРаскраски.Вставить("_1", ЦветЗаполняетсяТолькоВручную);
мСтруктураЦветовРаскраски.Вставить("_2", ЦветЗаполняетсяАвтоКорректир);
мСтруктураЦветовРаскраски.Вставить("_3", ЦветЗаполняетсяАвтоБезКорректир);

мСтруктураМногострочныхРазделов = Новый Структура;

// ОПИСАНИЕ ПАРМЕТРОВ ФОРМЫ
//
мВерсияФормы = "06/07/2015";

мПечатныеФормы = Новый СписокЗначений;

// ОПИСАНИЕ ПАРАМЕТРОВ МНОГОСТРОЧНЫХ ГРУПП
// Идентификаторы многострочных групп
мИдГруппы1 = "R";

// количество строк многострочных разделов по "бумажной" форме отчета
// (минимальное количество строк, которое должно присутствовать всегда)
//нужна редакция в зависимости от структуры многострочных групп приложений декларации

КолИсхСтрокИдГруппы1 = 1;

СтруктураГруппы1 = Новый ТаблицаЗначений;
СтруктураГруппы1.Колонки.Добавить("RG3", ОписаниеТиповЧисло);
СтруктураГруппы1.Колонки.Добавить("RG4", ОписаниеТиповЧисло1);
СтруктураГруппы1.Колонки.Добавить("RG5S", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG61S", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG62S", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG63S", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG7S", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG8D", ОписаниеТиповДата);
СтруктураГруппы1.Колонки.Добавить("RG9D", ОписаниеТиповДата);

// структура многострочных разделов формы
мСтруктураМногострочныхРазделов = Новый Структура;
мСтруктураМногострочныхРазделов.Вставить(мИдГруппы1, СтруктураГруппы1);

// структура исходных строк (минимального кол-ва) многострочных разделов формы
мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы1, КолИсхСтрокИдГруппы1);

мСоставПоказателей = "СоставПоказателей2015";

ЕстьДуплекс = ВосстановитьЗначение("РегламентированныеОтчеты_ЕстьДуплекс");
Если НЕ ТипЗнч(ЕстьДуплекс) = Тип("Булево") Тогда
	ЕстьДуплекс = Ложь;	
КонецЕсли;
