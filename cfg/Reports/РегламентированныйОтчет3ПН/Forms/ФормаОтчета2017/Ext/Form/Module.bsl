////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// поле табличного документа
//каждая закладка отчета - это объект ПолеТабличногоДокумента
Перем мОтчетОВакансиях;

Перем ЕДРПОУ, КВЕД, Инд;

// переменный для хранения бухгалтерских итогов
Перем мБухИтоги;

// хранит форматную строку представления
// денежных показателей отчета
Перем мСтрокаФормата;

// хранит коллекцию цветов раскраски автоматически
// заполняемых ячеек табличного документа
Перем мСтруктураЦветовРаскраски;

// хранит значение делителя при представлении
// чисел в назначенной размерности
Перем мДелитель;

// хранит версию формы отчета
Перем мВерсияФормы;

//вспомогательные переменные
Перем ТаблицаРезультатов;

// хранят имена многострочных разделов
Перем мИдГруппы1;
Перем мИдГруппы2;

// хранят структуру многострочных разделов
Перем мСтруктураМногострочныхРазделов;
Перем мСтруктураИсхКолвоСтрокРазделов;
Перем спПараметрыСтраницы;

Перем мПечатныеФормы Экспорт;

// Хранит название макета состава показателей
Перем мСоставПоказателей Экспорт;

Перем ЕстьДуплекс;

Перем мМенеджерЗвит1С Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция собирает значения редактирукемых ячеек табличного документа
// в коллекцию значений типа "структура".
//
// Параметры:
//  ТабличноеПоле - поле табличного документа текущей формы.
//
// Возвращаемое значение:
//  Структура.
//
Функция СобратьДанныеТекущегоТаблПоля(ТабличноеПоле)

	СтруктураПоказателейВнутрСвязи = Новый Структура;

	СтруктураДанныхПоля = Новый Структура;

	Для Инд = 0 По ТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличноеПоле.Области[Инд];
		Если Тип ("РисунокТабличногоДокумента") <> ТипЗнч (ТекущаяОбласть) Тогда
			Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если (ТекущаяОбласть.Имя <> "Подвал" И ТекущаяОбласть.Имя <> "Расчет" И ТекущаяОбласть.Имя <> "Шапка") Тогда
			Если НЕ(ТекущаяОбласть.СодержитЗначение) Тогда
				Продолжить;
			КонецЕсли;
       
			//Если (ТекущаяОбласть.Защита) Тогда
			//	Если Не СтруктураПоказателейВнутрСвязи.Свойство(ТекущаяОбласть.Имя) Тогда
			//		Продолжить;
			//	КонецЕсли;
			//КонецЕсли;
        
			ИмяПоказателя      = ТекущаяОбласть.Имя;
			ЗначениеПоказателя = ТекущаяОбласть.Значение;
			СтруктураДанныхПоля.Вставить(ИмяПоказателя, ЗначениеПоказателя);
        КонецЕсли;
	КонецЦикла;

	Возврат СтруктураДанныхПоля;

КонецФункции // СобратьДанныеТекущегоТаблПоля()

// Функция определяет варианты заполнения ячеек табличного документа
// по имени страницы основной панели формы.
//
// Параметры:
//   ИмяСтраницыПанели - строка, имя страницы основной панели формы.
//   ПолучитьЭталонную - булево, если истина, получаем эталонную
//                               (исходную) таблицу вариантов заполнения.
//
// Возвращаемое значение:
//   таблица значений, содержащая информацию о вариантах автозаполнения ячеек.
//
Функция ПолучитьТаблицуВариантовЗаполнения(ИмяСтраницыПанели, ПолучитьЭталонную = Ложь)
	Перем МассивВариантовЗаполненияСтраниц;
	Перем ТаблицаВариантыЗаполнения;
	Перем ТаблицаСтраницРаздела;

	Если мСтруктураВариантыЗаполнения.Свойство(ИмяСтраницыПанели, МассивВариантовЗаполненияСтраниц) Тогда

		Если НЕ ПолучитьЭталонную Тогда
			// получим варианты заполнения ячеек, заданные для активной страницы
			НомерТекущейСтраницы = 1;

			ТаблицаВариантыЗаполнения = МассивВариантовЗаполненияСтраниц[НомерТекущейСтраницы - 1];
		Иначе
			// Получим предустановленные (эталонные) варианты заполнения ячеек поля табличного
			// документа на заданной странице

			ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);

			ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

			ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

			ОписаниеТиповЧисло15   = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);

			ТаблВариантыЗаполнения = Новый ТаблицаЗначений;
			ТаблВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока15  );
			ТаблВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
			ТаблВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
			ТаблВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
			ТаблВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );

			// варианты заполнения определяем по макету СоставПоказателей
			МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет("СоставПоказателей2017");

			ТекОбласть = МакетСоставаПоказателей.Области.Найти(ИмяСтраницыПанели);
			Если ТекОбласть <> Неопределено Тогда
			
				Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
					// Перебираем строки макета.
					// Код показателя (по составу показателей) определяется по первой колонке макета
					КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
					ИмяПоказателя = КодПоказателя;

					Если КодПоказателя = "===" Тогда         // признак конечной строки
						Прервать;
					КонецЕсли;

					Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
						Продолжить;
					КонецЕсли;

					// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
					КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
					// вариант заполнения ячейки определяется по колонке 6 макета
					стрВариантЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 6).Текст);

					чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));
					Если чВариантЗаполнения <> 0 Тогда
						// Если в колонке 6 задан варианет заполнения показателя, 
						// то его значение может определяться по данным ИБ.
						// Дополняем таблицу значений вариантов заполнения
						НоваяСтрока = ТаблВариантыЗаполнения.Добавить();
						НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
						НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
					КонецЕсли;
				КонецЦикла;
			
			КонецЕсли;

			Если ТаблВариантыЗаполнения.Количество() > 0 Тогда
				ТаблицаВариантыЗаполнения = ТаблВариантыЗаполнения.Скопировать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат ТаблицаВариантыЗаполнения;

КонецФункции // ПолучитьТаблицуВариантовЗаполнения()

// Обновляет пользовательские настройки по вариантам заполнения
// показателей отчета в случае изменения версии формы отчета.
//
// Возвращаемое значение:
//   Булево - признак модифицированность пользовательских настроек.
//
Функция ОбновитьВариантыЗаполнения()
	Перем МассивВариантовЗаполненияСтраниц;

	ЕстьИзменения = Ложь;

	// Подлежат обновлению следующие структуры:
	// 1.Пользовательские настройки вариантов заполнения таблиц

	// Сначала создадим эталонные модели вариантов автозаполнения таблиц.
	// Их определяем по макету СоставПоказателей
	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет("СоставПоказателей2017");

	ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);

	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

	ОписаниеТиповЧисло15   = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);

	ЕстьИзменения = Ложь;

	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл
		// организован цикл по именованным областям макета,
		// содержащим состав показателей и варианты автозаполнения
		// таблиц на каждой странице основной панели формы.

		ТекОбласть  = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы = ТекОбласть.Имя;

		// Таблица значений содержит информацию о вариантах заполнения показателя отчета.
		// В колонках таблицы значений отображается следуящая информация:
		//   - имя ячейки (код показателя);
		//   - вариант заполнения ячейки;
		//   - вычисленное по данным ИБ значение ячейки;
		//   - величина автокорректировки значения ячейки;
		//   - примечание к ячейке.
		//
		ТаблицаВариантыЗаполнения = Новый ТаблицаЗначений;
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока15  );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );

		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета

			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;

			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 6).Текст);

			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));
			Если чВариантЗаполнения <> 0 Тогда
				// Если в колонке 6 задан варианет заполнения показателя, 
				// то его значение может определяться по данным ИБ.
				// Дополняем таблицу значений вариантов заполнения
				НоваяСтрока = ТаблицаВариантыЗаполнения.Добавить();
				НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
				НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
			КонецЕсли;

		КонецЦикла;

		Если Не мСтруктураВариантыЗаполнения.Свойство(ИмяСтраницы, МассивВариантовЗаполненияСтраниц) Тогда
			Если ТаблицаВариантыЗаполнения.Количество() > 0 Тогда
				// Рассмотрим вариант, когда в предыдущей версии формы по данной странице
				// не было предусмотрено автозаполнение по данным ИБ.
				// Добавим такую возможность в пользовательскую настройку

				// Для обеспечения назначения разных вариантов заполнения одного и того же показателя
				// на разных страницах, создаем массив, хранящий информацию о вариантах заполнения
				// таблиц по каждой странице многостраничного раздела.
				МассивВариантовЗаполненияСтраниц = Новый Массив;
				// Первым элементом массива является "эталонная" таблица значений вариантов заполнения
				МассивВариантовЗаполненияСтраниц.Добавить(ТаблицаВариантыЗаполнения);

				// сохраняем варианты заполнения таблиц на данной странице
				мСтруктураВариантыЗаполнения.Вставить(ИмяСтраницы, МассивВариантовЗаполненияСтраниц);

				ЕстьИзменения = Истина;
			КонецЕсли;
		Иначе
			Если ТаблицаВариантыЗаполнения.Количество() = 0 Тогда
				// Рассмотрим вариант, когда в предыдущей версии формы по данной странице
				// было предусмотрено автозаполнение таблицы, а в текущей версии - отключено.
				// Соответственно, из структуры вариантов автозаполнения удаляем элемент,
				// хранящий параметры автозаполнения ячеек текущей станицы.
				мСтруктураВариантыЗаполнения.Удалить(ИмяСтраницы);

				ЕстьИзменения = Истина;
			Иначе
				// Рассмотрим вариант, когда и в предыдущей, и в текущей версии формы было предусмотрено
				// автозаполнение текущей страницы. В данном случае обновляем таблицу значений, содержащей
				// пользовательские настройки параметров автозаполнения текущей страницы.

				Для каждого ЭлементМассива Из МассивВариантовЗаполненияСтраниц Цикл
					Для каждого СтрокаТаблицы Из ТаблицаВариантыЗаполнения Цикл
						КодПоказателя     = СтрокаТаблицы.КодПоказателя;
						ВариантЗаполнения = СтрокаТаблицы.ВариантЗаполнения;

						Если ЭлементМассива.Найти(КодПоказателя, "КодПоказателя") = Неопределено Тогда
							// Рассмотри вариант, когда в новой версии формы список реквизитов,
							// заполняемых автоматически по данным ИБ, дополнен новым реквизитом.
							НоваяСтрока = ЭлементМассива.Добавить();
							НоваяСтрока.КодПоказателя     = КодПоказателя;
							НоваяСтрока.ВариантЗаполнения = ВариантЗаполнения;

							ЕстьИзменения = Истина;
						КонецЕсли;
					КонецЦикла;

					Для каждого СтрокаТаблицы Из ЭлементМассива Цикл
						КодПоказателя = СтрокаТаблицы.КодПоказателя;

						Если ТаблицаВариантыЗаполнения.Найти(КодПоказателя, "КодПоказателя") = Неопределено Тогда
							// Рассмотрим вариант, когда в предыдущей версии формы для данной страницы
							// было предусмотрено автозаполнение ячейки по коду КодПоказателя, а в текущей - отключено.
							// В этом случае удаляем ячейку из таблицы пользовательских настроек вариантов автозаполнения.
							ЭлементМассива.Удалить(СтрокаТаблицы);

							ЕстьИзменения = Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	Возврат ЕстьИзменения;

КонецФункции // ОбновитьВариантыЗаполнения()

// Процедура управляет показом в форме периода построения отчета,
Процедура ПоказатьПериод()
	
	СтрПериодОтчета = ПредставлениеПериода(НачалоДня(мДатаНачалаПериодаОтчета), мДатаКонцаПериодаОтчета, "Л = uk_UA; ФП = Истина" );
	
    // Период в заголовке отчета
	ЭтаФорма.Заголовок = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление() + " (за " + СтрПериодОтчета + ")";	
	
	ЧастиДатыПодписи = РегламентированнаяОтчетность.ПолучитьЧастиДатыДляПодписи(ДатаПодписи,"uk_UA");
	
	мОтчетОВакансиях.Области.ДеньДаты.Значение 	= ЧастиДатыПодписи.День;
	
	МесяцУкр = Формат(ДатаПодписи, "Л=uk; ДЛФ=DD");
	МесяцУкр = Сред(МесяцУкр, Найти(МесяцУкр," ") + 1);
	МесяцУкр = Лев(МесяцУкр,  Найти(МесяцУкр," ") - 1);
	мОтчетОВакансиях.Области.МесяцДаты.Значение = МесяцУкр;
	
	Если ЧастиДатыПодписи.Свойство("ДвеПоследниеЦифрыГода") Тогда
		мОтчетОВакансиях.Области.ГодДаты.Значение = ЧастиДатыПодписи.ДвеПоследниеЦифрыГода;
	Иначе
		мОтчетОВакансиях.Области.ГодДаты.Значение = Формат(ДатаПодписи, "ДФ=yy");
	Конецесли;

КонецПроцедуры // ПоказатьПериод()	

// Процедура управляет выводом сведений о выбранной организации
// в соответствующие ячейки табличного документа.
// пока не прописаны коды - вернуться
Процедура ЗаполнитьСведенияОбОрганизации()
		
	ОргНазв = Организация.НаименованиеПолное;
	мОтчетОВакансиях.Области.ОРГ.Значение = ОргНазв;	
	
	СписокПоказателей = Новый СписокЗначений;
	СписокПоказателей.Добавить("","КодПоЕДРПОУ");	
	
	мОтчетОВакансиях.Области.ЕДРПОУ.Значение   	= РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, мДатаКонцаПериодаОтчета, СписокПоказателей).КодПоЕДРПОУ;	
	мОтчетОВакансиях.Области.АДРЕС.Значение    	= УправлениеКонтактнойИнформацией.ПолучитьАдресИзКонтактнойИнформации(Организация,"Юридический");
	мОтчетОВакансиях.Области.ТЕЛЕФОН.Значение 	= УправлениеКонтактнойИнформацией.ПолучитьТелефонИзКонтактнойИнформации(Организация);
	мОтчетОВакансиях.Области.ФАКС.Значение 		= УправлениеКонтактнойИнформацией.ПолучитьФаксИзКонтактнойИнформации(Организация);
	мОтчетОВакансиях.Области.ЕлПочта.Значение 	= УправлениеКонтактнойИнформацией.ПолучитьEmailИзКонтактнойИнформации(Организация);
	
	Отбор = Новый Структура("СтруктурнаяЕдиница,ОтветственноеЛицо", Организация, Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	ОтвЛица = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(КонецДня(мДатаКонцаПериодаОтчета), Отбор);
	Результат = ОтвЛица.Найти(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);

	Если Результат <> Неопределено Тогда
		ДанныеФИО = Новый Структура("ФизЛицо", Результат.ФизическоеЛицо);
		Попытка
			ФИОСрез = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(мДатаКонцаПериодаОтчета, ДанныеФИО);
			ФИОДиректораИДолжность = СокрЛП(ФИОСрез.Фамилия) + " " + ФИОСрез.Имя + " " + ФИОСрез.Отчество;
		Исключение
			ФИОДиректораИДолжность = "" + Результат.ФизическоеЛицо;	
		КонецПопытки;
		ФИОДиректораИДолжность = ФИОДиректораИДолжность + ", " + Нрег(Результат.Должность);
	Иначе
		ФИОДиректораИДолжность = "";	
	КонецЕсли;
	
	мОтчетОВакансиях.Области.РУКОВОДИТЕЛЬ.Значение = ФИОДиректораИДолжность;	
	
	КоличествоСтрок = мСтруктураМногострочныхРазделов[мИдГруппы1].Количество();
	Для НомерСтроки = 1 По КоличествоСтрок Цикл
	 	мОтчетОВакансиях.Области["M" + Формат(НомерСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G15"].Значение = ФИОДиректораИДолжность;
	КонецЦикла;
	
	мОтчетОВакансиях.Области.HZ.Значение = "V";
	
	ПоказатьПериод(); 
	
КонецПроцедуры // ЗаполнитьСведенияОбОрганизации()

// Процедура управляет представлением суммовых (денежных) показателей
// табличного документа при изменении размерности.
//
Процедура УстановитьФорматВывода()
	Перем СтруктураГруппы;

	///Если ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1 Тогда
		мДелитель = 1;
		мСтрокаФормата = "ЧЦ = 15; ЧДЦ = 2; ЧРД=,; ЧН=-; ЧО=0; ЧС = 0";

	///КонецЕсли;

	Для Каждого СтрПоказатели Из мТаблицаСоставПоказателей Цикл

		ИмяПоляТаблДокумента = СокрП(СтрПоказатели.ИмяПоляТаблДокумента);
		КодПоказателя        = СокрП(СтрПоказатели.КодПоказателяПоСоставу);
		ПризнМногострочность = СокрП(СтрПоказатели.ПризнМногострочности);

		Если КодПоказателя = "===" Тогда
			Прервать;
		КонецЕсли;

		Если (Лев(КодПоказателя, 2) = "//") ИЛИ (КодПоказателя = "###") Тогда
			Продолжить;
		КонецЕсли;

		КодПоказателяВФорме = СокрП(СтрПоказатели.КодПоказателяПоФорме);

		ИмяПоказателя = КодПоказателя;
		Если Не ПустаяСтрока(КодПоказателяВФорме) Тогда
			ИмяПоказателя = КодПоказателяВФорме;
		КонецЕсли;

		ТипДанныхПоказателя = СокрП(СтрПоказатели.ТипДанныхПоказателя);

		Если ТипДанныхПоказателя = "Е" Тогда

			ТекТабличноеПоле = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяПоляТаблДокумента];

			Если ПризнМногострочность <> "/" Тогда
				ФорматируемаяОбласть = ТекТабличноеПоле.Области[ИмяПоказателя];
				ФорматируемаяОбласть.Формат = мСтрокаФормата;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // УстановитьФорматВывода()

// Процедура управляет цветом фона автозаполняемых ячеек при:
//  а) автозаполнении;
//  б) ручном выборе другого варианта заполнения;
//  в) добавлении/удалении и листании дополнительных страниц.
//
// Цвет фона устанавливается в зависимости от варианта заполнения ячейки.
//
// Параметры:
//  ИмяЯчейки                - строка, имя ячейки, для которой устанавливаем цвет фона;
//  ВариантЗаполнения        - число,  вариант заполнения ячейки;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели, на которой для всех автозаполняемых
//                             ячеек табличного документа следует установить цвет фона.
//
Процедура УстановитьЦветФонаЯчейки(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "")
	Перем ЦветФона;

	Если Не ИмяЯчейки = Неопределено Тогда
		// устанавливаем цвет фона конкретной ячейки
		ИмяСтраницыПанели = "ОтчетОВакансиях";
		Если ИмяСтраницыПанели = Неопределено Тогда
			Возврат;
		КонецЕсли;

		ИмяПоляТаблДокумента = "ПолеТабличногоДокумента" + ИмяСтраницыПанели;

		Ключ = "_" + ВариантЗаполнения;
		мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);

		ЭлементыФормы[ИмяПоляТаблДокумента].Области[ИмяЯчейки].ЦветФона = ЦветФона;

	Иначе
		// устанавливаем цвет фона всех автозаполняемых ячеек активной страницы

		ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
		Если ТаблицаВариантыЗаполнения = Неопределено Тогда
			Возврат;
		КонецЕсли;

		Для Каждого Строка Из ТаблицаВариантыЗаполнения Цикл
			ИмяПоказателя     = Строка.КодПоказателя;
			ВариантЗаполнения = Строка.ВариантЗаполнения;
			Коммент           = Строка.Комментарий;

			ТекОбластьЯчеек = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя];
			Если (Не ПустаяСтрока(Коммент)) ИЛИ (Не ПустаяСтрока(ТекОбластьЯчеек.Примечание.Текст)) Тогда
				ТекОбластьЯчеек.Примечание.Текст = Коммент;
			КонецЕсли;

			Ключ = "_" + ВариантЗаполнения;
			мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);

			ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя].ЦветФона = ЦветФона;
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры // УстановитьЦветФонаЯчейки()
        
// Обновляет пользовательские настройки при изменении версии формы.
// унифицирована, однако использование пока ... вопрос
Процедура ОбновитьПользовательскиеНастройки(СписокСохранения)
	Перем ПоказателиОтчета;

	Сообщить(НСтр("ru='Измененилась версия формы отчета."
"Выполняется обновление пользовательских настроек.';uk='Змінилася версія форми звіту."
"Виконується відновлення користувальницьких настройок.'"), СтатусСообщения.Информация);

	// Обновляем варианты заполнения показателей отчета
	ЕстьИзмененияВариантыЗаполнения = ОбновитьВариантыЗаполнения();

	ВерсияФормы = СписокСохранения.ВерсияФормы;
	Если ЕстьИзмененияВариантыЗаполнения ИЛИ ВерсияФормы = "01/01/2005" Тогда
		//
		//Если  ВерсияФормы = "01/01/2005" Тогда
		//	Если СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета) Тогда
		//		Для Каждого ЭлементСтруктуры Из ПоказателиОтчета Цикл
		//			ИмяГруппы       = ЭлементСтруктуры.Ключ;
		//			СписокПоказателей = ЭлементСтруктуры.Значение;
		//			
		//			Если ИмяГруппы = "ПолеТабличногоДокументаОтчетОВакансиях" Тогда
		//				Для каждого Показатель Из СписокПоказателей Цикл
		//					Если Показатель.Ключ = "ГодДаты" Тогда
		//							СписокПоказателей.Вставить(Показатель.Ключ, Формат(ДатаПодписи, "ДФ=yy"));
		//					КонецЕсли;
		//				КонецЦикла;
		//			КонецЕсли;
		//		КонецЦикла;
		//		СписокСохранения.ПоказателиОтчета =  ПоказателиОтчета;
		//	КонецЕсли;
		//КонецЕсли;
		//// Если были изменения в пользовательских настройках,
		//// то устанавливаем флаг модифицированности формы.
		//Модифицированность = Истина;

		Сообщить(НСтр("ru='Обновление пользовательских настроек успешно завершено!';uk='Оновлення користувальницьких настройок успішно завершене!'"));

	Иначе

		Сообщить(НСтр("ru='Изменения в структуре пользовательских настроек не обнаружены!';uk='Зміни в структурі користувальницьких настройок не виявлені!'"));

	КонецЕсли;

КонецПроцедуры // ОбновитьПользовательскиеНастройки()
        
// Процедура отображает параметры автозаполнения ячейки в соответствующих элементах
// текущей страницы формы и, в зависимости от параметров, управляет доступностью 
// элементов .формы.
//
// Параметры:
//  Текущая ячейка - область ячеек табличного документа, по которой необходимо
//                   отобразить параметры автозаполнения.
//
Процедура ВывестиЭлементыВариантаЗаполнения(ТекущаяЯчейка)

	ИмяПоказателя = ТекущаяЯчейка.Имя;

	Если Найти(ИмяПоказателя, ":") > 0 Тогда
		// Неименованная область ячеек таблицы
		ДоступностьЭлементовВариантаЗаполнения(, ТекущаяЯчейка);
		Возврат;
	КонецЕсли;

	Если Не ТекущаяЯчейка.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
		ДоступностьЭлементовВариантаЗаполнения(, ТекущаяЯчейка);
		Возврат;
	КонецЕсли;

	Если НЕ ТекущаяЯчейка.СодержитЗначение Тогда
		ДоступностьЭлементовВариантаЗаполнения(, ТекущаяЯчейка);
		Возврат;
	КонецЕсли;

	Если ТекущаяЯчейка.Защита Тогда
		ДоступностьЭлементовВариантаЗаполнения(, ТекущаяЯчейка);
		Возврат;
	КонецЕсли;

	//ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
	ИмяТекущейСтраницыПанели = "ОтчетОВакансиях";

	ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	НайденнаяСтрока = ТаблицаВариантыЗаполнения.Найти(ИмяПоказателя, "КодПоказателя");
	Если НайденнаяСтрока = Неопределено Тогда
		ДоступностьЭлементовВариантаЗаполнения(, ТекущаяЯчейка);
		Возврат;
	КонецЕсли;

	ВариантЗаполнения = НайденнаяСтрока.ВариантЗаполнения;
	Дельта            = НайденнаяСтрока.Дельта;
	Коммент           = НайденнаяСтрока.Комментарий;

	ЭлементыФормы["ПолеВыбораВариантЗаполнения"    + ИмяТекущейСтраницыПанели].Значение = ВариантЗаполнения;
	ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = Дельта;
	ЭлементыФормы["ПолеВводаКомментарий"           + ИмяТекущейСтраницыПанели].Значение = Коммент;

	ДоступностьЭлементовВариантаЗаполнения(ВариантЗаполнения);

КонецПроцедуры // ВывестиЭлементыВариантаЗаполнения()

// Процедура устанавливает новый вариант заполнения активной ячейки,
// выполняет реорганизацию структуры вариантов заполнения ячеек
// и управляет видимостью элементов формы, отображающих параметры
// автозаполнения активной ячейки.
//
// Параметры:
//    НовыйВариант - число, соттветствует варианту заполнения ячейки.
//                   Может принимать значение:
//                       1 - не заполнять автоматически;
//                       2 - заполнять автоматически с корректировкой;
//                       3 - заполнять автоматически без корректировки.
//
Процедура ИзменитьВариантЗаполненияЯчейки(НовыйВариант)

	//ТекущаяСтраницаПанели     = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
	ИмяТекущейСтраницыПанели  = "ОтчетОВакансиях";

	ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущаяОбласть  = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
	ИмяПоказателя   = ТекущаяОбласть.Имя;

	НайденнаяСтрока = ТаблицаВариантыЗаполнения.Найти(ИмяПоказателя, "КодПоказателя");
	Если НайденнаяСтрока <> Неопределено Тогда
		НайденнаяСтрока.ВариантЗаполнения = НовыйВариант;
	КонецЕсли;

	Если НовыйВариант = 1 Тогда

		НайденнаяСтрока.Дельта = 0;
		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = 0;

	ИначеЕсли НовыйВариант = 2 Тогда

		Если (ТекущаяОбласть.Значение = 0) И (НайденнаяСтрока.Дельта = 0) Тогда
			ТекущаяОбласть.Значение = НайденнаяСтрока.ЗначениеАвто;
		КонецЕсли;

		Попытка
			Дельта = ТекущаяОбласть.Значение - НайденнаяСтрока.ЗначениеАвто;
		Исключение
		КонецПопытки;

		НайденнаяСтрока.Дельта = Дельта;
		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = Дельта;

	ИначеЕсли НовыйВариант = 3 Тогда

		Если (ТекущаяОбласть.Значение = 0) И (НайденнаяСтрока.Дельта = 0) Тогда
			ТекущаяОбласть.Значение = НайденнаяСтрока.ЗначениеАвто;
		КонецЕсли;

		НайденнаяСтрока.Дельта = 0;
		НайденнаяСтрока.Комментарий = "";

		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = 0;
		ЭлементыФормы["ПолеВводаКомментарий"           + ИмяТекущейСтраницыПанели].Значение = "";
		
		// Очищаем комментарий редактируемой ячейки
		ТекущаяОбласть.Примечание.Текст = "";

	КонецЕсли;

	УстановитьЦветФонаЯчейки(ИмяПоказателя, НовыйВариант);
	ДоступностьЭлементовВариантаЗаполнения(НовыйВариант);

КонецПроцедуры // ИзменитьВариантЗаполненияЯчейки()

// Процедура устанавливает новое значение автокорректировки активной ячейки,
// участвующей в автозаполнении, выполняет реорганизацию структуры вариантов
// заполнения ячеек и пересчитывает значение активной ячейки с учетом назначенной
// автокорректировки.
//
// Параметры:
//   Дельта - число, новое значение автокорректировки активной ячейки.
//
Процедура ИзменитьЗначениеАвтокорректировки(Дельта)

	//ТекущаяСтраницаПанели     = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
	ИмяТекущейСтраницыПанели  = "ОтчетОВакансиях";

	ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущаяОбласть    = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
	ИмяПоказателя     = ТекущаяОбласть.Имя;

	НайденнаяСтрока   = ТаблицаВариантыЗаполнения.Найти(ИмяПоказателя, "КодПоказателя");
	Если НайденнаяСтрока <> Неопределено Тогда
		НайденнаяСтрока.Дельта = Дельта;
		ТекущаяОбласть.Значение = НайденнаяСтрока.ЗначениеАвто + Дельта;
	КонецЕсли;
	
КонецПроцедуры // ИзменитьЗначениеАвтокорректировки()

// Процедура устанавливает комментарий по активной ячейки, участвующей в автозаполнении,
// выполняет реорганизацию структуры вариантов заполнения ячеек и устанавливает примечание
// по активной ячейке табличного документа.
//
// Параметры:
//   Комментарий - строка, содержащая примечание по активной ячейке.
//
Процедура ИзменитьКомментарийПоЯчейке(Коммент)

	//ТекущаяСтраницаПанели     = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
	ИмяТекущейСтраницыПанели  = "ОтчетОВакансиях";

	ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущаяОбласть  = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
	ИмяПоказателя   = ТекущаяОбласть.Имя;

	НайденнаяСтрока = ТаблицаВариантыЗаполнения.Найти(ИмяПоказателя, "КодПоказателя");
	Если НайденнаяСтрока <> Неопределено Тогда
		НайденнаяСтрока.Комментарий = Коммент;
	КонецЕсли;

	ТекущаяОбласть.Примечание.Текст = Коммент;

КонецПроцедуры // ИзменитьКомментарийПоЯчейке()

// Процедура очищает содержимое редактируемыхи и вычисляемых ячеек
// поля табличного документа, переданного параметром ВыбТабличноеПоле.
//
// Параметры:
//  ВыбТабличноеПоле - поле табличного документа.
//
Процедура ОчиститьТабличноеПоле(ВыбТабличноеПоле)

	Для Инд = 0 По ВыбТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ВыбТабличноеПоле.Области[Инд];

		Если Тип ("РисунокТабличногоДокумента") = ТипЗнч (ТекущаяОбласть) Тогда
			ТекущаяОбласть.Значение = ""; 
		Иначе	
			
			Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ТекущаяОбласть.СодержитЗначение Тогда
				Продолжить;
			КонецЕсли;
			
			//Если ТекущаяОбласть.Защита Тогда
			//	Продолжить;
			//КонецЕсли;
			
			ТекущаяОбласть.Очистить();
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ОчиститьТабличноеПоле()

// Процедура очищает содержимое редактируемых и вычисляемых
// ячеек табличного документа.
//
Процедура Очистить()
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	// Очищаем содержимое ячеек поля табличного документа
	ТекТабличноеПоле = мОтчетОВакансиях;
	ОчиститьТабличноеПоле(ТекТабличноеПоле);

	// Далее выясним, есть ли добавленные в многострочный раздел дополнительные строки.
	// При наличии таких строк удаляем их.
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл

		ИмяГруппы        = ЭлементСтруктуры.Ключ;
		СтруктураГруппы  = ЭлементСтруктуры.Значение;
		ТекТабличноеПоле = мОтчетОВакансиях;

		мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

		ТекущееКоличествоСтрок = СтруктураГруппы.Количество();

		Если ТекущееКоличествоСтрок > ИсходноеКоличествоСтрокГруппы Тогда
			// Имеются дополнительные строки.
			// Удаляем их

			НомерВерхЯчейкиОбласти = 999999999;
			НомерНижнЯчейкиОбласти = 0;
			Для Нстр =  ИсходноеКоличествоСтрокГруппы + 1 ПО ТекущееКоличествоСтрок Цикл

				ТекСтрокаТаблицы = СтруктураГруппы[НСтр - 1];
				
				ИмяПервойКолонки = СтруктураГруппы.Колонки[0].Имя;
				ИмяПервойЯчейкиУдаляемойСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Формат(НСтр,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(СтруктураГруппы.Колонки[0].Имя,2);

				// область первой ячейки удаляемой строки многострочного раздела
				ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];

				// номера строк удаляемой области
				Если НомерВерхЯчейкиОбласти > ОбластьЯчейки.Верх Тогда
				
					НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;	
				
				КонецЕсли;
				Если НомерНижнЯчейкиОбласти < ОбластьЯчейки.Низ Тогда
				
					НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;	
				
				КонецЕсли;

			КонецЦикла;
			
			// Определим область удаляемой строки
			УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );

			// непосредственно удаляем область строки табличного документа
			ТекТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
			
		КонецЕсли;

		СтруктураГруппы.Очистить();
		Для Сч = 1 По ИсходноеКоличествоСтрокГруппы Цикл
			СтруктураГруппы.Добавить();	
		КонецЦикла;
		
	КонецЦикла;
	
 	ЗаполнитьСведенияОбОрганизации();
	СинхронизацияРазделов1и2();
	
	// Устанавливаем флаг модифицированности формы
	Модифицированность = Истина;
	
КонецПроцедуры // Очистить()

Процедура РазбитьПечатнуюФорму(ПечФорма)
	
	ИмяФормы = "ОтчетОВакансиях";
	ПустойТаблДок = Новый ТабличныйДокумент;
	//ПустойТаблДок.АвтоМасштаб = Истина;
	ПустойТаблДок.АвтоМасштаб = Ложь;
	ПустойТаблДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ПустойТаблДок.ЧерноБелаяПечать = Истина;
	ПустойТаблДок.ЧерноБелыйПросмотр = Истина;
	ПослВерх = 1;
	Если ЕстьДуплекс Тогда
		НовТаблДок = Новый ТабличныйДокумент();
	КонецЕсли;

	Для Сч = 1 По ПечФорма.ВысотаТаблицы Цикл
		
		Если НЕ ПустойТаблДок.ПроверитьВывод(ПечФорма.ПолучитьОбласть(ПослВерх, 0, Сч, 0)) Тогда
			Пока ПослВерх < Сч-1 И ПечФорма.ПолучитьОбласть(Сч-1, , Сч-1, ).Область().ВместеСоСледующим Цикл
				Сч = Сч - 1;
			КонецЦикла; 
			Если НЕ ЕстьДуплекс Тогда
				НовТаблДок = Новый ТабличныйДокумент();
			КонецЕсли;
			НовТаблДок.Вывести(ПечФорма.ПолучитьОбласть(ПослВерх, 0, Сч - 1, 0));
			НовТаблДок.АвтоМасштаб = Истина;
			НовТаблДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
			НовТаблДок.ЧерноБелаяПечать = Истина;
			НовТаблДок.ЧерноБелыйПросмотр = Истина;
			НовТаблДок.ОтображатьСетку = Ложь;
			НовТаблДок.ОтображатьЗаголовки = Ложь;
			Если НЕ ЕстьДуплекс Тогда
				мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
			КонецЕсли;
			ПослВерх = Сч;
		КонецЕсли;
		
		Если ПечФорма.Область(Сч, 0, Сч, 0).КонецСтраницы Тогда
			
			Если НЕ ЕстьДуплекс Тогда
				НовТаблДок = Новый ТабличныйДокумент();
			КонецЕсли;
			НовТаблДок.Вывести(ПечФорма.ПолучитьОбласть(ПослВерх, 0, Сч, 0));
			НовТаблДок.АвтоМасштаб = Истина;
			НовТаблДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
			НовТаблДок.ЧерноБелаяПечать = Истина;
			НовТаблДок.ЧерноБелыйПросмотр = Истина;
			НовТаблДок.ОтображатьСетку = Ложь;
			НовТаблДок.ОтображатьЗаголовки = Ложь;
			Если НЕ ЕстьДуплекс Тогда
				мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
			КонецЕсли;
			ПослВерх = Сч + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПослВерх <= ПечФорма.ВысотаТаблицы Тогда
		Если НЕ ЕстьДуплекс Тогда
			НовТаблДок = Новый ТабличныйДокумент();
		КонецЕсли;
		НовТаблДок.Вывести(ПечФорма.ПолучитьОбласть(ПослВерх, 0, ПечФорма.ВысотаТаблицы, 0));
		НовТаблДок.АвтоМасштаб = Истина;
		НовТаблДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		НовТаблДок.ЧерноБелаяПечать = Истина;
		НовТаблДок.ЧерноБелыйПросмотр = Истина;
		НовТаблДок.ОтображатьСетку = Ложь;
		НовТаблДок.ОтображатьЗаголовки = Ложь;
		Если НЕ ЕстьДуплекс Тогда
			мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьДуплекс Тогда
		мПечатныеФормы.Добавить(НовТаблДок, ИмяФормы);
	КонецЕсли;
	
КонецПроцедуры
        
// Процедура управляет выводом отчета на печать.
//
// Параметры:
//  ВидПечати - строка, задающая способ печати:
//              "ПоказатьБланк" - непосредственный вывод на печать;
//              "ПечататьСразу" - с предварительным просмотром.
//
Процедура Печать(ВидПечати)
    мПечатныеФормы.Очистить();
	
	ОбновитьСтруктуруМногострочныхРазделов();
	
	ВысотаОтчета = мОтчетОВакансиях.Области["ПредупреждениеОПечати"].Верх - 2;
	
	ПечатнаяФорма = Новый ТабличныйДокумент();
	
	Для Сч = 1 По ВысотаОтчета Цикл
		
		Если НЕ мОтчетОВакансиях.Область(Сч,0,Сч,0).Видимость Тогда
			Продолжить;
		КонецЕсли;
		
		ПечатнаяФорма.Вывести(мОтчетОВакансиях.ПолучитьОбласть(Сч,0,Сч,0));		
		
	КонецЦикла;
	
	РазбитьПечатнуюФорму(ПечатнаяФорма);
	
	РегламентированнаяОтчетность.ПроставитьНомераЛистов(ЭтаФорма, ВидПечати, Ложь);

КонецПроцедуры // Печать()


// Формирует структуру данных отчета и помещает в реквизит типа ХранилищеЗначения
// документа РегламентированныйОтчет.
// 
// При первом сохранении данных отчета создает новый документ этого вида.
// Структура данных отчета состоит из элементов:
//   - значения редактируемых реквизитов отчета;
//   - данные по многострочным разделам отчета;
//   - данные по  многостраничным разделам отчета;
//   - данные по вариантам заполнения реквизитов,
//     участвующих в автозаполнении.
//
Функция Сохранить() Экспорт
	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчета(ЭтаФорма) Тогда
		Возврат Ложь;
	КонецЕсли;
	Если (мСохраненныйДок = Неопределено) ИЛИ (мСкопированаФорма <> Неопределено) Тогда
		// Отчет новый или был скопирован.

		Если мСохраненныйДок = Неопределено Тогда
			// создаем новый документ вида РегламентированныйОтчет
			мСохраненныйДок  = Документы.РегламентированныйОтчет.СоздатьДокумент();
		КонецЕсли;

		мСохраненныйДок.Дата = мДатаКонцаПериодаОтчета;
		мСохраненныйДок.УстановитьВремя();
		мСохраненныйДок.УстановитьНовыйНомер();

	КонецЕсли;

	// установим текущие значения реквизитов документа
	мСохраненныйДок.ИсточникОтчета             = ЭтотОбъект.Метаданные().Имя;
	мСохраненныйДок.НаименованиеОтчета         = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление();
	мСохраненныйДок.ДатаНачала                 = НачалоДня(мДатаНачалаПериодаОтчета);
	мСохраненныйДок.ДатаОкончания              = мДатаКонцаПериодаОтчета;
	мСохраненныйДок.ВыбраннаяФорма             = мВыбраннаяФорма;
	мСохраненныйДок.Организация                = Организация;
	мСохраненныйДок.ДатаПодписи                = ДатаПодписи;
	//мСохраненныйДок.ЕдиницаИзмерения           = ЕдиницаИзмерения;
	мСохраненныйДок.ТочностьЕдиницыИзмерения   = ТочностьЕдиницыИзмерения;
	мСохраненныйДок.ВидОтчетности              = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	мСохраненныйДок.Комментарий                = Комментарий;

	// обновляем структуру многострочных разделов
	ОбновитьСтруктуруМногострочныхРазделов();

	// формируем данные редактируемых ячеек таблицы
	ПоказателиОтчета = Новый Структура();

	ИмяТекТабличногоПоля    = "ПолеТабличногоДокументаОтчетОВакансиях";
	
	ТекТабличноеПоле        = мОтчетОВакансиях;

	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
	ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);

	// формируем список сохранения
	СписокСохранения = Новый Структура();

	// вставляем данные редактируемых ячеек таблицы	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);

	// вставляем данные вариантов автозаполнения ячеек
	СписокСохранения.Вставить("ДанныеВариантовАвтоЗаполнения", мСтруктураВариантыЗаполнения);
	
	// вставляем данные многострочных разделов
	СписокСохранения.Вставить("ДанныеМногострочныхРазделов", мСтруктураМногострочныхРазделов);

	// вствляем версию формы
	СписокСохранения.Вставить("ВерсияФормы", мВерсияФормы);

	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;

	// Сохраняем выбранные листы для печати в хранилище
	ХранилищеДанных = Новый ХранилищеЗначения(мДеревоСтраницОтчета);
	мСохраненныйДок.ДеревоНастройкиСтраниц = ХранилищеДанных;
	
	// записываем документ, хранящий данные отчета
	мСохраненныйДок.Записать();
	Модифицированность = Ложь;

	РегламентированнаяОтчетность.ПослеЗаписиРегламентированногоОтчета(ЭтаФорма);
	Возврат Истина;
КонецФункции // Сохранить()

// Восстанавливает сохраненные данные отчета.
//
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногострочныхРазделов;
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем ВерсияФормы;

	// восстанавливаем реквизиты отчета
	Организация              = мСохраненныйДок.Организация;
	
	ЭтаФорма.ЭлементыФормы.НадписьНазваниеОрганизации.Заголовок = Организация.Наименование;

	ДатаПодписи              = мСохраненныйДок.ДатаПодписи;
	//ЕдиницаИзмерения         = мСохраненныйДок.ЕдиницаИзмерения;
	ТочностьЕдиницыИзмерения = мСохраненныйДок.ТочностьЕдиницыИзмерения;
	Комментарий              = мСохраненныйДок.Комментарий;

	// восстанавливаем сохраненные данные отчета
	СписокСохранения = мСохраненныйДок.ДанныеОтчета.Получить();
	
	// восстанавливаем сохраненные данные многострочных разделов
	Если СписокСохранения.Свойство( "ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов) Тогда
		мСтруктураМногострочныхРазделов = ДанныеМногострочныхРазделов;

		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы       = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭлементСтруктуры.Значение;

			ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
		КонецЦикла;
	КонецЕсли;


	// восстанавливаем сохраненные данные вариантов автозаполнения ячеек
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) Тогда
		мСтруктураВариантыЗаполнения = ДанныеВариановАвтоЗаполнения;
	КонецЕсли;

	// восстанавливаем версию формы
	Если СписокСохранения.Свойство("ВерсияФормы", ВерсияФормы) Тогда
		Если ВерсияФормы <> мВерсияФормы Тогда
			// Версия формы изменилась.
			// Обновляем пользовательские настройки.
			ОбновитьПользовательскиеНастройки(СписокСохранения);
		КонецЕсли;
	КонецЕсли;

	// восстановим сохраненные данные редактируемых ячеек
	СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета );

	Для Каждого ПоказателиСтраницы Из ПоказателиОтчета Цикл
		ИмяТекТабличноеПоле = ПоказателиСтраницы.Ключ;
		ТекТабличноеПоле    = ЭлементыФормы[ИмяТекТабличноеПоле];
		ПоказателиТаблПоле  = ПоказателиСтраницы.Значение;

		Для Каждого Показатель Из ПоказателиТаблПоле Цикл
			ИмяПоказателя       = Показатель.Ключ;
			ЗначениеПоказателя  = Показатель.Значение;

			// установим значение в таблице
			Попытка
				ТекТабличноеПоле.Области[ИмяПоказателя].Значение = ЗначениеПоказателя;
			Исключение
			КонецПопытки;
		КонецЦикла;
	КонецЦикла;

	УстановитьФорматВывода();
КонецПроцедуры // ВосстановитьСохраненныеДанные()

// Процедура выполняет заполнение отчета автоматически по данным ИБ.
//
Процедура ЗаполнитьАвто() Экспорт      
	
	Если (РегламентированнаяОтчетность.ИДКонфигурации() = "УПП") 
	 ИЛИ (РегламентированнаяОтчетность.ИДКонфигурации() = "ЗУП")
	 ИЛИ (РегламентированнаяОтчетность.ИДКонфигурации() = "УТП")Тогда
			 
			 ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.Должность,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.КоличествоСтавок,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.МинимальнаяТарифнаяСтавка,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.МаксимальнаяТарифнаяСтавка,
			 |	Сумма(РаботникиОрганизацийСрезПоследних.ЗанимаемыхСтавок) КАК Занято
			 |ИЗ
			 |	РегистрСведений.ШтатноеРасписаниеОрганизаций.СрезПоследних(&Дата, ) КАК ШтатноеРасписаниеОрганизацийСрезПоследних
			 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&Дата, ) КАК РаботникиОрганизацийСрезПоследних
			 |		ПО ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации = РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации
			 |			И ШтатноеРасписаниеОрганизацийСрезПоследних.Должность = РаботникиОрганизацийСрезПоследних.Должность
			 |ГДЕ
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.КоличествоСтавок > 0
			 |И
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации В
			 |	(
			 |	Выбрать   
			 |			Ссылка
			 |		Из 
			 |			Справочник.ПодразделенияОрганизаций	КАК ПодразделенияОрганизаций 
			 |		Где
			 |			ПодразделенияОрганизаций.Владелец = &Организация )
			 |СГРУППИРОВАТЬ ПО
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.Должность,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.КоличествоСтавок,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.МаксимальнаяТарифнаяСтавка,
			 |	ШтатноеРасписаниеОрганизацийСрезПоследних.МинимальнаяТарифнаяСтавка
			 |";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата",мДатаКонцаПериодаОтчета);
		Запрос.УстановитьПараметр("Организация", Организация);
		ВыборкаВакансий = Запрос.Выполнить().Выбрать();
		
		КвоСтрок = 0;
	   
		Пока ВыборкаВакансий.Следующий() Цикл
			
			Если ВыборкаВакансий.Занято = NULL Тогда
				Занято = 0;
			Иначе
				Занято = ВыборкаВакансий.Занято;
			КонецЕсли;
			
			Если (ВыборкаВакансий.КоличествоСтавок - Занято) > 0 Тогда	
				
				Если КвоСтрок > 0 Тогда
			    	ВставитьДополнительнуюСтроку(мИдГруппы2); 
			    	ВставитьДополнительнуюСтроку(мИдГруппы1); 
				КонецЕсли;
				  
			   	КвоСтрок = КвоСтрок + 1;
			   	
				ОбластьЯчейки = мОтчетОВакансиях.Области["R" + Формат(КвоСтрок,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G1"];
				ОбластьЯчейки.Значение = ВыборкаВакансий.Должность;
				
				ОбластьЯчейки = мОтчетОВакансиях.Области["R" + Формат(КвоСтрок,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G2"];
				ОбластьЯчейки.Значение = ВыборкаВакансий.КоличествоСтавок - Занято;
				
				ОбластьЯчейки = мОтчетОВакансиях.Области["R" + Формат(КвоСтрок,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G3"];
				ОбластьЯчейки.Значение = ВыборкаВакансий.МинимальнаяТарифнаяСтавка;
				
				ОбластьЯчейки = мОтчетОВакансиях.Области["R" + Формат(КвоСтрок,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G4"];
				ОбластьЯчейки.Значение = ВыборкаВакансий.МаксимальнаяТарифнаяСтавка;
				
				ОбластьЯчейки = мОтчетОВакансиях.Области["R" + Формат(КвоСтрок,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G5"];
				ОбластьЯчейки.Значение = "(" + ВыборкаВакансий.ПодразделениеОрганизации + ")";
				
			КонецЕсли;
			
	   КонецЦикла;   	      
	   
	   ОбновитьСтруктуруМногострочныхРазделов();	
	   
	   Модифицированность = Истина;
	   
	   СинхронизацияРазделов1и2();
	   
   Иначе
	   
	   ПоказатьПредупреждение(,НСтр("ru = 'Для данной конфигурации автоматическое заполнение не предусмотрено!'; uk = 'Для цієї конфігурації автоматичне заповнення не передбачено!'"));
	   
   КонецЕсли;

КонецПроцедуры // ЗаполнитьАвто()

 Процедура ЗаполнитьНачальныеНастройки(СоставПоказателей) Экспорт

	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет("СоставПоказателей2017");

	ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);

	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

	ОписаниеТиповЧисло15   = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);

	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл

		ТекОбласть    = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы   = ТекОбласть.Имя;

		// Таблица значений содержит информацию о вариантах заполнения показателя отчета.
		// В колонках таблицы значений отображается следуящая информация:
		//   - имя ячейки (код показателя);
		//   - вариант заполнения ячейки;
		//   - вычисленное по данным ИБ значение ячейки;
		//   - величина автокорректировки значения ячейки;
		//   - примечание к ячейке.
		//
		ТаблицаВариантыЗаполнения = Новый ТаблицаЗначений;
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока15  );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );

		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета

			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;

			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// признак многострочности определяется по третьей колонке макета
			ПризнМногострочность = СокрЛП(МакетСоставаПоказателей.Область(Ном, 3).Текст);
			// по четвертой колонке определяется тип данных реквизита
			ТипДанныхРеквизита   = СокрЛП(МакетСоставаПоказателей.Область(Ном, 4).Текст);
			// по колонке 5 определяется исходное количество строк многострочного раздела
			стрКолСтрокИсходный  = СокрЛП(МакетСоставаПоказателей.Область(Ном, 5).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = СокрЛП(МакетСоставаПоказателей.Область(Ном, 6).Текст);

			чКолСтрокИсходный    = ? (ПустаяСтрока(стрКолСтрокИсходный),  0, Число(стрКолСтрокИсходный));
			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));

			// формируем таблицу значений, содержащей состав показателей отчета
			НоваяСтрока = мТаблицаСоставПоказателей.Добавить();
			НоваяСтрока.ИмяПоляТаблДокумента   = ИмяСтраницы;
			НоваяСтрока.КодПоказателяПоСоставу = КодПоказателя;
			НоваяСтрока.КодПоказателяПоФорме   = КодПоказателяПоФорме;
			НоваяСтрока.ПризнМногострочности   = ПризнМногострочность;
			НоваяСтрока.ТипДанныхПоказателя    = ТипДанныхРеквизита;

			Если чВариантЗаполнения <> 0 Тогда
				// Если в колонке 6 задан варианет заполнения показателя, 
				// то его значение может определяться по данным ИБ.
				// Дополняем таблицу значений вариантов заполнения
				НоваяСтрока = ТаблицаВариантыЗаполнения.Добавить();
				НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
				НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
			КонецЕсли;

		КонецЦикла;

		Если ТаблицаВариантыЗаполнения.Количество() > 0 Тогда
			// имеются автоматически заполняемые ячейки

			// Для обеспечения возможности назначения разных вариантов заполнения одного и того же показателя
			// на разных страницах, создаем массив, хранящий информацию о вариантах заполнения ячеек по каждой
			// странице многостраничного раздела.
			МассивВариантовЗаполненияСтраниц = Новый Массив;
			МассивВариантовЗаполненияСтраниц.Добавить(ТаблицаВариантыЗаполнения);

			// Создаем структуру, содержащей варианты заполнения показателей отчета.
			//
			// В качестве ключа используется имя страницы основной панели формы, содержащей
			//   табличный документ с автоматически заполняемыми показателями.
			// В качестве значения принимаем Массив из таблицы значений - вариантов заполнения
			//   показателей по каждой странице многостраничного раздела.
			//
			мСтруктураВариантыЗаполнения.Вставить(ИмяСтраницы, МассивВариантовЗаполненияСтраниц);
		Конецесли;

	КонецЦикла;

	ЭлементыФормы.ПолеТабличногоДокументаОтчетОВакансиях.ОбластьПечати = ЭлементыФормы.ПолеТабличногоДокументаОтчетОВакансиях.Области.Найти("ПредупреждениеОПечати");

КонецПроцедуры // ЗаполнитьНачальныеНастройки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Процедура управляет доступностью полей указания параметров заполнения ячеек,
// которые могут быть заполнены при автозаполнении отчета.
//
// Параметры:
//  Вариант         - число, вариант заполнения ячейки;
//  Текущая область - область ячеек табличного документа, ссылка на ячейку.
//
Процедура ДоступностьЭлементовВариантаЗаполнения(Вариант = Неопределено, ТекущаяОбласть = Неопределено)
	
	//ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
	ИмяТекущейСтраницыПанели = "ОтчетОВакансиях";

	Если Не мСтруктураВариантыЗаполнения.Свойство(ИмяТекущейСтраницыПанели) Тогда
		Возврат;
	КонецЕсли;

	ДоступностьПолеВыбораВариантЗаполнения    = Ложь;
	ДоступностьПолеВводаЗначениеКорректировки = Ложь;
	ДоступностьПолеВводаКомментарий           = Ложь;

	Если Вариант <> Неопределено Тогда
		ДоступностьПолеВыбораВариантЗаполнения            = Истина;
		Если Вариант <> 3 Тогда
			ДоступностьПолеВводаКомментарий               = Истина;
			Если Вариант = 2 Тогда
				ДоступностьПолеВводаЗначениеКорректировки = Истина;
			КонецЕсли;
		КонецЕсли;

	Иначе
		ОписаниеЯчейки = "";
		Если ТекущаяОбласть.ЦветФона = Новый Цвет(192, 220, 192) Тогда
			ОписаниеЯчейки    = НСтр("ru='Значение вычисляется автоматически';uk='Значення обчислюється автоматично'");
		ИначеЕсли ТекущаяОбласть.ЦветФона = Новый Цвет(255, 255, 192) Тогда
			ОписаниеЯчейки    = НСтр("ru='Значение вводится вручную';uk='Значення вводиться вручну'");
		КонецЕсли;

		ЭлементыФормы["ПолеВыбораВариантЗаполнения"    + ИмяТекущейСтраницыПанели].Значение = "";
		ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Значение = 0;
		ЭлементыФормы["ПолеВводаКомментарий"           + ИмяТекущейСтраницыПанели].Значение = ОписаниеЯчейки;

	КонецЕсли;

	ЭлементыФормы["ПолеВыбораВариантЗаполнения"    + ИмяТекущейСтраницыПанели].Доступность = ДоступностьПолеВыбораВариантЗаполнения;
	ЭлементыФормы["ПолеВводаЗначениеКорректировки" + ИмяТекущейСтраницыПанели].Доступность = ДоступностьПолеВводаЗначениеКорректировки;
	ЭлементыФормы["ПолеВводаКомментарий"           + ИмяТекущейСтраницыПанели].Доступность = ДоступностьПолеВводаКомментарий;

КонецПроцедуры // ДоступностьЭлементовВариантаЗаполнения()
   
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	// выполняем начальные настройки
	ЗаполнитьНачальныеНастройки("СоставПоказателей2017");

	// формируем список элементов подменю, связанное с кнопкой <Печать>
	ЭлементМеню = ЭлементыФормы.КоманднаяПанельДействий.Кнопки.ПодменюПечати;
	Если ЭлементМеню.Кнопки.Количество() = 0 Тогда
		ЭлементМеню.Кнопки.Добавить("ПечататьСразу", ТипКнопкиКоманднойПанели.Действие,
		НСтр("ru='Печатать сразу';uk='Друкувати відразу'"), Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
		
		ЭлементМеню.Кнопки.Добавить("ПоказатьБланк", ТипКнопкиКоманднойПанели.Действие,
	                            НСтр("ru='Предварительный просмотр';uk='Попередній перегляд'"), Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
								
		ЭлементМеню.Кнопки.Добавить("Разделитель1", ТипКнопкиКоманднойПанели.Разделитель);

		ЭлементМеню.Кнопки.Добавить("ЕстьДуплекс", ТипКнопкиКоманднойПанели.Действие,
		                            НСтр("ru='Принтер поддерживает автом. дуплекс';uk='Принтер підтримує автом. дуплекс'"), Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
									
		ЭлементМеню.Кнопки.ЕстьДуплекс.Пометка = ЕстьДуплекс;
	КонецЕсли;							

	ЛинияСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 2);
	ЛинияПусто = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);

	ЭтаФорма.ЭлементыФормы.НадписьНазваниеОрганизации.Заголовок = Организация.Наименование;
	
	мМассивТиповЛиний = Новый Массив();
	мМассивТиповЛиний.Вставить(0, ЛинияПусто);
	мМассивТиповЛиний.Вставить(1, ЛинияСнизу);

КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	Перем ИсходноеКоличествоСтрокГруппы;

	ЭтаФорма.АвтоЗаголовок = истина;

	мОтчетОВакансиях = ЭлементыФормы.ПолеТабличногоДокументаОтчетОВакансиях;			
	
	ТекТабличноеПоле = мОтчетОВакансиях;
	
	Если мСохраненныйДок = Неопределено Тогда
		
		// создаем исходную структуру многострочных разделов
		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭлементСтруктуры.Значение;
			ТекТабличноеПоле = мОтчетОВакансиях;
			мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
			Для НСтр = 1 По  ИсходноеКоличествоСтрокГруппы Цикл
				СтрокаГруппы = СтруктураГруппы.Добавить();
				Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
					ИмяКолонки    = Колонка.Имя;
					Префикс = ПолучитьПрефикс(Нстр);
					КодПоказателя    = Сред (ИмяКолонки,1,1) + Префикс + НСтр + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
					ОбластьЯчейки = ТекТабличноеПоле.Области[КодПоказателя];
					Если ОбластьЯчейки.СодержитЗначение Тогда
						ЗначениеЯчейки = ОбластьЯчейки.Значение;
					Иначе                  
						ЗначениеЯчейки = ОбластьЯчейки.Текст;
					КонецЕсли;
					СтрокаГруппы[ИмяКолонки] = ЗначениеЯчейки;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		Если мСкопированаФорма <> Неопределено Тогда
			// документ скопирован
			ВосстановитьСохраненныеДанные();
		Иначе
			// Это новый документ.
			// Установим реквизиты формы по умолчанию
			//////ЕдиницаИзмерения         = Перечисления.ПорядкиОкругленияОтчетности.Окр1;
			ТочностьЕдиницыИзмерения = 0;

			УстановитьФорматВывода();
			
			ЭлементыФормы.НадписьНазваниеОрганизации.Заголовок= Организация.Наименование;			
			ЗаполнитьСведенияОбОрганизации();
			
		КонецЕсли;

	Иначе
		// При открытии или при копировании сохраненного
		// отчета восстанавливаем сохраненные данные.
		ВосстановитьСохраненныеДанные();

	КонецЕсли;
    
	ПоказатьПериод();
	мОтчетОВакансиях.ТолькоПросмотр = Истина;			

	УстановитьЦветФонаЯчейки(,,"ОтчетОВакансиях");		
    СинхронизацияРазделов1и2(Ложь);
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПередЗакрытием" формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если Модифицированность = Истина Тогда

		Ответ = Вопрос(НСтр("ru='Данные были изменены. Сохранить изменения ?';uk='Дані були змінені. Зберегти зміни ?'"), РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);

		Если Ответ = КодВозвратаДиалога.Да Тогда
			Если НЕ Сохранить() Тогда
				Отказ = Истина;
			КонецЕсли;
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;

	КонецЕсли;
    СохранитьЗначение("РегламентированныеОтчеты_ЕстьДуплекс", ЕстьДуплекс);

КонецПроцедуры // ПередЗакрытием()



////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Заполнить" 
// командной панели формы.
//
Процедура КоманднаяПанельФормыЗаполнить(Кнопка)

	// Заглушка
	//ЗаполнитьДемо();
	
	Очистить();
	
	ЗаполнитьАвто();

КонецПроцедуры // КоманднаяПанельФормыЗаполнить

// Процедура вызывается при нажатии кнопки "Очистить".
//
Процедура КоманднаяПанельФормыОчистить(Кнопка)

	Очистить();

КонецПроцедуры // КоманднаяПанельФормыОчистить()

// Процедура вызывается при выборе пункта подменю "Печать" командной панели
// действий формы. 
// Отрабатывает выбор печатной формы.
//
Процедура КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы(Кнопка)

	Если Кнопка <> Неопределено Тогда
		Если Найти(Кнопка.Имя, "Дуплекс") >0 Тогда
		     ЕстьДуплекс    = НЕ Кнопка.Пометка;
			 Кнопка.Пометка = НЕ Кнопка.Пометка;
		Иначе
			 Печать(Кнопка.Имя);
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры // КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы()

// Процедура вызывается нажатием кнопки "ОК" командной панели действий формы.
// Выполняет сохранение данных отчета и закрывает текущую форму.
//
Процедура КоманднаяПанельДействийДействиеСохранитьИЗакрыть(Кнопка)

	Если Сохранить() Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры // КоманднаяПанельДействийДействиеСохранитьИЗакрыть()

// Процедура вызывается нажатием кнопки "Записать" командной панели действий формы.
// Выполняет сохранение данных отчета.
//
Процедура КоманднаяПанельДействийДействиеЗаписать(Кнопка)

	Сохранить();

КонецПроцедуры // КоманднаяПанельДействийДействиеЗаписать()

// Процедура - обработчик события "ПриИзменении" поля ввода даты
// представления отчета.
//
Процедура ПолеВводаДатаСдачиПриИзменении(Элемент)

	
	ПоказатьПериод();
	
	//	мОтчетФССУтратаТрудосп.Области.ДатаПодписиОтчета.Значение = ДатаПодписи;
	Модифицированность = Истина;

КонецПроцедуры // ПолеВводаДатаСдачиПриИзменении()

// Процедура вызывается при активизации поля табличного документа.
//   Определяет текущую область табличного документа и вызывает процедуру 
// управления доступностью кнопок "Добавить" и "Удалить" на странице формы.
//
Процедура ПолеТабличногоДокументаОтчетОВакансияхПриАктивизацииОбласти(Элемент)

	ТекущаяОбласть = Элемент.ТекущаяОбласть;
	ВывестиЭлементыВариантаЗаполнения(Элемент.ТекущаяОбласть);

КонецПроцедуры // ПолеТабличногоДокументаОтчетОВакансияхПриАктивизацииОбласти()

Функция ПолучитьПрефикс(ТекущееЧисло)
	Если ТекущееЧисло < 10 Тогда
		Префикс = "000";
	Иначе
		Префикс = "00";
	КонецЕсли;
	Возврат(Префикс)
КонецФункции

// Обновляет структуру многострочных разделов.
// Заполняет таблицу значений многострочного раздела
// значениями соответствующих ячеек.
// похоже унифицирована и актуальна только при наличии таких разделов
Процедура ОбновитьСтруктуруМногострочныхРазделов()
	// Заполняет таблицу значений многострочного раздела
	// указанными в соответствующих ячейках значениями
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = ЭлементСтруктуры.Значение;
		ТекТабличноеПоле = мОтчетОВакансиях;		
		
		Для НСтр = 1 По СтруктураГруппы.Количество() Цикл
			ТекСтрокаТаблицы = СтруктураГруппы[НСтр - 1];
			Префикс = ПолучитьПрефикс(НСтр);
		    НСтрНов = НСтр + 1;
			ПрефиксНов = ПолучитьПрефикс(НСтрНов);
			Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
				ИмяКолонки    = Колонка.Имя;
				ИмяЯчейки    = Сред (ИмяКолонки,1,1) + Префикс + НСтр + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
				ИмяНовОбластиЯчейки = Сред (ИмяКолонки,1,5) + ПрефиксНов + НСтрНов + Сред (ИмяКолонки,2,СтрДлина(ИмяКолонки)-1);
	    		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяЯчейки];
     			Если ОбластьЯчейки.СодержитЗначение Тогда
					ЗначениеЯчейки = ОбластьЯчейки.Значение;
				Иначе
					ЗначениеЯчейки = ОбластьЯчейки.Текст;
				КонецЕсли;
    			ТекСтрокаТаблицы[ИмяКолонки] = ЗначениеЯчейки;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	спПараметрыСтраницы.Добавить(СтруктураГруппы.Количество(),"КвоСтрокТаблицы");

КонецПроцедуры // ОбновитьСтруктуруМногострочныхРазделов()

// Создает новую строку многострочного раздела.
//
Процедура ВставитьДополнительнуюСтроку(ИмяГруппы)
	Перем СтруктураГруппы;
	
	ТекТабличноеПоле = мОтчетОВакансиях;
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);

	// определяем последнюю строку многострочной группы
	КолСтрок = СтруктураГруппы.Количество();
	
	ИмяПервойЯчейкиПоследнейСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Формат(КолСтрок,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(СтруктураГруппы.Колонки[0].Имя,2);
	// область первой ячейки последней строки многострочного раздела
	ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];

	// номера строк исходной области
	НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
	НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;

	СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;

	// номера строк вставляемой области
	НомерВерхЯчейкиНовойОбласти = НомерВерхЯчейкиОбласти + СдвигСтрок;
	НомерНижнЯчейкиНовойОбласти = НомерНижнЯчейкиОбласти + СдвигСтрок;

	// определяем вставляемую область
	ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );

	// добавляем в таблицу новую строку
	ТекТабличноеПоле.ВставитьОбласть( ВставляемаяОбласть,,ТипСмещенияТабличногоДокумента.ПоВертикали );
    КолСтрокНов = КолСтрок + 1;
	// переопределяем имена вставленной области
	НомерПроставлен = Ложь;
	
	Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл                    
		ИмяКолонки          = Колонка.Имя;
		
		//Если ...
		ИмяОбластиЯчейки    = Сред(ИмяКолонки,1,1) + Формат(КолСтрок,   "ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(ИмяКолонки,2);
		ИмяНовОбластиЯчейки = Сред(ИмяКолонки,1,1) + Формат(КолСтрокНов,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(ИмяКолонки,2);
		//КонецЕсли;
		
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];
		ОбластьЯчейки.Имя = ИмяНовОбластиЯчейки;
		
		// определяем границы ячейки исходной области
		НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
		НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
		НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
		НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;
		
		// Назначаем имя ячейки исходной области,
		// (т.к. в результате вставки новой области оно было очищено)
		ОбластьЯчейкиИсходнойОбласти = ТекТабличноеПоле.Область(НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
		ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЯчейки;
		
		// очищаем значения ячеек вставленной области
		Если (ОбластьЯчейки.СодержитЗначение)  Тогда
			
			Если  ИмяГруппы = мИдГруппы2 
				И ИмяКолонки = "MG15" Тогда
				// оставим имя исполнителя без очистки.
			Иначе
				ОбластьЯчейки.Очистить();
				ОбластьЯчейки.Примечание.Текст = "";
			КонецЕсли;
			
		Иначе
			// порядековый номер последней строки перед вставкой
			ИсходныйНомер        = Число(ОбластьЯчейки.Текст);
			НовыйПорядковыйНомер = ИсходныйНомер + 1;
			
			ОбластьЯчейки.Текст  = Строка(НовыйПорядковыйНомер);
		КонецЕсли;
		Если НЕ НомерПроставлен Тогда	
			НомерПроставлен = Истина;
			Если ИмяГруппы = мИдГруппы1 Тогда
				ОбластьТекстовыйНомер = ТекТабличноеПоле.Область(ОбластьЯчейки.Верх,1,ОбластьЯчейки.Верх,1 );
				ОбластьТекстовыйНомер.Текст = Строка(КолСтрок + 1);
			ИначеЕсли ИмяГруппы = мИдГруппы2 Тогда
				ОбластьТекстовыйНомер = ТекТабличноеПоле.Область(ОбластьЯчейки.Верх + 2,13,ОбластьЯчейки.Верх + 2,17);
				ОбластьТекстовыйНомер.Текст = Строка(КолСтрок + 1);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	

	//КонецЕсли;
	
	// Назначим текущей областью таблицы первую редактируемую ячейку добавленной строки.
	// Определяем текущую область таблицы
	ИмяТекущейОбласти = Сред (СтруктураГруппы.Колонки[0].Имя,1,1) + Формат(КолСтрокНов,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред (СтруктураГруппы.Колонки[0].Имя,2);
	
	ТекТабличноеПоле.ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];

	// добавляем в структуру многострочной группы новую строку
	СтруктураГруппы.Добавить();

	Модифицированность = Истина;

КонецПроцедуры // ВставитьДополнительнуюСтроку()

// Удаляет строку многострочного раздела формы.
//
Процедура УдалитьДополнительнуюСтроку(ИмяГруппы)
	
	Перем ИсходноеКоличествоСтрокГруппы;
	Перем СтруктураГруппы;

	// исходное количество строк группы
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

	ТекТабличноеПоле = мОтчетОВакансиях;
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);

	// Выполним также проверку вхождения текущей (выделенной) ячейки в многострочный раздел
	ТекущаяОбласть = ТекТабличноеПоле.ТекущаяОбласть;
	ИмяЯчейки      = ТекущаяОбласть.Имя;
	
	Если ИмяГруппы = мИдГруппы1 Тогда
 		Если мСтруктураМногострочныхРазделов[мИдГруппы1].Колонки.Найти(Лев(ИмяЯчейки, 1) + Прав(ИмяЯчейки,2)) = Неопределено Тогда
			Предупреждение(НСтр("ru = 'Курсор не стоит на удаляемой строке раздела ""I. ОСНОВНІ ДАНІ ПРО ВАКАНСІЇ""!'; uk = 'Курсор не розташований на рядку розділу ""I. ОСНОВНІ ДАНІ ПРО ВАКАНСІЇ""!'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
	НомУдаляемойСтроки 			 = Число(Сред(ИмяЯчейки,2,4));
	КоличествоСтрокПослеУдаления = СтруктураГруппы.Количество() -1;
	
	Если ИмяГруппы = мИдГруппы1 Тогда
	
		Ответ = Вопрос(НСтр("ru = 'Удалить выбранную строку? (Внимание! данная операция необратима)'; uk = 'Видалити обраний рядок? (Увага! дана операція необоротна)'"), РежимДиалогаВопрос.ДаНетОтмена, 60, КодВозвратаДиалога.Да);

	  	Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		УдалитьДополнительнуюСтроку(мИдГруппы2);
		
	КонецЕсли;

 
	// предварительно обновим структуру многострочных разделов
	ОбновитьСтруктуруМногострочныхРазделов();

	// номер строки многострочного раздела определим
	// по имени выделенной ячейки
	НомерСтрокиТаблицы = НомУдаляемойСтроки - 1; // нумерация строк в таблице значений начинается с нуля

	Если СтруктураГруппы.Количество() > ИсходноеКоличествоСтрокГруппы Тогда
		// удаляется добавленная строка
		ИмяПервойЯчейкиУдаляемойСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Формат(НомУдаляемойСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(СтруктураГруппы.Колонки[0].Имя,2);
    
		// область первой ячейки удаляемой строки многострочного раздела
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];

		// номера строк удаляемой области
		НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
		НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;

		// Определим область удаляемой строки
		УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );

		// непосредственно удаляем область строки табличного документа
		ТекТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );

		КолвоСдвигаемыхСтрок = СтруктураГруппы.Количество() - НомУдаляемойСтроки;

		// обновим имена ячеек сдвинутых строк
		Если КолвоСдвигаемыхСтрок > 0 Тогда    
			Для НСтр = НомУдаляемойСтроки + 1 По СтруктураГруппы.Количество() Цикл
              	ТекСтрока = СтруктураГруппы[НСтр-1];
                НСтрНов = НСтр - 1;
				НомерПроставлен = Ложь;
				Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
					
					ИмяСдвинутойЯчейки    = Сред(Колонка.Имя,1,1) + Формат(НСтр,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(Колонка.Имя,2);
					НовоеИмяСдвинутойЯчейки = Сред(Колонка.Имя,1,1) + Формат(НСтрНов,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(Колонка.Имя,2);

					ОбластьСдвинутойЯчейки     = ТекТабличноеПоле.Области[ИмяСдвинутойЯчейки];
					ОбластьСдвинутойЯчейки.Имя = НовоеИмяСдвинутойЯчейки;
			
					Если НЕ НомерПроставлен Тогда
						НомерПроставлен = Истина;
						Если ИмяГруппы = мИдГруппы1 Тогда
							ОбластьТекстовыйНомер = ТекТабличноеПоле.Область(ОбластьСдвинутойЯчейки.Верх,1,ОбластьСдвинутойЯчейки.Верх,1 );
							ОбластьТекстовыйНомер.Текст = Строка(НСтрНов);
						ИначеЕсли ИмяГруппы = мИдГруппы2 Тогда
							// это колонка MG_
							ОбластьТекстовыйНомер = ТекТабличноеПоле.Область(ОбластьСдвинутойЯчейки.Верх+2,13,ОбластьСдвинутойЯчейки.Верх+2,17);
							ОбластьТекстовыйНомер.Текст = Строка(НСтрНов);
						КонецЕсли;					
					КонецЕсли;

				КонецЦикла;
				
				
			КонецЦикла;

		КонецЕсли;

		// обновляем структуру многострочного раздела
		СтруктураГруппы.Удалить(НомерСтрокиТаблицы);

	Иначе
		// строка не удаляется в форме табличного документа,
		// а должна осуществляться сдвиг строк

		// обновляем структуру многострочного раздела
		// методом удаления выбранной строки и добавления пустой строки
		СтруктураГруппы.Удалить(НомерСтрокиТаблицы);

		ДобавляемаяСтрока = СтруктураГруппы.Добавить();

		// обновляем порядковые номера строк, выводимых в табличный документ
		Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
			Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл

				НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
				НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
					
				ИмяЯчейки    = Сред(КолонкаТаблицы.Имя,1,1) + Формат(НомерСтрокиВФормеТаблицы,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(КолонкаТаблицы.Имя,2);
				
				ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];

				Если НЕ(ОбластьЯчейкиТаблицы.СодержитЗначение) Тогда
					ПервыйНомерВГруппе = 1;
					// обновляем порядковый номер строки
					НовыйПорядковыйНомер = Строка(ПервыйНомерВГруппе + НомерСтрокиВТаблице);
					СтрокаТаблицы[КолонкаТаблицы.Имя] = НовыйПорядковыйНомер;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;

		// Обновляем экранную форму табличного документа
		ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);

	КонецЕсли;

	// назначим текущей областью таблицы первую редактируемую ячейку
	// удаленной строки, если ее порядковый номер меньше кол-ва строк
	// многострочного раздела, или последней строки раздела в противном случае

	// определяем текущую область таблицы
	НомТекущейСтроки  = Мин( НомУдаляемойСтроки, СтруктураГруппы.Количество() );
	
	ИмяТекущейОбласти    = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Формат(НомТекущейСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") +  Сред(СтруктураГруппы.Колонки[0].Имя,2);
	Если ИмяГруппы = мИдГруппы1 Тогда
		ТекТабличноеПоле.ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	КонецЕсли;

	Модифицированность = Истина;

КонецПроцедуры // УдалитьДополнительнуюСтроку()	

// Процедура управляет выводом всех страниц многостраничного раздела на печать.
//
// Параметры:
//   СтруктураПараметровСтраницы - структура, содержащая параметры печаты отчета:
//
//      - имя страницы основной панели формы (многостраничного раздела);
//      - имя выводимого на печать листа;
//      - ориентация страницы;
//      - имя табличного документа, который выводится на печать;
//      - имя области табличного документа, выводимой на печать (содержит не пустое значение,
//        когда выводится на печать не весь табличный документ, а часть его);
//      - строка, задающая способ печати.Принимает значения:
//           "ПечататьСразу" - непосредственный вывод на печать;
//           "ПечататьБланк" - с предварительным просмотром.
//   унифицирована, но актульная для отчетов, их имеющих
// Процедура выводит строки многострочного раздела в табличный документ.
// При отсутствии в табличном документе необходимого количества строк
// добавляет отсутствующие строки.
//
// Параметры:
//  ИмяГруппы       - строка, имя многострочного раздела;
//  СтруктураГруппы - таблица значений, содержащая значения показателей многострочного раздела.
//  похоже унифицирована, используется при  восстановлении сохраненных данных
Процедура ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы)
	Перем ИсходноеКоличествоСтрокГруппы;

	// определим исходное колво строк многострочного раздела,
	// соответствующее бумажной форме отчета
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

	// реальное кол-во строк (колво заполненных строк)
	ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();

	ТекТабличноеПоле = мОтчетОВакансиях;

	// При восстановлении сохраненных данных отчета сравниваем исходное кол-во
	// отображаемых в форме строк многострочного раздела с реальным количеством
	// созданных строк в сохраненном отчете.
	Если ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы Тогда

		///////////////////////////////////////////////////////
		// получаем описание координат каждой колонки многострочной части (лево, право, высота)
		СтруктураКоординатЯчеек = Новый Структура();
		ВысотаСтроки = 0;
		Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
			
			ИмяКолонки          = Колонка.Имя;
			
			ИмяОбластиЯчейки    = Сред(ИмяКолонки,1,1) + Формат(1,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(ИмяКолонки,2);
			ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];

			Координаты = Новый Структура();
			// определяем границы ячейки исходной области
			Координаты.Вставить("НомерВерхСтрокиЯчейки",  ОбластьЯчейки.Верх);
			Координаты.Вставить("НомерНижнСтрокиЯчейки",  ОбластьЯчейки.Низ);
			Координаты.Вставить("НомерЛевоКолонкиЯчейки", ОбластьЯчейки.Лево);
			Координаты.Вставить("НомерПравКолонкиЯчейки", ОбластьЯчейки.Право);
		
			СтруктураКоординатЯчеек.Вставить(ИмяКолонки, Координаты);
			
			Если ВысотаСтроки < ОбластьЯчейки.Низ - ОбластьЯчейки.Верх + 1 Тогда
				ВысотаСтроки = ОбластьЯчейки.Низ - ОбластьЯчейки.Верх + 1;
			КонецЕсли;
			
		КонецЦикла;
			
		///////////////////////////////////////////////////////
		// вставляем необходимое количество строк за один раз
		ИмяПервойЯчейкиПоследнейСтроки = Сред(СтруктураГруппы.Колонки[0].Имя,1,1) + Формат(ИсходноеКоличествоСтрокГруппы,"ЧЦ=4;ЧВН=0;ЧГ=;")  +  Сред(СтруктураГруппы.Колонки[0].Имя,2);
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];

		// номера строк исходной области
		НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
		НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
		
		ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
		// добавляем в таблицу новые строки
		ИсходнаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти + (ТекущееКоличествоСтрокГруппы - ИсходноеКоличествоСтрокГруппы - 1) * ВысотаСтроки, );	
		ТекТабличноеПоле.ВставитьОбласть(ВставляемаяОбласть,ИсходнаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		///////////////////////////////////////////////////////
		// переименовываем все ячейки многострочной части сверху вниз
		// при этом удаляем старые имена, если они есть
		Для НомерСтроки = 1 По ТекущееКоличествоСтрокГруппы Цикл

			НомерПроставлен = Ложь;
			// переопределяем имена вставленной области
			Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл

				ИмяКолонки          = Колонка.Имя;
				ИмяОбластиЯчейки    = Сред(ИмяКолонки,1,1) + Формат(НомерСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + Сред(ИмяКолонки,2);
				
				Если НомерСтроки <= ИсходноеКоличествоСтрокГруппы Тогда
					// удаляем старые имана (они сдвинуты)
					ТекТабличноеПоле.Области[ИмяОбластиЯчейки].Имя = "";
				КонецЕсли;
				
				// определяем границы ячейки заданной строки и колонки 
				НомерВерхСтрокиЯчейки  = СтруктураКоординатЯчеек[ИмяКолонки].НомерВерхСтрокиЯчейки + ВысотаСтроки * (НомерСтроки - 1);
				НомерНижнСтрокиЯчейки  = СтруктураКоординатЯчеек[ИмяКолонки].НомерНижнСтрокиЯчейки + ВысотаСтроки * (НомерСтроки - 1);
				НомерЛевоКолонкиЯчейки = СтруктураКоординатЯчеек[ИмяКолонки].НомерЛевоКолонкиЯчейки;
				НомерПравКолонкиЯчейки = СтруктураКоординатЯчеек[ИмяКолонки].НомерПравКолонкиЯчейки;

				// Назначаем имя ячейки области,
				ОбластьЯчейки = ТекТабличноеПоле.Область(НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
                ОбластьЯчейки.Имя = ИмяОбластиЯчейки; 
				
				// очищаем значения ячеек 
				Если ОбластьЯчейки.СодержитЗначение Тогда
					ОбластьЯчейки.Очистить();
					// вставляем номер
					Если Не НомерПроставлен Тогда
						Если ИмяГруппы = мИдГруппы1 Тогда
							ОбластьТекстовыйНомер = ТекТабличноеПоле.Область(ОбластьЯчейки.Верх,1,ОбластьЯчейки.Верх,1 );
							ОбластьТекстовыйНомер.Текст = Строка(НомерСтроки);
						ИначеЕсли ИмяГруппы = мИдГруппы2 Тогда
							ОбластьТекстовыйНомер = ТекТабличноеПоле.Область(ОбластьЯчейки.Верх + 2,13,ОбластьЯчейки.Верх + 2,17);
							ОбластьТекстовыйНомер.Текст = Строка(НомерСтроки);
						КонецЕсли;
						НомерПроставлен = Истина;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	// выводим значения строк
	Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
		Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл
            ИмяКолонки          = КолонкаТаблицы.Имя;

			НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
			НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;

			ИмяЯчейки    = Сред(ИмяКолонки,1,1) + Формат(НомерСтрокиВФормеТаблицы,"ЧЦ=4;ЧВН=0;ЧГ=;") +  Сред(ИмяКолонки,2);
					
			ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
			ЗначениеЯчейки           = СтрокаТаблицы[КолонкаТаблицы.Имя];

			Если ОбластьЯчейкиТаблицы.СодержитЗначение Тогда
				// задаем значение ячейки
				ОбластьЯчейкиТаблицы.Значение = ЗначениеЯчейки;
			Иначе
				// устанавливаем порядковый номер строки
				ОбластьЯчейкиТаблицы.Текст    = ЗначениеЯчейки;
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;

КонецПроцедуры // ВывестиРазделВТабличныйДокумент()

Процедура КнопкаДобавитьДопСтрокиОтчетОВакансияхНажатие(Элемент)
	
	ВставитьДополнительнуюСтроку(мИдГруппы2);
	ВставитьДополнительнуюСтроку(мИдГруппы1);
	СинхронизацияРазделов1и2();
	
КонецПроцедуры

Процедура КнопкаУдалитьДопСтрокиОтчетОВакансияхНажатие(Элемент)
	
	УдалитьДополнительнуюСтроку(мИдГруппы1);
	// перенес в процедуру УдалитьДополнительнуюСтроку() ибо действие нужно проводить синхронно
	//УдалитьДополнительнуюСтроку(мИдГруппы2);
	СинхронизацияРазделов1и2();
	
КонецПроцедуры

Процедура ПолеВыбораВариантЗаполненияОтчетОВакансияхПриИзменении(Элемент)
	ИзменитьВариантЗаполненияЯчейки(Элемент.Значение);
КонецПроцедуры

Процедура ПолеВводаЗначениеКорректировкиОтчетОВакансияхПриИзменении(Элемент)
	Дельта = Элемент.Значение;
	ИзменитьЗначениеАвтокорректировки(Дельта);

	Модифицированность = Истина;
КонецПроцедуры

Процедура ПолеВводаКомментарийОтчетОВакансияхПриИзменении(Элемент)
	Коммент = Элемент.Значение;
	ИзменитьКомментарийПоЯчейке(Коммент);
КонецПроцедуры

Процедура КоманднаяПанельФормыРасширитьПолеБланка(Кнопка)
	
	РегламентированнаяОтчетность.ПриНажатииНаКнопкуПереходаВРежимВводаДанных(ЭтаФорма);
	
КонецПроцедуры

Функция СформироватьДанныеДляЗвит1С() Экспорт 
	
	мСохраненныйДок   = Неопределено;	
	мСкопированаФорма = Неопределено;
	
	ПередОткрытием(Ложь, Истина);
	ПриОткрытии();
	
	Очистить();
	ЗаполнитьАвто();
	
	Если мМенеджерЗвит1С.ФлагОтладки Тогда
		ЭтаФорма.ОткрытьМодально();
	КонецЕсли;
	
	// формируем список сохранения
	СписокСохранения = Новый Структура();
	// расшифровка
	Попытка
		ВЫПОЛНИТЬ("СписокСохранения.Вставить(""РасшифровкаОтчета"", СпП);");
	Исключение КонецПопытки;
	
	ПоказателиОтчета = Новый Структура();
	ИмяТекТабличногоПоля    = "ПолеТабличногоДокументаОтчетОВакансиях";
	ТекТабличноеПоле        = ЭлементыФормы["ПолеТабличногоДокументаОтчетОВакансиях"];
	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
	ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);

	ПоказателиОтчета.Вставить("СлужебныеДанные", Новый Структура("ДатаПодписи", ДатаПодписи));
	
	// показатели
	СписокСохранения.Вставить("ДанныеОтчета", ПоказателиОтчета);
	
	Возврат СписокСохранения;
	
КонецФункции

Процедура ПолеТабличногоДокументаОтчетОВакансияхВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ИмяОбласти = Область.Имя;
	Если     (Лев(ИмяОбласти, 1) = "R" И Прав(ИмяОбласти, 2) = "G6") 
		 ИЛИ (   Лев(ИмяОбласти, 1) = "M" 
		      И Сред(ИмяОбласти, 6, 1) = "G" 
			  И Область.СодержитЗначение
			  И Область.Защита
			  И Область.ТипЗначения = Новый ОписаниеТипов("Булево") 
			 )
		 Тогда
		
		мОтчетОВакансиях.Области[ИмяОбласти].Значение = НЕ мОтчетОВакансиях.Области[ИмяОбласти].Значение;
		
		Если (Лев(ИмяОбласти, 1) = "R" И Прав(ИмяОбласти, 2) = "G6") Тогда
			СинхронизацияРазделов1и2();
		КонецЕсли;

	КонецЕсли;

	Если ИмяОбласти = "HZ" Тогда
	    мОтчетОВакансиях.Области["HZ"].Значение = "V";
		мОтчетОВакансиях.Области["HZU"].Значение = "";
	КонецЕсли;
	
	Если ИмяОбласти = "HZU" Тогда
	    мОтчетОВакансиях.Области["HZU"].Значение = "V";
		мОтчетОВакансиях.Области["HZ"].Значение = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаОтчетОВакансияхПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ИмяОбласти = Область.Имя;
	
	Если    (Лев(ИмяОбласти, 1) = "R" И Прав(ИмяОбласти, 2) = "G1")
		ИЛИ (Лев(ИмяОбласти, 1) = "R" И Прав(ИмяОбласти, 2) = "G2") Тогда
		
		СинхронизацияРазделов1и2(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СинхронизацияРазделов1и2(ПересчитыватьКоличествоСтраниц = Истина)
	
	КолДополнений = 0;
	
	// устанавливаем значения названия профессии, количества вакансий в разделе 2 синхронно с разделом 1
	ТаблицаРаздела1 = мСтруктураМногострочныхРазделов[мИдГруппы1];
	КоличествоСтрок = ТаблицаРаздела1.Количество();
	
	Для НомерСтроки = 1 По КоличествоСтрок Цикл
		
		Руководитель = мОтчетОВакансиях.Области["РУКОВОДИТЕЛЬ"].Значение;
		КоличествоВакансий = мОтчетОВакансиях.Области["R" + Формат(НомерСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G2"].Значение;
		ВыводитьДопРаздел  = мОтчетОВакансиях.Области["R" + Формат(НомерСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G6"].Значение;
		
		мОтчетОВакансиях.Области["M" + Формат(НомерСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G2"].Значение  = КоличествоВакансий;
		мОтчетОВакансиях.Области["M" + Формат(НомерСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G15"].Значение = Руководитель;
		
		Если ВыводитьДопРаздел Тогда
			КолДополнений = КолДополнений + 1;
		КонецЕсли;
		
		// Устанавливаем видимость строк раздела 2, в зависимости от того стоит ли "флажок" в колонке 5 раздела 1
		ОбластьРаздела2 = мОтчетОВакансиях.Области["M" + Формат(НомерСтроки,"ЧЦ=4;ЧВН=0;ЧГ=;") + "G_"]; //по высоте соответствует области
		
		мОтчетОВакансиях.Область(ОбластьРаздела2.Верх, ,ОбластьРаздела2.Низ, ).Видимость = ВыводитьДопРаздел;
		
	КонецЦикла;
	
	// Устанаволиваем количество строк дополнения в зависимости от количество установленных "флажков" в колонке 5 раздела 1
	Если ПересчитыватьКоличествоСтраниц Тогда
		мОтчетОВакансиях.Области.КолДополнений.Значение = КолДополнений;
	КонецЕсли;
	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ОписаниеТиповЧисло = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);

ОписаниеТиповЧисло2 = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(2,0);

ОписаниеТиповСтрока = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(254);
ОписаниеТиповСтрокаНеогр = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(0);

ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");

МассивТипов = Новый Массив;
МассивТипов.Добавить(Тип("Дата"));
ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));

ЦветЗаполняетсяТолькоВручную    = Новый Цвет(255, 255, 225);
ЦветЗаполняетсяАвтоКорректир    = Новый Цвет(255, 240, 200);
ЦветЗаполняетсяАвтоБезКорректир = Новый Цвет(230, 240, 220);

// структура цветов раскраски автозаполняемых ячеек
мСтруктураЦветовРаскраски = Новый Структура;
мСтруктураЦветовРаскраски.Вставить("_1", ЦветЗаполняетсяТолькоВручную);
мСтруктураЦветовРаскраски.Вставить("_2", ЦветЗаполняетсяАвтоКорректир);
мСтруктураЦветовРаскраски.Вставить("_3", ЦветЗаполняетсяАвтоБезКорректир);

мСтруктураМногострочныхРазделов = Новый Структура;

// ОПИСАНИЕ ПАРМЕТРОВ ФОРМЫ
//
// Версия формы
мВерсияФормы = "01/01/2017";

мПечатныеФормы = Новый СписокЗначений;

// ОПИСАНИЕ ПАРАМЕТРОВ МНОГОСТРОЧНЫХ ГРУПП
// Идентификаторы многострочных групп
мИдГруппы1 = "R";
мИдГруппы2 = "M";

// количество строк многострочных разделов по "бумажной" форме отчета
// (минимальное количество строк, которое должно присутствовать всегда)
//нужна редакция в зависимости от структуры многострочных групп приложений декларации

КолИсхСтрокИдГруппы1 = 1;
КолИсхСтрокИдГруппы2 = 1;

СтруктураГруппы1 = Новый ТаблицаЗначений;
СтруктураГруппы1.Колонки.Добавить("RG1", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG2", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG3", ОписаниеТиповЧисло);
СтруктураГруппы1.Колонки.Добавить("RG4", ОписаниеТиповЧисло);
СтруктураГруппы1.Колонки.Добавить("RG5", ОписаниеТиповСтрока);
СтруктураГруппы1.Колонки.Добавить("RG6", ОписаниеТиповБулево);

СтруктураГруппы2 = Новый ТаблицаЗначений;
СтруктураГруппы2.Колонки.Добавить("MG_");
СтруктураГруппы2.Колонки.Добавить("MG2",  ОписаниеТиповСтрока);
СтруктураГруппы2.Колонки.Добавить("MG3",  ОписаниеТиповСтрока);

СтруктураГруппы2.Колонки.Добавить("MG4_0", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_1", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_2", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_3", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_4", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_5", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_6", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_7", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_8", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_9", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG4_10", ОписаниеТиповБулево);

СтруктураГруппы2.Колонки.Добавить("MG5",   ОписаниеТиповСтрокаНеогр);

СтруктураГруппы2.Колонки.Добавить("MG6_0", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_1", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_2", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_3", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_4", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_5", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_6", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_7", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_8", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_9", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_10", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG6_Описание",   ОписаниеТиповСтрока);

СтруктураГруппы2.Колонки.Добавить("MG7_0", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG7_1", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG7_2", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG7_3", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG7_4", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG7_5", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG7_Описание",   ОписаниеТиповСтрока);

СтруктураГруппы2.Колонки.Добавить("MG8_0", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG8_1", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG8_2", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG8_3", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG8_4", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG8_Описание",   ОписаниеТиповСтрока);

СтруктураГруппы2.Колонки.Добавить("MG9_0", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG9_1", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG9_2", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG9_3", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG9_4", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG9_Описание",   ОписаниеТиповСтрокаНеогр);

СтруктураГруппы2.Колонки.Добавить("MG10_0", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG10_1", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG10_2", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG10_3", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG10_4", ОписаниеТиповБулево);

СтруктураГруппы2.Колонки.Добавить("MG11_0", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG11_1", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG11_2", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG11_3", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG11_4", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG11_5", ОписаниеТиповБулево);
СтруктураГруппы2.Колонки.Добавить("MG11_6", ОписаниеТиповБулево);

СтруктураГруппы2.Колонки.Добавить("MG12",  ОписаниеТиповСтрокаНеогр);

СтруктураГруппы2.Колонки.Добавить("MG13",  ОписаниеТиповСтрока);

СтруктураГруппы2.Колонки.Добавить("MG14",  ОписаниеТиповСтрокаНеогр);

СтруктураГруппы2.Колонки.Добавить("MG15",  ОписаниеТиповСтрока);

// структура многострочных разделов формы
мСтруктураМногострочныхРазделов = Новый Структура;
мСтруктураМногострочныхРазделов.Вставить(мИдГруппы1, СтруктураГруппы1);
мСтруктураМногострочныхРазделов.Вставить(мИдГруппы2, СтруктураГруппы2);

// структура исходных строк (минимального кол-ва) многострочных разделов формы
мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы1, КолИсхСтрокИдГруппы1);
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы2, КолИсхСтрокИдГруппы2);

//списрк значений для разбиения по страницам многострочной части отчета
//при его печати
спПараметрыСтраницы = Новый СписокЗначений;
спПараметрыСтраницы.Добавить(52, "КвоСтрокНаСтранице");
спПараметрыСтраницы.Добавить(3, "ВысотаОснШапки");
спПараметрыСтраницы.Добавить(1,"ВысотаДопШапки");
спПараметрыСтраницы.Добавить(14, "ВысотаПодвала");

// Список выбора поля выбора "ВариантЗаполнения"
ЭлементыФормы.ПолеВыбораВариантЗаполненияОтчетОВакансиях.СписокВыбора.Добавить(1, НСтр("ru='не заполнять автоматически';uk='не заповнювати автоматично'"));
ЭлементыФормы.ПолеВыбораВариантЗаполненияОтчетОВакансиях.СписокВыбора.Добавить(2, НСтр("ru='заполнять автоматически с корректировкой';uk='заповнювати автоматично з коригуванням'"));
ЭлементыФормы.ПолеВыбораВариантЗаполненияОтчетОВакансиях.СписокВыбора.Добавить(3, НСтр("ru='заполнять автоматически без корректировки';uk='заповнювати автоматично без коригування'"));

мСоставПоказателей = "СоставПоказателей2017";

ЕстьДуплекс = ВосстановитьЗначение("РегламентированныеОтчеты_ЕстьДуплекс");
Если НЕ ТипЗнч(ЕстьДуплекс) = Тип("Булево") Тогда
	ЕстьДуплекс = Ложь;	
КонецЕсли;
