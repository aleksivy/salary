
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ДокументОбъект = Документы.ВводСведенийОПлановыхНачисленияхРаботниковОрганизаций.СоздатьДокумент();
	ДокументОбъект.Дата = Период;
	ДокументОбъект.Комментарий = НСтр("ru='Переход на отчет по ЕСВ 2016 г.';uk='Перехід на звіт з ЄСВ 2016 р.'");   
	ДокументОбъект.Организация = Организация; 
	ДокументОбъект.УстановитьНовыйНомер();
	
	ТаблицаВзносов = Взносы.Скопировать();
	ТаблицаВзносов.Колонки.Добавить("Налог");
	ТаблицаВзносов.Колонки.Добавить("Действие");
	ТаблицаВзносов.Колонки.Добавить("ДатаДействия");
	
	ТаблицаВзносов.ЗагрузитьКолонку(ТаблицаВзносов.ВыгрузитьКолонку("Прекратить"),"Налог");
	ТаблицаВзносов.ЗаполнитьЗначения(Перечисления.ВидыДействияСНачислением.Прекратить,"Действие");
	ТаблицаВзносов.ЗаполнитьЗначения(Период - 60*60*24,"ДатаДействия");
	
	ТаблицаВзносовФОТ = Новый ТаблицаЗначений;
	ТаблицаВзносовФОТ.Колонки.Добавить("Сотрудник");
	ТаблицаВзносовФОТ.Колонки.Добавить("Налог");
	ТаблицаВзносовФОТ.Колонки.Добавить("Действие");
	ТаблицаВзносовФОТ.Колонки.Добавить("ДатаДействия");
	
	Для Каждого СтрокаТаблицы Из ВзносыФОТ Цикл
		НС = ТаблицаВзносовФОТ.Добавить();	
		НС.Сотрудник = СтрокаТаблицы.Сотрудник;
		НС.ДатаДействия = Период - 60*60*24;
		НС.Налог = СтрокаТаблицы.Прекратить;
		НС.Действие = Перечисления.ВидыДействияСНачислением.Прекратить;
		НС = ТаблицаВзносовФОТ.Добавить();	
		НС.Сотрудник = СтрокаТаблицы.Сотрудник;
		НС.ДатаДействия = Период;
		НС.Налог = СтрокаТаблицы.Начать;
		НС.Действие = Перечисления.ВидыДействияСНачислением.Начать;
	КонецЦикла;	
	
	ДокументОбъект.Взносы.Загрузить(ТаблицаВзносов);
	ДокументОбъект.ВзносыФОТ.Загрузить(ТаблицаВзносовФОТ);
	ДокументОбъект.ПолучитьФорму().Открыть();
КонецПроцедуры

Процедура ПриОткрытии()
	
	Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),"ОсновнаяОрганизация");
	Период = Дата(2016,6,1);

	
КонецПроцедуры

Процедура ЗаполнитьНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Сообщить(НСтр("ru='Не указана организация!';uk='Не зазначена організація!'"), СтатусСообщения.Важное);
		Возврат		
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Сообщить(НСтр("ru='Не указан период!';uk='Не зазначений період!'"), СтатусСообщения.Важное);
		Возврат		
	КонецЕсли;	
	
	Взносы.Очистить();
	ВзносыФот.Очистить();
		
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Взносы", Справочники.Налоги.Взносы);
	Запрос.УстановитьПараметр("ВзносыФОТ", Справочники.Налоги.ВзносыФОТ);
	
	СписокВзносов = Новый Массив;
	СписокВзносов.Добавить(Справочники.Налоги.ЕСВРаботникиСР);
	СписокВзносов.Добавить(Справочники.Налоги.ЕСВМобилизованные);
	СписокВзносов.Добавить(Справочники.Налоги.ЕСВГПХ);
	Запрос.УстановитьПараметр("ЗаменяемыеВзносы", СписокВзносов);
	
	СписокВзносовФОТ = Новый Массив;
	СписокВзносовФОТ.Добавить(Справочники.Налоги.ЕСВФОТИнвалиды);
	СписокВзносовФОТ.Добавить(Справочники.Налоги.ЕСВФОТРаботники);
	СписокВзносовФОТ.Добавить(Справочники.Налоги.ЕСВФОТГПХСудостр);
	СписокВзносовФОТ.Добавить(Справочники.Налоги.ЕСВФОТСудостр);

	Запрос.УстановитьПараметр("ЗаменяемыеВзносыФОТ", СписокВзносовФОТ);

	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	               |	ВзносыВФонды.Налог.Родитель КАК Родитель,
				   |	ВзносыВФонды.Налог,
	               |	ВзносыВФонды.Сотрудник
				   |ПОМЕСТИТЬ ВТВсеВзносы
				   |ИЗ
	               |	РегистрСведений.ВзносыВФондыРаботниковОрганизаций.СрезПоследних(&Период, Организация = &Организация И Налог.Актуальность) КАК ВзносыВФонды
				   |ГДЕ 
				   |	ВзносыВФонды.Актуальность
				   |ИНДЕКСИРОВАТЬ ПО
				   |	Сотрудник,Налог
				   |;
				   |
				   |ВЫБРАТЬ
	               |	ВзносыВФонды.Налог,
	               |	ВзносыВФонды.Сотрудник
				   |ПОМЕСТИТЬ ВТВзносы
				   |ИЗ
	               |	ВТВсеВзносы КАК ВзносыВФонды
				   |ГДЕ
				   |	ВзносыВФонды.Родитель = &Взносы 
				   |ИНДЕКСИРОВАТЬ ПО
				   |	Сотрудник,Налог
				   |;
				   |
				   |ВЫБРАТЬ
	               |	Взносы.Налог КАК Прекратить,
	               |	Взносы.Сотрудник КАК Сотрудник
				   |ИЗ ВТВзносы КАК Взносы
				   |УПОРЯДОЧИТЬ ПО
				   |	Сотрудник.Наименование,Налог.Наименование
				   |";
	Взносы = Запрос.Выполнить().Выгрузить();
			   
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВзносыВФонды.Налог,
	               |	ВзносыВФонды.Сотрудник
				   |ПОМЕСТИТЬ ВТВзносыФОТ
				   |ИЗ
	               |	ВТВсеВзносы КАК ВзносыВФонды
				   |ГДЕ
				   |	ВзносыВФонды.Родитель = &ВзносыФОТ
				   |	И ВзносыВФонды.Налог В (&ЗаменяемыеВзносыФОТ)
				   |ИНДЕКСИРОВАТЬ ПО
				   |	Сотрудник,Налог
			   	   |;
				   |
				   |ВЫБРАТЬ
	               |	ВзносыФОТ.Налог КАК Прекратить,
				   |    ВЫБОР
				   |    	КОГДА Взносы.Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВМобилизованные) 
				   |         ТОГДА ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТМобил)
				   |    	КОГДА Взносы.Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВРаботникиСР) И ВзносыФОТ.Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТРаботники)
				   |         ТОГДА ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТРаботникиСР)
				   |    	КОГДА Взносы.Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВРаботникиСР) И ВзносыФОТ.Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТИнвалиды)
				   |         ТОГДА ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТИнвалидыСР)
				   |    	КОГДА ВзносыФОТ.Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТГПХСудостр) 
				   |         ТОГДА ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТГПХ)
				   |    	КОГДА ВзносыФОТ.Налог = ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТСудостр) 
				   |         ТОГДА ЗНАЧЕНИЕ(Справочник.Налоги.ЕСВФОТРаботники)
				   |    КОНЕЦ КАК Начать,
	               |	Взносы.Сотрудник
				   |ПОМЕСТИТЬ ВТЗамены
				   |ИЗ
	               |	ВТВзносы КАК Взносы
	               |ЛЕВОЕ СОЕДИНЕНИЕ 
				   |	ВТВзносыФОТ КАК ВзносыФОТ
				   |ПО
				   |	Взносы.Сотрудник = ВзносыФОТ.Сотрудник
				   |ГДЕ
				   |	Взносы.Налог В (&ЗаменяемыеВзносы)
				   |;
				   |
				   |ВЫБРАТЬ
	               |	Замены.Прекратить,
				   |    Замены.Начать,
	               |	Замены.Сотрудник
				   |ИЗ
	               |	ВТЗамены КАК Замены
				   |ГДЕ
				   |	НЕ Замены.Прекратить ЕСТЬ NULL
				   |	И НЕ Замены.Начать ЕСТЬ NULL";
	ВзносыФОТ = Запрос.Выполнить().Выгрузить();
	ТаблицаЗамен = ВзносыФОТ.Скопировать(,"Прекратить");
 
	
КонецПроцедуры
