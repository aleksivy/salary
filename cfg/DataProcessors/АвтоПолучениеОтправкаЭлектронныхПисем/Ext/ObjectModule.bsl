
#Если Клиент Тогда

Функция ПолучитьТаблицуУчетныхЗаписейДляТранспорта()

	Если ПравоДоступа("Чтение", Метаданные.Справочники.УчетныеЗаписиЭлектроннойПочты) Тогда
	
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("ТекущийПользователь", глЗначениеПеременной("глТекущийПользователь"));
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭлектроннойПочты.Ссылка                                 КАК УчетнаяЗапись,
		|	УчетныеЗаписиЭлектроннойПочты.АдресЭлектроннойПочты                  КАК АдресЭлектроннойПочты,
		|	УчетныеЗаписиЭлектроннойПочты.ИнтервалАвтоПолученияОтправкиСообщений КАК ИнтервалОбновления,
		|	УчетныеЗаписиЭлектроннойПочты.ДействиеАвтополученияОтправкиСообщений КАК Действие
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|ГДЕ
		|	УчетныеЗаписиЭлектроннойПочты.АвтоПолучениеОтправкаСообщений
		|	И
		|	УчетныеЗаписиЭлектроннойПочты.ОтветственныйЗаАвтоПолучениеОтправкуСообщений = &ТекущийПользователь
		|	И
		|	НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
		|";
		
		Возврат Запрос.Выполнить().Выгрузить();
	
	КонецЕсли; 

КонецФункции

Процедура ОбновитьСписокУчетныхЗаписей() Экспорт
	
	ТаблицаУчетныхЗаписей = ПолучитьТаблицуУчетныхЗаписейДляТранспорта();
	
	Если ТипЗнч(ТаблицаУчетныхЗаписей) <> Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли; 
	
	ТаблицаУчетныхЗаписей.Индексы.Добавить("УчетнаяЗапись");

	СохраненнаяТаблица = ВосстановитьЗначение("ТаблицаУчетныхЗаписейАвтоПолученияОтправки");

	Если ТипЗнч(СохраненнаяТаблица) = Тип("ТаблицаЗначений") Тогда
		Для каждого СтрокаТаблицы Из СохраненнаяТаблица Цикл
			НайденнаяСтрока = ТаблицаУчетныхЗаписей.Найти(СтрокаТаблицы.УчетнаяЗапись, "УчетнаяЗапись");
			Если НайденнаяСтрока <> Неопределено Тогда
				Если НайденнаяСтрока.ИнтервалОбновления <> СтрокаТаблицы.ИнтервалОбновления Тогда
					Сообщить(НСтр("ru='Изменился интервал автополучения/отправки писем для учетной записи ""';uk='Змінився інтервал автоотримання/відправлення листів для облікового запису ""'") + НайденнаяСтрока.УчетнаяЗапись + НСтр("ru='"". Новое значение ';uk='"". Нове значення '") + НайденнаяСтрока.ИнтервалОбновления + НСтр("ru=' мин.';uk=' хв.'"));
				КонецЕсли;
				Если НайденнаяСтрока.Действие <> СтрокаТаблицы.Действие Тогда
					Сообщить(НСтр("ru='Изменилось действие автополучения/отправки писем для учетной записи ""';uk='Змінилася дія автоотримання/відправлення листів для облікового запису ""'") + НайденнаяСтрока.УчетнаяЗапись + НСтр("ru='"". Новое значение ';uk='"". Нове значення '") + НайденнаяСтрока.Действие + ".");
				КонецЕсли;
			Иначе
				Сообщить(НСтр("ru='Из списка автополучения/отправки удалена учетная запись ""';uk='Зі списку автоотримання/відправки вилучений обліковий запис ""'") + СтрокаТаблицы.УчетнаяЗапись + """.");
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	УчетныеЗаписиАвтоматическогоТранспортаПисем.Очистить();
	
	Для каждого СтрокаТаблицы Из ТаблицаУчетныхЗаписей Цикл
		
		НоваяСтрокаТЧ = УчетныеЗаписиАвтоматическогоТранспортаПисем.Добавить();
		НоваяСтрокаТЧ.УчетнаяЗапись         = СтрокаТаблицы.УчетнаяЗапись;
		НоваяСтрокаТЧ.ИнтервалОбновления    = СтрокаТаблицы.ИнтервалОбновления;
		НоваяСтрокаТЧ.АдресЭлектроннойПочты = СтрокаТаблицы.АдресЭлектроннойПочты;
		НоваяСтрокаТЧ.Действие              = СтрокаТаблицы.Действие;
		
		Если ТипЗнч(СохраненнаяТаблица) = Тип("ТаблицаЗначений") Тогда
			СохраненнаяТаблица.Индексы.Добавить("УчетнаяЗапись");
			НайденнаяСтрока = СохраненнаяТаблица.Найти(СтрокаТаблицы.УчетнаяЗапись, "УчетнаяЗапись");
		Иначе
			НайденнаяСтрока = Неопределено;
		КонецЕсли;
		
		Если НайденнаяСтрока = Неопределено Тогда
			Сообщить(НСтр("ru='В список автополучения/отправки добавлена учетная запись ""';uk='У список автоотримання/відправки додано обліковий запис ""'") + СтрокаТаблицы.УчетнаяЗапись + НСтр("ru='"". Интервал обновления ';uk='"". Інтервал оновлення '") + СтрокаТаблицы.ИнтервалОбновления + НСтр("ru=' мин. Действие: ';uk=' хв. Дія: '") + СтрокаТаблицы.Действие);
		КонецЕсли; 
	
	КонецЦикла;
	
	СохранитьЗначение("ТаблицаУчетныхЗаписейАвтоПолученияОтправки", ТаблицаУчетныхЗаписей.Скопировать());
	
КонецПроцедуры

#КонецЕсли
