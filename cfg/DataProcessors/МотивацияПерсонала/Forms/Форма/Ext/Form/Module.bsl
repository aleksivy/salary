////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Переменная учетной политики
Перем мПоддерживатьНесколькоСхемМотивации;

// Переменные, хранящие сведения о подрядах сортировок 
Перем мПорядокСортировкиПодразделенияПоКоду;
Перем мПорядокСортировкиДолжностейПоКоду;

// Переменные механизма встроенной справки
Перем ОтображатьСправкуФормы;

// Валюта
Перем мПоказватьВалюту;
Перем мВалютаПоУмолчанию;

// Объекты форм
Перем мШтатноеРасписание;

// Хранит в себе сведения о видах расчтов
Перем мУпрВидыРасчетов;
Перем мРегВидыРасчетов;

Перем ОбработкаКомментариев;

Перем мЖирныйШрифт;
Перем ОтображенныеОрганизации;

Перем мВведенныеДолжности;
Перем мВведенныеВР;

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПервоеПодразделение(Подразделение, Организация, НазваниеОрганизациии)
	
	Если ОтображенныеОрганизации[Организация] <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодразделенияОрганизаций.Ссылка,
	|	ПодразделенияОрганизаций.Владелец.Наименование КАК НазваниеОрганизациии
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделенияОрганизаций.Наименование ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	НазваниеОрганизациии = Выборка.НазваниеОрганизациии;
	
	ОтображенныеОрганизации[Организация] = Истина;
	
	Возврат Выборка.Ссылка = Подразделение;
	
КонецФункции //ПервоеПодразделение


// Устанавливает отбор по виду расчета
//
Процедура ОтборПоВидуРасчета()
	
	ТабличноеПолеПоДолжности.Отбор.ВидРасчета.Использование = Истина;
	ТабличноеПолеПоДолжности.Отбор.ВидРасчета.ВидСравнения = ВидСравнения.ВСписке;
	СхемыМотивацииПодразделения.Отбор.ВидРасчета.Использование = Истина;
	СхемыМотивацииПодразделения.Отбор.ВидРасчета.ВидСравнения = ВидСравнения.ВСписке;
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
		Если мУпрВидыРасчетов = Неопределено Тогда
			мУпрВидыРасчетов = Новый СписокЗначений;
			УпрВидыРасчетов = ПланыВидовРасчета.УправленческиеНачисления.Выбрать();
			Пока УпрВидыРасчетов.Следующий() Цикл
				мУпрВидыРасчетов.Добавить(УпрВидыРасчетов.Ссылка);
			КонецЦикла;
			мУпрВидыРасчетов.Добавить(ПланыВидовРасчета.УправленческиеНачисления.ПустаяСсылка());
		КонецЕсли;
		ТабличноеПолеПоДолжности.Отбор.ВидРасчета.Значение = мУпрВидыРасчетов;
		СхемыМотивацииПодразделения.Отбор.ВидРасчета.Значение = мУпрВидыРасчетов;
	Иначе
		Если мРегВидыРасчетов = Неопределено Тогда
			мРегВидыРасчетов = Новый СписокЗначений;
			РегВидыРасчетов = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.Выбрать();
			Пока РегВидыРасчетов.Следующий() Цикл
				мРегВидыРасчетов.Добавить(РегВидыРасчетов.Ссылка);
			КонецЦикла;
			мРегВидыРасчетов.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка());
		КонецЕсли;		
		ТабличноеПолеПоДолжности.Отбор.ВидРасчета.Значение = мРегВидыРасчетов;
		СхемыМотивацииПодразделения.Отбор.ВидРасчета.Значение = мРегВидыРасчетов;
	КонецЕсли;
		
КонецПроцедуры //ОтборПоВидуРасчета()

// Управляет валютой в зависимости от вида расчета
//
Процедура ПриИзмененииВидаРасчета(Значение, ИмяОбъекта)

	СведенияОВидеРасчета = РаботаСДиалогами.ПолучитьСведенияОВидеРасчетаСхемыМотивации(мСведенияОВидахРасчета, Значение);

	Для СчПоказателей = 1 По Мин(СведенияОВидеРасчета["КоличествоПоказателей"],6) Цикл
		Если НЕ ЗначениеЗаполнено(ЭлементыФормы[ИмяОбъекта].ТекущиеДанные["Валюта" + СчПоказателей]) И 
			СведенияОВидеРасчета["Валюта" + СчПоказателей + "Видимость"] Тогда
			ЭлементыФормы[ИмяОбъекта].ТекущиеДанные["Валюта" + СчПоказателей] = мВалютаПоУмолчанию;
		КонецЕсли;
		Если НЕ СведенияОВидеРасчета["Валюта" + СчПоказателей + "Видимость"] Тогда
			ЭлементыФормы[ИмяОбъекта].ТекущиеДанные["Валюта" + СчПоказателей] = Справочники.Валюты.ПустаяСсылка();
		КонецЕсли;
		Если НЕ СведенияОВидеРасчета["Показатель" + СчПоказателей + "Видимость"] Тогда
			ЭлементыФормы[ИмяОбъекта].ТекущиеДанные["Показатель" + СчПоказателей] = 0;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Устанавливает видимость колонок "Валюта" 
// 
Процедура УстанвоитьВидимостьВалюты()
	
	ЭлементыФормы.ТабличноеПолеПоДолжности.Колонки.Валюта1.Видимость = мПоказватьВалюту;
	ЭлементыФормы.ТабличноеПолеПоДолжности.Колонки.Валюта2.Видимость = мПоказватьВалюту;
	ЭлементыФормы.ТабличноеПолеПоДолжности.Колонки.Валюта3.Видимость = мПоказватьВалюту;
	ЭлементыФормы.ТабличноеПолеПоДолжности.Колонки.Валюта4.Видимость = мПоказватьВалюту;
	
	ЭлементыФормы.СхемыМотивацииПодразделения.Колонки.Валюта1.Видимость = мПоказватьВалюту;
	ЭлементыФормы.СхемыМотивацииПодразделения.Колонки.Валюта2.Видимость = мПоказватьВалюту;
	ЭлементыФормы.СхемыМотивацииПодразделения.Колонки.Валюта3.Видимость = мПоказватьВалюту;
	ЭлементыФормы.СхемыМотивацииПодразделения.Колонки.Валюта4.Видимость = мПоказватьВалюту;	

КонецПроцедуры //УстанвоитьВидимостьВалюты

// Устанавливает заголовок реквизита "НадписьУправлениеВидимостьюДолжностей"
//
Процедура ЗаголовокДолжностей()
	
	Если ПоказыватьДолжности Тогда
		ЭлементыФормы.НадписьУправлениеВидимостьюДолжностей.Заголовок = НСтр("ru='Скрыть схемы мотивации по должности';uk='Приховати схеми мотивації по посаді'");
	Иначе
		ЭлементыФормы.НадписьУправлениеВидимостьюДолжностей.Заголовок = НСтр("ru='Показать схемы мотивации по должности';uk='Показати схеми мотивації по посаді'");
	КонецЕсли;
	
КонецПроцедуры //ЗаголовокДолжностей

// Устанавливает отбор по подразделению и должности для ТЧ "СхемыМотивацииПодразделения"
//
Процедура УстановитьОтборСхемыМотивацииПоДолжности(Подразделение)
	
	СхемыМотивацииПодразделения.Отбор.Должность.Значение.Очистить();
	
	Если Подразделение = НеОпределено или Подразделение.Пустая() Тогда
		
		ОтборПоВидуРасчета();
		СхемыМотивацииПодразделения.Обновить();
		
	Иначе
		
		СхемыМотивацииПодразделения.Отбор.Подразделение.Значение.Добавить(Неопределено);
		СхемыМотивацииПодразделения.Отбор.Подразделение.Значение.Добавить(Подразделение);
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	СхемыМотивацииРаботников.Должность,
		|	СхемыМотивацииРаботников.ВидРасчета
		|ИЗ
		|	РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
		|ГДЕ
		|	СхемыМотивацииРаботников.Подразделение = &Подразделение";
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И СхемыМотивацииРаботников.Организация = &Организация";
			Запрос.УстановитьПараметр("Организация", Организация);
		КонецЕсли;
		
		мВведенныеДолжности = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Должность");
		мВведенныеВР = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидРасчета");
	
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ШтатноеРасписаниеОрганизаций.Должность
		|ИЗ
		|	РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
		|ГДЕ
		|	ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации = &Подразделение
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	СхемыМотивацииРаботников.Должность
		|ИЗ
		|	РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
		|ГДЕ
		|	СхемыМотивацииРаботников.Подразделение = &Подразделение";
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|	И СхемыМотивацииРаботников.Организация = &Организация";
			Запрос.УстановитьПараметр("Организация", Организация);
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СхемыМотивацииПодразделения.Отбор.Должность.Значение.Добавить(Выборка.Должность);
		КонецЦикла;
		
		УстановитьОтборПоВидуСМ();
		
		СхемыМотивацииПодразделения.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры //УстановитьОтборСхемыМотивацииПоДолжности

// Устанавливает отбор виду схемы мотивации
//
Процедура УстановитьОтборПоВидуСМ()
	
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
		// получим учтенную политику по персоналу в разрезе поддержки нескольких схем мотивации 
		Если мПоддерживатьНесколькоСхемМотивации	= Неопределено Тогда
			глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналу").Свойство("ПоддерживатьНесколькоСхемМотивации",мПоддерживатьНесколькоСхемМотивации);
		КонецЕсли;
		
		Если мПоддерживатьНесколькоСхемМотивации Тогда
			СхемыМотивацииПодразделения.Отбор.ВидСхемыМотивации.Использование	= Истина;
			СхемыМотивацииПодразделения.Отбор.ВидСхемыМотивации.ВидСравнения	= ВидСравнения.Равно;
			СхемыМотивацииПодразделения.Отбор.ВидСхемыМотивации.Значение		= ВидСхемыМотивации;
			
			ТабличноеПолеПоДолжности.Отбор.ВидСхемыМотивации.Использование	= Истина;
			ТабличноеПолеПоДолжности.Отбор.ВидСхемыМотивации.ВидСравнения	= ВидСравнения.Равно;
			ТабличноеПолеПоДолжности.Отбор.ВидСхемыМотивации.Значение		= ВидСхемыМотивации;
			
		Иначе
			СхемыМотивацииПодразделения.Отбор.ВидСхемыМотивации.Использование	= Ложь;
			ТабличноеПолеПоДолжности.Отбор.ВидСхемыМотивации.Использование	= Ложь;
		КонецЕсли;
		
		СхемыМотивацииПодразделения.Отбор.Организация.Использование	= Ложь;
		ТабличноеПолеПоДолжности.Отбор.Организация.Использование	= Ложь;
	Иначе 
		СхемыМотивацииПодразделения.Отбор.ВидСхемыМотивации.Использование	= Ложь;
		ТабличноеПолеПоДолжности.Отбор.ВидСхемыМотивации.Использование	= Ложь;
	КонецЕсли;
		
КонецПроцедуры //УстановитьОтборПоВидуСМ

// Формирует запрос к регистру сведений "Схемы мотиваций"
// и вызывает процедуру печати схемы мотиваций.
//
// Параметры
//  Режим  – Текст. Режим формирования запроса
//			Может принимать значения:
//				"ПоВсем" - по всей организации или по всему предприятию
//				"По подразделению" - по текущему подразделению
//				"По должности" - по текущей должности 
//  
Процедура СформироватьЗапросДляПечати(Режим = "ПоВсем")
	
	Если Режим = "По подразделению" Тогда
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
			Подразделение	= ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока;
		Иначе
			Подразделение	= ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока;
		КонецЕсли;
	КонецЕсли;
	
	Если Режим <> "ПоВсем" Тогда
		Должность		= ЭлементыФормы.ТабличноеПолеДолжности.ТекущаяСтрока;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСхемыМотивации",ВидСхемыМотивации);
	Запрос.УстановитьПараметр("ПодразделениеНеопределено", Неопределено);
	Запрос.УстановитьПараметр("Должность", Должность);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ВидСхемыМотивации",ВидСхемыМотивации);
	Запрос.УстановитьПараметр("Подразделение",Подразделение);
	
	Если Режим = "По подразделению" И ЗначениеЗаполнено(Подразделение) Тогда

		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ШтатноеРасписаниеОрганизаций.Должность
			|ПОМЕСТИТЬ ВТДолжности
			|ИЗ
			|	РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
			|ГДЕ
			|	ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации = &Подразделение";
		Иначе
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СхемыМотивацииРаботников.Должность
			|ПОМЕСТИТЬ ВТДолжности
			|ИЗ
			|	РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
			|ГДЕ
			|	СхемыМотивацииРаботников.Подразделение = &Подразделение";
		КонецЕсли;
		ВТДолжности = "ВТДолжности";
		Запрос.Выполнить();
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	СхемыМотивацииРаботников.ВидСхемыМотивации,
	|	СхемыМотивацииРаботников.Организация,
	|	СхемыМотивацииРаботников.Подразделение,
	|	СхемыМотивацииРаботников.Должность,
	|	СхемыМотивацииРаботников.ВидРасчета,
	|	СхемыМотивацииРаботников.Показатель1,
    |	СхемыМотивацииРаботников.Валюта1,
    |	СхемыМотивацииРаботников.Показатель2,
    |	СхемыМотивацииРаботников.Валюта2,
    |	СхемыМотивацииРаботников.Показатель3,
    |	СхемыМотивацииРаботников.Валюта3,
	|	СхемыМотивацииРаботников.Показатель4,
	|	СхемыМотивацииРаботников.Валюта4,
    |	СхемыМотивацииРаботников.Показатель5,
    |	СхемыМотивацииРаботников.Валюта5,
	|	СхемыМотивацииРаботников.Показатель6,
	|	СхемыМотивацииРаботников.Валюта6
	|ИЗ
	|	РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников";
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	СхемыМотивацииРаботников.ВидРасчета ССЫЛКА ПланВидовРасчета.ОсновныеНачисленияОрганизаций";
		Если Режим <> "ПоВсем" Тогда
			ТекстЗапроса = ТекстЗапроса + "
		|	И СхемыМотивацииРаботников.Организация <> Значение(Справочник.Организации.ПустаяСсылка)";
		КонецЕсли;
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	СхемыМотивацииРаботников.ВидРасчета ССЫЛКА ПланВидовРасчета.УправленческиеНачисления";
		Если мПоддерживатьНесколькоСхемМотивации = Неопределено Тогда
			глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналу").Свойство("ПоддерживатьНесколькоСхемМотивации",мПоддерживатьНесколькоСхемМотивации);
		КонецЕсли;
		Если мПоддерживатьНесколькоСхемМотивации Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|	И СхемыМотивацииРаботников.ВидСхемыМотивации = &ВидСхемыМотивации";
		КонецЕсли;
	КонецЕсли;

	Если Режим = "По подразделению" И ЗначениеЗаполнено(Подразделение) Тогда
		ТекстЗапроса = ТекстЗапроса + "
	|	И (СхемыМотивацииРаботников.Подразделение = &Подразделение
	|	ИЛИ СхемыМотивацииРаботников.Подразделение = &ПодразделениеНеопределено И Должность В (ВЫБРАТЬ Должность ИЗ ВТДолжности))";
	ИначеЕсли Режим = "По должности" Тогда
		ТекстЗапроса = ТекстЗапроса + "
	|	И СхемыМотивацииРаботников.Должность = &Должность
	|	И СхемыМотивацииРаботников.Подразделение = &ПодразделениеНеопределено";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	СхемыМотивацииРаботников.ВидСхемыМотивации,
	|	СхемыМотивацииРаботников.Организация,
	|	СхемыМотивацииРаботников.Подразделение,
	|	СхемыМотивацииРаботников.Должность";
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбработкаКомментариев.УдалитьСообщения();
		Если Режим = "По должности" Тогда
			Если Должность = Неопределено Тогда
				ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Не выбрана должность!';uk='Не обрана посада!'"), Перечисления.ВидыСообщений.Информация);
			Иначе
				Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
					ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Для организации ';uk='Для організації '")+ Организация = НСтр("ru=' по должности: ';uk=' по посаді: '")+ Должность +НСтр("ru=' не введены начисления!';uk=' не введені нарахування!'"), Перечисления.ВидыСообщений.Информация);
				Иначе
					ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='По должности: ';uk='По посаді: '")+ Должность +НСтр("ru=' не введены начисления!';uk=' не введені нарахування!'"), Перечисления.ВидыСообщений.Информация);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Режим = "По подразделению" Тогда
			Если Подразделение = Неопределено Тогда
				ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Не выбрана должность!';uk='Не обрана посада!'"), Перечисления.ВидыСообщений.Информация);
			Иначе
				Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
					ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Для организации ';uk='Для організації '")+ Организация = НСтр("ru=' подразделению: ';uk=' підрозділу: '")+ Подразделение +НСтр("ru=' не введены начисления!';uk=' не введені нарахування!'"), Перечисления.ВидыСообщений.Информация);
				Иначе
					ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='По подразделению: ';uk='По підрозділу: '")+ Подразделение +НСтр("ru=' не введены начисления!';uk=' не введені нарахування!'"), Перечисления.ВидыСообщений.Информация);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Отсутствуют начисления!';uk='Відсутні нарахування!'"), Перечисления.ВидыСообщений.Информация);
		КонецЕсли;
		
		ОбработкаКомментариев.ПоказатьСообщения();
		
		Возврат;
		
	КонецЕсли;
	ТаблицаВидовРасчета = РезультатЗапроса.Выгрузить();

	ПечатьСМ(ТаблицаВидовРасчета, ВидОрганизационнойСтруктурыПредприятия);

КонецПроцедуры //СформироватьЗапросДляПечати

Процедура ПодготовитьСправкуФормы()
	
	ВидОрганизационнойСтруктурыПредприятия_ПоСтруктуреЮридическихЛиц	= ?(ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц, "inline", "none");
	ВидОрганизационнойСтруктурыПредприятия_ПоЦентрамОтветственности		= ?(ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности, "inline", "none");
	
	ЦветФонаСправки		= РаботаСДиалогами.ВернутьШестнадцатиричноеПредставлениеЦвета(РаботаСДиалогами.ВстроеннаяСправка_ЦветФона());
	ЦветСсылкиСправки	= РаботаСДиалогами.ВернутьШестнадцатиричноеПредставлениеЦвета(РаботаСДиалогами.ВстроеннаяСправка_ЦветСсылки());
	
	Если Не ПоказыватьДолжности Тогда
		СправкаПоДолжностям = "Для того чтоб открыть схемы мотивации по должности нужно нажать на гиперссылку <A id=Команда href=""V8:ShowMotivationSchemesByPosition""> ""Показать схемы мотивации по должности"" </A>";
	КонецЕсли;
	
	ЭлементыФормы.ПанельСправкиФормы.ЦветРамки			= РаботаСДиалогами.ВстроеннаяСправка_ЦветРамки();
	ЭлементыФормы.НадписьЗакрытьСправкуФормы.ЦветФона	= РаботаСДиалогами.ВстроеннаяСправка_ЦветФона();
	ЭлементыФормы.НадписьЗакрытьСправкуФормы.ЦветТекста	= РаботаСДиалогами.ВстроеннаяСправка_ЦветСсылки();

	ТекстМакетаВстроеннойСправки = "
	|<HTML>
	|	<HEAD>
	|		<META http-equiv=Content-Type content=""text/html; charset=utf-8"">" + РаботаСДиалогами.ВстроеннаяСправка_СтилиДокумента() + "
	|	</HEAD>
	|
	|	<BODY aLink="+ЦветСсылкиСправки+" vLink="+ЦветСсылкиСправки+" link="+ЦветСсылкиСправки+" bgColor="+ЦветФонаСправки+" scroll=auto><FONT face=""MS Sans Serif"" size=1>
	|		<IMG src="+РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.КартинкаВстроеннойСправкиФормы, ЭлементыФормы.ВстроеннаяСправка)+">
	|		<DIV>Схема мотивации задает начисления, которые выплачиваются сотруднику на определенной должности.<BR>Вы можете задать схему мотивации:<LI>для должности, и тогда схема будет применяться для всех сотрудников предприятия, работающих на данной должности, вне зависимости от места работы (подразделения). " + СправкаПоДолжностям + "
	|		<LI>для конкретного рабочего места, и тогда схема будет применяться для сотрудников, работающих на данной должности в конкретном подразделении.</LI></DIV>
	|		<DIV>Начисления, введенные для всех сотрудников по указанной должности, выделяются <FONT color=#556B2F>таким</FONT> цветом. </DIV>
	|		<DIV>Формирование схем мотивации по рабочим местам может выполняться в соответствии со 
	|			<A id=ОткрытьСписок href=""V8:Справочники.ПодразделенияОрганизаций"">структурой организаций</A> - юридических лиц вашего предприятия или в соответствии со структурой <A id=ОткрытьСписок href=""V8:Справочники.Подразделения"">центров ответственности</A>. <BR>В начале работы со схемами мотивации рабочих мест вам необходимо выбрать один из этих режимов, но в дальнейшем вы сможете изменить выбранный вами режим.
	|				<DIV>Сейчас установлен режим формирования схем мотивации 
	|					<P id=ВидОрганизационнойСтруктурыПредприятия_ПоСтруктуреЮридическихЛиц style=""DISPLAY:"+ВидОрганизационнойСтруктурыПредприятия_ПоСтруктуреЮридическихЛиц+"""><A id=Команда href=""V8:SwitchOrganizationalStructure"">"+ВидОрганизационнойСтруктурыПредприятия+"</P>
	|					<P id=ВидОрганизационнойСтруктурыПредприятия_ПоЦентрамОтветственности style=""DISPLAY:"+ВидОрганизационнойСтруктурыПредприятия_ПоЦентрамОтветственности+"""><A id=Команда href=""V8:SwitchOrganizationalStructure"">"+ВидОрганизационнойСтруктурыПредприятия+"</A></P>
	|			.
	|				</DIV>
	|		<DIV>Если <A id=ОткрытьСписок href=""V8:РегистрыСведений.УчетнаяПолитикаПоПерсоналу""> учетная политика </A> по персоналу поддерживает работу с несколькими схемами мотивации, то кроме основной схемы мотивации вы можете формировать временные схемы мотивации и использовать их при начислении заработной платы.</DIV>
	|	</FONT></BODY>
	|</HTML>";
	
	ЭлементыФормы.ВстроеннаяСправка.УстановитьТекст(ТекстМакетаВстроеннойСправки);
	
КонецПроцедуры

// Обновляет содержимое справки
//
Процедура ОбновитьСправкуФормы(ПолеДанных = Неопределено)
	
	ДокHTML = ЭлементыФормы.ВстроеннаяСправка.Документ;
	
	Если ПолеДанных = Неопределено ИЛИ ПолеДанных = "ВидОрганизационнойСтруктурыПредприятия" Тогда
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ВидОрганизационнойСтруктурыПредприятия_ПоСтруктуреЮридическихЛиц", ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц);
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "ВидОрганизационнойСтруктурыПредприятия_ПоЦентрамОтветственности", ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности);
		ДокHTML.getElementById("ВидОрганизационнойСтруктурыПредприятия_ПоСтруктуреЮридическихЛиц").innerHTML	= "" + ВидОрганизационнойСтруктурыПредприятия;
		ДокHTML.getElementById("ВидОрганизационнойСтруктурыПредприятия_ПоЦентрамОтветственности").innerHTML	= "" + ВидОрганизационнойСтруктурыПредприятия;
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСправкуФормы()

// Устанавливает видимость справки
//
Процедура УстановитьВидимостьСправкиФормы(ОбновитьСправку = Истина)
	
	РаботаСДиалогами.ИзменитьВидимостьПанелиПоГоризонтали(ЭтаФорма, ОтображатьСправкуФормы, "ОсновнаяПанель", "СправкиФормы");
	
	ЭлементыФормы.КоманднаяПанельСправкиФормы.Кнопки.ПереключениеСправкиФормы.Пометка = ОтображатьСправкуФормы;
	
	Если ОтображатьСправкуФормы Тогда
		ПодключитьОбработчикИзмененияДанных("ВидОрганизационнойСтруктурыПредприятия",	"ОбновитьСправкуФормы");
		Если ОбновитьСправку Тогда
			ОбновитьСправкуФормы();
		КонецЕсли;
		
	Иначе
		ОтключитьОбработчикИзмененияДанных("ВидОрганизационнойСтруктурыПредприятия");
		
	КонецЕсли;
	
КонецПроцедуры //УстановитьВидимостьСправкиФормы

Процедура ВыполнитьКомандуФормы(Команда, ТекущееЗначение = "") Экспорт
	
	Если Команда = "ВыборОрганизации" Тогда
		Форма = Справочники.Организации.ПолучитьФормуВыбора();
		Форма.ПараметрТекущаяСтрока	= Организация;
		Форма.ВладелецФормы			= Организация;
		Форма.Открыть();
	ИначеЕсли Команда = "ShowMotivationSchemesByPosition" Тогда
		НадписьУправлениеВидимостьюДолжностейНажатие();
	ИначеЕсли Команда = "SwitchOrganizationalStructure" Тогда
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности;
		Иначе
			ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
		КонецЕсли;
		ВидОрганизационнойСтруктурыПредприятияПриИзменении();
	Иначе
		Сообщить(НСтр("ru='Неизвестная команда: ';uk='Невідома команда: '") + Команда);
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКомандуСправки()

Процедура ВстроеннаяСправкаonclick(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnClick(Элемент, pEvtObj, ЭтаФорма);
	
КонецПроцедуры

Процедура ВстроеннаяСправкаonmouseout(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOut(Элемент, pEvtObj);
	
КонецПроцедуры

Процедура ВстроеннаяСправкаonmouseover(Элемент, pEvtObj)
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOver(Элемент, pEvtObj);
	
КонецПроцедуры

// Возвращает первое подразделение выбранной организации или предприятия
//
// Параметры
//
// Возвращаемое значение:
//   СправочникиСсылка.ПодразделенияОрганизаций   – первое подразделение организации.
//   ИЛИ (зависит от заданного режима формирования схем мотивации)
//   СправочникиСсылка.Подразделения   – первое подразделение предприятия.
//
Функция ПервоеПодразделениеОрганизации()

	Запрос = Новый Запрос;
	
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодразделенияОрганизаций.Ссылка
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Владелец = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодразделенияОрганизаций.Наименование";
		
		Запрос.УстановитьПараметр("Организация", Организация);
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Подразделения.Ссылка
		|ИЗ
		|	Справочник.Подразделения КАК Подразделения
		|
		|УПОРЯДОЧИТЬ ПО
		|	Подразделения.Наименование"
	
	КонецЕсли; 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.Ссылка, ?(ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц,Справочники.ПодразделенияОрганизаций.ПустаяСсылка(),Справочники.Подразделения.ПустаяСсылка()));
	
КонецФункции // ПервоеПодразделениеОрганизации()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()
	
	УстановитьВидимостьСправкиФормы(Ложь);
	
	ЗаголовокДолжностей();
		
	ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоЦФО.Пометка = НЕ ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоОрганизациям.Пометка = ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;		
	
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		мПоказватьВалюту= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ИспользуютсяНачисленияВВалюте");		
		УстанвоитьВидимостьВалюты();
		Если мПоказватьВалюту Тогда
			мВалютаПоУмолчанию = Константы.ВалютаРегламентированногоУчета.Получить();
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока) Тогда
			ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока = ПервоеПодразделениеОрганизации();
		КонецЕсли;
		
	Иначе
		мПоказватьВалюту = Истина;
		глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналу").Свойство("ПоддерживатьНесколькоСхемМотивации",мПоддерживатьНесколькоСхемМотивации);
		мВалютаПоУмолчанию = Константы.ВалютаУправленческогоУчета.Получить();
			
		Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока) Тогда
			ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока = ПервоеПодразделениеОрганизации();
		КонецЕсли;
		
	КонецЕсли;
	
	ВидОрганизационнойСтруктурыПредприятияПриИзменении();
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	ТабличноеПолеПодразделенияОрганизаций.Порядок.Установить("Владелец, Наименование");
	
	СхемыМотивацииПодразделения.Порядок.Установить("Должность Возр, ВидРасчета Возр");
	
КонецПроцедуры //ПриОткрытии

// Процедура - обработчик события "ПередОткрытием" формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	СхемыМотивацииПодразделения.Колонки.Добавить("Подразделение", Ложь);
	
	СхемыМотивацииПодразделения.Отбор.Подразделение.ВидСравнения = ВидСравнения.ВСписке;
	СхемыМотивацииПодразделения.Отбор.Подразделение.Использование = Истина;
	СхемыМотивацииПодразделения.Отбор.Должность.ВидСравнения = ВидСравнения.ВСписке;
	СхемыМотивацииПодразделения.Отбор.Должность.Использование = Истина;
	
	// вид организационной структуры
	ВидОрганизационнойСтруктурыПредприятия = ВосстановитьЗначение("МотивацияПерсоналаВидОрганизационнойСтруктурыПредприятия");
	Если НЕ ЗначениеЗаполнено(ВидОрганизационнойСтруктурыПредприятия) Тогда
		ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности;
	КонецЕсли;
	
	ПоказыватьДолжности = ВосстановитьЗначение("МотивацияПерсоналаПоказыватьДолжности");
	Если НЕ ЗначениеЗаполнено(ПоказыватьДолжности) Тогда
		ПоказыватьДолжности = Ложь;
	КонецЕсли;
	
	РаботаСДиалогами.ИзменитьВидимостьПанелиПоВертикали(ЭтаФорма, ПоказыватьДолжности, "РабочаяОбласть", "СхемыМотивацийПоДолжностям");
	
	// Справка формы
	ОтображатьСправкуФормы = ВосстановитьЗначение("МотивацияПерсоналаСправкаФормы");
	Если НЕ ЗначениеЗаполнено(ОтображатьСправкуФормы) Тогда
		ОтображатьСправкуФормы = Истина;
	КонецЕсли;
	
	мПорядокСортировкиПодразделенияПоКоду = ВосстановитьЗначение("МотивацияПерсоналаПорядокСортировкиПодразделенияПоКоду");
	Если мПорядокСортировкиПодразделенияПоКоду = Неопределено Тогда
		мПорядокСортировкиПодразделенияПоКоду = 0;
	КонецЕсли;
	
	мПорядокСортировкиДолжностейПоКоду = ВосстановитьЗначение("МотивацияПерсоналаПорядокСортировкиДолжностейПоКоду");
	Если мПорядокСортировкиДолжностейПоКоду = Неопределено Тогда
		мПорядокСортировкиДолжностейПоКоду = 0;
	КонецЕсли;
	
	// проставляем организацию пользователя по умолчанию
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	КонецЕсли;
	
	ПодготовитьСправкуФормы();
	
КонецПроцедуры //ПередОткрытием

// Процедура - обработчик события "ПередЗакрытием" формы
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СохранитьЗначение("МотивацияПерсоналаВидОрганизационнойСтруктурыПредприятия",ВидОрганизационнойСтруктурыПредприятия);
	СохранитьЗначение("МотивацияПерсоналаСправкаФормы",ОтображатьСправкуФормы);
	СохранитьЗначение("МотивацияПерсоналаПоказыватьДолжности",ПоказыватьДолжности);	
	СохранитьЗначение("МотивацияПерсоналаПорядокСортировкиПодразделенияПоКоду", мПорядокСортировкиПодразделенияПоКоду);
	СохранитьЗначение("МотивацияПерсоналаПорядокСортировкиДолжностейПоКоду",мПорядокСортировкиДолжностейПоКоду);

КонецПроцедуры //ПередЗакрытием

// Процедура - обработчик события "ОбработкаОповещения" формы
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗакрытоШР" Тогда
		ТабличноеПолеПодразделенияПриАктивизацииСтроки();
	ИначеЕсли ИмяСобытия = "ОбновитьФорму" Тогда
		Если мРегВидыРасчетов <> Неопределено Тогда
			Если Параметр = "ОсновныеНачисленияОрганизаций" Тогда
				мРегВидыРасчетов.Добавить(Источник);
			ИначеЕсли Параметр = "УправленческиеНачисления" Тогда
				мУпрВидыРасчетов.Добавить(Источник);
			КонецЕсли;
			ОтборПоВидуРасчета();
			СхемыМотивацииПодразделения.Обновить();
			ТабличноеПолеПоДолжности.Обновить();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОбновитьФормуМотивацииПерсонала" Тогда
		
		глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналу").Свойство("ПоддерживатьНесколькоСхемМотивации",мПоддерживатьНесколькоСхемМотивации);
        ВидОрганизационнойСтруктурыПредприятияПриИзменении();

	КонецЕсли;
	
КонецПроцедуры //ОбработкаОповещения

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Управляет видимостью справки
//
Процедура ПереключитьВидимостьСправкиФормы(Кнопка)
	
	ОтображатьСправкуФормы = НЕ ОтображатьСправкуФормы;
	УстановитьВидимостьСправкиФормы();
	
КонецПроцедуры //ПереключитьВидимостьСправкиФормы

// Устанавливает отборы в зависимости от выбранной организационной структуры 
//
Процедура ВидОрганизационнойСтруктурыПредприятияПриИзменении()
		
	Кнопки = ЭлементыФормы.КоманднаяПанельПредприятие.Кнопки;
	КнопкиДействие = ЭлементыФормы.КоманднаяПанельПредприятие.Кнопки.Действие.Кнопки;

	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		
		ЭлементыФормы.ПанельПодразделения.ТекущаяСтраница = ЭлементыФормы.ПанельПодразделения.Страницы.ПодразделенияОрганизаций;
		
		// установим отбор по должности организации
		УстановитьОтборСхемыМотивацииПоДолжности(ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока);
		
		ЭлементыФормы.НадписьВидСМ.Видимость = Ложь;
		ЭлементыФормы.НадписьСМ.Видимость = Ложь;
		
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоЦФО.Пометка = Ложь;
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоОрганизациям.Пометка = Истина;
		
		мПоказватьВалюту = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ИспользуютсяНачисленияВВалюте");
		УстанвоитьВидимостьВалюты();
		
		Если мПоказватьВалюту Тогда
			мВалютаПоУмолчанию = Константы.ВалютаРегламентированногоУчета.Получить();
		КонецЕсли;	
		
		Если Кнопки.Найти("ШтатноеРасписание") = Неопределено Тогда
			Кнопки.Добавить("ШтатноеРасписание", ТипКнопкиКоманднойПанели.Действие, НСтр("ru='Штатное расписание';uk='Штатний розклад'"),Новый Действие("ОткрытьШтатноеРасписание"));
		
			НоваяКнопка = КнопкиДействие.Добавить("ШтатноеРасписание", ТипКнопкиКоманднойПанели.Действие, НСтр("ru='Штатное расписание';uk='Штатний розклад'"),Новый Действие("ОткрытьШтатноеРасписание"));
 			НоваяКнопка.Подсказка = НСтр("ru='Открыть штатное расписание';uk='Відкрити штатний розклад'");
			НоваяКнопка.Пояснение = НСтр("ru='Открыть штатное расписание';uk='Відкрити штатний розклад'");
		КонецЕсли;

		Если КнопкиДействие.Найти("ЗаполнитьПоШтатномуРасписанию") = Неопределено Тогда
			НоваяКнопка = КнопкиДействие.Добавить("ЗаполнитьПоШтатномуРасписанию", ТипКнопкиКоманднойПанели.Действие, НСтр("ru='Заполнить по штатному расписанию';uk='Заповнити за штатним розкладом'"),Новый Действие("КоманднаяПанельПредприятиеЗаполнитьПоШтатномуРасписанию"));		
			
			НоваяКнопка.Подсказка = НСтр("ru='Заполнить начисления работников организации по штатному расписанию (по выбранному подразделению)';uk='Заповнити нарахування працівників організації за штатним розкладом (по вибраному підрозділу)'");		
			НоваяКнопка.Пояснение = НСтр("ru='Заполнить начисления работников организации по штатному расписанию (по выбранному подразделению)';uk='Заповнити нарахування працівників організації за штатним розкладом (по вибраному підрозділу)'");		
			НоваяКнопка.ИзменяетДанные = Истина;
		КонецЕсли;
		
	Иначе
		
		ЭлементыФормы.ПанельПодразделения.ТекущаяСтраница = ЭлементыФормы.ПанельПодразделения.Страницы.ПоЦФО;
		
		// установим отбор по должности предприятия
		УстановитьОтборСхемыМотивацииПоДолжности(ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока);
		
		// получим учтенную политику по персоналу в разрезе поддержки нескольких схем мотивации 
		Если мПоддерживатьНесколькоСхемМотивации = Неопределено Тогда
			глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналу").Свойство("ПоддерживатьНесколькоСхемМотивации",мПоддерживатьНесколькоСхемМотивации);
		КонецЕсли;
		Если мПоддерживатьНесколькоСхемМотивации Тогда
			ЭлементыФормы.НадписьВидСМ.Видимость	= Истина;
			ЭлементыФормы.НадписьСМ.Видимость		= Истина;
		Иначе
			ЭлементыФормы.НадписьВидСМ.Видимость	= Ложь;
			ЭлементыФормы.НадписьСМ.Видимость		= Ложь;
		КонецЕсли;
		
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоЦФО.Пометка = Истина;
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоОрганизациям.Пометка = Ложь;
		
		мПоказватьВалюту = Истина;
		УстанвоитьВидимостьВалюты();
		
		мВалютаПоУмолчанию = Константы.ВалютаУправленческогоУчета.Получить();
		
		Если Кнопки.Найти("ШтатноеРасписание") <> Неопределено Тогда
			Индекс = Кнопки.Индекс(Кнопки.ШтатноеРасписание);
			Кнопки.Удалить(Индекс);
		КонецЕсли;
		Если  КнопкиДействие.Найти("ЗаполнитьПоШтатномуРасписанию") <> Неопределено Тогда
			Индекс = КнопкиДействие.Индекс(КнопкиДействие.ЗаполнитьПоШтатномуРасписанию);
			КнопкиДействие.Удалить(Индекс);
		КонецЕсли;
		Если  КнопкиДействие.Найти("ШтатноеРасписание") <> Неопределено Тогда
			Индекс = КнопкиДействие.Индекс(КнопкиДействие.ШтатноеРасписание);
			КнопкиДействие.Удалить(Индекс);
		КонецЕсли;
		
		
		// установим текс гиперссылки
		Если ВидСхемыМотивации = Справочники.ВариантыCхемМотивации.ПустаяСсылка() Тогда
			ЭлементыФормы.НадписьВидСМ.Заголовок = НСтр("ru='Основная схема мотивации';uk='Основна схема мотивації'");
		Иначе
			ЭлементыФормы.НадписьВидСМ.Заголовок = ВидСхемыМотивации.Наименование;
		КонецЕсли;
		
	КонецЕсли;	
	
	ОтборПоВидуРасчета();
	ТабличноеПолеПоДолжности.Обновить();

	
КонецПроцедуры //ВидОрганизационнойСтруктурыПредприятияПриИзменении

// Процедура - обработчик события "Нажатие" гиперссылки НадписьВидСМ
//
Процедура НадписьВидСМНажатие(Элемент)
	
	Меню = Новый СписокЗначений;
	
	Меню.Добавить("Основная схема мотивации",,НЕ ЗначениеЗаполнено(ВидСхемыМотивации));
	
	Выборка = Справочники.ВариантыCхемМотивации.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Меню.Количество() < 5 Тогда
			Меню.Добавить(Выборка.Ссылка,,ВидСхемыМотивации = Выборка.Ссылка);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Меню.Добавить("Открыть список временных схем мотивации");
	
	ВидСМ = ВыбратьИзМеню(Меню,);
	Если ВидСМ <> Неопределено Тогда
		ВидСМ = ВидСМ.Значение;	
		Если ВидСМ = "Основная схема мотивации" Тогда
			ВидСхемыМотивации = Справочники.ВариантыCхемМотивации.ПустаяСсылка();
			Элемент.Заголовок = "Основная схема мотивации";
		ИначеЕсли ВидСМ = "Открыть список временных схем мотивации" Тогда
			Справочники.ВариантыCхемМотивации.ПолучитьФорму("ФормаСписка").Открыть();
		Иначе
			ВидСхемыМотивации = ВидСМ;
			Элемент.Заголовок = ВидСхемыМотивации.Наименование;
		КонецЕсли;
		
		УстановитьОтборПоВидуСМ();
	КонецЕсли;

КонецПроцедуры //НадписьВидСМНажатие

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ "СхемыМотивацииПодразделения" ФОРМЫ

// Процедура - обработчик события "ПриАктивизацииЯчейки" табличного поля
//
Процедура СхемыМотивацииПодразделенияПриАктивизацииЯчейки(Элемент)
	
	ТекущаяКолонка = Элемент.ТекущаяКолонка;
	Если ТекущаяКолонка.Имя = "Должность" Тогда
		
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
			СписокВыбора = ПроцедурыУправленияПерсоналом.ВыбратьИзКадровогоПланаПредприятия("Должность", ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока);
		Иначе
			СписокВыбора = ПроцедурыУправленияПерсоналом.ВыбратьИзШтатногоРасписанияОрганизации("Должность",  Организация, ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока);
		КонецЕсли;
		КоличествоЗначений = СписокВыбора.Количество();
		
		ПолеВвода = ТекущаяКолонка.ЭлементУправления;
		ПолеВвода.СписокВыбора = СписокВыбора;
		ПолеВвода.КнопкаСпискаВыбора = КоличествоЗначений > 0;
		ПолеВвода.ВысотаСпискаВыбора = Мин(10, КоличествоЗначений);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ВыводеСтроки" табличного поля
//
Процедура СхемыМотивацииПодразделенияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если Не ЗначениеЗаполнено(ДанныеСтроки.ВидРасчета) Тогда
		
		Для СчПоказателей = 1 По 6 Цикл
			ОформлениеСтроки.Ячейки["НаименованиеПоказатель" + СчПоказателей].Видимость		= Ложь;
			ОформлениеСтроки.Ячейки["Показатель" + СчПоказателей].Видимость					= Ложь;
			ОформлениеСтроки.Ячейки["Валюта" + СчПоказателей].Видимость						= Ложь;
			ОформлениеСтроки.Ячейки["НаименованиеПоказательТР" + СчПоказателей].Видимость	= Ложь;
			ОформлениеСтроки.Ячейки["ТарифныйРазряд" +СчПоказателей].Видимость				= Ложь;
		КонецЦикла;

	ИначеЕсли ДанныеСтроки.Подразделение = НеОпределено Тогда
		Если мВведенныеДолжности.Найти(ДанныеСтроки.Должность) = Неопределено Или мВведенныеВР.Найти(ДанныеСтроки.ВидРасчета) = Неопределено Тогда
			РаботаСДиалогами.ПриВыводеСтрокиПлановыхНачисленийИУдержаний(Элемент, ОформлениеСтроки, ДанныеСтроки, мСведенияОВидахРасчета);
			ОформлениеСтроки.ЦветТекста = WebЦвета.ТемноОливковоЗеленый;
		Иначе
			ОформлениеСтроки.ЦветТекста = WebЦвета.ЦветМорскойВолныТемный;
			ОформлениеСтроки.Ячейки.Показатели.УстановитьТекст(НСтр("ru='<данная строка не учитывается при расчете>';uk='<цей рядок не враховується при розрахунку>'"));
			
			Для СчПоказателей = 1 По 6 Цикл
				ОформлениеСтроки.Ячейки["НаименованиеПоказатель" + СчПоказателей].Видимость		= Ложь;
				ОформлениеСтроки.Ячейки["Показатель" + СчПоказателей].Видимость					= Ложь;
				ОформлениеСтроки.Ячейки["Валюта" + СчПоказателей].Видимость						= Ложь;
				ОформлениеСтроки.Ячейки["НаименованиеПоказательТР" + СчПоказателей].Видимость	= Ложь;
				ОформлениеСтроки.Ячейки["ТарифныйРазряд" +СчПоказателей].Видимость				= Ложь;
			КонецЦикла;
		КонецЕсли;
	Иначе
		РаботаСДиалогами.ПриВыводеСтрокиПлановыхНачисленийИУдержаний(Элемент, ОформлениеСтроки, ДанныеСтроки, мСведенияОВидахРасчета);
		
	КонецЕсли;
	
КонецПроцедуры //СхемыМотивацииПодразделенияПриВыводеСтроки

// Процедура - обработчик события "ПередОкончаниемРедактирования" табличного поля
//
Процедура СхемыМотивацииПодразделенияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ВидРасчета) Тогда
		Отказ = Истина;
		ОбработкаКомментариев.УдалитьСообщения();
		ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='В редактируемой строке не указан вид расчета!';uk='У рядку, що редагується, не зазначений вид розрахунку!'"), Перечисления.ВидыСообщений.Ошибка);
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли;
	
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока;
	Иначе
		Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока;
	КонецЕсли;
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("Подразделение",Подразделение);
	Запрос.УстановитьПараметр("Должность",ТекущиеДанные.Должность);
	Запрос.УстановитьПараметр("ВидРасчета",ТекущиеДанные.ВидРасчета);
	Запрос.УстановитьПараметр("Организация",ТекущиеДанные.Организация);
	Запрос.УстановитьПараметр("ВидСхемыМотивации",ТекущиеДанные.ВидСхемыМотивации);
	
	Запрос.Текст= "ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	ВЫБОР
	              |		КОГДА СхемыМотивацииРаботников.Подразделение = НЕОПРЕДЕЛЕНО
	              |			ТОГДА 1
	              |		КОГДА СхемыМотивацииРаботников.Подразделение = &Подразделение
	              |			ТОГДА 2
	              |	КОНЕЦ КАК КодОшибки,
	              |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СхемыМотивацииРаботников.ВидРасчета) КАК Количество
	              |ИЗ
	              |	РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
	              |ГДЕ
	              |	(СхемыМотивацииРаботников.Подразделение = НЕОПРЕДЕЛЕНО
	              |			ИЛИ СхемыМотивацииРаботников.Подразделение = &Подразделение)
	              |	И СхемыМотивацииРаботников.Должность = &Должность
	              |	И СхемыМотивацииРаботников.ВидРасчета = &ВидРасчета
	              |	И СхемыМотивацииРаботников.Организация = &Организация
	              |	И СхемыМотивацииРаботников.ВидСхемыМотивации = &ВидСхемыМотивации
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВЫБОР
	              |		КОГДА СхемыМотивацииРаботников.Подразделение = НЕОПРЕДЕЛЕНО
	              |			ТОГДА 1
	              |		КОГДА СхемыМотивацииРаботников.Подразделение = &Подразделение
	              |			ТОГДА 2
	              |	КОНЕЦ";

	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ДопустимоеПревышение = ?(НоваяСтрока, 0, 1);
		ОбработкаКомментариев.УдалитьСообщения();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.КодОшибки = 1 И Выборка.Количество > ДопустимоеПревышение Тогда
				//Отказ = Истина;
				//ОбработкаКомментариев.ДобавитьСообщение("Для данной должности уже введена запись.", Перечисления.ВидыСообщений.Ошибка);
			ИначеЕсли Выборка.Количество > ДопустимоеПревышение Тогда
				ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='По данной должности, подразделению и виду расчета уже введена запись.';uk='По даній посаді, підрозділу і виду розрахунку вже введений запис.'"), Перечисления.ВидыСообщений.Ошибка);
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
		Если Отказ Тогда
			ОбработкаКомментариев.ПоказатьСообщения();
		КонецЕсли;
	КонецЕсли;
	
	// впишем, если надо должность в отбор
	Если СхемыМотивацииПодразделения.Отбор.Должность.Значение.НайтиПоЗначению(Элемент.ТекущиеДанные.Должность) = НеОпределено Тогда
		СхемыМотивацииПодразделения.Отбор.Должность.Значение.Добавить(Элемент.ТекущиеДанные.Должность);
		СхемыМотивацииПодразделения.Обновить();
	КонецЕсли;
	
КонецПроцедуры //СхемыМотивацииПодразделенияПередОкончаниемРедактирования

// Процедура - обработчик события "ПередНачаломДобавления" табличного поля
//
Процедура СхемыМотивацииПодразделенияПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока;
	Иначе
		Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		ОбработкаКомментариев.УдалитьСообщения();
		Отказ = Истина;
		ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Новая строка не может быть добавлена, так как не выполнено позиционирование на подразделении!';uk='Новий рядок не може бути доданий, так як не виконано позиціонування на підрозділі!'"),Перечисления.ВидыСообщений.Ошибка);
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;

КонецПроцедуры //СхемыМотивацииПодразделенияПередНачаломДобавления


// Процедура - обработчик события "ПриНачалеРедактирования" табличного поля
//
Процедура СхемыМотивацииПодразделенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ДанныеСтроки = Элемент.ТекущиеДанные;
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока;
		Иначе
			Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока;
		КонецЕсли;
		
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			ДанныеСтроки.ВидРасчета			= ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка();
			ДанныеСтроки.Подразделение 		= Подразделение;
			ДанныеСтроки.Организация		= Организация;
		Иначе
			ДанныеСтроки.ВидРасчета			= ПланыВидовРасчета.УправленческиеНачисления.ПустаяСсылка();
			ДанныеСтроки.Подразделение 		= Подразделение;
		КонецЕсли;

	КонецЕсли;
	

КонецПроцедуры //СхемыМотивацииПодразделенияПриНачалеРедактирования

// Процедура - обработчик события "ПриИзменении" реквизита "ВидРасчета" табличного поля
//
Процедура СхемыМотивацииПодразделенияВидРасчетаПриИзменении(Элемент)
	
	ПриИзмененииВидаРасчета(Элемент.Значение, "СхемыМотивацииПодразделения");
	
КонецПроцедуры //СхемыМотивацииПодразделенияВидРасчетаПриИзменении

// Процедура - обработчик события "НачалоВыбораИзСписка" реквизита "Должность" табличного поля
//
Процедура СхемыМотивацииПодразделенияДолжностьНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВыбора = Элемент.СписокВыбора;
	ЭлементСписка = ВыбратьИзСписка(СписокВыбора,Элемент,СписокВыбора.НайтиПоЗначению(Элемент.Значение));
	Если ЭлементСписка <> Неопределено Тогда
		Элемент.Значение = ЭлементСписка.Значение;
	КонецЕсли;

КонецПроцедуры //СхемыМотивацииПодразделенияДолжностьНачалоВыбораИзСписка

Процедура СхемыМотивацииПодразделенияПередУдалением(Элемент, Отказ)
	
	Если Не ЗначениеЗаполнено(Элемент.ТекущаяСтрока.Подразделение) Тогда
		Отказ = Истина;
		ОбработкаКомментариев.УдалитьСообщения();
		ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Нельзя удалять начисления, введенные для должности в табличной части по предприятию. Для удаления данного начисление, установите курсор на табличной части ""Должности"". ';uk='Не можна вилучати нарахування, введені для посади в табличній частині по підприємству. Для вилучення даного нарахування, встановіть курсор на табличної частини ""Посади"". '"),Перечисления.ВидыСообщений.Ошибка);
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ "ТабличноеПолеПодразделения" ФОРМЫ

// Процедура - обработчик события "ПриАктивизацииСтроки" табличного поля
//
Процедура ТабличноеПолеПодразделенияПриАктивизацииСтроки()
	
	Организация = ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока.Владелец;
	Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока;
	УстановитьОтборСхемыМотивацииПоДолжности(Подразделение);
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ТекстКнопиПечать = НСтр("ru='Печать мотивации ';uk='Друк мотивації '") + Подразделение;
		ЭлементыФормы.КоманднаяПанельПредприятие.Кнопки.Действие.Кнопки.Печать.Текст = ТекстКнопиПечать;	
		ЭлементыФормы.НадписьПредприятие.Заголовок = НСтр("ru=' Предприятие (';uk=' Підприємство ('") + Подразделение + ")";
	Иначе
		ТекстКнопиПечать = "Печать мотивации";		
		ЭлементыФормы.НадписьПредприятие.Заголовок = НСтр("ru=' Предприятие';uk=' Підприємство'");
	КонецЕсли;
	
	
	мПоказватьВалюту = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ИспользуютсяНачисленияВВалюте");
	УстанвоитьВидимостьВалюты();
	Если мПоказватьВалюту Тогда
		мВалютаПоУмолчанию = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;
	
КонецПроцедуры //ТабличноеПолеПодразделенияПриАктивизацииСтроки

// Процедура - обработчик события "ПриПолученииДанных" табличного поля
//
Процедура ТабличноеПолеПодразделенияОрганизацийПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ОтображенныеОрганизации.Очистить();
	
	Для Каждого Оформление Из ОформленияСтрок Цикл
		Если Оформление.ДанныеСтроки = НеОпределено Тогда
			Оформление.Ячейки.Наименование.УстановитьТекст(НСтр("ru='Все организации';uk='Усі організації'"));
			Оформление.Ячейки.Наименование.ОтображатьКартинку	= Ложь;
			Оформление.Ячейки.Наименование.Шрифт				= мЖирныйШрифт;
			Оформление.Ячейки.Организация.Видимость				= Ложь;
			Продолжить;
		КонецЕсли;
		
		ПерваяСтрока		= Истина;
		Представление		= "";
		НазваниеОрганизации	= "";
		
		Если ПервоеПодразделение(Оформление.ДанныеСтроки.Ссылка, Оформление.ДанныеСтроки.Владелец, НазваниеОрганизации) Тогда
			Оформление.Ячейки.Организация.УстановитьТекст("      " + НазваниеОрганизации);
			Оформление.Ячейки.Организация.Шрифт = мЖирныйШрифт;
		Иначе
			Оформление.Ячейки.Организация.Видимость = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры //ТабличноеПолеПодразделенияОрганизацийПриПолученииДанных

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ "ТабличноеПолеПодразделенияПоЦФО" ФОРМЫ

// Процедура - обработчик события "ПриАктивизацииСтроки" табличного поля
//
Процедура ТабличноеПолеПодразделенияПоЦФОПриАктивизацииСтроки(Элемент)
	
	Подразделение = Элемент.ТекущаяСтрока;
	УстановитьОтборСхемыМотивацииПоДолжности(Подразделение);
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ТекстКнопиПечать = НСтр("ru='Печать мотивации ';uk='Друк мотивації '") + Подразделение;
		ЭлементыФормы.КоманднаяПанельПредприятие.Кнопки.Действие.Кнопки.Печать.Текст = ТекстКнопиПечать;	
		ЭлементыФормы.НадписьПредприятие.Заголовок = НСтр("ru=' Предприятие (';uk=' Підприємство ('") + Подразделение + ")";
	Иначе
		ТекстКнопиПечать = "Печать мотивации";
		ЭлементыФормы.НадписьПредприятие.Заголовок = НСтр("ru=' Предприятие';uk=' Підприємство'");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ "ТабличноеПолеПоДолжности" ФОРМЫ

// Процедура - обработчик события "ВыводеСтроки" табличного поля
//
Процедура ТабличноеПолеПоДолжностиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
    Если ДанныеСтроки.ВидРасчета = Неопределено 
    	Или ДанныеСтроки.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка() Тогда
    	
    	Для СчПоказателей = 1 По 6 Цикл
    		ОформлениеСтроки.Ячейки["НаименованиеПоказатель" + СчПоказателей].Видимость		= Ложь;
    		ОформлениеСтроки.Ячейки["Показатель" + СчПоказателей].Видимость					= Ложь;
    		ОформлениеСтроки.Ячейки["Валюта" + СчПоказателей].Видимость						= Ложь;
    		ОформлениеСтроки.Ячейки["НаименованиеПоказательТР" + СчПоказателей].Видимость	= Ложь;
    		ОформлениеСтроки.Ячейки["ТарифныйРазряд" +СчПоказателей].Видимость				= Ложь;
    	КонецЦикла;

	Иначе
    
    	РаботаСДиалогами.ПриВыводеСтрокиПлановыхНачисленийИУдержаний(Элемент, ОформлениеСтроки, ДанныеСтроки, мСведенияОВидахРасчета);
    КонецЕсли;
	
КонецПроцедуры //ТабличноеПолеПоДолжностиПриВыводеСтроки

// Процедура - обработчик события "ПриИзменении" реквизита "ВидРасчета" табличного поля
//
Процедура ТабличноеПолеПоДолжностиВидРасчетаПриИзменении(Элемент)
	
	ПриИзмененииВидаРасчета(Элемент.Значение, "ТабличноеПолеПоДолжности");
	
КонецПроцедуры

// Процедура - обработчик события "ПриОкончанииРедактирования" табличного поля
//
Процедура ТабличноеПолеПоДолжностиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	мРегВидыРасчетов = Неопределено;
	ТабличноеПолеДолжностиПриАктивизацииСтроки();

	СхемыМотивацииПодразделения.Обновить();
	
КонецПроцедуры //ТабличноеПолеПоДолжностиПриОкончанииРедактирования

// Процедура - обработчик события "ПриНачалеРедактирования" табличного поля
//
Процедура ТабличноеПолеПоДолжностиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ДанныеСтроки = Элемент.ТекущиеДанные;
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			ДанныеСтроки.ВидРасчета			= ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка();
			ДанныеСтроки.Организация		= Организация;
		Иначе
			ДанныеСтроки.ВидРасчета			= ПланыВидовРасчета.УправленческиеНачисления.ПустаяСсылка();
		КонецЕсли;
		ДанныеСтроки.Должность			= ЭлементыФормы.ТабличноеПолеДолжности.ТекущаяСтрока;
		
	КонецЕсли;	
	
КонецПроцедуры //ТабличноеПолеПоДолжностиПриНачалеРедактирования

// Процедура - обработчик события "ПередОкончаниемРедактирования" табличного поля
//
Процедура ТабличноеПолеПоДолжностиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидРасчета) Тогда
		ОбработкаКомментариев.УдалитьСообщения();
		Отказ = Истина;
		ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='В редактируемой строке не указан вид расчета!';uk='У рядку, що редагується, не зазначений вид розрахунку!'"),Перечисления.ВидыСообщений.Ошибка);
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Должность",ТекущиеДанные.Должность);
	Запрос.УстановитьПараметр("ВидРасчета",ТекущиеДанные.ВидРасчета);
	Запрос.УстановитьПараметр("Организация",ТекущиеДанные.Организация);
	Запрос.УстановитьПараметр("ВидСхемыМотивации",ТекущиеДанные.ВидСхемыМотивации);
	
	Запрос.Текст= "ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |	ВЫБОР
	              |		КОГДА СхемыМотивацииРаботников.Подразделение = НЕОПРЕДЕЛЕНО
	              |			ТОГДА 1
	              |	КОНЕЦ КАК КодОшибки,
	              |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СхемыМотивацииРаботников.ВидРасчета) КАК Количество
	              |ИЗ
	              |	РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
	              |ГДЕ
	              |	СхемыМотивацииРаботников.Подразделение = НЕОПРЕДЕЛЕНО
	              |	И СхемыМотивацииРаботников.Должность = &Должность
	              |	И СхемыМотивацииРаботников.ВидРасчета = &ВидРасчета
	              |	И СхемыМотивацииРаботников.Организация = &Организация
	              |	И СхемыМотивацииРаботников.ВидСхемыМотивации = &ВидСхемыМотивации
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВЫБОР
	              |		КОГДА СхемыМотивацииРаботников.Подразделение = НЕОПРЕДЕЛЕНО
	              |			ТОГДА 1
	              |	КОНЕЦ";

	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		ДопустимоеПревышение = ?(НоваяСтрока, 0, 1);
		ОбработкаКомментариев.УдалитьСообщения();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Количество > ДопустимоеПревышение Тогда
				ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Для данной должности уже введена запись.';uk='Для даної посади вже введений запис.'"), Перечисления.ВидыСообщений.Ошибка);
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
		Если Отказ Тогда
			ОбработкаКомментариев.ПоказатьСообщения();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //ТабличноеПолеПоДолжностиПередОкончаниемРедактирования

Процедура СхемыМотивацииПодразделенияПередНачаломИзменения(Элемент, Отказ)
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ДанныеСтроки.Подразделение) Тогда
		Предупреждение(НСтр("ru='Для редактирования строки схемы мотивации должности ""';uk='Для редагування рядка схеми мотивації посади ""'") + ДанныеСтроки.Должность+НСтр("ru='"" перейдите в раздел Должности данной формы';uk='"" перейдіть в розділ Посади даної форми'"));
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ "ТабличноеПолеДолжности" ФОРМЫ

// Процедура - обработчик события "ПриАктивизацииСтроки" табличного поля
//
Процедура ТабличноеПолеДолжностиПриАктивизацииСтроки()
	
	Должность		= ЭлементыФормы.ТабличноеПолеДолжности.ТекущаяСтрока;
		
	Если ЗначениеЗаполнено(Должность) Тогда
		ТекстКнопиПечать = НСтр("ru='Печать мотивации ""';uk='Друк мотивації ""'") + Должность + """";
		ЭлементыФормы.КоманднаяПанельСМПоДолжности.Кнопки.ПодменюДолжности.Кнопки.Печать.Текст = ТекстКнопиПечать;
		ЭлементыФормы.НадписьДолжности.Заголовок = НСтр("ru=' Должности (';uk=' Посади ('") + Должность + ")";
	Иначе
		ТекстКнопиПечать = "Печать мотивации";
	КонецЕсли;
	
	ТабличноеПолеПоДолжности.Отбор.Должность.Использование	= Истина;
	ТабличноеПолеПоДолжности.Отбор.Должность.ВидСравнения	= ВидСравнения.Равно;
	Если Должность = Неопределено Тогда
		ТабличноеПолеПоДолжности.Отбор.Должность.Значение	= Справочники.ДолжностиОрганизаций.ПустаяСсылка();
	Иначе
		ТабличноеПолеПоДолжности.Отбор.Должность.Значение	= Должность;
	КонецЕсли;
	
	ТабличноеПолеПоДолжности.Отбор.Подразделение.Использование	= Истина;
	ТабличноеПолеПоДолжности.Отбор.Подразделение.ВидСравнения	= ВидСравнения.Равно;
	ТабличноеПолеПоДолжности.Отбор.Подразделение.Значение = НеОпределено;
	
	УстановитьОтборПоВидуСМ();
	
	ОтборПоВидуРасчета();
	ТабличноеПолеПоДолжности.Обновить();
	
КонецПроцедуры //ТабличноеПолеДолжностиПриАктивизацииСтроки

// Процедура - обработчик события "Выбор(" табличного поля
//
Процедура ТабличноеПолеДолжностиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры //ТабличноеПолеДолжностиВыбор


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНДНОЙ ПАНЕЛИ "КоманднаяПанельДолжности" ФОРМЫ

// Открывает штатное расписание
//
Процедура ОткрытьШтатноеРасписание(Кнопка)
	
	Если мШтатноеРасписание = Неопределено или Не мШтатноеРасписание.Открыта() Тогда
		
		мШтатноеРасписание = РегистрыСведений.ШтатноеРасписаниеОрганизаций.ПолучитьФорму("ФормаСписка");		
		Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			мШтатноеРасписание.ЭлементыФормы.Организация.Значение = Организация;
			мШтатноеРасписание.ЭлементыФормы.Подразделения.ТекущаяСтрока = ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока;
		КонецЕсли;
		мШтатноеРасписание.ПоложениеПрикрепленногоОкна = ВариантПрикрепленияОкна.Право;
		мШтатноеРасписание.Открыть();			
	Иначе		
		мШтатноеРасписание.Активизировать();		
	КонецЕсли;
	
КонецПроцедуры //ОткрытьШтатноеРасписание

// Сворачивает панель "ПанельСхемыМотивацийПоДолжностям" 
//
Процедура НадписьУправлениеВидимостьюДолжностейНажатие()
	
	ПоказыватьДолжности = Не ПоказыватьДолжности;
	
	ЗаголовокДолжностей();
		
	РаботаСДиалогами.ИзменитьВидимостьПанелиПоВертикали(ЭтаФорма, ПоказыватьДолжности, "РабочаяОбласть", "СхемыМотивацийПоДолжностям");
	
	ПодготовитьСправкуФормы();
	
КонецПроцедуры //НадписьУправлениеВидимостьюДолжностейНажатие

// Печатает все схемы мотивации
//
Процедура ДействияФормыПечатьДляВсейКомпании(Кнопка)
	
	СформироватьЗапросДляПечати("ПоВсем");

КонецПроцедуры

// Печатает схемы мотивации по текущему подразделению
//
Процедура ДействияФормыПоПодразделению(Кнопка)
	
	СформироватьЗапросДляПечати("По подразделению");
	
КонецПроцедуры

// Печатает схемы мотивации по текущей должности
//
Процедура ДействияФормыПоДолжности(Кнопка)
	
	СформироватьЗапросДляПечати("По должности");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНДНОЙ ПАНЕЛИ "КоманднаяПанельРежимВвода" ФОРМЫ

// Устанавливает порядок ввода схем мотиваций по ЦФО
//
Процедура КоманднаяПанельРежимВводаПоЦФО(Кнопка)
	
	ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности;
	ВидОрганизационнойСтруктурыПредприятияПриИзменении();
	ЭлементыФормы.ТабличноеПолеПодразделенияПоЦФО.ТекущаяСтрока = ПервоеПодразделениеОрганизации();
	
КонецПроцедуры // КоманднаяПанельРежимВводаПоЦФО

// Устанавливает порядок ввода схем мотиваций по Организации
//
Процедура КоманднаяПанельРежимВводаПоОрганизациям(Кнопка)

	ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	ВидОрганизационнойСтруктурыПредприятияПриИзменении();
	ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока = ПервоеПодразделениеОрганизации();
	
КонецПроцедуры //КоманднаяПанельРежимВводаПоОрганизациям

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНДНОЙ ПАНЕЛИ "ПанельПодразделения" ФОРМЫ

// Процедура - заполняет схемы мотивации по штатному расписанию
//
Процедура КоманднаяПанельПредприятиеЗаполнитьПоШтатномуРасписанию(Кнопка)
	
	Подразделение = ЭлементыФормы.ТабличноеПолеПодразделенияОрганизаций.ТекущаяСтрока;
	Запрос = Новый Запрос;
	Если ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		
		Если ЗначениеЗаполнено(Подразделение) Тогда
			ТекстЗапроса = 	
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ЕСТЬNULL(СхемыМотивации.Должность, ШтатноеРасписаниеОрганизаций.Должность) КАК РезультатДолжность
			|ИЗ
			|	РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			СхемыМотивацииРаботников.Должность КАК Должность
			|		ИЗ
			|			РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
			|		ГДЕ
			|			СхемыМотивацииРаботников.Подразделение = &Подразделение
			|			И СхемыМотивацииРаботников.Организация = &Организация) КАК СхемыМотивации
			|		ПО СхемыМотивации.Должность = ШтатноеРасписаниеОрганизаций.Должность
			|ГДЕ
			|	СхемыМотивации.Должность ЕСТЬ NULL И ШтатноеРасписаниеОрганизаций.Должность ЕСТЬ НЕ NULL";
		Иначе
			ТекстЗапроса = 	
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
			|	ШтатноеРасписаниеОрганизаций.Должность КАК РезультатДолжность
			|ИЗ
			|	РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			СхемыМотивацииРаботников.Должность КАК Должность,
			|			СхемыМотивацииРаботников.Подразделение КАК Подразделение
			|		ИЗ
			|			РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
			|		ГДЕ
			|			СхемыМотивацииРаботников.Организация = &Организация) КАК СхемыМотивации
			|		ПО СхемыМотивации.Должность = ШтатноеРасписаниеОрганизаций.Должность
			|			И ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации = СхемыМотивации.Подразделение
			|ГДЕ
			|	СхемыМотивации.Должность ЕСТЬ NULL 
			|	И ШтатноеРасписаниеОрганизаций.Должность ЕСТЬ НЕ NULL ";
			
		КонецЕсли;

				
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		Запрос.УстановитьПараметр("Организация", Организация);		
		ПВР = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка();
		
		Запрос.Текст = ТекстЗапроса;
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			Пока Выборка.Следующий() Цикл
				МенеджерЗаписиСМ = РегистрыСведений.СхемыМотивацииРаботников.СоздатьМенеджерЗаписи();
				МенеджерЗаписиСМ.Организация	= Организация;
				Если ЗначениеЗаполнено(Подразделение) Тогда
					МенеджерЗаписиСМ.Подразделение 	= Подразделение;
				Иначе
					МенеджерЗаписиСМ.Подразделение 	= Выборка.ПодразделениеОрганизации;
				КонецЕсли;
				МенеджерЗаписиСМ.Должность		= Выборка.РезультатДолжность;
				МенеджерЗаписиСМ.ВидРасчета		= ПВР;
				МенеджерЗаписиСМ.Записать();
			КонецЦикла;
			
		КонецЕсли;
		
		ОтборПоВидуРасчета();
		ТабличноеПолеПоДолжности.Обновить();
			
	КонецЕсли;
		
КонецПроцедуры // КоманднаяПанельПредприятиеЗаполнитьПоШтатномуРасписанию



////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСведенияОВидахРасчета = Новый Соответствие;

мЖирныйШрифт = Новый Шрифт(ШрифтыСтиля.ШрифтТекста,,, Истина);
ОтображенныеОрганизации = Новый Соответствие;
