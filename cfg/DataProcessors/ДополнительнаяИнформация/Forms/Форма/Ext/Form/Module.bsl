Перем ЭтаФормаЗаголовок, ПоказФормы;

// Показ страницы в текущей форме или в форме в режиме рабочего стола
// 
Процедура ПросмотрСтраницы(Идентификатор)
	Отбор = Новый Структура();
	Отбор.Вставить("Идентификатор",Идентификатор);
	СтрокаТаблицаСтраниц = ТаблицаСтраниц.НайтиСтроки(Отбор);
	Если СтрокаТаблицаСтраниц.Количество()>0 Тогда
		ИмяФайлаСтартовойСтраницы = СтрокаТаблицаСтраниц[0].ИмяФайлаСтартовойСтраницы;
		ИзвлечьСтраницу(КаталогДопИнфо, СтрокаТаблицаСтраниц[0].ИмяМакета);
		Если ИмяФайлаСтартовойСтраницы<>"" Тогда
			ЭлементыФормы.ПолеHTMLДокумента1.Перейти(КаталогДопИнфо + "\" + ИмяФайлаСтартовойСтраницы);
			Заголовок = ЭтаФормаЗаголовок + ?(СтрокаТаблицаСтраниц[0].Раздел=НСтр("ru=' Главная';uk=' Головна'"),"",": " + СтрокаТаблицаСтраниц[0].Раздел +" / "+СтрокаТаблицаСтраниц[0].НаименованиеСтартовойСтраницы);
		Иначе
			Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Данные страницы %1 не найдены';uk='Дані сторінки %1 не знайдені'"), ИмяФайлаСтартовойСтраницы));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Вызов выбранной из командной панели страницы
// 
Процедура ВызовСтраницы(Кнопка)
	ПросмотрСтраницы(Кнопка.Имя);
КонецПроцедуры

Процедура ПриОткрытии()
	
	// страницы есть
	МассивПоказываемыхИнформационныхСтраниц = Новый Массив;
	МассивИнформационныхСтраниц = Новый Массив;
	МассивРекламныхСтраниц = Новый Массив;
	// обработать таблицу страниц и сформировать командную панель
	ТекРаздел = ""; Индекс = 0;
	ОбработкаНажатия = Новый Действие("ВызовСтраницы");
	Для Каждого СтрокаТаблицы из ТаблицаСтраниц Цикл
		Если СтрокаТаблицы.Раздел="Реклама" Тогда
			МассивРекламныхСтраниц.Добавить(СтрокаТаблицы.Идентификатор);
		ИначеЕсли СтрокаТаблицы.ВключатьВПервыйПоказ = 1 Тогда
			МассивПоказываемыхИнформационныхСтраниц.Добавить(СтрокаТаблицы.Идентификатор);
		ИначеЕсли СтрокаТаблицы.ВключатьВПервыйПоказ = 2 Тогда
			МассивИнформационныхСтраниц.Добавить(СтрокаТаблицы.Идентификатор);
		КонецЕсли;
		Если СтрокаТаблицы.Раздел=НСтр("ru=' Главная';uk=' Головна'") Тогда
			Кнопка = ЭлементыФормы.КоманднаяПанель1.Кнопки.Добавить(СтрокаТаблицы.Идентификатор,ТипКнопкиКоманднойПанели.Действие,СтрокаТаблицы.Раздел,ОбработкаНажатия);
			Кнопка = ЭлементыФормы.КоманднаяПанель1.Кнопки.Добавить("",ТипКнопкиКоманднойПанели.Разделитель,,);
			ТекРаздел = СтрокаТаблицы.Раздел;
			Индекс = Индекс + 2;
		Иначе
			Если ТекРаздел<>СтрокаТаблицы.Раздел Тогда
				ЭлементыФормы.КоманднаяПанель1.Кнопки.Вставить(Индекс+3);
				Кнопка = ЭлементыФормы.КоманднаяПанель1.Кнопки.Вставить(Индекс+3,,ТипКнопкиКоманднойПанели.Подменю,СтрокаТаблицы.Раздел);
				ТекРаздел = СтрокаТаблицы.Раздел;
				Индекс = Индекс + 2;
			КонецЕсли;
			Кнопка.Кнопки.Добавить(СтрокаТаблицы.Идентификатор,ТипКнопкиКоманднойПанели.Действие,СтрокаТаблицы.НаименованиеСтартовойСтраницы,ОбработкаНажатия);
		КонецЕсли;
	КонецЦикла;
	
	СоздатьКаталог(КаталогДопИнфо);
	УдалитьФайлы(КаталогДопИнфо,"*.*");
	ЭтаФормаЗаголовок = Заголовок;
	Если МассивРекламныхСтраниц.Количество()
			+ МассивИнформационныхСтраниц.Количество() 
			+ МассивПоказываемыхИнформационныхСтраниц.Количество()>0 Тогда
		ПросмотрСтраницы(ВыборСтраницы(?(МассивРекламныхСтраниц.Количество()>0, МассивРекламныхСтраниц, ?(МассивПоказываемыхИнформационныхСтраниц.Количество()>0,МассивПоказываемыхИнформационныхСтраниц,МассивИнформационныхСтраниц))));
	Иначе
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если Обработки.ДополнительнаяИнформация.ПолучитьФорму("ФормаРабочийСтол").Открыта()
			ИЛИ КонфигурацияБазовая() Тогда
		ЭлементыФормы.ПоказыватьПриНачалеРаботы.Видимость = Ложь;
	Иначе
		ТМП = ВосстановитьЗначение("ПоказСтартовойФормыДополнительнойИнформации");
		ПоказыватьПриНачалеРаботы = ?(ТипЗнч(ТМП)=Тип("Неопределено"), Истина, ТМП);
		Если Метаданные.Константы.Найти("НомерВерсииКонфигурации") <> Неопределено И ПоказыватьПриНачалеРаботы <> Истина Тогда
			ДатаБлижайшегоПоказа  = ВосстановитьЗначение("ДатаБлижайшегоПоказаДополнительнойИнформации");
			Если ДатаБлижайшегоПоказа = Неопределено Или ДатаБлижайшегоПоказа <= ТекущаяДатаСеанса() Тогда
				ПоказыватьПриНачалеРаботы = Истина;
			Иначе
				ТекущаяВерсияМетаданных = Метаданные.Версия;
				СохраненнаяВерсияМетаданных = ВосстановитьЗначение("ВерсияМетаданных");
				КонфигурацияОбновлена = СохраненнаяВерсияМетаданных = Неопределено Или СохраненнаяВерсияМетаданных <> ТекущаяВерсияМетаданных;
				ПоказыватьПриНачалеРаботы = КонфигурацияОбновлена ИЛИ НЕ ПустаяСтрока(ТекущаяВерсияМетаданных) И ТекущаяВерсияМетаданных  <> Константы.НомерВерсииКонфигурации.Получить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗакрытии()
	УдалитьФайлы(КаталогДопИнфо,"*.*");
КонецПроцедуры

Процедура ПолеHTMLДокумента1onclick(Элемент, pEvtObj)
	Попытка
		Если Найти(pEvtObj.srcElement.href,"file:///")>0 Тогда
			ИмяФайла = СтрЗаменить(pEvtObj.srcElement.href,"file:///","");
			Файл = Новый Файл(СтрЗаменить(СтрЗаменить(ИмяФайла,"%20"," "),"/","\"));
			Если Файл.Существует() Тогда
				Отбор = Новый Структура();
				Отбор.Вставить("ИмяФайлаСтартовойСтраницы",Файл.Имя);
				СтрокаТаблицаСтраниц = ТаблицаСтраниц.НайтиСтроки(Отбор);
				Если СтрокаТаблицаСтраниц.Количество()>0 Тогда
					ИмяФайлаСтартовойСтраницы = СтрокаТаблицаСтраниц[0].ИмяФайлаСтартовойСтраницы;
					Если ИмяФайлаСтартовойСтраницы<>"" Тогда
						Заголовок = ЭтаФормаЗаголовок + ?(СтрокаТаблицаСтраниц[0].Раздел=НСтр("ru=' Главная';uk=' Головна'"),"",": " + СтрокаТаблицаСтраниц[0].Раздел +" / "+СтрокаТаблицаСтраниц[0].НаименованиеСтартовойСтраницы);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
КонецПроцедуры
               
Процедура ПоказыватьПриНачалеРаботыПриИзменении(Элемент)
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	Если День(ТекущаяДатаСеанса) < 15 Тогда
		ДатаБлижайшегоПоказа = Дата(Год(ТекущаяДатаСеанса), Месяц(ТекущаяДатаСеанса), 15, 0, 0, 0);
	Иначе
		ДатаБлижайшегоПоказа = КонецМесяца(ТекущаяДатаСеанса) + 1;
	КонецЕсли;
	
	СохранитьЗначение("ПоказСтартовойФормыДополнительнойИнформации",ПоказыватьПриНачалеРаботы);
	СохранитьЗначение("ДатаБлижайшегоПоказаДополнительнойИнформации", ДатаБлижайшегоПоказа);
	СохранитьЗначение("ВерсияМетаданных", Метаданные.Версия);
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	ОбработкаОписателя();
	Если ТаблицаСтраниц.Количество()=0 Тогда
		// нет показываемых страниц; обработку можно не открывать
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

