Перем ЭтаФормаЗаголовок;
Перем МассивРекламныхСтраниц;

// Показ страницы в текущей форме или в форме в режиме рабочего стола
// 
Процедура ПросмотрСтраницы(Идентификатор)
	Отбор = Новый Структура();
	Отбор.Вставить("Идентификатор",Идентификатор);
	СтрокаТаблицаСтраниц = ТаблицаСтраниц.НайтиСтроки(Отбор);
	Если СтрокаТаблицаСтраниц.Количество()>0 Тогда
		ИзвлечьСтраницу(КаталогДопИнфо + "\RS", СтрокаТаблицаСтраниц[0].ИмяМакета);
		ИмяФайлаСтартовойСтраницы = СтрокаТаблицаСтраниц[0].ИмяФайлаСтартовойСтраницы;
		Если ИмяФайлаСтартовойСтраницы<>"" Тогда
			ЭлементыФормы.ПолеHTMLДокумента1.Перейти(КаталогДопИнфо + "\RS\" + ИмяФайлаСтартовойСтраницы);
			Заголовок = ЭтаФормаЗаголовок +": " + СтрокаТаблицаСтраниц[0].Раздел +" / "+СтрокаТаблицаСтраниц[0].НаименованиеСтартовойСтраницы;
		Иначе
			Сообщить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Данные страницы %1 не найдены';uk='Дані сторінки %1 не знайдені'"), ИмяФайлаСтартовойСтраницы));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Осуществляет случайный выбор страницы из массива рекламных страниц
// 
Процедура ПоказПервойСтраницы(МассивСтраниц)
	Если МассивСтраниц.Количество()>1 Тогда
		ТМП = Час(ТекущаяДата())*60*60+Минута(ТекущаяДата())*60+Секунда(ТекущаяДата());
		Индекс = ТМП - Цел(ТМП/МассивСтраниц.Количество())*МассивСтраниц.Количество();
	Иначе
		Индекс = 0;
	КонецЕсли;
	ПросмотрСтраницы(МассивСтраниц[Индекс]);
КонецПроцедуры

// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	Если КонфигурацияБазовая() Тогда
		ЭлементыФормы.ПоказФормы.Видимость = 0;
	КонецЕсли;
	
	Если МассивРекламныхСтраниц.Количество() > 0 Тогда
		ПоказПервойСтраницы(МассивРекламныхСтраниц);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии()
	
	УдалитьФайлы(КаталогДопИнфо + "\RS", "*.*");
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	Если День(ТекущаяДатаСеанса) < 15 Тогда
		ДатаБлижайшегоПоказа = Дата(Год(ТекущаяДатаСеанса), Месяц(ТекущаяДатаСеанса), 15, 0, 0, 0);
	Иначе
		ДатаБлижайшегоПоказа = КонецМесяца(ТекущаяДатаСеанса) + 1;
	КонецЕсли;
	
	СохранитьЗначение("ПоказСтартовойФормыДополнительнойИнформации", ПоказФормы);
	СохранитьЗначение("ДатаБлижайшегоПоказаДополнительнойИнформации", ДатаБлижайшегоПоказа);
	СохранитьЗначение("ВерсияМетаданных", Метаданные.Версия);
	
КонецПроцедуры // ПриЗакрытии()

// Обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ТМП = ВосстановитьЗначение("ПоказСтартовойФормыДополнительнойИнформации");
	ПоказФормы = ?(ТипЗнч(ТМП) = Тип("Неопределено"), Истина, ТМП);
	
	Если Метаданные.Константы.Найти("НомерВерсииКонфигурации") <> Неопределено И ПоказФормы <> Истина Тогда
		ДатаБлижайшегоПоказа = ВосстановитьЗначение("ДатаБлижайшегоПоказаДополнительнойИнформации");
		Если ДатаБлижайшегоПоказа = Неопределено Или ДатаБлижайшегоПоказа <= ТекущаяДатаСеанса() Тогда
			ПоказФормы = Истина;
		Иначе
			ТекущаяВерсияМетаданных = Метаданные.Версия;
			СохраненнаяВерсияМетаданных = ВосстановитьЗначение("ВерсияМетаданных");
			КонфигурацияОбновлена = СохраненнаяВерсияМетаданных = Неопределено Или СохраненнаяВерсияМетаданных <> ТекущаяВерсияМетаданных;
			ПоказФормы = КонфигурацияОбновлена Или НЕ ПустаяСтрока(ТекущаяВерсияМетаданных) И ТекущаяВерсияМетаданных <> Константы.НомерВерсииКонфигурации.Получить();
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПоказФормы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбработкаОписателя();
	Если ТаблицаСтраниц.Количество() = 0 Тогда
		Отказ = Истина; // нет показываемых страниц; обработку можно не открывать
		Возврат;
	КонецЕсли;
	
	МассивИнформационныхСтраниц = Новый Массив;
	МассивРекламныхСтраниц      = Новый Массив;
	
	// обработать таблицу страниц и сформировать командную панель
	ОбработкаНажатия = Новый Действие("ВызовСтраницы");
	
	Для Каждого СтрокаТаблицы Из ТаблицаСтраниц Цикл
		
		Если СтрокаТаблицы.Раздел = "Реклама" И СтрокаТаблицы.ФормаПоказа = 1 Тогда
			МассивРекламныхСтраниц.Добавить(СтрокаТаблицы.Идентификатор);
		ИначеЕсли СтрокаТаблицы.ФормаПоказа = 1 Тогда
			МассивИнформационныхСтраниц.Добавить(СтрокаТаблицы.Идентификатор);
		КонецЕсли;
		
	КонецЦикла;
	
	СоздатьКаталог(КаталогДопИнфо + "\RS");
	УдалитьФайлы(КаталогДопИнфо + "\RS", "*.*");
	ЭтаФормаЗаголовок = Заголовок;
	
	Если МассивРекламныхСтраниц.Количество() = 0 Тогда
		Обработки.ДополнительнаяИнформация.ПолучитьФорму().Открыть();
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()
