
Процедура ПроцентПриИзменении(Элемент)
	
	Если Элемент.Имя = "Процент3" Тогда
		ИндексСледующийПроцент = 1;
	Иначе
		ИндексСледующийПроцент = Строка(Число(Прав(Элемент.Имя, 1)) + 1);
	КонецЕсли;
	ЭлементФормы = ЭлементыФормы["Процент" + ИндексСледующийПроцент];
	Сумма = Процент1 + Процент2 + Процент3;
	Превышение = Сумма - 100;
	ЭлементФормы.Значение = ЭлементФормы.Значение - Превышение;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если ПустаяСтрока(Показатель)
	 ИЛИ ПустаяСтрока(Поле)
	 ИЛИ ПустаяСтрока(Наименование) Тогда
	    УстановитФокусНаНезаполненный();
		Возврат;
	КонецЕсли;
	
	СозданоПользовательскоеПоле = Ложь;
	Если ПользовательскоеПоле = Неопределено Тогда
		ПользовательскоеПоле = КомпоновщикНастроек.Настройки.ПользовательскиеПоля.Элементы.Добавить(Тип("ПользовательскоеПолеВыборКомпоновкиДанных"));
		СозданоПользовательскоеПоле = Истина;
	Иначе
		ПользовательскоеПоле.Варианты.Элементы.Очистить();
	КонецЕсли;
	ПользовательскоеПоле.Заголовок = Наименование;
	Вариант = ПользовательскоеПоле.Варианты.Элементы.Добавить();
	Вариант.Значение = "ABCКлассификация КлассA (" + Процент1 + ")";
	Вариант.Представление = "A - Класс";
	// Добавим фиктивный отбор
	ЭлементОтбора = Вариант.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Поле);
	ЭлементОтбора.ПравоеЗначение = Новый ПолеКомпоновкиДанных(Поле);
	ЭлементОтбора = Вариант.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Показатель);
	ЭлементОтбора.ПравоеЗначение = Новый ПолеКомпоновкиДанных(Показатель);
	
	Вариант = ПользовательскоеПоле.Варианты.Элементы.Добавить();
	Вариант.Значение = "ABCКлассификация КлассB (" + Процент2 + ")";
	Вариант.Представление = "B - Класс";
	
	Вариант = ПользовательскоеПоле.Варианты.Элементы.Добавить();
	Вариант.Значение = "ABCКлассификация КлассC (" + Процент3 + ")";
	Вариант.Представление = "C - Класс";
	
	Закрыть(СозданоПользовательскоеПоле);
	
КонецПроцедуры

Процедура УстановитФокусНаНезаполненный()
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Наименование;
	КонецЕсли;
	Если Поле = Неопределено Тогда
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Поле;
	КонецЕсли;
	Если Показатель = Неопределено Тогда
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Показатель;
	КонецЕсли;
	Если Процент1 + Процент2 + Процент3 <> 100 Тогда
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Процент1;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ПустаяСтрока(Наименование) Тогда
		Наименование = "ABC - классификация";
	КонецЕсли;
	СписокРесурсов = ТиповыеОтчеты.ПолучитьСписокДоступныхРесурсов(КомпоновщикНастроек, Ложь, Ложь);
	Для каждого ЭлементСписка Из СписокРесурсов Цикл
		ЭлементСписка.Значение = Строка(ЭлементСписка.Значение);
	КонецЦикла;
	
	ЭлементыФормы.Показатель.СписокВыбора = СписокРесурсов;
	
	Если ПользовательскоеПоле <> Неопределено Тогда
		
		Наименование = ПользовательскоеПоле.Заголовок;
		Поле = Строка(ПользовательскоеПоле.Варианты.Элементы[0].Отбор.Элементы[0].ЛевоеЗначение);
		Показатель= Строка(ПользовательскоеПоле.Варианты.Элементы[0].Отбор.Элементы[1].ЛевоеЗначение);
		Значение = ПользовательскоеПоле.Варианты.Элементы[0].Значение;
		Значение = Сред(Значение, Найти(Значение, "(") + 1, Найти(Значение, ")") - Найти(Значение, "(") - 1);
		Процент1 = Число(Значение);
		
		Значение = ПользовательскоеПоле.Варианты.Элементы[1].Значение;
		Значение = Сред(Значение, Найти(Значение, "(") + 1, Найти(Значение, ")") - Найти(Значение, "(") - 1);
		Процент2 = Число(Значение);
		
		Значение = ПользовательскоеПоле.Варианты.Элементы[2].Значение;
		Значение = Сред(Значение, Найти(Значение, "(") + 1, Найти(Значение, ")") - Найти(Значение, "(") - 1);
		Процент3 = Число(Значение);
		
	КонецЕсли;
	
	УстановитФокусНаНезаполненный();
	
КонецПроцедуры               

Процедура ПолеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаВыбораПоля = ПолучитьОбщуюФорму("ФормаВыбораДоступногоПоляКомпоновщикаНастроек");
	ФормаВыбораПоля.КомпоновщикНастроек = КомпоновщикНастроек;
	Результат = ФормаВыбораПоля.ОткрытьМодально();
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элемент.Значение = Строка(Результат.Поле);
	
КонецПроцедуры

