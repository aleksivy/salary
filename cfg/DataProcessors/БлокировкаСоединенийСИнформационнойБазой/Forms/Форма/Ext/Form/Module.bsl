////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// получаем значения параметров ранее установленной блокировки	
	ТекущийРежим = ПолучитьБлокировкуУстановкиСоединений();
	
	УстановитьБлокировкуСоединений 	= ТекущийРежим.Установлена;
	Сообщение						= ТекущийРежим.Сообщение;
	КодРазрешения					= ТекущийРежим.КодРазрешения;
	
	Если УстановитьБлокировкуСоединений Тогда
		НачалоБлокировки 				= ТекущийРежим.Начало;
		ОкончаниеБлокировки				= ТекущийРежим.Конец;
	Иначе	
		// если блокировка не установлена, можно предположить, что
		// пользователь открыл форму для установки блокировки. 
		// Поэтому устанавливаем дату блокировки равной текущей дате	
		НачалоБлокировки = ТекущаяДата();
	КонецЕсли;			
КонецПроцедуры // ПередОткрытием

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()
	
	ЭлементыФормы.ПанельКодРазрешения.Свертка = РежимСверткиЭлементаУправления.Нет;
	
	// в случае, если пользователь не имеет административных прав - 
	// предоставляем ему возможность только просмотра данных блокировки
	Если Не ПравоДоступа("Администрирование", Метаданные) Тогда
		ТолькоПросмотр = Истина;
		ЭлементыФормы.ПанельКодРазрешения.Свертка = РежимСверткиЭлементаУправления.Низ;
		ЭлементыФормы.Сообщение.УстановитьПривязку(ГраницаЭлементаУправления.Низ,
		                                             ЭлементыФормы.ПанельКодРазрешения, ГраницаЭлементаУправления.Верх);
	КонецЕсли;		
КонецПроцедуры // ПриОткрытии()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура устанавливает блокировку соединений
//
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если УстановитьБлокировкуСоединений Тогда
		
		// проверки возможности установки блокировки				
		Если ЗначениеЗаполнено(ОкончаниеБлокировки) и  НачалоБлокировки > ОкончаниеБлокировки Тогда
			Сообщить(НСтр("ru='Дата окончания блокировки не можеть быть меньше даты начала блокировки. Блокировка не установлена...';uk='Дата закінчення блокування не може бути менше дати початку блокування. Блокування не встановлене...'"), СтатусСообщения.Важное);
			
			Возврат;
		КонецЕсли;	
				
		ТекстВопроса = НСтр("ru='После установки блокировки в информационной базе будет запущен "
"режим завершения работы всех пользователей (включая Вас) "
"на период действия блокировки."
"Вы действительно хотите установить блокировку?';uk='Після встановлення блокування в інформаційній базі буде запущений "
"режим завершення роботи всіх користувачів (включаючи Вас) "
"на період дії блокування."
"Ви дійсно хочете встановити блокування?'");
		
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, 60,КодВозвратаДиалога.Нет, НСтр("ru='Установка блокировки соединений с информационной базой';uk=""Встановлення блокування з'єднань із інформаційною базою"""));				
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
        КонецЕсли;	       
	КонецЕсли;	
	
	УстановитьБлокировку();		
КонецПроцедуры // КнопкаВыполнитьНажатие(Кнопка)

// Процедура выбора периода блокировки
//
Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	
	НастройкаПериода.УстановитьПериод(НачалоБлокировки, ?(ОкончаниеБлокировки = '0001-01-01', ОкончаниеБлокировки, КонецДня(ОкончаниеБлокировки)));
	
	НастройкаПериода.РедактироватьКакИнтервал 	= Истина;
	НастройкаПериода.РедактироватьКакПериод 	= Истина;
	НастройкаПериода.ВариантНастройки 			= ВариантНастройкиПериода.Период;
	
	Если НастройкаПериода.Редактировать() Тогда
		НачалоБлокировки 	= НастройкаПериода.ПолучитьДатуНачала();
		ОкончаниеБлокировки = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры // ВыбПериодНажатие

