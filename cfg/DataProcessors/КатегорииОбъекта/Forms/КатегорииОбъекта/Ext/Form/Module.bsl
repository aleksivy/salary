////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()

	ЭтаФорма.Заголовок = НСтр("ru='Категории: ';uk='Категорії: '") + ОбработкаОбъектКатегорииОбъекта.ОбъектОтбораКатегорий;

КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если Модифицированность Тогда
		Вопрос = НСтр("ru='Измененные категории объекта не были записаны! Сохранить изменения?';uk=""Змінені категорії об'єкта не були записані! Зберегти зміни?""");
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена);

		Если      Ответ = КодВозвратаДиалога.Да Тогда
			ОбработкаОбъектКатегорииОбъекта.ЗаписатьКатегорииОбъекта();

		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаписиНовогоОбъекта(Объект, Источник)

	ОбработкаОбъектКатегорииОбъекта.ПроверитьДобавитьНовуюКатегорию(Объект.Ссылка, ЭлементыФормы.КатегорииОбъекта);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

Процедура КоманднаяПанельДействиеЗаписать(Кнопка)

	Если ОбработкаОбъектКатегорииОбъекта.ЗаписатьКатегорииОбъекта() Тогда
		Модифицированность = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура КоманднаяПанельДействиеПеречитать(Кнопка)

	Если Модифицированность Тогда
		Вопрос = НСтр("ru='Измененные категории объекта не были записаны! Продолжить?';uk=""Змінені категорії об'єкта не були записані! Продовжити?""");
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ОкОтмена);

		Если Ответ <> КодВозвратаДиалога.ОК Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ОбработкаОбъектКатегорииОбъекта.ПрочитатьЗаполнитьКатегорииОбъекта();

	Модифицированность = Ложь;

КонецПроцедуры

Процедура КатегорииОбъектаПередНачаломДобавления(Элемент, Отказ, Копирование)

	Отказ = Истина;

	ОбработкаОбъектКатегорииОбъекта.ОткрытьФормуНовойКатегории(ЭтаФорма);

КонецПроцедуры

Процедура КатегорииОбъектаПередУдалением(Элемент, Отказ)

	Отказ = Истина;

	ОбработкаОбъектКатегорииОбъекта.ИнвертироватьПометкуУдаленияКатегории(Элемент.ТекущиеДанные);

КонецПроцедуры

Процедура КатегорииОбъектаПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если Элемент.Колонки.ПометкаУдаления.Видимость Тогда
		ОформлениеСтроки.Ячейки.ПометкаУдаления.ОтображатьКартинку = Истина;
		ОформлениеСтроки.Ячейки.ПометкаУдаления.ИндексКартинки     = Число(ДанныеСтроки.ПометкаУдаления);
	КонецЕсли;

	Если Элемент.Колонки.Принадлежность.Видимость Тогда
		ОформлениеСтроки.Ячейки.Принадлежность.ОтображатьФлажок    = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура КатегорииОбъектаПриИзмененииФлажка(Элемент, Колонка)

	Модифицированность = Истина;

КонецПроцедуры

Процедура КнопкаOKНажатие(Элемент)

	// Запись категорий объекта и закрытие формы.
	Если ОбработкаОбъектКатегорииОбъекта.ЗаписатьКатегорииОбъекта() Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

