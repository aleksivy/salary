 // Данная функция возвращает строку с версией платформы, которая требуется для работы конфигурации. 
// Версия возвращается в формате <основная версия>.<младшая версия>.<релиз>.<дополнительный номер релиза>
// или более коротком, например, "8.1.10.5", "8.1.13"
// Если требований к версии платформы нет, функция может возвращать пустую строку
Функция РекомендуемаяВерсияПлатформы()
	Возврат "8.3.14";
КонецФункции

Функция ДопустимаяВерсияПлатформы()
	Возврат "8.2.19";
КонецФункции

// Проверка версии платформы.
// Если версия платформы боле поздняя, чем требуется, работа программы будет прекращена.
Процедура ПроверитьВерсиюПлатформы() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ТекущаяВерсия = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СистемнаяИнформация.ВерсияПриложения, ".");
	РекомендуемаяВерсия = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(РекомендуемаяВерсияПлатформы(), ".");
	
	Для Счетчик = РекомендуемаяВерсия.Количество() + 1 По ТекущаяВерсия.Количество() Цикл
		РекомендуемаяВерсия.Добавить("0");
	КонецЦикла;
	
	ВерсияПодходит = Истина;
	Для Счетчик = 0 По ТекущаяВерсия.ВГраница() Цикл
		Текущая = Число(ТекущаяВерсия[Счетчик]);
		Рекомендуемая = ?(ПустаяСтрока(РекомендуемаяВерсия[Счетчик]), 0, Число(РекомендуемаяВерсия[Счетчик]));
		Если Текущая > Рекомендуемая Тогда
			Прервать;
		ИначеЕсли Текущая < Рекомендуемая Тогда
			ВерсияПодходит = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ВерсияПодходит Тогда
		Возврат;
	КонецЕсли;
	
	Если Найти(СистемнаяИнформация.ВерсияПриложения,ДопустимаяВерсияПлатформы())= 1  Тогда
		//Версия не рекомендуемая, но допустимая
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Для работы с данной конфигурацией рекомендуется версия платформы %1 или более поздняя.
			|Используемая сейчас версия - %2.
			|Рекомендуется обновить версию платформы.
			|Для получения новой версии платформы воспользуйтесь сервисом ""Обновление программ"" на Портале ИТС (portal.1c.eu)
			|",РекомендуемаяВерсияПлатформы(),СистемнаяИнформация.ВерсияПриложения);
			
		Сообщить(ТекстВопроса,Статуссообщения.Важное);	
		
	Иначе	
	
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Для работы с данной конфигурацией требуется версия платформы %1 или более поздняя.
			|Используемая сейчас версия - %2.
			|Рекомендуется прекратить работу программы и обновить версию платформы. В противном случае некоторые функции программы будут недоступны или будут работать неправильно.
			| 
			|Для получения новой версии платформы воспользуйтесь сервисом ""Обновление программ"" на Портале ИТС (portal.1c.eu)
			|
			|Прекратить работу программы?",РекомендуемаяВерсияПлатформы(),СистемнаяИнформация.ВерсияПриложения);
			
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
			ПрекратитьРаботуСистемы();
		
		КонецЕсли;
	
	КонецЕсли;		
		
	
			
КонецПроцедуры
