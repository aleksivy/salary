////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мТабличнаяЧасть;

Перем мСписокВозможныхКатегорийРасчета;

// Переменные для запоминания значений реквизитов до очистки значения
Перем мБылаОчередностьРасчета;
Перем мБылСпособОтраженияВУпрУчете;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

// Открывает список видов расчета для подбора
//
Процедура ОткрытьФормуПодбора(ИмяПВР)
	
	ФормаПодбора = ПланыВидовРасчета[ИмяПВР].ПолучитьФормуВыбора(, ЭтаФорма);
	ФормаПодбора.ЗакрыватьПриВыборе = Ложь;
	ФормаПодбора.мМассивВыбранныхЗначений	= ЭтаФорма[мТабличнаяЧасть].ВыгрузитьКолонку("ВидРасчета");
	ФормаПодбора.Открыть();
	
КонецПроцедуры

// Составляет описания-расшифровки различных реквизитов
//
// Параметры
//  ИмяОбновляемогоЭлемента - строка, содержит имя описываемого реквизита или имя закладки
//
Процедура ОбновитьПредставлениеЭлемента(ИмяОбновляемогоЭлемента)
	
	Если ИмяОбновляемогоЭлемента = "СпособОтраженияВУпручете" или ИмяОбновляемогоЭлемента = "ЗакладкаУправленческийУчет" Тогда
		РасшифровкаОтражениеВУпручете = РаботаСДиалогами.ПолучитьПредставлениеСпособаОтраженияНачисленияВУпрУчете(СпособОтраженияВУпручете);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - вызывается при открытии формы
//
Процедура ПриОткрытии()
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПодМенюБазовыхВидовРасчета",Новый Действие("КоманднаяПанельБазовыеВидыРасчетаПодборПодМеню"));
	СтруктураДействий.Вставить("КнопкаБазовыхВидовРасчета",Новый Действие("КоманднаяПанельБазовыеВидыРасчетаПодбор"));
	РаботаСДиалогами.СоздатьКнопкуПодбораДляПВР(Ссылка, ЭтаФорма);
	
	Если ЭтоНовый() Тогда

		// Инициализация реквизитов для нового объекта
		Если НЕ ЗначениеЗаполнено(СпособРасчета) Тогда
			СпособРасчета		= Перечисления.СпособыРасчетаОплатыТруда.ПроизвольнаяФормула;
			ПроизвольнаяФормулаРасчета = Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(КатегорияРасчета) Тогда
			КатегорияРасчета = Перечисления.КатегорииРасчетов.ЗависимоеПервогоУровня;
		КонецЕсли;		
		
	КонецЕсли;
	
	// Заполним реквизиты формы, обслуживающие работу переключателей
	ЭтоПервичноеНачисление = КатегорияРасчета = Перечисления.КатегорииРасчетов.Первичное;
    ЗаданСпособОтраженияВУпрУчете = ЗначениеЗаполнено(СпособОтраженияВУпрУчете);
	
	// Установим доступность ЭУ в зависимости от значений реквизиов
	ЭлементыФормы.ОчередностьРасчета.ТолькоПросмотр = ЭтоПервичноеНачисление;
	ЭлементыФормы.СпособОтраженияВУпрУчете.ТолькоПросмотр = Не ЗаданСпособОтраженияВУпрУчете;
	
	// Для предопределённых элементов запрещено редактирование этих реквизитов:
	ЭлементыФормы.ОчередностьНачисления.Доступность  = Не Предопределенный;
	ЭлементыФормы.ОчередностьНачисления1.Доступность = Не Предопределенный;
	ЭлементыФормы.ЗадатьФормулу.Доступность 		 = Не Предопределенный;
	
	// список возможных значений категории расчета
	мСписокВозможныхКатегорийРасчета = Новый СписокЗначений;
	мСписокВозможныхКатегорийРасчета.Добавить(Перечисления.КатегорииРасчетов.ЗависимоеПервогоУровня);
	мСписокВозможныхКатегорийРасчета.Добавить(Перечисления.КатегорииРасчетов.ЗависимоеВторогоУровня);
	мСписокВозможныхКатегорийРасчета.Добавить(Перечисления.КатегорииРасчетов.ЗависимоеТретьегоУровня);
	ЭлементыФормы.ОчередностьРасчета.ДоступныеЗначения = мСписокВозможныхКатегорийРасчета;
	Если мСписокВозможныхКатегорийРасчета.НайтиПоЗначению(КатегорияРасчета) = Неопределено Тогда
		ОчередностьРасчета = Перечисления.КатегорииРасчетов.ПустаяСсылка();
		ЭлементыФормы.ОчередностьРасчета.ОтметкаНезаполненного = Ложь;
	Иначе
		ОчередностьРасчета = мСписокВозможныхКатегорийРасчета.НайтиПоЗначению(КатегорияРасчета).Значение;
	КонецЕсли;

	// установим текст формулы расчета
	Элементыформы.СпособРасчетаПредставление.УстановитьТекст(ПроведениеРасчетов.ВизуализироватьФормулуРасчета(ЭтотОбъект, "HTML"));
	
	// установим видимость панелей в описании формулы расчета
	РаботаСДиалогами.УстановитьОтборыИСверткуПоказателей(ЭлементыФормы, Показатели, ПроизвольнаяФормулаРасчета);
	
КонецПроцедуры

// Процедура - обработчик события "ПередЗаписью" вида расчета
//
Процедура ПередЗаписью(Отказ)
	
	Если Модифицированность Тогда
		
		ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
		ОбработкаКомментариев.УдалитьСообщения();
		
		// Проверка правильности настройки вида расчета
		РаботаСДиалогами.ПроверитьПВРНаПустыеСтрокиВТЧ(ЭтотОбъект, Истина);
		РезультатПроверки = ПроведениеРасчетов.ПроверитьНастройкуВидаРасчета(ЭтотОбъект, Отказ, Ложь);
		Если не Отказ Тогда
			Если не ПустаяСтрока(РезультатПроверки) Тогда
				Ответ = Вопрос(РезультатПроверки + НСтр("ru=' Записать вид расчета?';uk=' Записати вид розрахунку?'"), РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
				// отказались от записи
				Отказ = Ответ <> КодВозвратаДиалога.Да;
			КонецЕсли;
		Иначе	
			ОбработкаКомментариев.ДобавитьСообщение(НСтр("ru='Начисление не записано!';uk='Нарахування не записано!'"), Перечисления.ВидыСообщений.Информация);	
		КонецЕсли;
		
		ОбработкаКомментариев.ПоказатьСообщения();
		
	КонецЕсли;
	
КонецПроцедуры


// Процедура - обработчик события "ПослеЗаписи"
//
Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьФорму", "УправленческиеНачисления" , ЭтотОбъект.Ссылка);

КонецПроцедуры


Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВводФормулыРасчета" и Источник = ЭтаФорма Тогда
		ПроведениеРасчетов.УстановитьПараметрыВидаРасчета(Параметр, ЭтотОбъект, ЭлементыФормы);
		РаботаСДиалогами.УстановитьОтборыИСверткуПоказателей(ЭлементыФормы, Показатели, ПроизвольнаяФормулаРасчета);
		
	ИначеЕсли ИмяСобытия = "ПодборОтменаВыбора" Тогда
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ВидРасчета", Параметр);
		НайденныеСтроки = Источник.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого ТекСтрока Из НайденныеСтроки Цикл
			Источник.Удалить(ТекСтрока);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события "Обработка выбора"
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
	Если мТабличнаяЧасть = "БазовыеВидыРасчета" Тогда
		
		Если БазовыеВидыРасчета.Найти(ЗначениеВыбора,"ВидРасчета") = Неопределено Тогда
			БазовыеВидыРасчета.Добавить().ВидРасчета = ЗначениеВыбора;
		КонецЕсли;	
		
	ИначеЕсли мТабличнаяЧасть = "ВытесняющиеВидыРасчета"  Тогда
		
		Если ВытесняющиеВидыРасчета.Найти(ЗначениеВыбора,"ВидРасчета") = Неопределено Тогда
			ВытесняющиеВидыРасчета.Добавить().ВидРасчета = ЗначениеВыбора;
		КонецЕсли;
						
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - вызывается при изменении переключателя "ОчередностьНачисления"
//
Процедура ОчередностьНачисленияПриИзменении()
	
	ЭлементыФормы.ОчередностьРасчета.ТолькоПросмотр = ЭтоПервичноеНачисление;
	Если ЭтоПервичноеНачисление Тогда
		КатегорияРасчета = Перечисления.КатегорииРасчетов.Первичное;
		мБылаОчередностьРасчета = ОчередностьРасчета; // запомним значение
		ОчередностьРасчета = Перечисления.КатегорииРасчетов.ПустаяСсылка();
		ЭлементыФормы.ОчередностьРасчета.ОтметкаНезаполненного = Ложь;
	Иначе
		ОчередностьРасчета = мБылаОчередностьРасчета; // восстановим предыдущее значение
		Если Не ЗначениеЗаполнено(ОчередностьРасчета) Тогда
			ОчередностьРасчета = Перечисления.КатегорииРасчетов.ЗависимоеПервогоУровня;	
		КонецЕсли;
		КатегорияРасчета = ОчередностьРасчета;
		ЭлементыФормы.ОчередностьРасчета.ОтметкаНезаполненного = Не ЗначениеЗаполнено(ОчередностьРасчета);
	КонецЕсли;
		
КонецПроцедуры //ПереключательПриИзменении

Процедура ОчередностьРасчетаПриИзменении(Элемент)
	
	КатегорияРасчета = Элемент.Значение;
	
КонецПроцедуры

Процедура ОчередностьРасчетаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлементСписка = ВыбратьИзСписка(мСписокВозможныхКатегорийРасчета,Элемент,мСписокВозможныхКатегорийРасчета.НайтиПоЗначению(Элемент.Значение));
	Если ЭлементСписка <> Неопределено Тогда
		Элемент.Значение = ЭлементСписка.Значение;
		КатегорияРасчета = Элемент.Значение;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаданСпособОтраженияВУпрУчетеПриИзменении(Элемент)
	
	Если ЗаданСпособОтраженияВУпрУчете Тогда
		СпособОтраженияВУпрУчете = мБылСпособОтраженияВУпрУчете;
	Иначе	
		мБылСпособОтраженияВУпрУчете = СпособОтраженияВУпрУчете;
		СпособОтраженияВУпрУчете = Справочники.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка();
	КонецЕсли; 	
	
	ЭлементыФормы.СпособОтраженияВУпрУчете.ТолькоПросмотр = Не ЗаданСпособОтраженияВУпрУчете; 
	
	ОбновитьПредставлениеЭлемента("СпособОтраженияВУпручете");

	
КонецПроцедуры

Процедура СпособОтраженияВУпрУчетеПриИзменении(Элемент)
	
	ОбновитьПредставлениеЭлемента("СпособОтраженияВУпручете");
	
КонецПроцедуры

Процедура ЗадатьФормулуНажатие(Элемент)
	
	РаботаСДиалогами.ОткрытьФормуРедактированияФормулы(ЭтаФорма, Показатели, Наименование, ФормулаРасчета, Ссылка, "ПлановыеНачисленияРаботников");
	
КонецПроцедуры

Процедура ВидУчетаРабочегоВремениПриИзменении(Элемент)
	
	// укажем тарифной ставке необходимость её запроса при вводе кадровых документов
	Если ВидУчетаРабочегоВремени.РабочееВремя Тогда
		Вводить = Истина;
	Иначе
		Вводить = Ложь;
	КонецЕсли;
	
	Для Каждого СтрокаПоказателей Из Показатели Цикл
		Показатель = СтрокаПоказателей.Показатель;
		Если Показатель.ТарифнаяСтавка Или Показатель = Справочники.ПоказателиСхемМотивации.ТарифнаяСтавкаДневная Или
			Показатель = Справочники.ПоказателиСхемМотивации.ТарифнаяСтавкаЧасовая Или
			Показатель = Справочники.ПоказателиСхемМотивации.ТарифнаяСтавкаМесячная Тогда
			СтрокаПоказателей.ЗапрашиватьПриКадровыхПеремещениях = Вводить;
		КонецЕсли;
	КонецЦикла;	

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНОЙ ПАНЕЛИ ТЧ БазовыеВидыРасчета

Процедура КоманднаяПанельБазовыеВидыРасчетаПодборПодМеню(Кнопка)
	
	мТабличнаяЧасть = "БазовыеВидыРасчета";
	ОткрытьФормуПодбора(Кнопка.Имя);
	
КонецПроцедуры

Процедура КоманднаяПанельБазовыеВидыРасчетаПодбор(Кнопка)
	
	мТабличнаяЧасть = "БазовыеВидыРасчета";
	ОткрытьФормуПодбора(Метаданные().Имя);
	
КонецПроцедуры

Процедура КоманднаяПанельВытесняющиеВидыРасчетаПодбор(Кнопка)
	
	мТабличнаяЧасть = "ВытесняющиеВидыРасчета";
	ОткрытьФормуПодбора(Метаданные().Имя);
	
КонецПроцедуры

Процедура ПоказателиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

Процедура ПоказателиПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = Истина;
КонецПроцедуры

Процедура БазовыеВидыРасчетаПередУдалением(Элемент, Отказ)
	
	Если Не Отказ Тогда
		ДанныеСтроки = Элемент.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Оповестить("ПодборОтменаВыбора", ДанныеСтроки.ВидРасчета, Элемент.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура БазовыеВидыРасчетаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ПодборВыбор", ДанныеСтроки.ВидРасчета, Элемент.Значение);
	
КонецПроцедуры

Процедура ВытесняющиеВидыРасчетаПередУдалением(Элемент, Отказ)
	
	Если Не Отказ Тогда
		ДанныеСтроки = Элемент.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Оповестить("ПодборОтменаВыбора", ДанныеСтроки.ВидРасчета, Элемент.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВытесняющиеВидыРасчетаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ПодборВыбор", ДанныеСтроки.ВидРасчета, Элемент.Значение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мБылаОчередностьРасчета = Перечисления.КатегорииРасчетов.ПустаяСсылка();
мБылСпособОтраженияВУпрУчете = Справочники.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка();


