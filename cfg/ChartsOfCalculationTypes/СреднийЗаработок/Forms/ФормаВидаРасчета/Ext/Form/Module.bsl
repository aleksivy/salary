////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем мТабличнаяЧасть;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ
// 

Процедура ОткрытьФормуПодбора(ИмяПВР)

	ФормаПодбора = ПланыВидовРасчета[ИмяПВР].ПолучитьФормуВыбора(, ЭтаФорма);
	ФормаПодбора.ЗакрыватьПриВыборе = Ложь;
	ФормаПодбора.Открыть();

КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - вызывается при открытии формы
Процедура ПриОткрытии()

	// Для премий состав базы расчетов должен выбираться из ПВР "ОсновныеНачисленияОрганизаций"
	Если Ссылка = ПланыВидовРасчета.СреднийЗаработок.ПоПремиям 
		ИЛИ Ссылка = ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиям Тогда
		
		МассивДоступныхТипов = Новый Массив;
		МассивДоступныхТипов.Добавить(Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций"));
		ЭлементыФормы.БазовыеВидыРасчета.Колонки.ВидРасчета.ЭлементУправления.ОграничениеТипа = Новый ОписаниеТипов(МассивДоступныхТипов);
		ЭлементыФормы.БазовыеВидыРасчета.Колонки.ВидРасчета.ЭлементУправления.ВыбиратьТип = Ложь;
		
		ДействиеПоКнопке = Новый Действие("КоманднаяПанельБазовыеВидыРасчетаПодборПоПремиям");
		ЭлементыФормы.КоманднаяПанельБазовыеВидыРасчета.Кнопки.Подбор.Действие = ДействиеПоКнопке;
		
	ИначеЕсли Ссылка = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремии Тогда	
		ЭлементыФормы.КоманднаяПанельБазовыеВидыРасчета.Кнопки.Подбор.Доступность = Ложь;
	Иначе
		
		РаботаСДиалогами.СоздатьКнопкуПодбораДляПВР(Ссылка, ЭтаФорма);
		
	КонецЕсли; 

КонецПроцедуры

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
	Если мТабличнаяЧасть = "БазовыеВидыРасчета" Тогда
		
		Если БазовыеВидыРасчета.Найти(ЗначениеВыбора,"ВидРасчета") = Неопределено Тогда
			БазовыеВидыРасчета.Добавить().ВидРасчета = ЗначениеВыбора;
		КонецЕсли;
		
	ИначеЕсли мТабличнаяЧасть = "ВедущиеВидыРасчета" Тогда
		
		Если ВедущиеВидыРасчета.Найти(ЗначениеВыбора,"ВидРасчета") = Неопределено Тогда
			ВедущиеВидыРасчета.Добавить().ВидРасчета = ЗначениеВыбора;
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события начала выбора значения "Вид расчета"
Процедура БазовыеВидыРасчетаВидРасчетаНачалоВыбора(Элемент, СтандартнаяОбработка)

	Если Ссылка = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремии 
		Или Ссылка = ПланыВидовРасчета.СреднийЗаработок.ПоПремиям
		Или Ссылка = ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиям Тогда
			Элемент.Значение = ЭлементыФормы.БазовыеВидыРасчета.Колонки.ВидРасчета.ЭлементУправления.ОграничениеТипа.ПривестиЗначение(Элемент.Значение);		
	КонецЕсли;	

КонецПроцедуры

Процедура БазовыеВидыРасчетаПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Если Ссылка = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремии Тогда
		
		Сообщить(НСтр("ru='Вид расчета: По годовой премии. Редактирование расчетной базы не предусмотрено.';uk='Вид розрахунку: По річній премії. Редагування розрахункової бази не передбачено.'"));
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНОЙ ПАНЕЛИ ТЧ БазовыеВидыРасчета

// Не является ошибкой проверки конфигурации - Обработчик устанавливается динамически методом "Действие"
// в процедуре "СоздатьКнопкуПодбораДляПВР" общего модуля "РаботаСДиалогами"
// 
Процедура КоманднаяПанельБазовыеВидыРасчетаПодборПодМеню(Кнопка)
	
	мТабличнаяЧасть = "БазовыеВидыРасчета";
	ОткрытьФормуПодбора(Кнопка.Имя);
	
КонецПроцедуры

// Не является ошибкой проверки конфигурации - Обработчик устанавливается динамически методом "Действие"
// в процедуре "СоздатьКнопкуПодбораДляПВР" общего модуля "РаботаСДиалогами"
// 
Процедура КоманднаяПанельБазовыеВидыРасчетаПодбор(Кнопка)
	
	мТабличнаяЧасть = "БазовыеВидыРасчета";
	ОткрытьФормуПодбора("ОсновныеНачисленияОрганизаций");
	
КонецПроцедуры

Процедура КоманднаяПанельБазовыеВидыРасчетаПодборПоПремиям(Кнопка)
	
	мТабличнаяЧасть = "БазовыеВидыРасчета";
	ОткрытьФормуПодбора("ОсновныеНачисленияОрганизаций");
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНОЙ ПАНЕЛИ ТЧ ВедущиеВидыРасчета

// Не является ошибкой проверки конфигурации - Обработчик устанавливается динамически методом "Действие"
// в процедуре "СоздатьКнопкуПодбораДляПВР" общего модуля "РаботаСДиалогами"
// 
Процедура КоманднаяПанельВедущиеВидыРасчетаПодМеню(Кнопка)
	
	мТабличнаяЧасть = "ВедущиеВидыРасчета";
	ОткрытьФормуПодбора(Кнопка.Имя);
	
КонецПроцедуры

// Не является ошибкой проверки конфигурации - Обработчик устанавливается динамически методом "Действие"
// в процедуре "СоздатьКнопкуПодбораДляПВР" общего модуля "РаботаСДиалогами"
// 
Процедура КоманднаяПанельВедущиеВидыРасчетаПодбор(Кнопка)
	
	мТабличнаяЧасть = "ВедущиеВидыРасчета";
	
	Для каждого СтрокаКолекции Из Метаданные().БазовыеВидыРасчета Цикл
		
		ОткрытьФормуПодбора(СтрокаКолекции.Имя);
		Прервать;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура КоманднаяПанельВедущиеВидыРасчетаЗаполнить(Кнопка)
	
	Если Модифицированность() Тогда	
		Если Вопрос(НСтр("ru='Перед заполнением необходимо записать. Записать вид расчета?';uk='Перед заповненням необхідно записати. Записати вид розрахунку?'"), РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Нет Тогда
			// отказались от записи
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Записать();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамВидРасчета", Ссылка);
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Основные.ВидРасчета КАК ВидРасчета
	|ИЗ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК Базовые
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ВедущиеВидыРасчета КАК Основные
	|ПО Базовые.ВидРасчета = Основные.Ссылка 
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.ВедущиеВидыРасчета КАК Ведущие
	|ПО 	Ведущие.Ссылка = &парамВидРасчета
	|	И	Основные.ВидРасчета = Ведущие.ВидРасчета 
	|
	|ГДЕ 	Базовые.Ссылка = &парамВидРасчета
	|	И	Основные.ВидРасчета <> ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка)
	|	И	Ведущие.ВидРасчета ЕСТЬ NULL  // только те которых не хватает
	|
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Основные.ВидРасчета КАК ВидРасчета
	|ИЗ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК Базовые
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ВытесняющиеВидыРасчета КАК Основные
	|ПО Базовые.ВидРасчета = Основные.Ссылка 
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.ВедущиеВидыРасчета КАК Ведущие
	|ПО 	Ведущие.Ссылка = &парамВидРасчета
	|	И	Основные.ВидРасчета = Ведущие.ВидРасчета 
	|
	|ГДЕ 	Базовые.Ссылка = &парамВидРасчета
	|	И	Основные.ВидРасчета <> ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка)
	|	И	Ведущие.ВидРасчета ЕСТЬ NULL  // только те которых не хватает
	|
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Основные.ВидРасчета КАК ВидРасчета
	|ИЗ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК Базовые
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций.БазовыеВидыРасчета КАК Основные
	|ПО Базовые.ВидРасчета = Основные.Ссылка 
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.ВедущиеВидыРасчета КАК Ведущие
	|ПО 	Ведущие.Ссылка = &парамВидРасчета
	|	И	Основные.ВидРасчета = Ведущие.ВидРасчета 
	|
	|ГДЕ 	Базовые.Ссылка = &парамВидРасчета
	|	И	Основные.ВидРасчета <> ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка)
	|	И	Ведущие.ВидРасчета ЕСТЬ NULL  // только те которых не хватает
	|
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Базовые.ВидРасчета КАК ВидРасчета
	|ИЗ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК Базовые
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.ВедущиеВидыРасчета КАК Ведущие
	|ПО 	Ведущие.Ссылка = &парамВидРасчета
	|	И	Базовые.ВидРасчета = Ведущие.ВидРасчета 
	|
	|ГДЕ 	Базовые.Ссылка = &парамВидРасчета
	|	И	Базовые.ВидРасчета <> ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка)
	|	И	Ведущие.ВидРасчета ЕСТЬ NULL  // только те которых не хватает
	|";
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		СтрокаВедущие = ВедущиеВидыРасчета.Добавить();
		СтрокаВедущие.ВидРасчета = Выборка.ВидРасчета;
	КонецЦикла;
	
КонецПроцедуры


