Процедура ПриОткрытии()

	СписокСчетов = Новый Массив;
	Счет         = ПараметрСчет;

	Пока Счет <> Планысчетов.Налоговый.ПустаяСсылка() Цикл
		СписокСчетов.Добавить(Счет);
		Счет = Счет.Родитель;
	КонецЦикла;

	Если СписокСчетов.Количество() <> 0 Тогда

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокСчетов", СписокСчетов);

		Если ПараметрКорреспонденция = ВидДвиженияБухгалтерии.Дебет Тогда
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КорректныеКорреспонденцииСчетов.СчетКт КАК Ссылка
			|ИЗ
			|	РегистрСведений.КорректныеКорреспонденцииСчетов КАК КорректныеКорреспонденцииСчетов
			|
			|ГДЕ
			|	КорректныеКорреспонденцииСчетов.СчетДт В (&СписокСчетов)";
		Иначе
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КорректныеКорреспонденцииСчетов.СчетДт КАК Ссылка
			|ИЗ
			|	РегистрСведений.КорректныеКорреспонденцииСчетов КАК КорректныеКорреспонденцииСчетов
			|
			|ГДЕ
			|	КорректныеКорреспонденцииСчетов.СчетКт В (&СписокСчетов)";
		КонецЕсли;

		Выборка = Запрос.Выполнить().Выбрать();

		СписокСчетов = Новый Массив;
		ПустойСчет   = Ложь;

		Пока Выборка.Следующий() Цикл

			Если Выборка.Ссылка = Планысчетов.Налоговый.ПустаяСсылка() Тогда

				ПустойСчет = Истина;

				Прервать;

			КонецЕсли;

			СписокСчетов.Добавить(Выборка.Ссылка);

		КонецЦикла;

		Если Не ПустойСчет Тогда

			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("СписокСчетов", СписокСчетов);
			Запрос.Текст = "
			|ВЫБРАТЬ 
			|	Основной.Ссылка
			|ИЗ
			|	ПланСчетов.Основной КАК Основной
			|
			|ГДЕ
			|	Основной.Ссылка В ИЕРАРХИИ (&СписокСчетов)";
			Выборка = Запрос.Выполнить().Выбрать();

			СписокЗначений = Новый СписокЗначений;
			Пока Выборка.Следующий() Цикл
				СписокЗначений.Добавить(Выборка.Ссылка);
			КонецЦикла;

			ЭлементОтбора = ПланСчетовСписок.Отбор.Ссылка;

			ЭлементОтбора.ВидСравнения  = ВидСравнения.ВСписке;
			ЭлементОтбора.Значение      = СписокЗначений;
			ЭлементОтбора.Использование = Истина;

		КонецЕсли;

	КонецЕсли;

	ЭлементыФормы.ДействияФормы.Кнопки.КорректныеСчета.Пометка = ПланСчетовСписок.Отбор.Ссылка.Использование;

КонецПроцедуры

Процедура СписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Колонка = ОформлениеСтроки.Ячейки;

	Если ДанныеСтроки.Ссылка.ЗапретитьИспользоватьВПроводках Тогда
		ОформлениеСтроки.ЦветФона = WebЦвета.СветлоЖелтый;
	КонецЕсли;

	Вид = ДанныеСтроки.Ссылка.Вид;

	Если Вид = ВидСчета.Активный  Тогда
		Колонка.Вид.Текст = "А";
	ИначеЕсли Вид = ВидСчета.Пассивный  Тогда
		Колонка.Вид.Текст = "П";
	Иначе
		Колонка.Вид.Текст = "АП";
	КонецЕсли;

	Колонка.Вид.ОтображатьТекст = Истина;

КонецПроцедуры




