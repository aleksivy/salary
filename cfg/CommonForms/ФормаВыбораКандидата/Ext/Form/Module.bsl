////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем СтруктураПоиска;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ПолучитьДанные()
	
	Запрос = Новый Запрос();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КандидатыНаРаботу.ФизЛицо,
	|	КандидатыНаРаботу.Статус,
	|	КандидатыНаРаботу.ФизЛицо.Наименование КАК ФизЛицоНаименование
	|ИЗ
	|	РегистрСведений.КандидатыНаРаботу.СрезПоследних(, Активность = ИСТИНА) КАК КандидатыНаРаботу";
	
	Если НЕ СтатусКандидатов.Пустая() Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ГДЕ
		|		КандидатыНаРаботу.Статус = &СтатусКандидатов";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизлицоНаименование";
	
	Запрос.Текст = ТекстЗапроса;
		
	Запрос.УстановитьПараметр("СтатусКандидатов", СтатусКандидатов);
	СписокКандидатов = Запрос.Выполнить().Выгрузить();
	
	// устанавливает текущую позицию
	СтруктураПоиска.Физлицо = ПараметрФизлицо;
	
	Строки = СписокКандидатов.НайтиСтроки(СтруктураПоиска);
	Если Строки.Количество() > 0 Тогда
		ЭлементыФормы.СписокКандидатов.ТекущаяСтрока = Строки[0];
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	СтруктураПоиска = Новый Структура("Физлицо");
	
	// получить данные
	ПолучитьДанные();
	
	// сформировать колонки табличного поля
	КолонкаФизлицо = ЭлементыФормы.СписокКандидатов.Колонки.Добавить("ФизлицоНаименование", "Кандидат");
	КолонкаФизлицо.Данные = "ФизлицоНаименование";
	КолонкаСтатус = ЭлементыФормы.СписокКандидатов.Колонки.Добавить("Статус", "Статус");
	КолонкаСтатус.Данные = "Статус";
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыОбновитьСписок(Кнопка)
	
	// получить данные
	Если ЭлементыФормы.СписокКандидатов.ТекущаяСтрока = НеОпределено Тогда
		ПараметрФизлицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	Иначе
		ПараметрФизлицо = ЭлементыФормы.СписокКандидатов.ТекущаяСтрока.Физлицо;
	КонецЕсли;
	
	ПолучитьДанные();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

Процедура СтатусКандидатовПриИзменении(Элемент)
	
	// получить данные
	Если ЭлементыФормы.СписокКандидатов.ТекущаяСтрока = НеОпределено Тогда
		ПараметрФизлицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	Иначе
		ПараметрФизлицо = ЭлементыФормы.СписокКандидатов.ТекущаяСтрока.Физлицо;
	КонецЕсли;
	
	ПолучитьДанные();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура СписокКандидатовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	Если РежимВыбора Тогда
		ВыбранныйНаборДанных = Новый Структура("Физлицо, Статус",Элемент.ТекущаяСтрока.Физлицо,Элемент.ТекущиеДанные.Статус);
		ОповеститьОВыборе(ВыбранныйНаборДанных);
	Иначе
		ВыбраннаяСтрока.Физлицо.ПолучитьФорму().Открыть();
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура СписокКандидатовПередНачаломИзменения(Элемент, Отказ)
	
	Элемент.ТекущаяСтрока.Физлицо.ПолучитьФорму().Открыть();
	Отказ = Истина;
	
КонецПроцедуры

