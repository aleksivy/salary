
Процедура ПриОткрытии()
	
	ЭлементыФормы.ПолеТабличногоДокумента.ВставитьОбласть(ПечатныйДокумент.Область());
	
	ЭлементыФормы.ПолеТабличногоДокумента.ОтображатьЗаголовки = Ложь;
	ЭлементыФормы.ПолеТабличногоДокумента.ОтображатьСетку     = Ложь;
	ЭлементыФормы.ПолеТабличногоДокумента.Защита              = Защита;
	ЭлементыФормы.ПолеТабличногоДокумента.ТолькоПросмотр      = Истина;
	ЭлементыФормы.ПолеТабличногоДокумента.АвтоМасштаб         = ПечатныйДокумент.АвтоМасштаб;
	ЭлементыФормы.ПолеТабличногоДокумента.ОриентацияСтраницы  = ПечатныйДокумент.ОриентацияСтраницы;
	ЭлементыФормы.ПолеТабличногоДокумента.ПолеСверху          = ПечатныйДокумент.ПолеСверху;
	ЭлементыФормы.ПолеТабличногоДокумента.ПолеСнизу           = ПечатныйДокумент.ПолеСнизу;
	ЭлементыФормы.ПолеТабличногоДокумента.ПолеСлева           = ПечатныйДокумент.ПолеСлева;
	ЭлементыФормы.ПолеТабличногоДокумента.ПолеСправа          = ПечатныйДокумент.ПолеСправа;
	ЭлементыФормы.ПолеТабличногоДокумента.РазмерКолонтитулаСверху = ПечатныйДокумент.РазмерКолонтитулаСверху;
	ЭлементыФормы.ПолеТабличногоДокумента.РазмерКолонтитулаСнизу  = ПечатныйДокумент.РазмерКолонтитулаСнизу;
	ЭлементыФормы.ПолеТабличногоДокумента.ИмяПараметровПечати = ПечатныйДокумент.ИмяПараметровПечати;
	ЭлементыФормы.ПолеТабличногоДокумента.ПовторятьПриПечатиСтроки = ПечатныйДокумент.ПовторятьПриПечатиСтроки;
	
КонецПроцедуры

Процедура КоманднаяПанельФормыОтправитьПоПочте(Кнопка)
	
	УправлениеОтчетами.ОтправитьДокументПоЭлектроннойПочте(ЭлементыФормы.ПолеТабличногоДокумента, Заголовок);

КонецПроцедуры

#Если Клиент Тогда
//процедура не является ошибкой проверки конфигурации.
//вызывается в документе "Платежное поручение исходящее" при формировании отчета по заработной плате в банк
Процедура ЗаписатьXLS()
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = НСтр("ru='Укажите каталог';uk='Вкажіть каталог'");
	Диалог.Фильтр = "Документ Microsoft Excel (*.xls)|*.xls|";
	Если ОбъектПечати.Метаданные().Имя = "ПлатежноеПоручениеИсходящее" Тогда
		Диалог.ПолноеИмяФайла = "Ведомость на перечисление зарплаты от " + Формат(ОбъектПечати.Дата,"ДФ=ддММгггг") ;	
	КонецЕсли;
	Если Не Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ПечатныйДокумент.Записать(Диалог.ПолноеИмяФайла,ТипФайлаТабличногоДокумента.XLS); 
	
	
КонецПроцедуры
#КонецЕсли

Процедура КоманднаяПанельФормыЗаписать(Кнопка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.ПолноеИмяФайла = Заголовок;
	Диалог.Фильтр = "Табличный документ (*.mxl)|*.mxl|Документ HTML (*.htm)|*.htm|Документ HTML3 (*.htm)|*.htm|Документ HTML4 (*.htm)|*.htm|Лист Excel (*.xls)|*.xls|Текстовый файл (*.txt)|*.txt|Табличный документ v7.7 (*.mxl)|*.mxl|Лист Excel95 (*.xls)|*.xls|Лист Excel97 (*.xls)|*.xls";
	
	Если Диалог.Выбрать() Тогда
		
		ТипФайла = ТипФайлаТабличногоДокумента.MXL;
		Если Диалог.ИндексФильтра = 1 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.HTML;
		ИначеЕсли Диалог.ИндексФильтра = 2 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.HTML3;
		ИначеЕсли Диалог.ИндексФильтра = 3 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.HTML4;
		ИначеЕсли Диалог.ИндексФильтра = 4 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.XLS;
		ИначеЕсли Диалог.ИндексФильтра = 5 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.TXT;
		ИначеЕсли Диалог.ИндексФильтра = 6 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.MXL7;
		ИначеЕсли Диалог.ИндексФильтра = 7 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.XLS95;
		ИначеЕсли Диалог.ИндексФильтра = 8 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.XLS97;
		КонецЕсли; 
		
		ЭлементыФормы.ПолеТабличногоДокумента.Записать(Диалог.ПолноеИмяФайла, ТипФайла);
		
	КонецЕсли;
	
КонецПроцедуры


 ПечатныйДокумент = Новый ТабличныйДокумент;


