////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
// Выполняет провеку возможности печати текущего объекта метаданных.
// Формирует дерево макетов печати объекта метаданных.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Получить объект метаданных (переданный форме)
	Объект = КлючУникальности;
	
	// Установить заголовок
	Заголовок = НСтр("ru='Печать: ';uk='Друк: '") + Объект;
	Если Не ВладелецФормы = Неопределено Тогда
		Если Не РаботаСДиалогами.ПроверитьМодифицированностьВФорме(Объект, ВладелецФормы) Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Проверка возможности печати объекта
	Если Метаданные.Документы.Содержит(Объект.Метаданные()) И Объект.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить И Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Объект.Проведен) Тогда
		Предупреждение(НСтр("ru='Недостаточно полномочий для печати непроведенного документа!';uk='Недостатньо повноважень для друку непроведенного документа!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НаПринтер = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "ПечатьДокументовБезПредварительногоПросмотра") = Истина;
	
	// Получить внутренние печатные формы (имена макетов объектов)
	Попытка
		СтруктураВнутреннихПечатныхФорм = Объект.ПолучитьСтруктуруПечатныхФорм()
	Исключение
		СтруктураВнутреннихПечатныхФорм = Новый Структура;
	КонецПопытки;
	
	// Сформировать дерево макетов печати
	ДеревоМакетовПечати = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(Объект.Ссылка, СтруктураВнутреннихПечатныхФорм,,,Ложь);
	
	// Установить макеты печати по умолчанию
	ВыделенныеСтроки = ЭлементыФормы.ДеревоМакетовПечати.ВыделенныеСтроки;
	ВыделенныеСтроки.Очистить();
	Для каждого ТекущаяСтрока Из ДеревоМакетовПечати.Строки Цикл
		Если ТекущаяСтрока.Пометка Тогда
			
			Если НЕ ВыделенныеСтроки.Количество() Тогда
				ЭлементыФормы.ДеревоМакетовПечати.ТекущаяСтрока = ТекущаяСтрока;
			КонецЕсли;
			
			ВыделенныеСтроки.Добавить(ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередОткрытием()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ ФОРМЫ

// Процедура - обработчик события "ПечатиПриАктивизацииСтроки" элемента формы ДеревоМакетовПечати.
// Устанавливает текст подсказки.
//
Процедура ДеревоМакетовПечатиПриАктивизацииСтроки(Элемент)
	
	ЭлементыФормы.Подсказка.Заголовок = ?(Элемент.ТекущиеДанные = Неопределено, "", Элемент.ТекущиеДанные.Подсказка);
	
КонецПроцедуры // ДеревоМакетовПечатиПриАктивизацииСтроки()

// Процедура - обработчик события "Нажатие" элемента формы Печать.
// Закрывает форму. Возвращает значение Истина. Признак выбора макета для печати.
//
Процедура ПечатьНажатие(Элемент)
	
	Закрыть(Истина);
	
КонецПроцедуры // ПечатьНажатие()

// Процедура - обработчик события "Выбор" элемента формы ДеревоМакетовПечати.
// Закрывает форму. Возвращает значение Истина. Признак выбора макета для печати.
//
Процедура ДеревоМакетовПечатиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Закрыть(Истина);
	
КонецПроцедуры // ДеревоМакетовПечатиВыбор()

// Процедура - обработчик события "ПриВыводеСтроки" элемента формы ДеревоМакетовПечати.
// Устанавливает выделенный шрифт помеченным строкам.
//
Процедура ДеревоМакетовПечатиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.Пометка Тогда
		ОформлениеСтроки.Шрифт = Новый Шрифт(,,Истина);
	КонецЕсли;
	
КонецПроцедуры // ДеревоМакетовПечатиПриВыводеСтроки()

// Процедура - обработчик события "Нажатие" элемента формы ПоУмолчанию.
// Устанавливает макет печати по умолчанию.
//
Процедура ПоУмолчаниюНажатие(Элемент)
	
	// Устнанавливает пометку текущей строке дерева макетов печати.
	// Снимает пометку у остальных строк
	ТекущиеДанные = ЭлементыФормы.ДеревоМакетовПечати.ТекущиеДанные;
	Для каждого Строка Из ДеревоМакетовПечати.Строки Цикл
		Если Строка = ТекущиеДанные Тогда
			Строка.Пометка = Истина;
		ИначеЕсли Строка.Пометка Тогда
			Строка.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Запоминает макет печать по умолчанию для текущего объекта метаданных.
	ИмяМетаданных = КлючУникальности.Метаданные().Имя;
	Если ТекущиеДанные = Неопределено Тогда
		СохранитьЗначение(ИмяМетаданных + "МакетПечати", Ложь);
	Иначе
		СохранитьЗначение(ИмяМетаданных + "МакетПечати", ТекущиеДанные.Текст);
		ВладелецФормы.ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ДействиеПечать.Текст = ТекущиеДанные.Текст;
	КонецЕсли;
	
КонецПроцедуры // ПоУмолчаниюНажатие()
