
Процедура ДобавитьПодчиненныеЭлементы(МассивРодителейПользователей, МассивЭлементов)

	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Пользователи.Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	НЕ Пользователи.ЭтоГруппа
	|	И
	|	Пользователи.Ссылка В ИЕРАРХИИ(&РодителиПользователей)
	|";
	
	Запрос.УстановитьПараметр("РодителиПользователей", МассивРодителейПользователей);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивЭлементов.Добавить(Выборка.Ссылка);
	КонецЦикла;

КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	МассивЭлементов = Новый Массив;
	МассивРодителейПользователей = Новый Массив;;
	
	Для каждого ВыделеннаяСтрока Из ЭлементыФормы.ГруппыПользователей.ВыделенныеСтроки Цикл
		Если ЗначениеЗаполнено(ВыделеннаяСтрока) Тогда
			МассивЭлементов.Добавить(ВыделеннаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ВыделеннаяСтрока Из ЭлементыФормы.Пользователи.ВыделенныеСтроки Цикл
		Если ВыделеннаяСтрока.ЭтоГруппа Тогда
			МассивРодителейПользователей.Добавить(ВыделеннаяСтрока);
		Иначе
			МассивЭлементов.Добавить(ВыделеннаяСтрока);
		КонецЕсли; 
	КонецЦикла;
	
	ДобавитьПодчиненныеЭлементы(МассивРодителейПользователей, МассивЭлементов);
	
	Закрыть(МассивЭлементов);
	
КонецПроцедуры

Процедура ГруппыПользователейВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ВыбраннаяСтрока) Тогда
		
		МассивЗначений = Новый Массив;
		МассивЗначений.Добавить(ВыбраннаяСтрока);
		
		Закрыть(МассивЗначений);
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПользователиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбраннаяСтрока) = Тип("СправочникСсылка.Пользователи") Тогда
		МассивЗначений = Новый Массив;
		Если ВыбраннаяСтрока.ЭтоГруппа Тогда
			МассивРодителей = Новый Массив;
			МассивРодителей.Добавить(ВыбраннаяСтрока);
			ДобавитьПодчиненныеЭлементы(МассивРодителей, МассивЗначений);
		Иначе
			МассивЗначений.Добавить(ВыбраннаяСтрока);
		КонецЕсли;
		Закрыть(МассивЗначений);
	КонецЕсли; 
	
КонецПроцедуры
