
Перем мКнопкаПереключенияРежимов;

// Процедура изменяет режим отображения списков выбора.
//
Процедура УстановитьРежимОтображенияСписка()

	Если мКнопкаПереключенияРежимов.Пометка Тогда
		ЭлементыФормы.ПанельТабличныхПолей.ТекущаяСтраница = ЭлементыФормы.ПанельТабличныхПолей.Страницы.СписокВыбораПоПользователю;
		ЭлементыФормы.ДействияФормы.ИсточникДействий = ЭлементыФормы.СправочникСписокПоПользователю;
	Иначе
		ЭлементыФормы.ПанельТабличныхПолей.ТекущаяСтраница = ЭлементыФормы.ПанельТабличныхПолей.Страницы.СписокВыбора;
		ЭлементыФормы.ДействияФормы.ИсточникДействий = ЭлементыФормы.СправочникСписок;
	КонецЕсли;
	ЭлементыФормы.ДействияФормы.ЦветФона = ЦветаСтиля.ЦветФонаКнопки;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события формы "ПриОткрытии".
//
Процедура ПриОткрытии()
	//Если ПолучитьЗначениеПоУмолчанию(глТекущийПользователь,"ОткрыватьФормуВыбораСправочникаКонтрагентыСОтборомПоТекущемуПользователю") Тогда
	//	мКнопкаПереключенияРежимов.Пометка = Истина;
	//КонецЕсли;
	
	//СправочникСписокПоПользователю.Отбор.ОсновнойМенеджерПокупателя.Использование = Истина;
	//СправочникСписокПоПользователю.Отбор.ОсновнойМенеджерПокупателя.ВидСравнения  = ВидСравнения.Равно;
	//СправочникСписокПоПользователю.Отбор.ОсновнойМенеджерПокупателя.Значение      = глТекущийПользователь;
	//ЭлементыФормы.СправочникСписокПоПользователю.НастройкаОтбора.ОсновнойМенеджерПокупателя.Доступность = Ложь;
	
	УстановитьРежимОтображенияСписка();
	
	Если ЭлементыФормы.СправочникСписок.ТекущиеДанные <> Неопределено
	   И ЗначениеЗаполнено(ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка)
	   И НЕ ЭлементыФормы.СправочникСписок.ТекущиеДанные.ЭтоГруппа Тогда
		ЭлементыФормы.СправочникСписокПоПользователю.ТекущаяСтрока = ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка;
	КонецЕсли; 

КонецПроцедуры

// Процедура - обработчик события формы "ПередОткрытием".
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	мКнопкаПереключенияРежимов = ЭлементыФормы.ДействияФормы.Кнопки.Подменю.Кнопки.ОтборПоМенеджеру;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура - обработчик события "Нажатие" элемента формы "ДействияФормы.ОтборПоМенеджеру".
//
Процедура ДействияФормыОтборПоМенеджеру(Кнопка)
	
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	
	УстановитьРежимОтображенияСписка();
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" элемента формы "ДействияФормы.Выбрать".
//
Процедура ДействияФормыВыбрать(Кнопка)
	
	Если мКнопкаПереключенияРежимов.Пометка Тогда
		Если ЭлементыФормы.СправочникСписокПоПользователю.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		КонтрагентВыбора = ЭлементыФормы.СправочникСписокПоПользователю.ТекущиеДанные.Ссылка;
	Иначе
		Если ЭлементыФормы.СправочникСписок.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если ТипЗнч(ВладелецФормы) = Тип("ПолеВвода")
		  И ((ВладелецФормы.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы И НЕ ЭлементыФормы.СправочникСписок.ТекущиеДанные.ЭтоГруппа)
		  ИЛИ (ВладелецФормы.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы И ЭлементыФормы.СправочникСписок.ТекущиеДанные.ЭтоГруппа)) Тогда
			Возврат;
		КонецЕсли; 
		КонтрагентВыбора = ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	ОповеститьОВыборе(КонтрагентВыбора);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "ПриАктивизацииСтроки" элемента формы "СправочникСписок".
//
Процедура СправочникСписокПриАктивизацииСтроки(Элемент)
	
	ЭлементыФормы.СправочникДерево.ТекущаяСтрока = ЭлементыФормы.СправочникСписок.ТекущийРодитель;
	
КонецПроцедуры

// Процедура - обработчик события "Выбор" элемента формы "СправочникСписокПоПользователю".
//
Процедура СправочникСписокПоПользователюВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ОповеститьОВыборе(Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыПодбор(Кнопка)
	
	Справочники.Контрагенты.ПолучитьФорму("ФормаПодбораИзКлассификатора", ЭтаФорма, "ФормаПодбораИзКлассификатора").Открыть();
	
КонецПроцедуры

