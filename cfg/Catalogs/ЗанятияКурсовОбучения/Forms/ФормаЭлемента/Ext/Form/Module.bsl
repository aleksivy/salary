
Перем НомераИзмененныхСтрок;  // Хранит номера строк, веса в которых изменены пользователем 

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура итерационным способом распределяет веса
// Веса, введенные пользователем, не изменяются.
// Процедура работет, если пользователь ввел весов на сумму меньшую 100.
// В ряде случаев процедура снимает пометку с кнопки автоматического распределения весов.
//
// Параметры
//  НЕТ
//
Процедура КорректировкаВесов()
	
	// Создадим таблицу корректировки
	// | Номер строки | нормированный вес | текущий вес | заполнение |
	
	ТабКорректировки = новый ТаблицаЗначений;
	ТабКорректировки.Колонки.Добавить("НомерСтроки");
	ТабКорректировки.Колонки.Добавить("НомированныйВес");
	ТабКорректировки.Колонки.Добавить("ТекущийВес");
	ТабКорректировки.Колонки.Добавить("Заполнение");
	
	РазмерВведеныхПроценов = 0; // Количество процентов, которые введены пользователем
	РазмерКорректируемыхПроцентов = 0;  // Размер распределяемых процентов
	КоличествоИзмененныхСтрок = 0; // Количество строк, в которых пользователь внес данные
	
	Для каждого строкаТЧ из ИзучаемыеКомпетенции Цикл
		
		Если НомераИзмененныхСтрок[строкаТЧ.НомерСтроки] = Неопределено Тогда
			СтрокаТАбКорректировки = ТабКорректировки.Добавить();
			СтрокаТАбКорректировки.НомерСтроки = строкаТЧ.НомерСтроки;
			СтрокаТАбКорректировки.НомированныйВес = строкаТЧ.Вес;
			СтрокаТАбКорректировки.ТекущийВес = 0.00000000;
			СтрокаТАбКорректировки.Заполнение = 0.00000000;
			
			РазмерКорректируемыхПроцентов = РазмерКорректируемыхПроцентов + строкаТЧ.Вес;
			КоличествоИзмененныхСтрок = КоличествоИзмененныхСтрок + 1;
		Иначе
			РазмерВведеныхПроценов = РазмерВведеныхПроценов + строкаТЧ.Вес;	
		КонецЕсли;
		
	КонецЦикла;
	
	// Если нет ни одной строки для коррекции - отключить коррекцию
	Если ТабКорректировки.Количество() < 1 Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.КоррекцияВесов.Пометка = Ложь;
		Возврат;
	КонецЕсли;
	
	// нормирование 
	Если  РазмерКорректируемыхПроцентов = 0 Тогда
		Для Каждого СтроиТБ из ТабКорректировки Цикл
			СтроиТБ.НомированныйВес = 0;	
		КонецЦикла;		
	Иначе
		Для Каждого СтроиТБ из ТабКорректировки Цикл
			СтроиТБ.НомированныйВес = СтроиТБ.НомированныйВес / РазмерКорректируемыхПроцентов;	
		КонецЦикла;
	КонецЕсли;
	
	РаспределяемыйВес = 100 - РазмерВведеныхПроценов; // Сколько процентов надо распределить 
	
	// Если граничный случай - процентов меньше, чем строк. Минимальный процент - 1%
	КоличествоСтрок = ИзучаемыеКомпетенции.Количество() - КоличествоИзмененныхСтрок;
	Если РаспределяемыйВес < КоличествоСтрок Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.КоррекцияВесов.Пометка = Ложь;
		Возврат	
	КонецЕсли;
	
	// Цикл распределения 
	// ПозицияРаспределения - счетчик итераций
	
	Для  ПозицияРаспределения = 1 по РаспределяемыйВес Цикл
		
		// Найти максимальный разрыв в распределении
		НомерСтрокиДобавления = 0;
		МаксимальныйРазрыв = 1;
		
		Для Индекс = 0 По ТабКорректировки.Количество() - 1 Цикл
			
			СтроиТБ = ТабКорректировки[Индекс];
			ТекущийРазрыв = СтроиТБ.ТекущийВес - СтроиТБ.НомированныйВес;
			
			Если ТекущийРазрыв <= МаксимальныйРазрыв Тогда
				НомерСтрокиДобавления = Индекс; 
				МаксимальныйРазрыв = ТекущийРазрыв;
			КонецЕсли;
			
		КонецЦикла;
		
		// Добавить
		СтроиТБ = ТабКорректировки[НомерСтрокиДобавления];
		СтроиТБ.Заполнение = СтроиТБ.Заполнение + 1;
		
		// Пересчитать	
		Для Каждого СтроиТБ Из ТабКорректировки Цикл
			СтроиТБ.ТекущийВес = СтроиТБ.Заполнение / (ПозицияРаспределения);	
		КонецЦикла;
		
	КонецЦикла;
	
	// Заполнить таблицу весов
	Для Каждого СтрокиТБ Из ТабКорректировки Цикл
		СтрокаТЧ = ИзучаемыеКомпетенции[СтрокиТБ.НомерСтроки - 1 ];
		СтрокаТЧ.Вес = СтрокиТБ.Заполнение;
	КонецЦикла;
	
КонецПроцедуры // КорректировкаВесов()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	НомераИзмененныхСтрок = Новый Соответствие;
КонецПроцедуры
             
Процедура ПередЗаписью(Отказ)
	
	// Проверка ввода наименования
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда 
		Сообщить(НСтр("ru='Не введено наименование занятия. Введите наименование';uk='Не введено найменування заняття. Введіть найменування'"),СтатусСообщения.ОченьВажное);
		Отказ = Истина;
	КонецЕсли;
	
	// проверка ввода времени
	Если НЕ ДлительностьЗанятия > 0 Тогда 
		Сообщить(НСтр("ru='Не введена длительность занятия. Введите длительность';uk='Не введена тривалість заняття. Введіть тривалість'"),СтатусСообщения.ОченьВажное);
		Отказ = Истина;
	КонецЕсли;
	
	
	// Проверка ввода компетенции
	Для каждого строкаТЧ из ИзучаемыеКомпетенции Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Компетенция) Тогда 
			НомерСтроки = СтрокаТЧ.НомерСтроки;
			Сообщить("В строке №" + Строка(НомерСтроки) + НСтр("ru=' не введена компетенция. Введите компетенцию';uk=' не введена компетенція. Введіть компетенцію'"),СтатусСообщения.ОченьВажное);	
			Отказ = Истина;	
		КонецЕсли;
	КонецЦикла;

	// Проверка превышения процентов
	Если ИзучаемыеКомпетенции.Итог("Вес") > 100 Тогда
		
		НомераИзмененныхСтрок.Очистить();
		ОтветНаВопрос = Вопрос(НСтр("ru='Сумма весов изучения компетенций превышает 100%. Скорректировать веса автоматически перед сохранением?';uk='Сума ваг вивчення компетенцій перевищує 100%. Скорегувати ваги автоматично перед збереженням?'"), РежимДиалогаВопрос.ОКОтмена);
		Если ОтветНаВопрос = КодВозвратаДиалога.ОК Тогда
			КорректировкаВесов();
		Иначе
			// Включить помощника
			ЭлементыФормы.КоманднаяПанель1.Кнопки.КоррекцияВесов.Пометка = Истина;
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

Процедура ДлительностьЗанятияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	Элемент.Значение = Элемент.Значение + Направление * 0.25;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ

Процедура ИзучаемыеКомпетенцииПослеУдаления(Элемент)
	
	// Удалить все зарегистрированные изменения весов
	// т.к. строки сдвигаются.
	НомераИзмененныхСтрок.Очистить();
	
	Если ЭлементыФормы.КоманднаяПанель1.Кнопки.КоррекцияВесов.Пометка Тогда 
	// скорректировать
		КорректировкаВесов();
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается при измении веса в ТЧ
//
Процедура ИзучаемыеКомпетенцииВесОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	НомераИзмененныхСтрок.Вставить(ЭлементыФормы.ИзучаемыеКомпетенции.ТекущаяСтрока.НомерСтроки,Истина);	
	
КонецПроцедуры

// Процедура вызывается при изменении веса в ТЧ 
Процедура ИзучаемыеКомпетенцииВесПриИзменении(Элемент)
	
	Если ЭлементыФормы.КоманднаяПанель1.Кнопки.КоррекцияВесов.Пометка Тогда 
	// скорректировать
		КорректировкаВесов();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ТЧ

Процедура КоманднаяПанель1КоррекцияВесов(Кнопка)
	
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	
	Если Кнопка.Пометка Тогда 
	// скорректировать
		КорректировкаВесов();
	КонецЕсли;
	
КонецПроцедуры


