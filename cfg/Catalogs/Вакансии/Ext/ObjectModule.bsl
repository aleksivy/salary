///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Заголовок для сообщений об ошибках записи.
	Заголовок	= НСтр("ru='Запись элемента справочника Вакансии';uk='Запис елемента довідника Вакансії'")
				+ ?(ПустаяСтрока(Код), "", НСтр("ru=' с кодом ';uk=' з кодом '") + Код)
				+ ?(ПустаяСтрока(Наименование), "", НСтр("ru=' и наименованием <';uk=' і найменуванням <'") + Наименование + ">");
	
	Если Должность.Пустая() Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указана должность!';uk='Не зазначена посада!'"), Отказ, Заголовок);
	КонецЕсли;
	
	РежимНабораПерсонала = Константы.РежимНабораПерсонала.Получить();
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Если Организация.Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указана организация!';uk='Не зазначена організація!'"), Отказ, Заголовок);
		КонецЕсли;
		Если Подразделение.Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указано подразделение организации!';uk='Не вказаний підрозділ організації!'"), Отказ, Заголовок);
		КонецЕсли;
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		Если Организация <> Подразделение.Владелец Тогда
			ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Указано подразделение, принадлежащее другой организации!';uk='Зазначений підрозділ, що належить іншій організації!'"), Отказ, Заголовок);
		КонецЕсли;
	Иначе
		Если Подразделение.Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указано подразделение!';uk='Не вказаний підрозділ!'"), Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
