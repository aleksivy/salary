////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Макет = Справочники.Банки.ПолучитьМакет("КлассификаторБанков");

	КодЯзыка = Локализация.КодЯзыкаИнформационнойБазы();
	Макет.КодЯзыкаМакета = КодЯзыка;
	
	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;

	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);

	ТабличныйДокумент.ФиксацияСверху = ТабличныйДокумент.Области.ОбластьРасшифровки.Верх - 1;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКодМФО         	= ТабличныйДокумент.Области.КодМФО;
	ОбластьНаименование 	= ТабличныйДокумент.Области.Наименование;
	ОбластьГород  			= ТабличныйДокумент.Области.Город;
	ОбластьАдрес  			= ТабличныйДокумент.Области.Адрес;
	ОбластьЕДРПОУ  			= ТабличныйДокумент.Области.ЕДРПОУ;
	ОбластьТелефоны			= ТабличныйДокумент.Области.Телефоны;
	ОбластьКоррСчет			= ТабличныйДокумент.Области.КоррСчет;

	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		КодМФО         	= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодМФО.        	Лево, ТекущаяОбласть.Низ, ОбластьКодМФО.        Право).Текст;
		Наименование 	= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.  	Лево, ТекущаяОбласть.Низ, ОбластьНаименование.  Право).Текст;
		Город  			= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьГород.  		Лево, ТекущаяОбласть.Низ, ОбластьГород.  		Право).Текст;
		Адрес  			= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьАдрес.  		Лево, ТекущаяОбласть.Низ, ОбластьАдрес.  		Право).Текст;
		ЕДРПОУ  		= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьЕДРПОУ.  		Лево, ТекущаяОбласть.Низ, ОбластьЕДРПОУ.  		Право).Текст;
		Телефоны		= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьТелефоны.  		Лево, ТекущаяОбласть.Низ, ОбластьТелефоны.  	Право).Текст;
		КоррСчет		= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКоррСчет.  		Лево, ТекущаяОбласть.Низ, ОбластьКоррСчет.  	Право).Текст;
		
		// Проверка наличия выбранного банка.

		Ссылка = Справочники.Банки.НайтиПоКоду(КодМФО);

		Если НЕ Ссылка.Пустая() Тогда
			Вопрос = НСтр("ru='В справочнике ""Банки"" уже существует элемент с кодом (МФО) ""';uk='У довіднику ""Банки"" вже існує елемент з кодом (МФО) ""'") + КодМФО + НСтр("ru='""! Вы уверны, что нужно добавить новый?';uk='""! Ви впевнені, що потрібно додати новий?'");
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );

			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
				Возврат;			
			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена  Тогда		
				Возврат;
			КонецЕсли;
		КонецЕсли;

		// Создание нового банка.

		ФормаНовогоЭлемента = Справочники.Банки.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );

		ФормаНовогоЭлемента.Код             = КодМФО;
		ФормаНовогоЭлемента.Наименование    = Наименование;
		ФормаНовогоЭлемента.Город       	= Город;
		ФормаНовогоЭлемента.Адрес       	= Адрес;
		ФормаНовогоЭлемента.КодПоЕДРПОУ     = ЕДРПОУ;
		ФормаНовогоЭлемента.Телефоны       	= Телефоны;
		ФормаНовогоЭлемента.КоррСчет       	= КоррСчет;

		ФормаНовогоЭлемента.Открыть();
	Иначе
		
		Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		Если Форма.Открыта() Тогда
			Форма.СписокВыбора.Очистить();
		Иначе			
			СтуктураКолонок = Новый Структура();
			СтуктураКолонок.Вставить("Код", Новый Структура("Заголовок, Ширина", "МФО", 5));
			СтуктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование"));
			СтуктураКолонок.Вставить("Город", Новый Структура("Заголовок, Ширина", "Город"));
			СтуктураКолонок.Вставить("Адрес", Новый Структура("Заголовок, Ширина", "Адрес"));
			СтуктураКолонок.Вставить("КодПоЕДРПОУ", Новый Структура("Заголовок, Ширина", "ЕДРПОУ"));
			СтуктураКолонок.Вставить("Телефоны", Новый Структура("Заголовок, Ширина", "Телефоны"));
			СтуктураКолонок.Вставить("КоррСчет", Новый Структура("Заголовок, Ширина", "Корр. счет"));
			СтуктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));	
			Форма.ТипСправочника = "Банки";
			Форма.НастроитьФорму(СтуктураКолонок);
		КонецЕсли;	
		
		СписокВыбора =  Форма.СписокВыбора;		
		
		Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
			
			КодМФО         	= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКодМФО.        	Лево, ТекущаяСтрока, ОбластьКодМФО.        	Право).Текст;
			Наименование 	= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименование.  	Лево, ТекущаяСтрока, ОбластьНаименование.  	Право).Текст;
			Город  			= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьГород.  			Лево, ТекущаяСтрока, ОбластьГород.  		Право).Текст;
			Адрес  			= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьАдрес.  			Лево, ТекущаяСтрока, ОбластьАдрес.  		Право).Текст;
			ЕДРПОУ  		= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьЕДРПОУ.  			Лево, ТекущаяСтрока, ОбластьЕДРПОУ.  		Право).Текст;
			Телефоны		= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьТелефоны.  		Лево, ТекущаяСтрока, ОбластьТелефоны.  		Право).Текст;
			КоррСчет		= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКоррСчет.  		Лево, ТекущаяСтрока, ОбластьКоррСчет.  		Право).Текст;
			
			ЭтоЧисло = Истина;
			Попытка
				КодКакЧисло = Число(КодМФО);
			Исключение
				ЭтоЧисло = Ложь;
			КонецПопытки;
			
			Если ЭтоЧисло Тогда
				СтрокаПодбора = СписокВыбора.Добавить();
				СтрокаПодбора.Код = КодМФО;				
				СтрокаПодбора.Наименование = СтрПолучитьСтроку(Наименование, 1);
				СтрокаПодбора.Город = СтрПолучитьСтроку(Город, 1);
				СтрокаПодбора.Адрес = СтрПолучитьСтроку(Адрес, 1);
				СтрокаПодбора.КодПоЕДРПОУ = СтрПолучитьСтроку(ЕДРПОУ, 1);
				СтрокаПодбора.Телефоны = СтрПолучитьСтроку(Телефоны, 1);
				СтрокаПодбора.КоррСчет = СтрПолучитьСтроку(КоррСчет, 1);
				
				СтрокаПодбора.Видимость = Справочники.Банки.НайтиПоКоду(КодМФО).Пустая();
				
				СтрокаПодбора.Переносить = СтрокаПодбора.Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Форма.Открыть();
		
	КонецЕсли;

КонецПроцедуры



