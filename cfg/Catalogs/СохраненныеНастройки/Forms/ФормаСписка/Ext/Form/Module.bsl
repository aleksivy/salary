Процедура ПриОткрытии()
	
	Наименование = НоваяСсылка;
	
	// Установка доступности порядка.
	СохранениеНастроек.УстановитьДоступностьПорядка(ЭлементыФормы.СправочникДерево);
	
КонецПроцедуры

Процедура СправочникДеревоПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = ЭлементыФормы.СправочникДерево.ТекущиеДанные;
		
		ТекущиеДанные.Владелец           = глЗначениеПеременной("глТекущийПользователь");
	КонецЕсли;
	
КонецПроцедуры

Процедура СправочникДеревоВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьВСохраненныеНастройки();
	
КонецПроцедуры

Процедура ДобавитьВСохраненныеНастройкиНажатие(Кнопка)
	
	ДобавитьВСохраненныеНастройки();
	
КонецПроцедуры

Процедура ДобавитьВСохраненныеНастройки()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СохраненныеНастройки.Ссылка
	|ИЗ
	|	Справочник.СохраненныеНастройки КАК СохраненныеНастройки
	|ГДЕ
	|	СохраненныеНастройки.НастраиваемыйОбъект = &НоваяСсылка
	|	И СохраненныеНастройки.Владелец = &ТекущийПользователь"
	);
	Запрос.УстановитьПараметр("НоваяСсылка", НоваяСсылка);
	Запрос.УстановитьПараметр("ТекущийПользователь", глЗначениеПеременной("глТекущийПользователь"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если Вопрос(НСтр("ru='Ссылка на элемент ""';uk='Посилання на елемент ""'") + НоваяСсылка + НСтр("ru='"" уже существует в сохраненных настройках!';uk='"" вже існує в збережених настройках!'") + Символы.ПС + НСтр("ru='Добавить еще одну ссылку?';uk='Додати ще одне посилання?'"), РежимДиалогаВопрос.ОКОтмена,,КодВозвратаДиалога.Отмена, НСтр("ru='Добавление в СохраненныеНастройки';uk='Додавання в СохраненныеНастройки'")) = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Объект = Справочники.СохраненныеНастройки.СоздатьЭлемент();
	Объект.Родитель = ЭлементыФормы.СправочникДерево.ТекущаяСтрока;
	Объект.Наименование = Наименование;
	Объект.НастраиваемыйОбъект = НоваяСсылка;
	Объект.Владелец = глЗначениеПеременной("глТекущийПользователь");
	Попытка
	
		Объект.Записать();
	
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки());
		Возврат;
		
	КонецПопытки;
	
	Закрыть(Объект.Ссылка);
	
КонецПроцедуры

