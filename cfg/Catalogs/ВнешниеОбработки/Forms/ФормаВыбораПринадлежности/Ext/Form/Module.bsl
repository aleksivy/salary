
Функция ПолучитьСписокВыбранныхОбъектов()
	
	СписокОбъектов = Новый СписокЗначений;
	
	Для Каждого Документ Из СписокДокументов Цикл
		
		Если Документ.Пометка Тогда
			СписокОбъектов.Добавить(Документ.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Справочник Из СписокСправочников Цикл
		
		Если Справочник.Пометка Тогда
			СписокОбъектов.Добавить(Справочник.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокОбъектов;
	
КонецФункции

Процедура ЗаполнитьСписокОбъектов()
	
	Если УжеВыбранные = Неопределено  Тогда
		УжеВыбранные = Новый Соответствие;
	КонецЕсли;
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		
		Если УжеВыбранные[Документ] = Неопределено Тогда
			СписокДокументов.Добавить(Документ, ?(Документ.Синоним="", Документ.Имя, Документ.Синоним));
		КонецЕсли;
		
	КонецЦикла;
	
	ДоступныеСправочники = Новый Массив;
	ДоступныеСправочники.Добавить(Метаданные.Справочники.Контрагенты);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.Номенклатура);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.ФизическиеЛица);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.СотрудникиОрганизаций);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.Должности);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.ДолжностиОрганизаций);
	
	Для Каждого Справочник Из ДоступныеСправочники Цикл
		
		Если УжеВыбранные[Справочник] = Неопределено Тогда
			СписокСправочников.Добавить(Справочник, ?(Справочник.Синоним="", Справочник.Имя, Справочник.Синоним));
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ РежимПодбора Тогда
		ЭлементыФормы.СписокДокументов.Колонки.Значение.ДанныеФлажка = "";
		ЭлементыФормы.СписокСправочников.Колонки.Значение.ДанныеФлажка = "";
	КонецЕсли;
	
	ЗаполнитьСписокОбъектов();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыКнопкаОК(Кнопка)
	
	Если НЕ РежимПодбора Тогда
		Если ЭлементыФормы.Панель1.ТекущаяСтраница.Имя = "Документы" Тогда
			ОповеститьОВыборе(ЭлементыФормы.СписокДокументов.ТекущаяСтрока.Значение);
		Иначе
			ОповеститьОВыборе(ЭлементыФормы.СписокСправочников.ТекущаяСтрока.Значение);
		КонецЕсли;
	Иначе
		ОповеститьОВыборе(ПолучитьСписокВыбранныхОбъектов());
	КонецЕсли;
	
КонецПроцедуры


Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если НЕ РежимПодбора Тогда
		ОповеститьОВыборе(ЭлементыФормы.СписокДокументов.ТекущаяСтрока.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокСправочниковВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если НЕ РежимПодбора Тогда
		ОповеститьОВыборе(ЭлементыФормы.СписокСправочников.ТекущаяСтрока.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	ЗаполнитьСписокОбъектов();
	
КонецПроцедуры






