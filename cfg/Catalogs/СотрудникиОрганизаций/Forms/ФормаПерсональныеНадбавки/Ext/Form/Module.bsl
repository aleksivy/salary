////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мФормаВыбораВидаНадбавки;// Форма выбора вида надбавки
Перем мСпособыРасчетов; // список способов расчета надбавок
Перем мСписокПредопределенныхВР; // список ненужных видов расчета
Перем мВалютаТарифнойСтавки;
Перем мСведенияОВидахРасчетаОснНачислений;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы
Процедура ПриОткрытии()
	
	СтруктураКолонок = Новый Структура();
	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок.Вставить("Надбавка");

	// Установить ограничение - изменять видимость колонок табличной части
	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.ПерсональныеНадбавки.Колонки, СтруктураКолонок);
		
	мВалютаТарифнойСтавки = Константы.ВалютаРегламентированногоУчета.Получить();
	
	// Установим видимость реквизитов в зависимости от уч.политики по персоналу организаций
	мМассивЭУ = Новый Массив();
	мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта1);
	мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта2);
	мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта3);
	мМассивЭУ.Добавить(ЭлементыФормы.ПерсональныеНадбавки.Колонки.Валюта4);
	РаботаСДиалогами.УстановитьВидимостьЭУПоУчетнойПолитикеПоПерсоналу(мМассивЭУ,глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"),Организация);
	
	Заголовок = НСтр("ru='Персональные надбавки: ';uk='Персональні надбавки: '")+СправочникОбъект.Наименование;

КонецПроцедуры

// Процедура - обработчик события "ОбработкаВыбора" формы
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)

	Если Источник = мФормаВыбораВидаНадбавки Тогда
		Если ЭлементыФормы.ПерсональныеНадбавки.ТекущаяСтрока  = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ЭлементыФормы.ПерсональныеНадбавки.ТекущаяСтрока.Надбавка  = ЗначениеВыбора;
		ЭлементыФормы.ПерсональныеНадбавки.ТекущаяСтрока.Валюта1   = мВалютаТарифнойСтавки;
		
		// для ночных и вчерних определим "процент отплаты"
		Если ЗначениеВыбора = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ДоплатаЗаВечерниеЧасы Тогда
			ЭлементыФормы.ПерсональныеНадбавки.ТекущаяСтрока.Показатель2 = 100*РегистрыСведений.ПараметрыРасчетаЗарплатыОрганизаций.Получить(Новый Структура ("Организация", Организация)).КоэффициентВечерних;
		ИначеЕсли ЗначениеВыбора = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ДоплатаЗаНочныеЧасы Тогда
			ЭлементыФормы.ПерсональныеНадбавки.ТекущаяСтрока.Показатель2 = 100*РегистрыСведений.ПараметрыРасчетаЗарплатыОрганизаций.Получить(Новый Структура ("Организация", Организация)).КоэффициентНочных;
		КонецЕсли;

	КонецЕсли; 

КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы
Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьПерсональныеНадбавки");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ ПерсональныеНадбавки

// Процедура - обработчик события "НачалоВыбора" поля ввода Надбавка
Процедура ПерсональныеНадбавкиНадбавкаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	мФормаВыбораВидаНадбавки = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПолучитьФормуВыбора("ФормаВыбора", ЭтаФорма, "дляДокументаТрудовойДоговор");

	мФормаВыбораВидаНадбавки.Отбор.СпособРасчета.ВидСравнения	= ВидСравнения.ВСписке;
	мФормаВыбораВидаНадбавки.Отбор.СпособРасчета.Значение		= мСпособыРасчетов;
	мФормаВыбораВидаНадбавки.Отбор.СпособРасчета.Использование	= Истина;
	
	мФормаВыбораВидаНадбавки.Открыть();
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

// Процедура - обработчик события "ПриВыводеСтроки" ТЧ
//
Процедура ПерсональныеНадбавкиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если Не ЗначениеЗаполнено(ДанныеСтроки.Надбавка) Тогда
		Для СчПоказателей = 1 По 6 Цикл
			ОформлениеСтроки.Ячейки["НаименованиеПоказатель" + СчПоказателей].Видимость		= Ложь;
			ОформлениеСтроки.Ячейки["Показатель" + СчПоказателей].Видимость					= Ложь;		
			ОформлениеСтроки.Ячейки["Валюта" + СчПоказателей].Видимость						= Ложь;
			ОформлениеСтроки.Ячейки["НаименованиеПоказательТР" + СчПоказателей].Видимость	= Ложь;	
			ОформлениеСтроки.Ячейки["ТарифныйРазряд" +СчПоказателей].Видимость				= Ложь;
		КонецЦикла;
	Иначе
		РаботаСДиалогами.ПриВыводеСтрокиПлановыхНачисленийИУдержаний(Элемент, ОформлениеСтроки, ДанныеСтроки, мСведенияОВидахРасчетаОснНачислений);
	КонецЕсли;	

КонецПроцедуры //ПерсональныеНадбавкиПриВыводеСтроки



////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСпособыРасчетов = ПроведениеРасчетов.ПолучитьСписокВариантовНадбавок();
мСписокПредопределенныхВР = ПроведениеРасчетов.ПредопределенныеНачисленияОрганизации();

мСведенияОВидахРасчетаОснНачислений = Новый Соответствие;


