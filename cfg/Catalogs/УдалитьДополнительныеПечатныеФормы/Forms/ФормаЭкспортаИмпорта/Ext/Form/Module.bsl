Процедура ОсновныеДействияФормыКнопкаЭкспорт(Кнопка)
	
	Файл = Новый ЗаписьXML;
	Файл.ОткрытьФайл(ИмяФайла);
	Файл.ЗаписатьОбъявлениеXML();
	Файл.ЗаписатьКомментарий("Файл выгрузки дополнительных печатных форм");
	Файл.ЗаписатьНачалоЭлемента("ExtPrnFormExport");
	
	индекс = 0;
	Выборка = Справочники.УдалитьДополнительныеПечатныеФормы.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		Файл.ЗаписатьНачалоЭлемента("ExtPrnForm");
		Файл.ЗаписатьАтрибут("Description", Выборка.Наименование);
		Файл.ЗаписатьАтрибут("ExtProcFile", XMLСтрока(Выборка.ХранилищеВнешнейОбработки.Получить()));
		
		Для Каждого ПринадлежностьОбъект Из Выборка.Принадлежность Цикл
			Файл.ЗаписатьНачалоЭлемента("ExtPrnFormMetadata");
			Файл.ЗаписатьТекст(ПринадлежностьОбъект.СсылкаОбъекта.Метаданные().ПолноеИмя());
			Файл.ЗаписатьКонецЭлемента();
		КонецЦикла;		
		Файл.ЗаписатьКонецЭлемента();		
		индекс = индекс + 1;
	КонецЦикла;
		
	Файл.ЗаписатьКонецЭлемента();	
	
КонецПроцедуры

Процедура ОсновныеДействияФормыКнопкаИмпорт(Кнопка)
	
	Файл = Новый ЧтениеXML;
	Файл.ОткрытьФайл(ИмяФайла);
	Пока Файл.Прочитать() Цикл
		Если (Файл.ТипУзла = ТипУзлаXML.НачалоЭлемента) и (Файл.Имя = "ExtPrnFormExport") Тогда							
			Продолжить;
			
		ИначеЕсли (Файл.ТипУзла = ТипУзлаXML.НачалоЭлемента) и (Файл.Имя = "ExtPrnForm") Тогда			
			ЭлементВнешнейПечатнойФормы = Справочники.УдалитьДополнительныеПечатныеФормы.СоздатьЭлемент();
			ЭлементВнешнейПечатнойФормы.Наименование = Файл.ПолучитьАтрибут("Description");
			ЭлементВнешнейПечатнойФормы.ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(XMLЗначение(Тип("ДвоичныеДанные"), 
																										Файл.ПолучитьАтрибут("ExtProcFile")));				
			Продолжить;			
			
		ИначеЕсли (Файл.ТипУзла = ТипУзлаXML.НачалоЭлемента) и (Файл.Имя = "ExtPrnFormMetadata") Тогда			
			НоваяСтрока = ЭлементВнешнейПечатнойФормы.Принадлежность.Добавить();			
			
		ИначеЕсли (Файл.ТипУзла = ТипУзлаXML.Текст) Тогда			
			МетаданныеОбъекта = Неопределено;
			Попытка
				МетаданныеОбъекта = Метаданные.НайтиПоТипу(Тип(СтрЗаменить(Файл.Значение, ".", "Ссылка.")));
			Исключение
				Сообщить(НСтр("ru='Неудачная попытка установки принадлежности для печатной формы ""';uk='Невдала спроба встановлення приналежності для друкованої форми ""'")+ЭлементВнешнейПечатнойФормы.Наименование+"""", СтатусСообщения.Важное);
				Сообщить(НСтр("ru='Тип ""';uk='Тип ""'")+Файл.Значение+НСтр("ru='"" не определен.';uk='"" не визначений.'"), СтатусСообщения.Важное);
				ЭлементВнешнейПечатнойФормы.Принадлежность.Удалить(НоваяСтрока);
			КонецПопытки;
			
			Если МетаданныеОбъекта <> Неопределено Тогда
				НоваяСтрока.СсылкаОбъекта = Новый (СтрЗаменить(Файл.Значение, ".", "Ссылка."));
				Если Метаданные.Документы.Содержит(МетаданныеОбъекта) Тогда
					НоваяСтрока.ПредставлениеОбъекта = "Доукумент """ + ?(МетаданныеОбъекта.Синоним = "", МетаданныеОбъекта.Имя, МетаданныеОбъекта.Синоним) + """";
				ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
					НоваяСтрока.ПредставлениеОбъекта = "Справочник """ + ?(МетаданныеОбъекта.Синоним = "", МетаданныеОбъекта.Имя, МетаданныеОбъекта.Синоним) + """";
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли (Файл.ТипУзла = ТипУзлаXML.КонецЭлемента) и (Файл.Имя = "ExtPrnForm") Тогда
			ЭлементВнешнейПечатнойФормы.Записать();
			
		КонецЕсли;		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ИмяФайлаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФайла.Фильтр = "Файл данных(*.xml)|*.xml";
	ДиалогФайла.Расширение = "xml";
	ЕСли ДиалогФайла.Выбрать() Тогда
		ИмяФайла = ДиалогФайла.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры



