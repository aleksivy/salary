Перем мВалютаРегламентированногоУчета;

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Заполняет реквизит "ТекстКорреспондента"
 //
 // Параметры:
 //  Нет.
 //
Процедура ЗаполнитьТекстКорреспондента()

	мВладелец     = ЭтотОбъект.Владелец;

	Если НЕ ЗначениеЗаполнено(мВладелец) Тогда
		Возврат;
	КонецЕсли;

	ТипВладельца = ТипЗнч(мВладелец);
	Если ТипВладельца = Тип("СправочникСсылка.Организации") Тогда
		СтрКорреспондента = СокрЛП(мВладелец.НаименованиеПолное);
	ИначеЕсли ТипВладельца = Тип("СправочникСсылка.Контрагенты") Тогда
		СтрКорреспондента = СокрЛП(мВладелец.НаименованиеПолное);
	КонецЕсли;	

	Если НЕ БанкДляРасчетов.Пустая() Тогда
		СтрКорреспондента = СтрКорреспондента + " р/с " + НомерСчета
			+ " в " + Банк + " " + Банк.Город;
	КонецЕсли;	

	ТекстКорреспондента = СтрКорреспондента;	

КонецПроцедуры // ЗаполнитьТекстКорреспондента()

// Управляет видимостью некоторых элементов формы
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()

	Если (НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств))
	 ИЛИ (ВалютаДенежныхСредств = мВалютаРегламентированногоУчета) Тогда
		ЭлементыФормы.НомерИДатаРазрешения.Видимость = Ложь;
		ЭлементыФормы.НадписьНомерИДатаРазрешения.Видимость = Ложь;

	Иначе
		ЭлементыФормы.НомерИДатаРазрешения.Видимость = Истина;
		ЭлементыФормы.НадписьНомерИДатаРазрешения.Видимость = Истина;

	КонецЕсли;

КонецПроцедуры // УстановитьВидимость()

Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда
 		Если НЕ ЗначениеЗаполнено(ВидСчета) Тогда
 			ВидСчета = ЭлементыФормы.ВидСчета.СписокВыбора[0];
 		КонецЕсли;
		ЗаполнитьТекстКорреспондента();
		Если НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда
			ВалютаДенежныхСредств = мВалютаРегламентированногоУчета;
		КонецЕсли;
		
	КонецЕсли;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);
	
 	УстановитьВидимость();
 	ЗакрыватьПриВыборе = Ложь;

КонецПроцедуры

// Обработчик события ПослеЗаписи формы.
//
Процедура ПослеЗаписи()
	
	Если ЭтаФорма.ВладелецФормы <> Неопределено Тогда
	
		ОповеститьОВыборе(Ссылка);
	
	КонецЕсли; 
	
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ДЕЙСТВИЙ КОМАНДНЫХ ПАНЕЛЕЙ ЭЛЕМЕНТОВ

 // Обработчик события "ПриИзменении" реквизита "БанкДляРасчетов"
 //
Процедура БанкДляРасчетовПриИзменении(Элемент)
	ЗаполнитьТекстКорреспондента();
КонецПроцедуры

 // Обработчик события "ПриИзменении" реквизита "НомерСчета"
 //
Процедура НомерСчетаПриИзменении(Элемент)
	НомерСчета = СокрЛП(НомерСчета);

	Если НЕ ЭтоНовый() 
		И НЕ ЗначениеЗаполнено(Ссылка.НомерСчетаУстаревший)
		И НомерСчета <> Ссылка.НомерСчета
		И УправлениеДенежнымиСредствами.НомерБанковскогоСчетаСоответствуетСтандартуIBAN(НомерСчета)
		И НЕ УправлениеДенежнымиСредствами.НомерБанковскогоСчетаСоответствуетСтандартуIBAN(Ссылка.НомерСчета) Тогда
		
		НомерСчетаУстаревший = Ссылка.НомерСчета;
		
	КонецЕсли;
	
	БанкПоСчету = УправлениеДенежнымиСредствами.ПолучитьБанкПоНомеруСчетаIBAN(НомерСчета);
	Если ЗначениеЗаполнено(БанкПоСчету) Тогда
		Банк = БанкПоСчету;
	КонецЕсли;
	ЗаполнитьТекстКорреспондента();
КонецПроцедуры

 // Обработчик события "ПриИзменении" реквизита "Банк"
 //
Процедура БанкПриИзменении(Элемент)
	ЗаполнитьТекстКорреспондента();
КонецПроцедуры

 // Обработчик события "ПриИзменении" реквизита "ВалютаДенежныхСредств"
 //
Процедура ВалютаДенежныхСредствПриИзменении(Элемент)

	УстановитьВидимость()

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

СписокВидСчета = ЭлементыФормы.ВидСчета.СписокВыбора;
СписокВидСчета.Добавить(НСтр("ru='Текущий';uk='Поточний'"));
СписокВидСчета.Добавить(НСтр("ru='Депозитный';uk='Депозитний'"));
СписокВидСчета.Добавить(НСтр("ru='Ссудный';uk='Позичковий'"));
СписокВидСчета.Добавить(НСтр("ru='Иной';uk='Інший'"));

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
