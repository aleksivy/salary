////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Макет = Справочники.ОснованияЛьготногоСтажа.ПолучитьМакет("Классификатор");

	КодЯзыка = Локализация.КодЯзыкаИнформационнойБазы();
	Макет.КодЯзыкаМакета = КодЯзыка;
	
	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКод          = ТабличныйДокумент.Области.КодПФ;
	ОбластьНаименование = ТабличныйДокумент.Области.Наименование;
	ОбластьРодитель = ТабличныйДокумент.Области.Родитель;
	
	КодПФ          = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКод.         Лево, ТекущаяОбласть.Низ, ОбластьКод.         Право).Текст;
	Наименование = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
	Родитель = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьРодитель.Лево, ТекущаяОбласть.Низ, ОбластьРодитель.Право).Текст;
	
	Попытка
		Группа = Справочники.ОснованияЛьготногоСтажа[Родитель] 
	Исключение
		Группа = Справочники.ОснованияЛьготногоСтажа.ПустаяСсылка();
	КонецПопытки;	
	
	// Проверка наличия выбранного элемента.
	Ссылка = Справочники.ОснованияЛьготногоСтажа.НайтиПоНаименованию(Наименование);

	Если НЕ Ссылка.Пустая() Тогда
		
		Вопрос = НСтр("ru='В справочнике ""Основания льготного стажа"" уже существует элемент с наименованием ""';uk='У довіднику ""Підстави пільгового стажу"" вже існує елемент з найменуванням ""'") + Наименование + НСтр("ru='""! Открыть существующий?';uk='""! Відкрити існуючий?'");
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
		
		Если      Ответ = КодВозвратаДиалога.Да Тогда
			
			Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
			Возврат;

		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;

	// Создание нового элемента справочника.
	ФормаНовогоЭлемента = Справочники.ОснованияЛьготногоСтажа.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
	ФормаНовогоЭлемента.Родитель     = Группа;
	ФормаНовогоЭлемента.Наименование = Наименование;
	ФормаНовогоЭлемента.КодПФ	     = КодПФ;
	ФормаНовогоЭлемента.Открыть();
	
КонецПроцедуры
