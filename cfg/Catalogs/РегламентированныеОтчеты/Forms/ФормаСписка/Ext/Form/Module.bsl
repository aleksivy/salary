
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция определяет тип указанного в реквизите ФайлВнешнегоОтчета 
// элемента справочника внешнего отчета (обработки).
//
// Параметры
//  ФайлВнешнегоОтчета - строка - имя файла, указанное в реквизите элемента;
//  Расширение         - строка - расширение файла;
//
// Возвращаемое значение:
// - число, принимает значения:
//      - 1 - в реквизите указано наименование полное наименование
//            файла внешней обработки;
//      - 2 - в реквизите указано имя встроенного в конфигурацию отчета;
//      - 0 - в иных случаях.
//
Функция ОпределитьТипОтчета(ФайлВнешнегоОтчета, Расширение = "")

	ТипОтчета = 0;

	Если Не ПустаяСтрока(ФайлВнешнегоОтчета) Тогда

		ФайлВнешОтчета = Новый Файл(ФайлВнешнегоОтчета);
		Если ФайлВнешОтчета.Существует() Тогда
			ТипОтчета  = 1;
			Расширение = ФайлВнешОтчета.Расширение;
		ИначеЕсли Метаданные.Отчеты.Найти(ФайлВнешнегоОтчета) <> Неопределено Тогда
			ТипОтчета  = 2;
		ИначеЕсли Метаданные.Документы.Найти(ФайлВнешнегоОтчета) <> Неопределено Тогда
			ТипОтчета  = 3;
		КонецЕсли;

		Если ТипОтчета = 0 Тогда
			Сообщить(НСтр("ru='Не найден отчет ';uk='Не знайдений звіт '") + ФайлВнешнегоОтчета, СтатусСообщения.Внимание);
		КонецЕсли;

	Иначе

		Сообщить(НСтр("ru='Не выбран файл внешнего отчета!';uk='Не обрано файл зовнішнього звіту!'"), СтатусСообщения.Внимание);

	КонецЕсли;
	
	Возврат ТипОтчета;

КонецФункции // ОпределитьТипОтчета()

// Процедура открывает форму внутреннего или внешнего отчета,
// табличный или текстовый документ по указанному в реквизите
// ИсточникОтчета элемента справочника имени внутреннего или 
// файла внешнего отчета, текстового или табличного документа.
//
// Параметры
//  ФайлВнешнегоОтчета - строка - имя файла, указанное в реквизите элемента.
//
Процедура ОткрытьФормуОтчета(ФайлВнешнегоОтчета)
	Перем Расширение;
	
	ПравоДоступаКОтчету = РегламентированнаяОтчетность.ПравоДоступаКРегламентированномуОтчету(ФайлВнешнегоОтчета);
	Если ПравоДоступаКОтчету = Ложь Тогда
		Предупреждение(НСтр("ru='Недостаточно прав!';uk='Недостатньо прав!'"));
		Возврат;
	ИначеЕсли ПравоДоступаКОтчету = Неопределено Тогда
		Предупреждение(НСтр("ru='Отчет не найден!';uk='Звіт не знайдений!'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетность.ЭтоВнешнийОтчет(ФайлВнешнегоОтчета) И Метаданные.Документы.Найти(ФайлВнешнегоОтчета) <> Неопределено Тогда // это внутренний отчет-документ
			
		ВнутреннийОтчет = Документы[ФайлВнешнегоОтчета];
		ВыбФормаОтчета  = ВнутреннийОтчет.ПолучитьФорму("ОсновнаяФорма");
		ВыбФормаОтчета.РежимВыбора = Ложь;
		ВыбФормаОтчета.Открыть();
		Возврат;
			
	КонецЕсли;
	
	ТекОтчет = РегламентированнаяОтчетность.РеглОтчеты(ФайлВнешнегоОтчета);
	Если ТекОтчет = Неопределено Тогда
		Сообщить(НСтр("ru='Не удалось открыть отчет!';uk='Не вдалося відкрити звіт!'"), СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	ТекФорма = РегламентированнаяОтчетность.ФормаРеглОтчета(ФайлВнешнегоОтчета);
	Если ТекФорма = Неопределено Тогда
		Сообщить(НСтр("ru='Не удалось открыть отчет!';uk='Не вдалося відкрити звіт!'"), СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	ТекФорма.РежимВыбора = Ложь;
	ТекФорма.Открыть();
	Возврат;

КонецПроцедуры // ОткрытьФормуОтчета()

// Процедура открывает форму списка сохраненных отчетов,
// отфильтрованный по виду отчета, указанного в реквизите
// ФайлВнешнегоОтчета текущего элемента справочника.
//
// Параметры
//  ФайлВнешнегоОтчета - строка - имя файла, указанное в реквизите элемента.
//
Процедура ОткрытьФормуСпискаОтчетов(ТекЭлемент)
	Перем Расширение;
	Перем НаименованиеОтчета;

	ИсточникОтчетаДляОтбора = ТекЭлемент.ИсточникОтчета;
	НаименованиеОтчетаДляОтбора = ТекЭлемент.Наименование;

	Если РегламентированнаяОтчетность.ПустоеЗначение(ИсточникОтчетаДляОтбора) Тогда
		Возврат;
	КонецЕсли;
	
	ФормаСпискаОтчетов = Документы.РегламентированныйОтчет.ПолучитьФормуСписка("ФормаСписка", ЭтаФорма, "дляПолученияСпискаОтчетов");

	ФормаСпискаОтчетов.ЭлементыФормы.СписокВидовОтчета.СписокВыбора.Добавить(ИсточникОтчетаДляОтбора, НаименованиеОтчетаДляОтбора);

	ФормаСпискаОтчетов.Отбор.ИсточникОтчета.Использование = Истина;
	ФормаСпискаОтчетов.Отбор.ИсточникОтчета.Значение      = ИсточникОтчетаДляОтбора;

	ФормаСпискаОтчетов.мРежимРаботы = "ВызваноИзСправочника";
	// фильтр по форме будет всегда
	ФормаСпискаОтчетов.ЭлементыФормы.ОтборПоФорме.Значение = Истина;

	// ФормаСпискаОтчетов.ЭлементыФормы.ОтборПоОрг.Значение = Истина;

	ФормаСпискаОтчетов.РежимВыбора = Ложь;
	ФормаСпискаОтчетов.Открыть();

КонецПроцедуры // ОткрытьФормуСпискаОтчетов()

// Отображает в форме описание регламентированного отчета.
//
// Параметры
//  ТекЭлемент  – СправочникСсылка – текущий элемент справочника.
//
Процедура ПоказатьОписаниеОтчета(ТекЭлемент)

	ТекущееОписание = ТекЭлемент.Описание;
	ЭлементыФормы.НадписьОписаниеОтчета.Значение = ТекущееОписание;

КонецПроцедуры // ПоказатьОписаниеОтчета()

// Управляет видимостью кнопок командной панели ДействияФормы.
//
// Параметры
//  ТекЭлемент  – СправочникСсылка – текущий элемент справочника.
//
Процедура УстановитьВидимостьКнопок(ТекЭлемент)

	Показать = Истина;

	Если ТекЭлемент.ЭтоГруппа Тогда
		Показать = Ложь;
	КонецЕсли;

	Если РежимВыбора Тогда
		Показать = Ложь;
	КонецЕсли;

	ЭлементыФормы.ДействияФормы.Кнопки.КоманднаяПанельОткрытьОтчет.Доступность  = Показать;
	ЭлементыФормы.ДействияФормы.Кнопки.КоманднаяПанельСписокОтчетов.Доступность = Показать;

КонецПроцедуры // УстановитьВидимостьКнопок(ТекЭлемент)

// <Описание процедуры>
//
// Параметры
//  <Параметр1>  – <Тип.Вид> – <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  – <Тип.Вид> – <описание параметра>
//                 <продолжение описания параметра>
//
Процедура ИзменитьКодЭлемента(Направление)

	СортировкаПоКоду = Истина;

	Если Порядок.Количество() = 0 Тогда
		СортировкаПоКоду = Ложь;
	ИначеЕсли Порядок.Получить(0).Представление <> "Код" Тогда
		СортировкаПоКоду = Ложь;
	КонецЕсли;

	Если Не СортировкаПоКоду Тогда
		Предупреждение(НСтр("ru='Для изменения порядка следования "
							"необходимо установить сортировку по коду.';uk='Для зміни порядку проходження "
							"необхідно встановити сортування по коду.'"));

		Если Вопрос(НСтр("ru='Установить сортировку по коду?';uk='Установити сортування по коду?'"), РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да) = КодВозвратаДиалога.Да Тогда
			Порядок.Установить("Код");
		КонецЕсли;

		Возврат;
	КонецЕсли;

	НаправлСорт = Порядок.Получить(0).Направление;

	Если НаправлСорт = НаправлениеСортировки.Возр Тогда
		Направление = Направление * (-1);
	КонецЕсли;

	ТекущиеДанные = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекОбъект = ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка.ПолучитьОбъект();
		ТекОбъект.ИзменитьКод(Направление);
	КонецЕсли;

КонецПроцедуры // ИзменитьКодЭлемента()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	Отбор.НеПоказыватьВСписке.Значение       = Ложь;
	Отбор.НеПоказыватьВСписке.Использование  = Истина;

	ЭлементыФормы.СправочникСписок.АвтоКонтекстноеМеню = Ложь;
	ЭлементыФормы.СправочникСписок.КонтекстноеМеню     = ЭлементыФормы.ДействияФормы;

	Порядок.Установить("Код");

КонецПроцедуры // ПриОткрытии()

// Обработчик события "ОбработкаЗаписиНовогоОбъекта" формы.
//
Процедура ОбработкаЗаписиНовогоОбъекта(Объект, Источник)

	ЭлементыФормы.СправочникСписок.ТекущаяСтрока = Объект.Ссылка;

КонецПроцедуры // ОбработкаЗаписиНовогоОбъекта()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Открыть отчет"
// командной панели формы.
//
Процедура ДействияФормыКоманднаяПанельОткрытьОтчет(Кнопка)

	ТекЭлемент = ЭлементыФормы.СправочникСписок.ТекущиеДанные;

	Если ТекЭлемент = Неопределено Тогда
		Предупреждение(НСтр("ru='Выберите отчет!';uk='Виберіть звіт!'"));
		Возврат;
	КонецЕсли;
	
	Если Не ТекЭлемент.ЭтоГруппа Тогда
		ОткрытьФормуОтчета(ТекЭлемент.ИсточникОтчета);
	КонецЕсли;

КонецПроцедуры // ДействияФормыКоманднаяПанельОткрытьОтчет()

// Процедура вызывается при нажатии кнопки "Список отчетов"
// командной панели формы.
//
Процедура ДействияФормыКоманднаяПанельСписокОтчетов(Кнопка)

	Обработки.ОбщиеОбъектыРегламентированнойОтчетности.ПолучитьФорму("УправлениеОтчетностью").Открыть();

КонецПроцедуры // ДействияФормыКоманднаяПанельСписокОтчетов()

// Процедура вызывается при нажатии кнопки "Скрыть"
// командной панели формы.
//
Процедура ДействияФормыКоманднаяПанельСкрыть(Кнопка)

	ТекДанные = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Предупреждение(НСтр("ru='Выберите отчет!';uk='Виберіть звіт!'"));
		Возврат;
	КонецЕсли;

	ТекЭлемент = ТекДанные.Ссылка;
	НачатьТранзакцию();
		
	Если ТекЭлемент.ЭтоГруппа Тогда

		Попытка
			СпрРеглОтчеты = Справочники.РегламентированныеОтчеты;

			Выборка = СпрРеглОтчеты.ВыбратьИерархически(ТекЭлемент,ТекЭлемент.Владелец);
			Пока Выборка.Следующий() Цикл
				ЭлементСправочника = Выборка.ПолучитьОбъект();
				ЭлементСправочника.НеПоказыватьВСписке = Истина;
				ЭлементСправочника.Записать();
			КонецЦикла;

			// теперь установим реквизит НеПоказыватьВСписке 
			// для самой группы отчетов
			ГруппаСправочника = ТекЭлемент.ПолучитьОбъект();
			ГруппаСправочника.НеПоказыватьВСписке = Истина;
			ГруппаСправочника.Записать();

		Исключение
			Предупреждение(НСтр("ru='Не удалось записать элемент справочника:"
								"';uk='Не вдалося записати елемент довідника:"
								"'") + ОписаниеОшибки());
			
		КонецПопытки;
	Иначе

		Попытка 
			ЭлементСправочника = ТекЭлемент.ПолучитьОбъект();
			ЭлементСправочника.НеПоказыватьВСписке = Истина;
			ЭлементСправочника.Записать();
		Исключение
			Предупреждение(НСтр("ru='Не удалось записать элемент справочника:"
								"';uk='Не вдалося записати елемент довідника:"
								"'") + ОписаниеОшибки());

			Возврат;
		КонецПопытки;
		
	КонецЕсли;

	ЗафиксироватьТранзакцию();
	
КонецПроцедуры // ДействияФормыКоманднаяПанельСкрыть()

// Процедура вызывается при нажатии кнопки "Показать"
// командной панели формы.
//
Процедура ДействияФормыКоманднаяПанельПоказатьСкрытые(Кнопка)
	
	ФормаВосстановления = Справочники.РегламентированныеОтчеты.ПолучитьФорму("ФормаВыбораЭлементовДляВосстановленияПоказа");
	ФормаВосстановления.ОткрытьМодально();
	
	Обновить();

КонецПроцедуры // ДействияФормыКоманднаяПанельПоказатьСкрытые()

// Процедура - обработчик события "Действие" кнопки 
// КоманднаяПанельПереместитьВверх командной панели формы.
//
Процедура ДействияФормыКоманднаяПанельПереместитьВверх(Кнопка)

	ИзменитьКодЭлемента(-1);

КонецПроцедуры // ДействияФормыКоманднаяПанельПереместитьВверх()

// Процедура - обработчик события "Действие" кнопки 
// КоманднаяПанельПереместитьВниз командной панели формы.
//
Процедура ДействияФормыКоманднаяПанельПереместитьВниз(Кнопка)

	ИзменитьКодЭлемента(1);

КонецПроцедуры // ДействияФормыКоманднаяПанельПереместитьВниз()

// Процедура - обработчик события "Действие" кнопки 
// КоманднаяПанельОбновить командной панели формы.
//
Процедура ДействияФормыКоманднаяПанельОбновить(Кнопка)

	ФормаОбработкаОбновлениеОтчетности = Обработки.ОбновлениеРегламентированнойОтчетности.ПолучитьФорму("ОсновнаяФорма", ЭтаФорма);
	ФормаОбработкаОбновлениеОтчетности.Открыть();

КонецПроцедуры // ДействияФормыКоманднаяПанельОбновить()


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ДИАЛОГА

// Процедура - обработчик события "Выбор" табличного поля "Список" формы.
//
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Не ВыбраннаяСтрока.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуОтчета(ВыбраннаяСтрока.ИсточникОтчета);
	КонецЕсли; 
	
КонецПроцедуры // СписокВыбор()

// Процедура - обработчик события "ПередНачаломИзменения" табличного поля "Список" формы.
//
Процедура СписокПередНачаломИзменения(Элемент, Отказ)

	Если Элемент.ТекущиеДанные.Ссылка.Предопределенный Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры // СписокПередНачаломИзменения()

// Процедура - обработчик события "ПриОкончанииРедактирования" табличного поля "Список" формы.
//
Процедура СписокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	Если Не ОтменаРедактирования Тогда
		ПоказатьОписаниеОтчета(Элемент.ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры // СписокПриОкончанииРедактирования()

// Процедура - обработчик события "ПередУдалением" табличного поля "Список" формы.
//
Процедура СписокПередУдалением(Элемент, Отказ)

	Если Элемент.ТекущиеДанные.Ссылка.Предопределенный Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры // СписокПередУдалением()

// Процедура - обработчик события "ПриАктивизацииСтроки" табличного поля "Список" формы.
//
Процедура СписокПриАктивизацииСтроки(Элемент)

	ТекущиеДанные = Элемент.ТекущиеДанные;

	Если Не ТекущиеДанные = Неопределено Тогда
		ПоказатьОписаниеОтчета(ТекущиеДанные);
		УстановитьВидимостьКнопок(ТекущиеДанные);
	КонецЕсли;
	
	ЭлементыФормы.СправочникДерево.ТекущаяСтрока = ЭлементыФормы.СправочникСписок.ТекущийРодитель;

КонецПроцедуры // СписокПриАктивизацииСтроки()

// Процедура - обработчик события "ЗаписанНовыйОбъект" табличного поля "Список" формы.
//
Процедура СписокОбработкаЗаписиНовогоОбъекта(Элемент, Объект, СтандартнаяОбработка)

	ЭлементыФормы.СправочникСписок.ТекущаяСтрока = Объект.Ссылка;

КонецПроцедуры // СписокОбработкаЗаписиНовогоОбъекта()


// Процедура - обарботчик события "ПриВыводеСтроки" табличного поля "СправочникСписок" формы.
// Меняет текст к колонке "Наименование", в случае, если выводимой строке соответствует элемент
// справочника с признаком использования внешнего отчета.
Процедура СправочникСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Наименование.Текст = ДанныеСтроки.Наименование + ?(ДанныеСтроки.ВнешнийОтчетИспользовать = Истина, НСтр("ru=' (внешний)';uk=' (зовнішній)'"), "");
	
КонецПроцедуры

СправочникСписок.Колонки.Добавить("Описание");
СправочникСписок.Колонки.Добавить("ВнешнийОтчетИспользовать");
СправочникСписок.Колонки.Добавить("ИсточникОтчета");
