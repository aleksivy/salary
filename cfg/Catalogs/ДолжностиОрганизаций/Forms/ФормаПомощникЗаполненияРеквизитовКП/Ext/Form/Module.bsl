
Процедура ОсновныеДействияФормыЗаписать(Кнопка)
	Для Каждого Строка ИЗ ТЗДолжности Цикл
		Если ЗначениеЗаполнено(Строка.НаименованиеПоКП) ИЛИ ЗначениеЗаполнено(Строка.КодЗКППТР) Тогда
			Должность = Строка.Должность.ПолучитьОбъект();
			Должность.НаименованиеПоКП = Строка.НаименованиеПоКП;
			Должность.КодЗКППТР = Строка.КодЗКППТР;
			Должность.Записать();		
		КонецЕсли;
	КонецЦикла;
	Закрыть();
КонецПроцедуры

Процедура ОсновныеДействияФормыОтмена(Кнопка)
	Закрыть();
КонецПроцедуры

Функция СписокСоответствияКодаЗКППТР(КодКП)
	
	НайденнаяСтрока = ТЗПодходящихПрофессий.Найти(КодКП,"КодКП");
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат Новый СписокЗначений();
	Иначе	
		Возврат НайденнаяСтрока.СписокКодовЗКППТР;
	КонецЕсли;	

КонецФункции

Функция СписокСоответствияНаименованияПоКП(КодКП)
	
	НайденнаяСтрока = ТЗПодходящихПрофессий.Найти(КодКП,"КодКП");
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат Новый СписокЗначений();
	Иначе	
		Возврат НайденнаяСтрока.СписокНаименованийПоКП;
	КонецЕсли;	

КонецФункции

Процедура ЗаполнитьТЗПодходящихПрофессий()
	
	Макет = Справочники.ДолжностиОрганизаций.ПолучитьМакет("КлассификаторПрофессийИДолжностей");
	Если ТЗПодходящихПрофессий.Колонки.Количество() = 0 Тогда  
		ТЗПодходящихПрофессий.Колонки.Добавить("Должность");
		ТЗПодходящихПрофессий.Колонки.Добавить("КодКП");
		ТЗПодходящихПрофессий.Колонки.Добавить("СписокКодовЗКППТР");
		ТЗПодходящихПрофессий.Колонки.Добавить("СписокНаименованийПоКП");
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ДолжностиОрганизаций.Ссылка КАК Ссылка,
		|	ДолжностиОрганизаций.Наименование КАК Наименование,
		|	ДолжностиОрганизаций.КодКП КАК КодКП,
		|	ДолжностиОрганизаций.КодЗКППТР,
		|	ДолжностиОрганизаций.НаименованиеПоКП
		|ИЗ
		|	Справочник.ДолжностиОрганизаций КАК ДолжностиОрганизаций
		|УПОРЯДОЧИТЬ ПО
		|	НаименованиеПоКП";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(Выборка.КодКП) Тогда
				Продолжить
			Иначе	
				Если НЕ ЗначениеЗаполнено(Выборка.КодЗКППТР) И НЕ ЗначениеЗаполнено(Выборка.НаименованиеПоКП) Тогда
					НоваяДолжность = ТЗПодходящихПрофессий.Добавить();
					НоваяДолжность.Должность = Выборка.Ссылка;
					НоваяДолжность.КодКП = Выборка.КодКП;
					СписокКодовЗКППТР = Новый СписокЗначений();
					СписокНаименованийПоКП = Новый СписокЗначений();
					
					ТекущаяСтрока = 0;
					ТЗ = Макет.ТекущаяОбласть;
					Пока ТекущаяСтрока < Макет.ВысотаТаблицы Цикл
						ТЗ = Макет.НайтиТекст(СокрЛП(Выборка.КодКП), ТЗ,,,,,Истина);		
						Если ТЗ = Неопределено Тогда
							Прервать;
						КонецЕсли;                 
						
						СтрокаКодЗКППТР = "R" + Формат(ТЗ.Верх,"ЧГ=0")+"C3";
						СтрокаНаименованиеПоКП = "R" +  Формат(ТЗ.Верх,"ЧГ=0")+"C6";
						Если НЕ (СокрЛП(Макет.Область(СтрокаКодЗКППТР).Текст) = "" ИЛИ СокрЛП(Макет.Область(СтрокаКодЗКППТР).Текст) = "-") Тогда 
							СписокКодовЗКППТР.Добавить(СокрЛП(Макет.Область(СтрокаКодЗКППТР).Текст)); 
						КонецЕсли;	
						СписокНаименованийПоКП.Добавить(СокрЛП(Макет.Область(СтрокаНаименованиеПоКП).Текст));
						
						ТекущаяСтрока = ТЗ.Верх;
					КонецЦикла;
					СписокКодовЗКППТР.СортироватьПоЗначению(НаправлениеСортировки.Возр);
					СписокНаименованийПоКП.СортироватьПоЗначению(НаправлениеСортировки.Возр);
					
					НоваяДолжность.СписокКодовЗКППТР =  СписокКодовЗКППТР;
					НоваяДолжность.СписокНаименованийПоКП =  СписокНаименованийПоКП;
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	Если ТЗПодходящихПрофессий.Количество() = 0 Тогда 
		ЗаполнитьТЗПодходящихПрофессий();
		ПолучитьДолжностьИРекомендуемыеРеквизитыКП();
	КонецЕсли;
КонецПроцедуры

Процедура ПолучитьДолжностьИРекомендуемыеРеквизитыКП()
	
	Для Каждого СтрокаДолжности Из ТЗПодходящихПрофессий Цикл
		
		НоваяСтрока = ТЗДолжности.Добавить();
		НоваяСтрока.Должность= СтрокаДолжности.Должность;
		НоваяСтрока.КодКП 	 = СтрокаДолжности.КодКП;
		
		СписокКодовЗКППТР = СписокСоответствияКодаЗКППТР(СтрокаДолжности.КодКП);
		Если СписокКодовЗКППТР.Количество() > 0 Тогда
			НоваяСтрока.КодЗКППТР = СписокКодовЗКППТР[0].Значение;
		КонецЕсли;
		
		СписокНаименованийПоКП = СписокСоответствияНаименованияПоКП(СтрокаДолжности.КодКП);
		Если СписокНаименованийПоКП.Количество() > 0 Тогда
			НоваяСтрока.НаименованиеПоКП = СписокНаименованийПоКП[0].Значение;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ТабличноеПоле1НаименованиеПоКПНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Список = СписокСоответствияНаименованияПоКП(ЭлементыФормы.ТабличноеПоле1.ТекущиеДанные.КодКП);	
	ЭлементСписка = ВыбратьИзСписка(Список, Элемент, Список.НайтиПоЗначению(Элемент.Значение));
	Если ЭлементСписка <> Неопределено Тогда
		Элемент.Значение = ЭлементСписка.Значение;
	КонецЕсли;

КонецПроцедуры

Процедура ТабличноеПоле1КодЗКППТРНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Список = СписокСоответствияКодаЗКППТР(ЭлементыФормы.ТабличноеПоле1.ТекущиеДанные.КодКП);	
	ЭлементСписка = ВыбратьИзСписка(Список, Элемент, Список.НайтиПоЗначению(Элемент.Значение));
	
	Если ЭлементСписка <> Неопределено Тогда
		Элемент.Значение = ЭлементСписка.Значение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПоле1НаименованиеПоКПОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Элемент.Значение = ВыбранноеЗначение;

КонецПроцедуры

Процедура ТабличноеПоле1КодЗКППТРОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Элемент.Значение = ВыбранноеЗначение;
	
КонецПроцедуры











