////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыЗаписатьНаДиск(Кнопка)
	
	Конвертация = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если Конвертация = Неопределено Тогда
		Предупреждение(НСтр("ru='Выберите конвертацию, которую требуется сохранить на диск.';uk='Виберіть конвертацію, що потрібно зберегти на диск.'"));
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла				= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок		= НСтр("ru='Укажите каталог для записи файлов конвертации';uk='Укажіть каталог для запису файлів конвертації'");
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		Попытка
			КонвертацияПомощник				= Конвертация.ХранилищеПомощник.Получить();
			КонвертацияОписаниеПомощника	= Конвертация.ХранилищеОписаниеПомощника.Получить();
			КонвертацияОбработка			= Конвертация.ХранилищеОбработка.Получить();
			КонвертацияПравила				= Конвертация.ХранилищеПравила.Получить();
			
			Если	КонвертацияПомощник = Неопределено
				ИЛИ	КонвертацияОписаниеПомощника = Неопределено
				ИЛИ	КонвертацияОбработка = Неопределено
				ИЛИ	КонвертацияПравила = Неопределено Тогда
				КонвертацияПомощник				= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияПомощник + "_ert");
				КонвертацияОписаниеПомощника	= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияПомощник + "_efd");
				КонвертацияОбработка			= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияОбработка + "_ert");
				КонвертацияПравила				= Справочники.КонвертацииИзИнформационныхБаз1СПредприятия77.ПолучитьМакет(Конвертация.КонвертацияПравила + "_xml");
			КонецЕсли;
			
			КонвертацияПомощник.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".ert");
			КонвертацияОписаниеПомощника.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".efd");
			КонвертацияОбработка.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияОбработка + ".ert");
			КонвертацияПравила.Записать(ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПравила + ".xml");
			Предупреждение(НСтр("ru='Файлы конвертации записаны в каталог: ';uk='Файли конвертації записані в каталог: '")+ ДиалогВыбораФайла.Каталог);
		Исключение
			Предупреждение(НСтр("ru='Файлы конвертации записать не удалось.';uk='Файли конвертації записати не вдалося.'"));
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыЗаменитьФайлы(Кнопка)
	
	Конвертация = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если Конвертация = Неопределено Тогда
		Предупреждение(НСтр("ru='Выберите конвертацию, для которой требуется заменить файлы конвертации.';uk='Виберіть конвертацію, для якої потрібно замінити файли конвертації.'"));
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла				= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок		= НСтр("ru='Укажите каталог с файлами конвертации';uk='Вкажіть каталог з файлами конвертації'");
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ФайлПомощника			= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".ert";
		ФайлОписанияПомощника	= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПомощник + ".efd";
		ФайлОбработки			= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияОбработка + ".ert";
		ФайлПравил				= ДиалогВыбораФайла.Каталог + "\" + Конвертация.КонвертацияПравила + ".xml";
		
		Попытка
			ОбъектКонвертации	= Конвертация.Ссылка.ПолучитьОбъект();
			ОбъектКонвертации.ХранилищеПомощник				= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлПомощника));
			ОбъектКонвертации.ХранилищеОписаниеПомощника	= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлОписанияПомощника));
			ОбъектКонвертации.ХранилищеОбработка			= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлОбработки));
			ОбъектКонвертации.ХранилищеПравила				= Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлПравил));
			ОбъектКонвертации.Записать();
			Предупреждение(НСтр("ru='Файлы конвертации заменены.';uk='Файли конвертації замінені.'"));
		Исключение
			Предупреждение(НСтр("ru='Файлы конвертации заменить не удалось.';uk='Файли конвертації замінити не вдалося.'"));
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыВосставитьФайлы(Кнопка)
	
	Конвертация = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если Конвертация = Неопределено Тогда
		Предупреждение(НСтр("ru='Выберите конвертацию, для которой требуется восстановить файлы конвертации.';uk='Виберіть конвертацію, для якої потрібно відновити файли конвертації.'"));
		Возврат;
	КонецЕсли;
	
	Попытка
		ОбъектКонвертации	= Конвертация.Ссылка.ПолучитьОбъект();
		ОбъектКонвертации.ХранилищеПомощник				= Неопределено;
		ОбъектКонвертации.ХранилищеОписаниеПомощника	= Неопределено;
		ОбъектКонвертации.ХранилищеОбработка			= Неопределено;
		ОбъектКонвертации.ХранилищеПравила				= Неопределено;
		ОбъектКонвертации.Записать();
		Предупреждение(НСтр("ru='Файлы конвертации заменены.';uk='Файли конвертації замінені.'"));
	Исключение
		Предупреждение(НСтр("ru='Файлы конвертации заменить не удалось.';uk='Файли конвертації замінити не вдалося.'"));
	КонецПопытки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

СправочникСписок.Колонки.Добавить("ХранилищеПомощник");
СправочникСписок.Колонки.Добавить("ХранилищеОписаниеПомощника");
СправочникСписок.Колонки.Добавить("ХранилищеОбработка");
СправочникСписок.Колонки.Добавить("ХранилищеПравила");

