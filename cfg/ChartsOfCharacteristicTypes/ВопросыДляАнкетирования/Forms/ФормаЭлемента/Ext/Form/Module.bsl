Перем мТекущееОсновноеИзображение;
Перем мПустаяКартинка;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры
// Процедура устанавливает в поле картинки основное изображение вопроса
//
Процедура ПоказатьОсновноеИзображение() Экспорт
	
	Если мТекущееОсновноеИзображение = Неопределено Тогда
		ЭлементыФормы.ОсновноеИзображение.Картинка = мПустаяКартинка;
	Иначе
		ЭлементыФормы.ОсновноеИзображение.Картинка = мТекущееОсновноеИзображение;
	КонецЕсли;
	
КонецПроцедуры // ПоказатьОсновноеИзображение()

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	//Если ЭтоНовый() тогда
	//	ТипВопроса = Перечисления.ТипВопросаАнкеты.ОбычныйВопрос;
	//	ТипЗначения = Новый ОписаниеТипов("Строка");
	//КонецЕсли;
	ТипКонтактнойИнформации = ТипВидКонтакнойИнформации.Тип;
	ВидОбъектаКонтактнойИнформации = ТипВидКонтакнойИнформации.ВидОбъектаКонтактнойИнформации;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЭтоНовый() тогда
		КоличествоСтрокТаблицы  = 4;
		Длина                   = 60;
		ТипВопроса              = Перечисления.ТипВопросаАнкеты.СправочникСсылка_ВариантыОтветов;
	КонецЕсли;
	
	ОтветВВидеПриИзменении(ЭлементыФормы.ТипВопроса.Значение);
	ЭлементыФормы.ТабличноеПолеВариантыОтветов.Значение.Порядок.Очистить();
	ЭлементыФормы.ТабличноеПолеВариантыОтветов.Значение.Порядок.Установить("Код Возр");
	
	Попытка
		Если ВидОбъектаКонтактнойИнформации <> ЭтаФорма.ВладелецФормы.ВидОбъектаКонтактнойИнформации тогда
			ВидОбъектаКонтактнойИнформации = ЭтаФорма.ВладелецФормы.ВидОбъектаКонтактнойИнформации;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	мТекущееОсновноеИзображение = ОсновноеИзображение.Хранилище.Получить();
	ПоказатьОсновноеИзображение();
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ТипВопроса = Перечисления.ТипВопросаАнкеты.Табличный и КоличествоСтрокТаблицы < 1 Тогда
			Сообщить(НСтр("ru='Табличный вопрос может иметь одну и более строк. Отредактируйте количество строк вопроса.';uk='Табличне питання може мати один і більше рядків. Відредагуйте кількість рядків питання.'"));
			Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
Процедура ОтветВВидеПриИзменении(Значение)
	
	// длина строки
	ЭлементыФормы.Длина.Видимость		 		= Ложь;
	ЭлементыФормы.Надпись10.Видимость		 	= Ложь;
	ЭлементыФормы.Надпись11.Видимость		 	= Ложь;
	
	// Вес вопроса
	ЭлементыФормы.ТекстВесВопроса.Видимость		= Истина;
	ЭлементыФормы.ВесВопроса.Видимость			= Истина;
	
	ЭлементыФормы.ТипВопроса.Доступность 		= Истина;
	ЭлементыФормы.Надпись7.Доступность 			= Истина;
	ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.Страница4;
			
	Если Значение = Перечисления.ТипВопросаАнкеты.ПустаяСсылка() или 
		Значение =  Перечисления.ТипВопросаАнкеты.Число или
		Значение =  Перечисления.ТипВопросаАнкеты.Строка или
		Значение =  Перечисления.ТипВопросаАнкеты.Булево или
		Значение =  Перечисления.ТипВопросаАнкеты.Дата тогда
		ЭлементыФормы.ТекстВесВопроса.Видимость					= Ложь;
		ЭлементыФормы.ВесВопроса.Видимость						= Ложь;
	КонецЕсли;
	
	ВопросыКонтактнаяИнформация = Новый СписокЗначений();
	ВопросыКонтактнаяИнформация.Добавить(Перечисления.ТипВопросаАнкеты.Адрес);
	ВопросыКонтактнаяИнформация.Добавить(Перечисления.ТипВопросаАнкеты.АдресЭлектроннойПочты);
	ВопросыКонтактнаяИнформация.Добавить(Перечисления.ТипВопросаАнкеты.ВебСтраница);
	ВопросыКонтактнаяИнформация.Добавить(Перечисления.ТипВопросаАнкеты.Другое);
	ВопросыКонтактнаяИнформация.Добавить(Перечисления.ТипВопросаАнкеты.Телефон);

	Если (Предопределенный) и (Значение <>  Перечисления.ТипВопросаАнкеты.Табличный) тогда
		ЭлементыФормы.ТипВопроса.Доступность 					= Ложь;
		ЭлементыФормы.ТекстВесВопроса.Видимость					= Ложь;
		ЭлементыФормы.ВесВопроса.Видимость						= Ложь;
		Если Значение =  Перечисления.ТипВопросаАнкеты.Число ИЛИ Значение =  Перечисления.ТипВопросаАнкеты.Строка тогда
			ЭлементыФормы.Длина.Видимость		 		= Истина;
			ЭлементыФормы.Надпись10.Видимость		 	= Истина;
			ЭлементыФормы.Надпись11.Видимость		 	= Истина;
		КонецЕсли;
	ИначеЕсли Значение =  Перечисления.ТипВопросаАнкеты.Число тогда
		ЭлементыФормы.Длина.Видимость		 		= Истина;
		ЭлементыФормы.Надпись10.Видимость		 	= Истина;
		ЭлементыФормы.Надпись11.Видимость		 	= Истина;
		Если ТипЗначения <> Новый ОписаниеТипов("Число") тогда
			ТипЗначения = Новый ОписаниеТипов("Число");
		КонецЕсли;
	ИначеЕсли Значение =  Перечисления.ТипВопросаАнкеты.Строка тогда
		ЭлементыФормы.Длина.Видимость		 		= Истина;
		ЭлементыФормы.Надпись10.Видимость		 	= Истина;
		ЭлементыФормы.Надпись11.Видимость		 	= Истина;
		Если ТипЗначения <> Новый ОписаниеТипов("Строка") тогда
			ТипЗначения = Новый ОписаниеТипов("Строка");
		КонецЕсли;
	ИначеЕсли Значение =  Перечисления.ТипВопросаАнкеты.Текст тогда
		Если ТипЗначения <> Новый ОписаниеТипов("Строка") тогда
			ТипЗначения = Новый ОписаниеТипов("Строка");
		КонецЕсли;
	ИначеЕсли Значение =  Перечисления.ТипВопросаАнкеты.Дата тогда
		Если ТипЗначения <> Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата)) тогда
			ТипЗначения = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата));
		КонецЕсли;
	ИначеЕсли Значение =  Перечисления.ТипВопросаАнкеты.Булево тогда
		Если ТипЗначения <> Новый ОписаниеТипов("Булево") тогда
			ТипЗначения = Новый ОписаниеТипов("Булево");
		КонецЕсли;
	ИначеЕсли Значение =  Перечисления.ТипВопросаАнкеты.Табличный тогда
		// табличный вопрос
		Если ТипЗначения <> Новый ОписаниеТипов("Строка") тогда
			ТипЗначения = Новый ОписаниеТипов("Строка");
		КонецЕсли;
		Если Предопределенный тогда
			ЭлементыФормы.ТипВопроса.Доступность 	= Ложь;
			ЭлементыФормы.Надпись7.Доступность 		= Ложь;
			ЭлементыФормы.КолонкиТаблицы.ТолькоПросмотр = Истина;
		КонецЕсли;
		ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.Страница1;
	ИначеЕсли Значение =  Перечисления.ТипВопросаАнкеты.СправочникСсылка_ВариантыОтветов или 
			  Значение =  Перечисления.ТипВопросаАнкеты.СправочникСсылка_ВариантыОтветов_Несколько тогда
			  
		ЭлементыФормы.РамкаГруппы1.Заголовок = НСтр("ru='Варианты ответов';uk='Варіанти відповідей'");
		ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.Страница2;
		// вариант ответа
		Если Значение =  Перечисления.ТипВопросаАнкеты.СправочникСсылка_ВариантыОтветов тогда
			Если ТипЗначения <> Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов") тогда
				ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов");
			КонецЕсли;
		КонецЕсли;
		ЭлементыФормы.ТабличноеПолеВариантыОтветов.Видимость 	= Истина;
	ИначеЕсли ВопросыКонтактнаяИнформация.НайтиПоЗначению(Значение) <> Неопределено Тогда
		// это контактная информация
		ЭлементыФормы.ПанельВариантыОтвета.ТекущаяСтраница = ЭлементыФормы.ПанельВариантыОтвета.Страницы.Страница3;
		ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
		Если ТипЗначения <> ОписаниеТиповСтрока тогда
			ТипЗначения = ОписаниеТиповСтрока;
		КонецЕсли;
		Если Значение = Перечисления.ТипВопросаАнкеты.Адрес тогда
			ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Адрес;
		ИначеЕсли Значение = Перечисления.ТипВопросаАнкеты.АдресЭлектроннойПочты тогда
			ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		ИначеЕсли Значение = Перечисления.ТипВопросаАнкеты.ВебСтраница тогда
			ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.ВебСтраница;
		ИначеЕсли Значение = Перечисления.ТипВопросаАнкеты.Другое тогда
			ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Другое;
		ИначеЕсли Значение = Перечисления.ТипВопросаАнкеты.Телефон тогда
			ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
		КонецЕсли;
		
		ЭлементыФормы.ТекстВесВопроса.Видимость					= Ложь;
		ЭлементыФормы.ВесВопроса.Видимость						= Ложь;
		Если ТипКонтактнойИнформации <> ТипВидКонтакнойИнформации.Тип тогда
			// если к примеру сказали, что будет телефон, а у нас выбран адрес, тогда очистим
			ТипВидКонтакнойИнформации = Перечисления.ТипВопросаАнкеты.ПустаяСсылка();
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура ТипВопросаПриИзменении(Элемент)
	
	ОтветВВидеПриИзменении(Элемент.Значение);
	
КонецПроцедуры

//Процедура ТипВопросаНачалоВыбора(Элемент, СтандартнаяОбработка)
//	СтандартнаяОбработка	=	Ложь;
//КонецПроцедуры

Процедура ТипВидКонтакнойИнформацииНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(ТипКонтактнойИнформации) тогда
		Сообщить(НСтр("ru='Укажите тип контактной информации.';uk='Вкажіть тип контактної інформації.'"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидОбъектаКонтактнойИнформации) тогда
		Сообщить(НСтр("ru='Укажите вид объекта контактной информации.';uk=""Вкажіть вид об'єкта контактної інформації."""));
	КонецЕсли;
	
	УправлениеКонтактнойИнформацией.ОткрытьФормуВыбораВидаКИ(Истина, Элемент, ТипКонтактнойИнформации, ВидОбъектаКонтактнойИнформации);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЯ СПИСКА ПОДЧИНЕННОГО СПРАВОЧНИКА

// проверяем, записан ли вопрос
// иначе предлагаем записать - с тем, чтобы можно было 
// вводить записи подчиненного справочника
Процедура ТабличноеПолеВариантыОтветовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа)
	
	Если ЭтоНовый() Тогда
		Отказ = (Вопрос(НСтр("ru='Вопрос еще не записан! Записать?';uk='Питання ще не записане! Записати?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да,) <> КодВозвратаДиалога.Да);
		Если Не Отказ Тогда
			ЗаписатьВФорме();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеВариантыОтветовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ВариантыОтветов = Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов");
	Если ТипЗначения <> ВариантыОтветов тогда
		ТипЗначения = ВариантыОтветов;
		//ЭлементыФормы.ТипЗначения.Значение = ВариантыОтветов;
	КонецЕсли;
КонецПроцедуры

Процедура НаименованиеПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ПолнаяФормулировка) тогда
		ПолнаяФормулировка = Элемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновноеИзображениеНажатие(Элемент)
	
	Если ЭтоНовый() Тогда
		ОтветНаВопрос = Вопрос(НСтр("ru='Для добавления изображения необходимо записать элемент.';uk='Для додавання зображення необхідно записати елемент.'") + Символы.ПС + "Записать?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РаботаСФайлами.ОткрытьФормуИзображения(ЭтаФорма, ОсновноеИзображение, Ссылка);
	
КонецПроцедуры

// Обработчик события ОбработкаОповещения формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьФорму" и Источник = Ссылка Тогда
		
		ИмяОбновляемогоЭлемента = Параметр.ИмяЭлемента;
		
		Если ИмяОбновляемогоЭлемента = "ОсновноеИзображение" Тогда
			// обновляем картинку на первой странице
			Если мТекущееОсновноеИзображение <> ОсновноеИзображение.Хранилище.Получить() Тогда
				мТекущееОсновноеИзображение = ОсновноеИзображение.Хранилище.Получить();
				ПоказатьОсновноеИзображение();
			КонецЕсли;
					
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

// Обработчик события ОбработкаВыбора формы.
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
		
	Если ТипЗнч(ЗначениеВыбора) = Тип("СправочникСсылка.ХранилищеДополнительнойИнформации") Тогда
		
		Если НЕ ОсновноеИзображение = ЗначениеВыбора Тогда
			ОсновноеИзображение = ЗначениеВыбора;
		КонецЕсли;
		
		мТекущееОсновноеИзображение = ОсновноеИзображение.Хранилище.Получить();
		ПоказатьОсновноеИзображение();
		
	КонецЕсли;

КонецПроцедуры

Процедура ПослеЗаписи()
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
КонецПроцедуры



мПустаяКартинка             = Новый Картинка;
