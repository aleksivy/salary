Перем мУдалятьДвижения;


Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);

	Запрос.Текст = "
	|ВЫБРАТЬ 
	|	Дата, 
	| 	Ссылка 
	|ИЗ 
	|	Документ." + Метаданные().Имя + "
	|ГДЕ 
	|	Ссылка = &ДокументСсылка
	|";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "Кандидаты" документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса. В запросе данные документа дополняются значениями
//  проверяемых параметров из связанного с
//
Функция СформироватьЗапросПоКандидаты(Режим)

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);

	// угловыми скобками выделены изменяющиеся фрагменты текста запроса
	Запрос.Текст = "ВЫБРАТЬ
	               |	Док.ФизЛицо,
	               |	Док.НомерСтроки КАК НомерСтроки,
				   |    Док.ИсточникКандидата,
				   |    Док.Должность,
				   |	Док.ОсновнойМенеджер,
	               |	Док.ДатаПоступленияРезюме,
	               |	КандидатыНаРаботу.Статус КАК ПроверяемоеЗначение,
	               |	КандидатыНаРаботу.Период КАК ДатаРеквизита,
	               |	МИНИМУМ(ПересекающиесяСтроки.НомерСтроки) КАК КонфликтнаяСтрокаНомер
	               |ИЗ
	               |	Документ.РегистрацияКандидатов.Кандидаты КАК Док
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КандидатыНаРаботу КАК КандидатыНаРаботу
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |				МАКСИМУМ(КандидатыВнутри.Период) КАК ДатаЗначения,
	               |				Док.ФизЛицо КАК ФизЛицо
	               |			ИЗ
	               |				Документ.РегистрацияКандидатов.Кандидаты КАК Док
	               |					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КандидатыНаРаботу КАК КандидатыВнутри
	               |					ПО КандидатыВнутри.ФизЛицо = Док.ФизЛицо И КандидатыВнутри.Период <= Док.ДатаПоступленияРезюме
	               |			
	               |			ГДЕ
	               |				Док.Ссылка = &ДокументСсылка
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				Док.ФизЛицо) КАК СписокДат
	               |			ПО КандидатыНаРаботу.Период = СписокДат.ДатаЗначения И КандидатыНаРаботу.ФизЛицо = СписокДат.ФизЛицо
	               |		ПО КандидатыНаРаботу.ФизЛицо = Док.ФизЛицо
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияКандидатов.Кандидаты КАК ПересекающиесяСтроки
	               |		ПО Док.Ссылка = ПересекающиесяСтроки.Ссылка И Док.ФизЛицо = ПересекающиесяСтроки.ФизЛицо И Док.НомерСтроки < ПересекающиесяСтроки.НомерСтроки
	               |
	               |ГДЕ
	               |	Док.Ссылка = &ДокументСсылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Док.НомерСтроки,
	               |	Док.ФизЛицо,
				   |    Док.ИсточникКандидата,
				   |    Док.Должность,
				   |	Док.ОсновнойМенеджер,
	               |	Док.ДатаПоступленияРезюме,
	               |	КандидатыНаРаботу.Статус,
	               |	КандидатыНаРаботу.Период";
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоКандидаты()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "Кандидаты" документа.
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//  ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//  							  из результата запроса по кандидатам, 
//  Отказ 						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиКандидата(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = НСтр("ru='В строке номер ""';uk='У рядку номер ""'")+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									НСтр("ru='"" табл. части ""Кандидаты"": ';uk='"" табл. частини ""Кандидати"": '");
	
	// ФизЛицо
    ЕстьФизЛицо = Не ОбщегоНазначения.ЗначениеНеЗаполнено(ВыборкаПоСтрокамДокумента.ФизЛицо);
	Если Не ЕстьФизЛицо Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + НСтр("ru='не выбрано физическое лицо!';uk='не обрана фізична особа!'"), Отказ, Заголовок);
	КонецЕсли;

	// Дата
    ЕстьДатаПоступленияРезюме = Не ОбщегоНазначения.ЗначениеНеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаПоступленияРезюме);
	Если Не ЕстьДатаПоступленияРезюме Тогда	
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + НСтр("ru='не указана дата поступления резюме!';uk='не зазначена дата надходження резюме!'"), Отказ, Заголовок);
	КонецЕсли;

	Если ЕстьФизЛицо и ЕстьДатаПоступленияРезюме Тогда
		
		Если ВыборкаПоСтрокамДокумента.ДатаРеквизита = ВыборкаПоСтрокамДокумента.ДатаПоступленияРезюме Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + НСтр("ru='по кандидату на дату регистрации уже присутствует решение!';uk='по кандидату на дату реєстрації вже є рішення!'"), Отказ, Заголовок);
		КонецЕсли;
		
		Если ВыборкаПоСтрокамДокумента.ПроверяемоеЗначение = Перечисления.СостоянияКандидатаНаРаботу.Отклонен Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + НСтр("ru='указан отклоненный ранее претендент!';uk='вказаний відхилений раніше претендент!'"), Отказ, Заголовок);
		КонецЕсли;
		
		// Проверка: противоречие другой строке документа
		Если ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер <> NULL Тогда
			СтрокаСообщениеОбОшибке = НСтр("ru='кандидат не может быть указан в документе дважды (см. строку ';uk='кандидат не може бути вказаний в документі двічі (див. рядок '") + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер + ")!"; 
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиКандидата()

// Создает и заполняет структуру, содержащую имена регистров сведений 
//  по которым надо проводить документ
//
// Параметры: 
//  СтруктураПроведенияПоРегистрамСведений - структура, содержащая имена регистров сведений 
//                                           по которым надо проводить документ
//
// Возвращаемое значение:
//  Нет.
//
Процедура ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамСведений)

	СтруктураПроведенияПоРегистрамСведений = Новый Структура();
	
	СтруктураПроведенияПоРегистрамСведений.Вставить("КандидатыНаРаботу");
	

КонецПроцедуры // ЗаполнитьСтруктуруПроведенияПоРегистрамСведений

// Создает и заполняет структуру, содержащую имена регистров накоплений 
//  по которым надо проводить документ
//
// Параметры: 
//  СтруктураПроведенияПоРегистрамНакоплений - структура, содержащая имена регистров накоплений 
//                                             по которым надо проводить документ
//
// Возвращаемое значение:
//  Нет.
//
Процедура ЗаполнитьСтруктуруПроведенияПоРегистрамНакоплений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамНакоплений)

	СтруктураПроведенияПоРегистрамНакоплений = Новый Структура();
	
	СтруктураПроведенияПоРегистрамНакоплений.Вставить("ПриходПерсоналаПоИсточникам");
	

КонецПроцедуры // ЗаполнитьСтруктуруПроведенияПоРегистрамСведений

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//  ВыборкаПоШапкеДокумента                - выборка из результата запроса по шапке документа,
//  СтруктураПроведенияПоРегистрамСведений - структура, содержащая имена регистров 
//                                           сведений по которым надо проводить документ,
//  СтруктураПараметров                    - структура параметров проведения,
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, 
		  СтруктураПроведенияПоРегистрамСведений, СтруктураПараметров = "")

	// Если документ нужно проводить по регистру, то для него есть ключ в структуре
	ИмяРегистра = "КандидатыНаРаботу";
	Если СтруктураПроведенияПоРегистрамСведений.Свойство(ИмяРегистра) Тогда

		Движение = Движения[ИмяРегистра].Добавить();

		// Свойства
		Движение.Период                    = ВыборкаПоСтрокамДокумента.ДатаПоступленияРезюме;
		
		// Измерения
		Движение.ФизЛицо                   = ВыборкаПоСтрокамДокумента.ФизЛицо;

		// Ресурсы
		Движение.Статус           	       = Перечисления.СостоянияКандидатаНаРаботу.ПринятКРассмотрению;
		
		// Реквизиты
		Движение.Должность        		   = ВыборкаПоСтрокамДокумента.Должность;
		Движение.ОсновнойМенеджер          = ВыборкаПоСтрокамДокумента.ОсновнойМенеджер;
			
	КонецЕсли; 
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//  ВыборкаПоШапкеДокумента                  - выборка из результата запроса по шапке документа,
//  СтруктураПроведенияПоРегистрамНакоплений - структура, содержащая имена регистров 
//                                             накоплений по которым надо проводить документ,
//  СтруктураПараметров                      - структура параметров проведения,
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакоплений(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, 
		  СтруктураПроведенияПоРегистрамНакоплений, СтруктураПараметров = "")

	// Если документ нужно проводить по регистру, то для него есть ключ в структуре
	ИмяРегистра = "ПриходПерсоналаПоИсточникам";
	Если СтруктураПроведенияПоРегистрамНакоплений.Свойство(ИмяРегистра) Тогда

		Движение = Движения[ИмяРегистра].Добавить();

		// Свойства
		Движение.Период                    = ВыборкаПоСтрокамДокумента.ДатаПоступленияРезюме;
		
		// Измерения
		Движение.ИсточникПерсонала         = ВыборкаПоСтрокамДокумента.ИсточникКандидата;

		// Ресурсы
		Движение.Количество        	       = 1;
			

	КонецЕсли; 
	
		

КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//структура, содержащая имена регистров сведений по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамСведений;
    Перем СтруктураПроведенияПоРегистрамНакоплений;
	
	//Документ не используется
	Отказ = Истина;
	Возврат;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			// Создадим и заполним структуры, содержащие имена регистров, по которым в зависимости от типа учета
			// проводится документ. В дальнейшем будем считать, что если для регистра не создан ключ в структуре,
			// то проводить по нему не надо.
			ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамСведений);
            ЗаполнитьСтруктуруПроведенияПоРегистрамНакоплений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамНакоплений);
			
			// получим реквизиты табличной части
			РезультатЗапросаПоКандидаты = СформироватьЗапросПоКандидаты(Режим);
			ВыборкаПоКандидаты = РезультатЗапросаПоКандидаты.Выбрать();

			Пока ВыборкаПоКандидаты.Следующий() Цикл 

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиКандидата(ВыборкаПоШапкеДокумента, ВыборкаПоКандидаты, Отказ, Заголовок);

				Если НЕ Отказ Тогда
					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоКандидаты, СтруктураПроведенияПоРегистрамСведений);
					ДобавитьСтрокуВДвиженияПоРегистрамНакоплений(ВыборкаПоШапкеДокумента, ВыборкаПоКандидаты, СтруктураПроведенияПоРегистрамНакоплений);

				КонецЕсли;
				
				

			КонецЦикла;
			
		КонецЕсли; 

	КонецЕсли;

КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	ОбщегоНазначения.ДобавитьПрефиксУзла(Префикс);	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.ОчисткаКоллекцииДвиженийДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры // ПередЗаписью

Процедура ОбработкаУдаленияПроведения(Отказ)

	
	ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);

КонецПроцедуры // ОбработкаУдаленияПроведения





мДлинаСуток = 86400;
