Процедура ВидимостьБольничныеФССАвансом()
	
	ЭлементыФормы.БольничныеФССАвансом.Видимость = (БольничныеВид <> 1);
	
КонецПроцедуры

Процедура ВидимостьУплатаВзносов()
	
	ЭлементыФормы.УчитыватьУплатуВзносов.Видимость = (ВидВыплатыВзносы <> 0);
	ЭлементыФормы.ДокументНачисления.Видимость = (ВидВыплатыВзносы = 1);
	
КонецПроцедуры


Процедура ОсновныеДействияФормыДействиеОк(Кнопка)
	
	ДокументОбъект.мОкруглятьДо = ОкруглятьДо;
	ДокументОбъект.мПроцентВыплаты = ПроцентВыплаты;
	ДокументОбъект.мСортировка = ПорядокСотрудников;
	ДокументОбъект.мПериодС = ПериодС;
	ДокументОбъект.мПериодПо = ПериодПо;
	ДокументОбъект.мБольничныеВид = БольничныеВид;
	ДокументОбъект.мБольничныеФССАвансом = БольничныеФССАвансом;
	ДокументОбъект.мВидВыплатыВзносы = ВидВыплатыВзносы;
	ДокументОбъект.мУчитыватьУплатуВзносов = УчитыватьУплатуВзносов;
	ДокументОбъект.мДокументНачисления = ДокументНачисления;
	
	Если ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Очередная 
		или ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.ОчереднаяПогашениеДолгов  Тогда
		Если ОтборСотрудников = 0 Тогда
			ДокументОбъект.АвтозаполнениеОсновнаяВыплата();
		ИначеЕсли ОтборСотрудников = 1 Тогда
			ДокументОбъект.АвтозаполнениеОсновнаяВыплата(ложь,истина);
		ИначеЕсли ОтборСотрудников = 2 Тогда
			ДокументОбъект.АвтозаполнениеОсновнаяВыплата(ложь,ложь,истина);
		КонецЕсли;
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.ПогашениеДолгов  Тогда
		ДокументОбъект.АвтозаполнениеОсновнаяВыплата();
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Аванс  Тогда
		ДокументОбъект.АвтозаполнениеАванс();
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.АвансПредварительныйРасчет  Тогда
		ДокументОбъект.АвтозаполнениеАвансПредварительныйРасчет();		
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Депоненты  Тогда
		ДокументОбъект.АвтозаполнениеДепонированная();	
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Больничные  Тогда
		ДокументОбъект.АвтозаполнениеБольничные();
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.ОчереднаяВзносы  Тогда
		ДокументОбъект.АвтозаполнениеОсновнаяВыплатаВзносы();	
	КонецЕсли;
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЭтаФорма.Заголовок = НСтр("ru='Настройка заполнения - ';uk='Настройка заповнення - '") + ДокументОбъект.ВидВыплаты.Наименование;
	
	Если ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Очередная Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.Очередная;
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.ОчереднаяПогашениеДолгов Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.ОчереднаяСПогашением;
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.ПогашениеДолгов Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.ПогашениеДолгов;	
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Аванс
		ИЛИ ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.АвансПредварительныйРасчет Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.Аванс;
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Депоненты Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.Депоненты;
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.Больничные Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.Больничные;
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.ОчереднаяВзносы Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.Взносы;	
	КонецЕсли;
	Если ДокументОбъект.мОкруглятьДо = Неопределено ИЛИ  ДокументОбъект.мОкруглятьДо = 0 Тогда
		ОкруглятьДо = РегистрыСведений.ПараметрыРасчетаЗарплатыОрганизаций.Получить(Новый Структура ("Организация", ДокументОбъект.Организация)).ОкруглятьДо;
	Иначе
		ОкруглятьДо = ДокументОбъект.мОкруглятьДо;
	КонецЕсли;
	Если ОкруглятьДо = 0 Тогда
		ОкруглятьДо = 0.01;
	КонецЕсли;	
	Если ДокументОбъект.мПроцентВыплаты = Неопределено Тогда
		ПроцентВыплаты = 100;
	ИначеЕсли ДокументОбъект.ВидВыплаты = Справочники.ВидыВыплат.ОчереднаяПогашениеДолгов  Тогда	
		ПроцентВыплаты = 100;
	Иначе
		ПроцентВыплаты = ДокументОбъект.мПроцентВыплаты;
	КонецЕсли;
	Если ДокументОбъект.мСортировка = Неопределено Тогда
		ПорядокСотрудников = "";
	Иначе
		ПорядокСотрудников = ДокументОбъект.мСортировка;
	КонецЕсли;
	Если ДокументОбъект.мПериодС = Неопределено Тогда
		ПериодС = Дата(1,1,1);
	Иначе
		ПериодС = ДокументОбъект.мПериодС;
	КонецЕсли;
	Если ДокументОбъект.мПериодПо = Неопределено Тогда
		ПериодПо = НачалоМесяца(ДокументОбъект.ПериодРегистрации-1);
	Иначе
		ПериодПо = ДокументОбъект.мПериодПо;
	КонецЕсли;
	Если ДокументОбъект.мБольничныеВид = Неопределено Тогда
		БольничныеВид = 0;
	Иначе
		БольничныеВид = ДокументОбъект.мБольничныеВид;
	КонецЕсли;
	Если ДокументОбъект.мБольничныеФССАвансом = Неопределено Тогда
		БольничныеФССАвансом = Ложь;
	Иначе
		БольничныеФССАвансом = ДокументОбъект.мБольничныеФССАвансом;
	КонецЕсли;
	ОтборСотрудников = 0;
	
	ВидВыплатыВзносы = ДокументОбъект.мВидВыплатыВзносы;
	УчитыватьУплатуВзносов = ДокументОбъект.мУчитыватьУплатуВзносов;
	ДокументНачисления = ДокументОбъект.мДокументНачисления;
	
	ВидимостьБольничныеФССАвансом();
	ВидимостьУплатаВзносов();
	
КонецПроцедуры

Процедура ПериодСПриИзменении(Элемент)
	
	Элемент.Значение = НачалоМесяца(Элемент.Значение);
	
КонецПроцедуры

Процедура БольничныеВидПриИзменении(Элемент)
	
	ВидимостьБольничныеФССАвансом();
	
КонецПроцедуры

Процедура ДокументНачисленияНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;

	ФормаВыбора = Документы["НачислениеЗарплатыРаботникамОрганизаций"].ПолучитьФормуВыбора(,Элемент,);			
	ФормаВыбора.Отбор.Организация.ВидСравнения  = ВидСравнения.Равно;
	ФормаВыбора.Отбор.Организация.Значение      = ДокументОбъект.Организация;
	ФормаВыбора.Отбор.Организация.Использование = Истина;
	
	ФормаВыбора.Отбор.ПериодРегистрации.ВидСравнения  = ВидСравнения.Равно;
	ФормаВыбора.Отбор.ПериодРегистрации.Значение      = ДокументОбъект.ПериодРегистрации;
	ФормаВыбора.Отбор.ПериодРегистрации.Использование = Истина;
			
	ФормаВыбора.Открыть();
	
КонецПроцедуры

Процедура ПоОрганизацииПриИзменении(Элемент)
	
	ВидимостьУплатаВзносов();
	
КонецПроцедуры
