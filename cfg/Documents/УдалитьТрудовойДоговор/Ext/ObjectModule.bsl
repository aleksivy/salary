////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТрудовойДоговор.Дата,
		|	ТрудовойДоговор.ФизЛицо,
		|	ТрудовойДоговор.Организация,
		|	ТрудовойДоговор.ПодразделениеОрганизации,
		|	ТрудовойДоговор.Должность,
		|	ТрудовойДоговор.ВидЗанятости,
		|	ТрудовойДоговор.ГрафикРаботы,
		|	ТрудовойДоговор.Ссылка,
		|	ТрудовойДоговор.ДатаПриема,
		|	ТрудовойДоговор.СпособРасчета,
		|	ТрудовойДоговор.ТарифнаяСтавка,
		|	ТрудовойДоговор.ВалютаТарифнойСтавки,
		|	ТрудовойДоговор.ЗанимаемыхСтавок
		|ИЗ
		|	Документ.ТрудовойДоговор КАК ТрудовойДоговор
		|
		|ГДЕ
		|	ТрудовойДоговор.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указана организация, в которую принимается работник!';uk='Не зазначена організація, у яку приймається працівник!'"), Отказ, Заголовок);
	КонецЕсли;

	// ФизЛицо
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ФизЛицо) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не выбрано физическое лицо!';uk='Не обране фізична особа!'"), Отказ, Заголовок);
	КонецЕсли;

	// Подразделение
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПодразделениеОрганизации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указано подразделение, в которое принимается работник!';uk='Не вказаний підрозділ, в який приймається працівник!'"), Отказ, Заголовок);
	КонецЕсли;

	// Должность
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Должность) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указана должность работника!';uk='Не зазначена посада працівника!'"), Отказ, Заголовок);
	КонецЕсли;

	// Дата "с"
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаПриема) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указана дата приема!';uk='Не вказана дата прийому!'"), Отказ, Заголовок);
	КонецЕсли;

	// ВидЗанятости
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ВидЗанятости) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указан вид занятости!';uk='Не зазначений вид зайнятості!'"), Отказ, Заголовок);

	КонецЕсли;
	
	// ГрафикРаботы
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ГрафикРаботы) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указан график работы!';uk='Не зазначений графік роботи!'"), Отказ, Заголовок);
	КонецЕсли;
	
	// СпособРасчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.СпособРасчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указан способ расчетов с физлицом!';uk='Не вказаний спосіб розрахунків з фізособою!'"), Отказ, Заголовок);
	КонецЕсли;
	
	// ТарифнаяСтавка
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ВалютаТарифнойСтавки) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указан размер тарифной ставки!';uk='Не вказаний розмір тарифної ставки!'"), Отказ, Заголовок);
	КонецЕсли;
	
	// ВалютаТарифнойСтавки
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ВалютаТарифнойСтавки) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указана вылюта тарифной ставки!';uk='Не вказана валюта тарифної ставки!'"), Отказ, Заголовок);
	КонецЕсли;
	
	// ЗанимаемыхСтавок
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ЗанимаемыхСтавок) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указано количество занимаемых ставок!';uk='Не зазначена кількість ставок, що займаються!'"), Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

	КонецЕсли;

КонецПроцедуры
