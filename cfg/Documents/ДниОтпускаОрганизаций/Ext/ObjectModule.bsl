////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мДлинаСуток;

Функция ОпределитьДатуПриемаНаРаботу(Сотрудник) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Период	КАК ДатаПриемаНачало,
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(Период, ГОД, 1),СЕКУНДА, -1)		КАК ДатаПриемаОкончание	
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПервых(,Сотрудник=&Сотрудник)
	|";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДатаПриемаНачало;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	лтзДвижения = Движения.СернаДниОтпуска.ВыгрузитьКолонки();
	Для каждого лСтрока Из РаботникиОрганизации Цикл
		Если лСтрока.КвоДней = 0 Тогда Продолжить; КонецЕсли;
		лНоваяСтрока = лтзДвижения.Добавить();
		ЗаполнитьЗначенияСвойств(лНоваяСтрока, лСтрока);
		лНоваяСтрока.Активность = Истина;
		лНоваяСтрока.Регистратор = Ссылка;
		лНоваяСтрока.ВидДвижения = ?(лСтрока.Расход, ВидДвиженияНакопления.Расход, ВидДвиженияНакопления.Приход);
		лНоваяСтрока.Организация = Организация;
		лНоваяСтрока.ВидОтпуска = ВидОтпуска;
		лНоваяСтрока.ЭтоКорректировка = ЭтоКорректировка;
	КонецЦикла;
	Движения.СернаДниОтпуска.Загрузить( лтзДвижения );
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда Возврат; КонецЕсли;

	Если РаботникиОрганизации.Количество() = 0 Тогда
		КраткийСоставДокумента = "";
	Иначе
		КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(РаботникиОрганизации);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидОтпуска) или Не ВидОтпуска.ВедетсяУчетОстатков Тогда ВидОтпуска = Справочники.ВидыОтпусков.ЕжегодныйОсновной; КонецЕсли;
	
	// Проверим заполнение строк ТЧ
	Для каждого лСтрока Из РаботникиОрганизации Цикл
		Если Не ЗначениеЗаполнено(лСтрока.Период) Тогда лСтрока.Период = Дата; КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()

мДлинаСуток = 86400;

